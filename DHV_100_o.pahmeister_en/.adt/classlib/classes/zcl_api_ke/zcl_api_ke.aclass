class ZCL_API_KE definition
  public
  inheriting from CL_REST_RESOURCE
  create public .

public section.

  methods IF_REST_RESOURCE~GET
    redefinition .
protected section.
private section.
ENDCLASS.



CLASS ZCL_API_KE IMPLEMENTATION.


  METHOD if_rest_resource~get.

    CONSTANTS:
      lc_infty TYPE infty VALUE '0001'.

    DATA:
      ls_p0001  TYPE p0001,
      lv_data_exists TYPE boole_d,
      lt_errors TYPE wdy_key_value_list.

    TRY.
        DATA(lo_employee_api) = cl_hcmfab_employee_api=>get_instance( ).
        DATA(lv_pernr) = lo_employee_api->get_employeenumber_from_user( sy-uname ).
        cl_hcmfab_utilities=>read_infotype_record( EXPORTING iv_pernr       = lv_pernr
                                                             iv_infty       = lc_infty
                                                   IMPORTING es_pnnnn       = ls_p0001
                                                             ev_data_exists = lv_data_exists ).
      CATCH cx_hcmfab_common INTO DATA(lx_exc).
        lt_errors = zcl_api_utils=>write_to_error_tbl( iv_key    = zcl_api_utils=>gc_err
                                                       iv_value  = lx_exc->get_text( )
                                                       it_errors = lt_errors ).

    ENDTRY.
    IF lv_data_exists = abap_true.
      AUTHORITY-CHECK OBJECT 'ZHR_STRUC'
        FOR USER sy-uname
        ID 'KOKRS' FIELD ls_p0001-kokrs
        ID 'ACTVT' FIELD '03'.
      IF sy-subrc = 0.

        SELECT kokrs AS id,
               bezei AS val
          FROM tka01
         WHERE kokrs = @ls_p0001-kokrs
          INTO TABLE @DATA(lt_kokrs_text).
      ENDIF.
    ELSE.
      lt_errors = zcl_api_utils=>write_to_error_tbl( iv_key    = zcl_api_utils=>gc_err
                                                     iv_value  = CONV #( 'Предприятие не найдено'(001) )
                                                     it_errors = lt_errors ).
    ENDIF.

    GET REFERENCE OF lt_kokrs_text INTO DATA(lr_data).
    zcl_api_utils=>process_response( EXPORTING it_errors   = lt_errors
                                               ir_data     = lr_data
                                     CHANGING  cr_response = mo_response ).
  ENDMETHOD.
ENDCLASS.