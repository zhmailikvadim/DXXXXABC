FUNCTION ZFI_ADDITIONAL_DOC
  IMPORTING
    IV_NOCHECK TYPE ABAP_BOOL OPTIONAL
  TABLES
    I_COBK LIKE COBK
    I_COEP LIKE COEP.



* Описание : СОЗДАНИЕ ДОКУМЕНТА АВТОМАТИЧЕСКОГО ПЕРЕРАСЧЕТА МЕЖДУ ФУНКЦИОНАЛЬНЫМИ СФЕРАМИ
* Код ТС : DEV.030- УУ.06.E-010
* Разработчик : Бондарь Я.С.
* Постановка задачи: Сиренко А.А.
* Redmine #11550

  DATA:
    lt_vrgng TYPE RANGE OF coep-vrgng, "виды операций релевантные для красного сторно
    lt_awtyp TYPE RANGE OF coep-awtyp,
    lt_orgvg TYPE RANGE OF coep-vrgng,
    lt_wrttp TYPE RANGE OF coep-wrttp,
    lt_coep  TYPE tt_coep.

  SELECT SINGLE low                           "Выбрать значение TVARVC-LOW   для TVARVC -NAME = "ZSWITCH_ CO010".
    INTO @DATA(lv_low)                        "Описанное ниже расширение выполняется если значение TVARVC-LOW не пусто.
    FROM tvarvc
    WHERE name = 'ZSWITCH_CO010_C'
      AND type = 'P'
      AND numb = @space.
  IF lv_low IS INITIAL AND iv_nocheck = abap_false.
    RETURN.
  ENDIF.

  SELECT name,
         sign,
         opti AS option,
         low,
         high
   FROM tvarvc
   INTO TABLE @DATA(lt_tvarc)
   WHERE name IN ( 'ZCO_WRTTP_ADDOC', 'ZCO_LT_VRGNG', 'ZCOFKB_AWTYP','ZCOFKB_ORGVG' )
     AND type = 'S'.
  LOOP AT lt_tvarc ASSIGNING FIELD-SYMBOL(<ls_tvarc>).
    CASE <ls_tvarc>-name.
      WHEN 'ZCO_WRTTP_ADDOC'. APPEND CORRESPONDING #( <ls_tvarc> ) TO lt_wrttp.  " заполнение тип затрат - факт, по умолчанию 04
      WHEN 'ZCO_LT_VRGNG'.    APPEND CORRESPONDING #( <ls_tvarc> ) TO lt_vrgng.
      WHEN 'ZCOFKB_AWTYP'.    APPEND CORRESPONDING #( <ls_tvarc> ) TO lt_awtyp.
      WHEN 'ZCOFKB_ORGVG'.    APPEND CORRESPONDING #( <ls_tvarc> ) TO lt_orgvg.
    ENDCASE.
  ENDLOOP.

  IF lt_wrttp IS INITIAL.
    lt_wrttp = VALUE #( ( sign = 'I' option = 'EQ' low = '04' ) ).
  ENDIF.

  IF lt_awtyp IS INITIAL.
    lt_awtyp = VALUE #( ( sign = 'I' option = 'EQ' low = 'RMRP' ) ).
  ENDIF.

  LOOP AT i_cobk.
    lt_coep = VALUE #( FOR line IN i_coep[]
                         WHERE ( belnr = i_cobk-belnr AND wrttp IN lt_wrttp )  ( line ) ).
    gt_coep = lt_coep.  "Сохранение данных lt_coep для замещения
    PERFORM   post_one_doc USING i_cobk
                                 lt_vrgng    "разрешенные операции для красного сторно
                                 lt_awtyp    "разрешенные операции для счетов наличия
                                 lt_orgvg    "разрешенные операции для док согласования
                                 lt_coep .
  ENDLOOP.
ENDFUNCTION.