FUNCTION ZFI_CLEAR_FKBER
  TABLES
    T_ACCIT LIKE ACCIT.



  "DEV.030-УУ.06.E-010.docx  п.3.5.5
  " Формирование набора счетов
  DATA:
    lt_vals   TYPE STANDARD TABLE OF rgsb4,
    lt_hkonts TYPE RANGE OF bseg-hkont,
    lv_setid  TYPE sethier-setid.
  FIELD-SYMBOLS:
    <ls_vals>  TYPE rgsb4,
    <ls_accit> TYPE accit.
  CALL FUNCTION 'G_SET_GET_ID_FROM_NAME'
    EXPORTING
      shortname                = '2ORHKONT'
    IMPORTING
      new_setid                = lv_setid
    EXCEPTIONS
      no_set_found             = 1
      no_set_picked_from_popup = 2
      wrong_class              = 3
      wrong_subclass           = 4
      table_field_not_found    = 5
      fields_dont_match        = 6
      set_is_empty             = 7
      formula_in_set           = 8
      set_is_dynamic           = 9
      OTHERS                   = 10.
  IF sy-subrc <> 0.
    RETURN.
  ENDIF.
  CALL FUNCTION 'G_SET_GET_ALL_VALUES'
    EXPORTING
      setnr         = lv_setid
    TABLES
      set_values    = lt_vals
    EXCEPTIONS
      set_not_found = 1
      OTHERS        = 2.
  IF sy-subrc <> 0.
    RETURN.
  ENDIF.

  LOOP AT lt_vals ASSIGNING <ls_vals>.
    APPEND INITIAL LINE TO lt_hkonts ASSIGNING FIELD-SYMBOL(<ls_hkonts>).
    <ls_hkonts>-low = <ls_vals>-from.
    <ls_hkonts>-sign = 'I'.
    IF <ls_vals>-to IS NOT INITIAL.
      <ls_hkonts>-high = <ls_vals>-to.
      <ls_hkonts>-option = 'BT'.
    ELSE.
      <ls_hkonts>-option = 'EQ'.
    ENDIF.
  ENDLOOP.

  LOOP AT t_accit ASSIGNING <ls_accit>
     WHERE vorgn = 'KOAE'
       AND hkont IN lt_hkonts
       AND fkber <> space.
    CLEAR <ls_accit>-fkber.
  ENDLOOP.
ENDFUNCTION.