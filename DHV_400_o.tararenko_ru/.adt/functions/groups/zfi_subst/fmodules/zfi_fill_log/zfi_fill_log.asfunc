FUNCTION ZFI_FILL_LOG
  IMPORTING
    VALUE(IT_RETURN) TYPE TT_BAPIRET2
    VALUE(IV_BELNR) TYPE COBK-BELNR.



  DATA:
    lv_log_hnd TYPE balloghndl,
    ls_log     TYPE bal_s_log,
    lt_log_hnd TYPE bal_t_logh,
    ls_msg     TYPE bal_s_msg.

  "Настройка лога
  ls_log = VALUE #(
    object     = 'ZCO_CREATE_ADD_DOC'
    extnumber  = iv_belnr
    aldate_del = sy-datum + 7 " Записи в журнале хранятся 7 дней
    del_before = 'X'
    aldate     = sy-datum
    altime     = sy-uzeit
    aluser     = sy-uname
    altcode    = sy-tcode
    alprog     = sy-repid
    ).
  "Создание лога
  CALL FUNCTION 'BAL_LOG_CREATE'
    EXPORTING
      i_s_log      = ls_log
    IMPORTING
      e_log_handle = lv_log_hnd
    EXCEPTIONS
      OTHERS       = 1.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE 'A' NUMBER sy-msgno
       WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  LOOP AT it_return ASSIGNING FIELD-SYMBOL(<ls_ret>) WHERE type CA 'AE'.
    ls_msg = VALUE #(
      msgty = <ls_ret>-type
      msgid = <ls_ret>-id
      msgno = <ls_ret>-number
      msgv1 = <ls_ret>-message_v1
      msgv2 = <ls_ret>-message_v2
      msgv3 = <ls_ret>-message_v3
      msgv4 = <ls_ret>-message_v4
    ).

    CALL FUNCTION 'BAL_LOG_MSG_ADD'
      EXPORTING
        i_log_handle = lv_log_hnd
        i_s_msg      = ls_msg
      EXCEPTIONS
        OTHERS       = 1.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE 'A' NUMBER sy-msgno
          WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
  ENDLOOP.
  "сохранение лога
  append lv_log_hnd to lt_log_hnd.
  CALL FUNCTION 'BAL_DB_SAVE'
    EXPORTING
      i_t_log_handle = lt_log_hnd
    EXCEPTIONS
      OTHERS         = 1.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
       WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  " отсылка сообщения инициатору
  PERFORM send_message.

  COMMIT WORK.

ENDFUNCTION.