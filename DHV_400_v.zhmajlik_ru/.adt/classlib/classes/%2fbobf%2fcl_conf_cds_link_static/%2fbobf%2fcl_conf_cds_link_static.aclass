"! <p class="shorttext synchronized" lang="en">CDS link - static (helper) methods</p>
CLASS /bobf/cl_conf_cds_link_static DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    CLASS-METHODS check_bo_exists
      IMPORTING
        !iv_bo_name        TYPE /bobf/obm_name
      EXPORTING
        !ev_is_generated   TYPE abap_bool
        !ev_is_active      TYPE abap_bool
        !ev_is_customer_bo TYPE abap_bool
        !ev_bo_category    TYPE /bobf/obm_objcat
      RETURNING
        VALUE(rv_exists)   TYPE abap_bool .
    CLASS-METHODS get_generated_bos_for_view
      IMPORTING
        !iv_cds_view_name TYPE /bobf/conf_cds_view_name
      RETURNING
        VALUE(rt_bo_name) TYPE /bobf/t_conf_name .
    "! <p class="shorttext synchronized" lang="en">Identify the scenario (_quadrant_) for a CDS-generated BO</p>
    "! @parameter iv_object_category         | <p class="shorttext synchronized" lang="en">Object category of the Business Object</p>
    "! @parameter iv_feature_annotations     | <p class="shorttext synchronized" lang="en">Feature annotations of the Business Object</p>
    "! @parameter iv_root_active_persistence | <p class="shorttext synchronized" lang="en">Annotated 'WriteActivePersistence' of root CDS view</p>
    "! @parameter rv_scenario                | <p class="shorttext synchronized" lang="en">Constant /BOBF/IF_CONF_C=&gt;SC_SCENARIO-...</p>
    CLASS-METHODS identify_bo_scenario
      IMPORTING
        iv_object_category         TYPE /bobf/obm_objcat
        iv_feature_annotations     TYPE /bobf/conf_feature_annotations
        iv_root_active_persistence TYPE tabname
      RETURNING
        VALUE(rv_scenario)         TYPE /bobf/conf_scenario.

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS /BOBF/CL_CONF_CDS_LINK_STATIC IMPLEMENTATION.


  METHOD check_bo_exists.

    rv_exists         = abap_false.
    ev_is_generated   = abap_false.
    ev_is_active      = abap_false.
    ev_is_customer_bo = abap_false.
    CLEAR ev_bo_category.

    IF   ev_is_generated   IS SUPPLIED
      OR ev_is_active      IS SUPPLIED
      OR ev_is_customer_bo IS SUPPLIED
      OR ev_bo_category    IS SUPPLIED.

      NEW /bobf/cl_conf_model_api_adt( )->get_business_object(
        EXPORTING
          iv_business_object_name = iv_bo_name
          iv_header_only          = abap_true " !!!
        IMPORTING
          es_business_object      = DATA(ls_bo)
          ev_success              = DATA(lv_bo_exists) ).
      IF ls_bo IS INITIAL OR lv_bo_exists = abap_false.
        RETURN. " !!!
      ENDIF.

      rv_exists         = abap_true.
      ev_is_generated   = ls_bo-header-object_model_generated.
      ev_is_active      = ls_bo-header-is_active.
      ev_is_customer_bo = ls_bo-header-customer_bo.
      ev_bo_category    = ls_bo-header-objcat.

    ELSE.

      DATA(lv_bo_key) = /bobf/cl_conf_model_api_reuse=>get_bo_key_by_name( iv_bo_name ).
      IF lv_bo_key IS NOT INITIAL.
        rv_exists = abap_true.
      ENDIF.

    ENDIF.

  ENDMETHOD.


  METHOD get_generated_bos_for_view.

    " TODO: create and use a BOPF query?!

    DATA(lv_cds_view_name_up) = to_upper( iv_cds_view_name ).

    SELECT DISTINCT name INTO TABLE @rt_bo_name
      FROM /bobf/obm_node
        WHERE object_model_generated     = @abap_true
          AND object_model_cds_view_name = @lv_cds_view_name_up.

  ENDMETHOD.


  METHOD identify_bo_scenario.

    " identify the _quadrant_...
    IF iv_object_category = /bobf/if_conf_c=>sc_objcat_draft.
      IF iv_feature_annotations CS /bobf/if_conf_c=>sc_feature-draft_2_0.
        IF iv_root_active_persistence IS NOT INITIAL.
          " 'New application w/ draft'...
          rv_scenario = /bobf/if_conf_c=>sc_scenario-new_app_w_draft.
        ELSE.
          " 'Draft-enabled application' aka 'Union scenario'...
          rv_scenario = /bobf/if_conf_c=>sc_scenario-draft_enabled_app.
        ENDIF.
      ELSE.
        " 'Draft-enabled application' aka 'Union scenario'...
        rv_scenario = /bobf/if_conf_c=>sc_scenario-draft_enabled_app.
      ENDIF.
    ELSE.
      IF iv_root_active_persistence IS NOT INITIAL.
        " 'New application w/o draft' aka 'Existing scenario'...
        rv_scenario = /bobf/if_conf_c=>sc_scenario-new_app_wo_draft.
      ELSE.
        " 'Read-only application w/ quick actions'...
        rv_scenario = /bobf/if_conf_c=>sc_scenario-readonly_app_w_quick_actions.
      ENDIF.
    ENDIF.

  ENDMETHOD.
ENDCLASS.