*"* use this source file for your ABAP unit test classes
CLASS ltc_internal_property DEFINITION DEFERRED.
CLASS /bobf/cl_conf_readonly DEFINITION LOCAL FRIENDS ltc_internal_property.
CLASS ltc_internal_property DEFINITION FOR TESTING RISK LEVEL HARMLESS DURATION SHORT.
  PRIVATE SECTION.
    DATA mt_field_descr TYPE ddx031ltab.
    METHODS setup.
    METHODS non_draft FOR TESTING.
    METHODS draft_having_isactiveentity FOR TESTING.
    METHODS draft_wo_isactiveentity FOR TESTING.
ENDCLASS.

CLASS ltc_internal_property IMPLEMENTATION.
  METHOD setup.
    mt_field_descr = VALUE #(
      ( fieldname = 'KEY'                                                             position = 1 )
      ( fieldname = 'F1'                                                              position = 2 )
      ( fieldname = /bobf/if_conf_cds_link_c=>gc_element_name-draft-isactiveentity    position = 3 ) ).
  ENDMETHOD.
  METHOD non_draft.
    /bobf/cl_conf_readonly=>build_node_internal_props(
      EXPORTING
        iv_objcat            = /bobf/if_conf_c=>sc_objcat_bo
        it_field_descr       = mt_field_descr
        io_area_handle       = VALUE #( )
      RECEIVING
        rr_internal_property = DATA(lr_internal_property)
    ).
    cl_abap_unit_assert=>assert_initial( lr_internal_property ).
  ENDMETHOD.
  METHOD draft_having_isactiveentity.
    /bobf/cl_conf_readonly=>build_node_internal_props(
      EXPORTING
        iv_objcat            = /bobf/if_conf_c=>sc_objcat_draft
        it_field_descr       = mt_field_descr
        io_area_handle       = VALUE #( )
      RECEIVING
        rr_internal_property = DATA(lr_internal_property)
    ).
    cl_abap_unit_assert=>assert_bound( lr_internal_property ).
    cl_abap_unit_assert=>assert_equals(
      exp = VALUE /bobf/t_confro_prop_int(
        ( field_name = /bobf/if_conf_cds_link_c=>gc_element_name-draft-isactiveentity
          component = 3
          property_name = /bobf/if_conf_c=>sc_property_name_update_enable
          value = abap_false ) )
      act = lr_internal_property->*
    ).
  ENDMETHOD.
  METHOD draft_wo_isactiveentity.
    DELETE mt_field_descr WHERE fieldname = /bobf/if_conf_cds_link_c=>gc_element_name-draft-isactiveentity.
    /bobf/cl_conf_readonly=>build_node_internal_props(
      EXPORTING
        iv_objcat            = /bobf/if_conf_c=>sc_objcat_draft
        it_field_descr       = mt_field_descr
        io_area_handle       = VALUE #( )
      RECEIVING
        rr_internal_property = DATA(lr_internal_property)
    ).
    cl_abap_unit_assert=>assert_initial( lr_internal_property ).
  ENDMETHOD.
ENDCLASS.
