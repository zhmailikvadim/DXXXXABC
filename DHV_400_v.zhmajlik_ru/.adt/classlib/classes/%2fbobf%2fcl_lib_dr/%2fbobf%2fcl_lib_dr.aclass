"! <p class="shorttext synchronized" lang="en">Draft class default implementation for transactional apps</p>
CLASS /bobf/cl_lib_dr DEFINITION
  PUBLIC
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES:
      /bobf/if_frw_draft.


  PROTECTED SECTION.


  PRIVATE SECTION.


ENDCLASS.



CLASS /BOBF/CL_LIB_DR IMPLEMENTATION.


  METHOD /bobf/if_frw_draft~copy_draft_to_active_entity.

    CLEAR: et_failed_draft_key, et_key_link.

    TRY.
        DATA(lo_draft_copier) = /bobf/cl_lib_draft_copier=>create( is_ctx-bo_key ).
      CATCH /bobf/cx_frw.
        et_failed_draft_key = it_draft_key.
    ENDTRY.

    LOOP AT it_draft_key INTO DATA(ls_draft_key).
      lo_draft_copier->copy_draft_to_active( EXPORTING io_read       = io_read
                                                       io_modify     = io_modify
                                                       iv_draft_key  = ls_draft_key-key
                                             IMPORTING ev_active_key = DATA(lv_active_key)
                                             RECEIVING rv_success    = DATA(lv_success) ).
      IF lv_success = abap_true.
        INSERT VALUE #( active = lv_active_key draft = ls_draft_key-key ) INTO TABLE et_key_link.
      ELSE.
        APPEND VALUE #( key = ls_draft_key-key ) TO et_failed_draft_key.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD /bobf/if_frw_draft~create_draft_for_active_entity.

    CLEAR: eo_message, et_failed_active_key.

    TRY.
        DATA(lo_draft_copier) = /bobf/cl_lib_draft_copier=>create( is_ctx-bo_key ).
        LOOP AT it_key_link INTO DATA(ls_key_link).
          DATA(lv_success) = lo_draft_copier->copy_active_to_draft( io_read       = io_read
                                                                    io_modify     = io_modify
                                                                    iv_active_key = ls_key_link-active
                                                                    iv_draft_key  = ls_key_link-draft ).
          IF lv_success = abap_false.
            INSERT VALUE #( key = ls_key_link-active ) INTO TABLE et_failed_active_key.
          ENDIF.
        ENDLOOP.
      CATCH /bobf/cx_frw.
        CLEAR et_failed_active_key.
        LOOP AT it_key_link INTO DATA(ls_failed_key_link).
          INSERT VALUE #( key = ls_failed_key_link-active ) INTO TABLE et_failed_active_key.
        ENDLOOP.
    ENDTRY.

  ENDMETHOD.


  METHOD /bobf/if_frw_draft~resume.
    "empty default implementation
    RETURN.
  ENDMETHOD.
ENDCLASS.