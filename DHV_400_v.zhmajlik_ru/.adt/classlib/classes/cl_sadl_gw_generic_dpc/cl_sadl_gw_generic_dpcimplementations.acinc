CLASS lcx_internal_error DEFINITION INHERITING FROM cx_no_check FINAL.
ENDCLASS.

CLASS lcl_dpc_for_expand DEFINITION FINAL.
  PUBLIC SECTION.
    INTERFACES: if_sadl_gw_dpc_for_expand.

    METHODS: constructor
      IMPORTING
        io_dpc TYPE REF TO cl_sadl_gw_generic_dpc.

  PRIVATE SECTION.
    DATA:
      mo_dpc TYPE REF TO cl_sadl_gw_generic_dpc.

ENDCLASS.

CLASS cl_sadl_gw_generic_dpc DEFINITION LOCAL FRIENDS lcl_dpc_for_expand.

CLASS lcl_dpc_for_expand IMPLEMENTATION.

  METHOD constructor.
    mo_dpc = io_dpc.
    if_sadl_gw_dpc_for_expand~mv_odata_version = if_sadl_gw_dpc_for_expand~co_odata_version-v2.
  ENDMETHOD.

  METHOD if_sadl_gw_dpc_for_expand~get_entity_data.
    DATA(lv_entity_set_name) = io_query_options->get_entity_set_name( ).
    DATA(lo_sadl_runtime) = mo_dpc->mo_sadl_api->get_runtime( lv_entity_set_name ).
    mo_dpc->mo_common_runtime->get_entity_set( EXPORTING io_query_options = io_query_options
                                                         io_sadl_rt       = lo_sadl_runtime
                                                         io_mdp           = mo_dpc->mo_mdp
                                               IMPORTING et_data          = et_data ).
    mo_dpc->_add_messages_to_header( io_message_handler = lo_sadl_runtime->if_sadl_entity_transactional~get_message_handler( )
                                     iv_entity_set      = lv_entity_set_name ).
  ENDMETHOD.

  METHOD if_sadl_gw_dpc_for_expand~get_query_options.
    DATA(ls_entity_path) = VALUE if_sadl_gw_types_int=>ts_gw_entity_path( set_name  = iv_entity_set_name
                                                                          type_name = iv_entity_type_name ).
    TRY.
        ro_query_options  = mo_dpc->_get_query_options_entity_set(
          is_entity_path              = ls_entity_path
          it_requested_with_mandatory = it_requested_with_mandatory ).
      CATCH /iwbep/cx_mgw_tech_exception /iwbep/cx_mgw_busi_exception INTO DATA(lx).

*----- due to limited parameterization, this should not occur
        RAISE EXCEPTION TYPE cx_sadl_static EXPORTING previous = lx.
    ENDTRY.

  ENDMETHOD.

ENDCLASS.


CLASS lcl_request DEFINITION INHERITING FROM /iwbep/cl_mgw_request.

  PUBLIC SECTION.
    METHODS get_header IMPORTING io_request TYPE REF TO /iwbep/cl_mgw_request
                       EXPORTING et_headers TYPE tihttpnvp.
ENDCLASS.

CLASS lcl_request IMPLEMENTATION.
  METHOD get_header.
    et_headers = io_request->mr_request->technical_request-request_header.
  ENDMETHOD.
ENDCLASS.
