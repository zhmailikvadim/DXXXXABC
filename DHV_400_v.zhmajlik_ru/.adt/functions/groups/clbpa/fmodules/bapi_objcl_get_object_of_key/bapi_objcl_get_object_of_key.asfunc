FUNCTION BAPI_OBJCL_GET_OBJECT_OF_KEY
  IMPORTING
    VALUE(CLOBJECTKEYIN) TYPE BAPI1003_KEY-OBJECT_GUID
  EXPORTING
    VALUE(OBJECTNAME) LIKE BAPI1003_KEY-OBJECT
    VALUE(OBJECTTABLE) LIKE BAPI1003_KEY-OBJECTTABLE
    VALUE(CLASSTYPE) LIKE BAPI1003_KEY-CLASSTYPE
    VALUE(OBJECTNAME_LONG) LIKE BAPI1003_KEY-OBJECT_LONG
  TABLES
    RETURN LIKE BAPIRET2.




  DATA:
      lh_par1                            LIKE sy-msgv1.

  DATA:
      lv_objectname_long_exp             TYPE bapi1003_key-object_long.   " FLE correction: 2215859

** Class type settings need not to be coinsidered as only existing
** keys are dealt with; a non-MULTOBJ-class-type never will have
** such a key.


  REFRESH return.
  CLEAR:  objectname,
          objectname_long,                              "MFLE : 2170766
          objecttable.

  CALL FUNCTION 'CUOB_GET_OBJECT'
    EXPORTING
      object_number = clobjectkeyin
    IMPORTING
*     CLASS_NR      =
      class_type    = classtype
      object_id     = objectname_long                   "MFLE :  2170766
*     ROOT          =
*     ROOT_ID       =
*     STATUS        =
      table         = objecttable
*     ROOT_INSTANCE =
*     PARENT_INSTANCE =
*     PROFILE       =
*     INOB_OBJECT   =
    EXCEPTIONS
      not_found     = 1
      OTHERS        = 2.
  IF sy-subrc EQ 1.                      "object not found

    WRITE clobjectkeyin TO lh_par1.
    CALL FUNCTION 'BALW_BAPIRETURN_GET2'
      EXPORTING
        type   = 'E'
        cl     = 'CL'
        number = 763                 "object doesn't exist
        par1   = lh_par1
      IMPORTING
        return = return.
    APPEND return.
    CLEAR: classtype,
           objectname_long,                          "MFLE :  2170766
           objecttable.

  ELSEIF sy-subrc EQ 2.                  "some error

    CALL FUNCTION 'BALW_BAPIRETURN_GET2'
      EXPORTING
        type   = 'E'
        cl     = 'CL'
        number = 629                 "internal error
      IMPORTING
        return = return.
    APPEND return.
    CLEAR: classtype,
           objectname_long,                          "MFLE :  2170766
           objecttable.

  ELSE.                                  "object found

    CALL FUNCTION 'BALW_BAPIRETURN_GET2'
      EXPORTING
        type   = 'S'
        cl     = 'CL'
        number = 765                 "object found
      IMPORTING
        return = return.
    APPEND return.

  ENDIF.


*....FLE BAPI output: Grant exporting parameters' S4 compatibility...........
  TRY.                                                      "-->MFLE :  2170766
      cl_cls_chk_mapper=>convert_key_on_output(
        EXPORTING
          iv_class_type     = classtype
          iv_objectkey_long = objectname_long
          iv_object_table   = objecttable
        IMPORTING
          ev_objectkey      = objectname
          ev_objectkey_long = lv_objectname_long_exp
      ).
      objectname_long = lv_objectname_long_exp.             " FLE correction: 2215859

    CATCH cx_cls_chk_mapper INTO DATA(lx_conversion_exception).
      return-message = lx_conversion_exception->get_text( ).
      return-type = 'E'.
      APPEND return.
  ENDTRY.                                                   "<--MFLE :  2170766

ENDFUNCTION.
