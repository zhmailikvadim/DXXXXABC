FUNCTION SYSTEM_CALLSTACK
  IMPORTING
    MAX_LEVEL TYPE I DEFAULT 0
  EXPORTING
    CALLSTACK TYPE ABAP_CALLSTACK
    ET_CALLSTACK TYPE SYS_CALLST.




  TYPES: BEGIN OF abap_callstack_line_c,
           mainprogram TYPE abap_callstack_line-mainprogram,
           include     TYPE abap_callstack_line-include,
           line        TYPE abap_callstack_line-line,
           blocktype   TYPE abap_callstack_line-blocktype,
           blockname   TYPE abdbg-levent,
           flag_system,
         END   OF abap_callstack_line_c.

  DATA: l_depth             TYPE i,
        l_callstack_c       TYPE TABLE OF abap_callstack_line_c,
        l_callstack_line_c  TYPE          abap_callstack_line_c,
        l_callstack_line    LIKE LINE OF callstack,
        l_et_callstack_line LIKE LINE OF et_callstack.
*
  IF max_level = 0.
    l_depth = 0.
  ELSE.
    l_depth = max_level + 1.
  ENDIF.
  CALL 'ABAP_CALLSTACK' ID 'DEPTH'     FIELD l_depth
                        ID 'CALLSTACK' FIELD l_callstack_c.
  CLEAR callstack.
  LOOP AT l_callstack_c INTO l_callstack_line_c FROM 2.
    IF callstack IS REQUESTED.
      MOVE-CORRESPONDING l_callstack_line_c TO l_callstack_line.
      APPEND l_callstack_line TO callstack.
    ENDIF.
    IF et_callstack IS REQUESTED.
      l_et_callstack_line-progname  = l_callstack_line_c-mainprogram.
      l_et_callstack_line-eventname = l_callstack_line_c-blockname.
      CASE l_callstack_line_c-blocktype.
        WHEN 'FUNCTION'.     l_et_callstack_line-eventtype = 'FUNC'.
        WHEN 'METHOD'.       l_et_callstack_line-eventtype = 'METH'.
        WHEN 'MODULE (PBO)'. l_et_callstack_line-eventtype = 'MODULE(PBO)'.
        WHEN 'MODULE (PAI)'. l_et_callstack_line-eventtype = 'MODULE(PAI)'.
        WHEN OTHERS.
          l_et_callstack_line-eventtype = l_callstack_line_c-blocktype.
      ENDCASE.
      APPEND l_et_callstack_line TO et_callstack.
    ENDIF.
  ENDLOOP.
ENDFUNCTION.
