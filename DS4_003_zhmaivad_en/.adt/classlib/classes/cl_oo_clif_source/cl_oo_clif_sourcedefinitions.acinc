interface lif_source_part_plug deferred.
interface lif_source_part_plugin deferred.

*----------------------------------------------------------------------*
*       INTERFACE lif_source_part
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_source_part.
  constants:
    c_version_active   type r3state value 'A',
    c_version_inactive type r3state value 'I'.
  methods:
    get_source
      returning
        value(source_table) type rswsourcet,
    get_line_index
      returning
        value(line_index) type sedi_lindex,
    read
      importing
        version type r3state default c_version_inactive,
    get_pre_plug
      returning
        value(pre_plug) type ref to lif_source_part_plug,
    get_post_plug
      returning
        value(post_plug) type ref to lif_source_part_plug.
endinterface.                    "lif_source_part

*----------------------------------------------------------------------*
*       INTERFACE lif_source_part_plug DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_source_part_plug.
  types:
    type_plugin_table type standard table of ref to lif_source_part_plugin
                           with non-unique key table_line.
  methods:
    add_plugin
      importing
        plugin type ref to lif_source_part_plugin,
    get_plugins
      returning
        value(plugins) type type_plugin_table,
    get_source_part
      returning
        value(source_part) type ref to lif_source_part.
endinterface.                    "lif_source_part_plug
*----------------------------------------------------------------------*
*       INTERFACE lif_source_part_plugin DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_source_part_plugin.
  interfaces:
    lif_source_part.
  methods:
    get_plug
      returning
        value(plug) type ref to lif_source_part_plug,
    set_plug
      importing
        plug type ref to lif_source_part_plug.
endinterface.                    "lif_source_part_plugin DEFINITION


interface lif_importable_source_part.
  interfaces:
    lif_source_part.
  methods:
    import
      raising
        cx_oo_canceled.
endinterface.
*----------------------------------------------------------------------*
*       INTERFACE lif_source_part_editable
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_editable_source_part.
  interfaces:
    lif_source_part.
  types:
    type_lock type c length 1.
  constants:
    c_lock_unlocked             type type_lock value ' ',
    c_lock_locked               type type_lock value 'X',
    c_lock_unlocked_ephemerally type type_lock value '/'.
  methods:
    is_locked
      returning
        value(locked) type seox_boolean,
    lock
      importing
        suppress_access_permission type seox_boolean default seox_false
        lock_handle                type ref to if_adt_lock_handle optional
        authority_check            type seox_boolean default seox_true
      raising
        cx_oo_access_permission,
    save
      importing
        force type seox_boolean default seox_false
      raising
        cx_oo_source_save_failure
        cx_oo_canceled,
    unlock
      importing
        suppress_access_permission type seox_boolean default seox_false
        allow_relock               type seox_boolean default seox_false
      raising
        cx_oo_access_permission,
    relock
      importing
        suppress_access_permission type seox_boolean default seox_false
        lock_handle                type ref to if_adt_lock_handle optional
        authority_check            type seox_boolean default seox_true
      raising
        cx_oo_access_permission,
    access_permission
      importing
        access_mode     type seok_access_mode default seok_access_show
        lock_handle     type ref to if_adt_lock_handle optional
        authority_check type seox_boolean default seox_true
      raising
        cx_oo_access_permission,
    "! This methods tells the source part whether changes take place in
    "! an modification mode (aka extend mode)
    set_modification_mode_enabled
      importing
        modification_mode_enabled type seox_boolean,
    "! Returns whether the modification mode (aka extend mode) is enabled
    "! for the source part. This depends on system- and software component
    "! settings.
    is_modification_mode_enabled
      returning
        value(result) type seox_boolean,
    insert_into_working_area,
    corr_insert
      raising
        cx_oo_source_save_failure,
    refresh_original_source
      raising
        cx_oo_canceled,
    set_source
      importing
        source type rswsourcet.
  events
    deleted
      exporting
        value(source_part) type ref to lif_editable_source_part.
endinterface.                    "lif_source_part_editable

*----------------------------------------------------------------------*
*       INTERFACE lif_clif_source_part DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_scannable_source_part.
  interfaces:
    lif_source_part.
  methods:
    get_interval
      returning
        value(interval) type cl_source_scanner=>type_source_interval
      raising
        cx_oo_clif_scan_error,
    set_scanner
      importing
        scanner type ref to cl_oo_source_scanner,
    refresh
      raising
        cx_oo_canceled.
endinterface.                    "lif_scannable_source_part



*----------------------------------------------------------------------*
*       INTERFACE lif_persistence_lifecycle DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_persistence_lifecycle.
  types:
    ty_object_list type standard table of dwinactiv with non-unique key object obj_name.

  methods:
    insert_into_working_area
      importing
        object_type type e071-object
        object_name type e071-obj_name,
    access_permission
      importing
        object_type               type trobjtype
        clif_key                  type seoclskey
        method_name               type seocpdname optional
        access_mode               type seok_access_mode
        lock_handle               type ref to if_adt_lock_handle optional
        authority_check           type seox_boolean default seox_true
      exporting
        modification_mode_enabled type seox_boolean
      raising
        cx_oo_access_permission,
    corr_insert
      importing
        object_type       type trobjtype
        clif_key          type seoclskey
        method_name       type seocpdname optional
      changing
        correction_number type trkorr
        package           type devclass
      raising
        cx_oo_source_save_failure,
    get_inactive_objects
      changing
        object_list type ty_object_list.
endinterface.                    "lif_persistence_lifecycle DEFINITION

*----------------------------------------------------------------------*
*       INTERFACE lif_persistence_index DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_persistence_index.
  methods:
    add_method_position
      importing
        method_key      type seomtdkey
        source_position type cl_oo_source_scanner=>type_source_position,
    add_section_position
      importing
        exposure        type seoexpose
        source_position type cl_oo_source_scanner=>type_source_position.
endinterface.                    "lif_persistence_index DEFINITION

interface lif_persistence_modass.
  methods:
    "! Determines whether the whole class or an interface contains any modification
    "! that was made with modification assistant support
    is_clif_modified_with_modass
      importing
        object_type   type trobjtype
        clif_key      type seoclskey
      returning
        value(result) type seox_boolean,
    "! Determines whether modification assistant is enabled
    "! for the interface
    is_modass_enabled_4_interface
      importing
        interface_key type seoclskey
      returning
        value(result) type seox_boolean,
    "! Determines whether modification assistant is enabled
    "! for the whole class definition
    is_modass_enabled_4_class_def
      importing
        class_key     type seoclskey
      returning
        value(result) type seox_boolean,
    "! Determines whether modification assistant is enabled
    "! for a method_implementation
    is_modass_enabled_4_method
      importing
        method_key    type seomtdkey
      returning
        value(result) type seox_boolean,
    "! Checks whether the user has the permission to disable
    "! the modification assistant
    check_modass_permissions
      importing
        object_type   type trobjtype
        clif_key      type seoclskey
      returning
        value(result) type seox_boolean
      raising
        cx_oo_access_permission,
    "! Disables the modification assistant for a whole interface
    disable_modass_4_interface
      importing
        interface_key            type seoclskey
        value(correction_number) type trkorr
        sap_version_interface    type ref to cl_oo_sap_version_of_interface
      raising
        cx_oo_source_save_failure,
    "! Disables the modification assistant for a whole class
    "! definition (i.e., public-, protected- and private section)
    disable_modass_4_class_def
      importing
        class_key                type seoclskey
        value(correction_number) type trkorr
        sap_version_public       type ref to cl_oo_sap_version_of_section
        sap_version_protected    type ref to cl_oo_sap_version_of_section
        sap_version_private      type ref to cl_oo_sap_version_of_section
      raising
        cx_oo_source_save_failure,
    "! Reads the original SAP version for an interface
    read_sap_version_4_interface
      importing
        interface_key         type seoclskey
      exporting
        sap_version_interface type ref to cl_oo_sap_version_of_interface
      raising
        cx_oo_source_save_failure,
    "! Reads the original SAP versions for all sections of a class
    "! definition (i.e., public-, protected- and private section)
    read_sap_versions_4_class_def
      importing
        class_key             type seoclskey
      exporting
        sap_version_public    type ref to cl_oo_sap_version_of_section
        sap_version_protected type ref to cl_oo_sap_version_of_section
        sap_version_private   type ref to cl_oo_sap_version_of_section
      raising
        cx_oo_source_save_failure,
    "! Reads the existent modification information from SMODILOG
    read_modificationinfo_4_method
      importing
        method_key    type seomtdkey
      returning
        value(result) type ref to if_clm_tool_log,
    read_modificationinfo_4_sectn
      importing
        object_type   type trobjtype
        clif_key      type seoclskey
        version       type r3state
       returning
        value(result) type ref to if_clm_tool_log,
    "! Saves the existent modification information from SMODILOG
    save_modificationinfo_4_method
      importing
        method_key          type seomtdkey
        include_name        type progname
        modification_info   type ref to if_clm_tool_log
        correction_number   type trkorr
        smodi_transfer_only type seox_boolean default seox_true
      changing
        source              type rswsourcet optional
      raising
        cx_oo_source_save_failure,
    "! Disables the modification assistant for a method
    "! implementation
    disable_modass_4_method
      importing
        method_key               type seomtdkey
        original_source          type rswsourcet
        value(correction_number) type trkorr
      raising
        cx_oo_source_save_failure.
endinterface.

*----------------------------------------------------------------------*
*       INTERFACE lif_persistence_facade DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_persistence_facade.
  interfaces:
    if_oo_clif_persistence_source,
    if_oo_clif_persist_meta_data,
    lif_persistence_lifecycle,
    lif_persistence_modass.
endinterface.                    "lif_persistence_facade DEFINITION

*----------------------------------------------------------------------*
*       INTERFACE lif_data_provider DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_data_provider.
endinterface.                    "lif_data_provider DEFINITION

class lcl_clif_data_provider_map definition deferred.
*----------------------------------------------------------------------*
*       INTERFACE lif_signature_provider DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
interface lif_signature_provider.
  interfaces:
    lif_data_provider.
  types:
    begin of type_method_signature,
      method     type seoo_method_r,
      parameters type seos_parameters_r,
      exceptions type seos_exceptions_r,
    end of type_method_signature.
  methods:
    get_method_signature
      importing
        method_key              type seomtdkey
      returning
        value(method_signature) type type_method_signature,
    init
      importing
        clif_name         type csequence
        clif_provider_map type ref to lcl_clif_data_provider_map,
    refresh.

endinterface.                    "lif_signature_provider DEFINITION


*----------------------------------------------------------------------*
*       CLASS lcl_source_part_plug DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcl_source_part_plug definition.
  public section.
    interfaces:
      lif_source_part_plug.
    class-methods:
      create_plug
        importing
          source_part type ref to lif_source_part
        returning
          value(plug) type ref to lcl_source_part_plug.
  private section.
    data:
      plugins     type lif_source_part_plug=>type_plugin_table,
      source_part type ref to lif_source_part.
endclass.                    "lcl_source_part_plug DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_abstract_source_part DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcl_abstract_source_part definition abstract.
  public section.
    interfaces:
      lif_source_part
      abstract methods
      get_source
      get_line_index
      read,
      lif_source_part_plugin.
  protected section.
    data:
      plug        type ref to lif_source_part_plug,
      pre_plug    type ref to lcl_source_part_plug,
      post_plug   type ref to lcl_source_part_plug,
      persistence type ref to lif_persistence_facade,
      settings    type ref to if_oo_clif_source_settings.
    methods:
      init
        importing
          persistence type ref to lif_persistence_facade
          settings    type ref to if_oo_clif_source_settings.
endclass.                    "lcl_abstract_source_part DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_source_part DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcl_source_part definition inheriting from lcl_abstract_source_part.
  public section.
    class-methods:
      create_from_source
        importing
          source             type rswsourcet
          persistence        type ref to lif_persistence_facade
          settings           type ref to if_oo_clif_source_settings
        returning
          value(source_part) type ref to lcl_source_part.
    methods:
      lif_source_part~get_source     redefinition,
      lif_source_part~get_line_index redefinition,
      lif_source_part~read           redefinition.
  protected section.
    data:
      source type rswsourcet.
endclass.                    "lcl_source_part DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcx_source_part DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcx_source_part definition inheriting from cx_static_check.
endclass.                    "lcx_source_part DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcx_unknown_source_part DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcx_source_part_unknown definition inheriting from lcx_source_part.
endclass.                    "lcx_unknown_source_part DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_source_part_list DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcl_source_part_list definition inheriting from lcl_abstract_source_part abstract.
  public section.
    methods:
      lif_source_part~get_source     redefinition,
      lif_source_part~get_line_index redefinition,
      lif_source_part~read           redefinition.
  protected section.
    types:
      begin of type_source_part,
        id          type i,
        source_part type ref to lif_source_part,
        order       type i,
      end of type_source_part.
    types:
      type_source_part_list type sorted table of type_source_part
                                 with unique key id
                                 with non-unique sorted key order components order.
    methods:
      add_source_part
        importing
          source_part type ref to lif_source_part,
      get_source_part
        importing
          id                 type i
        returning
          value(source_part) type ref to lif_source_part
        raising
          lcx_source_part_unknown,
      get_source_part_list
        returning
          value(source_part_list) type type_source_part_list,
      init_source_part_list abstract,
      remove_source_part
        importing
          source_part type ref to lif_source_part,
      get_id_by_source_part
        importing
          source_part type ref to lif_source_part
        returning
          value(id)   type i
        raising
          lcx_source_part_unknown,
      handle_source_part_deleted
            for event deleted of lif_editable_source_part
        importing
            source_part,
      set_source_part_order
        importing
          id    type i
          order type i,
      reset_source_part_list.
  private section.
    methods:
      get_free_source_part_entry_id
        returning
          value(id) type i.
    data:
      source_part_list       type type_source_part_list,
      source_parts_initilzed type seox_boolean value seox_false.
endclass.                    "lcl_source_part_list DEFINITION

class lcl_clif_source_part_list definition deferred.