"! SALV OM Consumer class for a tree-like data display.
class CL_SALV_TREE definition
  public
  inheriting from CL_SALV_MODEL_BASE
  final
  create private

  global friends CL_SALV_ADAPTER
                 CL_SALV_CONTROLLER .

*"* public components of class CL_SALV_TREE
*"* do not include other source files here!!!
public section.

  interfaces if_salv_gui_om_model_info .
  methods if_salv_gui_om_display_object~get_display_object_type REDEFINITION.

  methods GET_METADATA .
  methods CONSTRUCTOR
    importing
      !R_CONTAINER type ref to CL_GUI_CONTAINER optional
      !HIDE_HEADER type SAP_BOOL optional
    preferred parameter R_CONTAINER .
  methods DISPLAY .
  class-methods FACTORY
    importing
      !R_CONTAINER type ref to CL_GUI_CONTAINER optional
      !HIDE_HEADER type SAP_BOOL optional
    exporting
      !R_SALV_TREE type ref to CL_SALV_TREE
    changing
      !T_TABLE type TABLE
    raising
      CX_SALV_ERROR .
  methods GET_AGGREGATIONS
    returning
      value(VALUE) type ref to CL_SALV_AGGREGATIONS .
  methods GET_COLUMNS
    returning
      value(VALUE) type ref to CL_SALV_COLUMNS_TREE .
  methods GET_EVENT
    returning
      value(VALUE) type ref to CL_SALV_EVENTS_TREE .
  methods GET_FUNCTIONS
    returning
      value(VALUE) type ref to CL_SALV_FUNCTIONS_TREE .
  methods GET_LAYOUT
    returning
      value(VALUE) type ref to CL_SALV_LAYOUT .
  methods GET_NODES
    returning
      value(VALUE) type ref to CL_SALV_NODES .
  methods GET_SELECTIONS
    returning
      value(VALUE) type ref to CL_SALV_SELECTIONS_TREE .
  methods GET_TREE_SETTINGS
    returning
      value(VALUE) type ref to CL_SALV_TREE_SETTINGS .
  methods SET_DATA
    changing
      !T_TABLE type STANDARD TABLE
    raising
      CX_SALV_NO_NEW_DATA_ALLOWED
      CX_SALV_ERROR .

  methods SET_SCREEN_STATUS
    redefinition .
protected section.
*"* protected components of class CL_SALV_TREE
*"* do not include other source files here!!!
private section.
*"* private components of class CL_SALV_TREE
*"* do not include other source files here!!!

  data HIDE_HEADER type SAP_BOOL .
  data R_LAYOUT type ref to CL_SALV_LAYOUT .
  data R_TREE_SETTINGS type ref to CL_SALV_TREE_SETTINGS .
  data R_AGGREGATIONS type ref to CL_SALV_AGGREGATIONS .
  data R_COLUMNS type ref to CL_SALV_COLUMNS_TREE .
  data R_CONTAINER type ref to CL_GUI_CONTAINER .
  data R_EVENTS type ref to CL_SALV_EVENTS .
  data R_NODES type ref to CL_SALV_NODES .
  data R_SELECTIONS type ref to CL_SALV_SELECTIONS_TREE .
  data R_TABLE type ref to DATA .
ENDCLASS.



CLASS CL_SALV_TREE IMPLEMENTATION.


method CONSTRUCTOR .

  super->constructor( ).

  me->r_container = r_container.

  me->hide_header = hide_header.

  create object me->r_controller type cl_salv_controller_tree
    exporting
      r_model = me.

  me->r_controller->if_salv_controller_metadata~register(
    name = 'CL_SALV_TREE' ).

  if cl_salv_gui_om_table_info=>is_cwf_changelog_supported( ).
    data(lts_registered_changes) = cl_salv_gui_grid_controler_ida=>ts_registered_changes.
    insert value #( name = cl_salv_model_base=>cs_registered_changes-soft_refresh
                   category = 'SALV_OM' ) into table lts_registered_changes.
    insert value #( name = cl_salv_model_base=>cs_registered_changes-full_refresh
                   category = 'SALV_OM' ) into table lts_registered_changes.
    insert value #( name = cl_salv_model_base=>cs_registered_changes-close_screen
                   category = 'SALV_OM' ) into table lts_registered_changes.
    me->o_fwk_changelog = cl_salv_gui_grid_fwk_changelog=>create( lts_registered_changes ).
  endif.

  me->model = if_salv_c_model=>tree.

  me->screen_status_pfstatus = 'STANDARD_TREE'.
  me->screen_status_report = 'SAPLSLVC_FULLSCREEN'.

  get_selections( ).
  get_functions( ).
  get_event( ).
  get_nodes( ).
  get_tree_settings( ).
  get_layout( ).
*
  create object me->r_header
    exporting
      r_controller = me->r_controller.

  create object me->r_footer
    exporting
      r_controller = me->r_controller.

endmethod.


method DISPLAY.

  check me->r_controller is bound.

  me->r_controller->display( ).

endmethod.


method FACTORY .

  data: r_table type ref to data.
  get reference of t_table into r_table.

  create object r_salv_tree
      exporting r_container = r_container
                hide_header = hide_header.

  try.
    call method r_salv_tree->set_data
        changing t_table = t_table.
  catch CX_SALV_NO_NEW_DATA_ALLOWED CX_SALV_ERROR.
    raise exception type CX_SALV_ERROR.
  endtry.

endmethod.


method GET_AGGREGATIONS .

  if me->r_aggregations is not bound.
    get_columns( ).
    create object me->r_aggregations
      exporting
        r_columns       = me->r_columns
        r_controller    = me->r_controller.
  endif.

  value = me->r_aggregations.

endmethod.


method GET_COLUMNS .

  if me->r_columns is not bound.
    create object me->r_columns
      exporting
        r_data       = me->r_table
        r_controller = me->r_controller.
    me->r_columns->create_columns( r_table ).
  endif.

  value = me->r_columns.

endmethod.


method GET_EVENT .

  if me->r_events is not bound.

    me->r_events = cl_salv_controller_events=>factory( cl_salv_controller_events=>c_type_tree ).

    data: lr_adapter type ref to cl_salv_tree_adapter_base.
    lr_adapter ?= me->r_controller->r_adapter.
    value ?= me->r_events.
    value->set_adapter( lr_adapter ).
    exit.

  endif.

  value ?= me->r_events.

endmethod.


method GET_FUNCTIONS.

  if me->r_functions is not bound.
    value = cl_salv_gui_om_metdata_factory=>get_functions_tree(
                        o_controller      = me->r_controller
                        use_fwk_changelog = abap_false
                        o_display_object  = me
                      ).
    me->r_functions ?= value.
  endif.
  value ?= me->r_functions.

endmethod.


method GET_LAYOUT.

  if r_layout is not bound.
    create object r_layout
      exporting
        r_controller = me->r_controller.
  endif.

  value = r_layout.

endmethod.


method GET_METADATA .

  if me->r_controller->r_adapter->function_call_active eq abap_false.
    me->r_controller->r_adapter->get_metadata( ).
  endif.

endmethod.


method GET_NODES.

  if me->r_nodes is not bound.
    create object me->r_nodes
      exporting
        r_controller = me->r_controller.
  endif.

  value = r_nodes.

endmethod.


method GET_SELECTIONS .

  if me->r_selections is not bound.
    create object me->r_selections
      exporting
        r_controller = me->r_controller.
  endif.

  value = me->r_selections.

endmethod.


method GET_TREE_SETTINGS .

  if me->r_tree_settings is not bound.
    create object me->r_tree_settings
      exporting
        r_controller = me->r_controller.
  endif.

  value = me->r_tree_settings.

endmethod.


method if_salv_gui_om_display_object~get_display_object_type.

  me->display_object = IF_SALV_C_TABLE_OBJECTS=>TREE.
  display_object_type = me->display_object.

endmethod.


  METHOD if_salv_gui_om_model_info~model_type.
    model_type = model.
  ENDMETHOD.


method SET_DATA .

  if me->r_controller->is_new_data_allowed( ) eq if_salv_c_bool_sap=>false.
    raise exception type cx_salv_no_new_data_allowed.
  endif.

  if not t_table is initial.
    raise exception type cx_salv_error.
  endif.

  get reference of t_table into me->r_table.

  me->get_columns( ).

  me->get_aggregations( ).
  me->r_aggregations->clear( ).

  check me->r_controller is bound.

  me->r_controller->set_changed(
    name         = 'CL_SALV_TABLE'
    method       = 'SET_DATA'
    flavour      = if_salv_c_changelist_flavour=>data_set ).

  data: tree_controller type ref to cl_salv_controller_tree.
  tree_controller ?= me->r_controller.
  create data tree_controller->data_row like line of t_table.

  data: lr_adapter type ref to cl_salv_tree_adapter_base.

  lr_adapter ?= me->r_controller->r_adapter.

  lr_adapter->set_data( ).

endmethod.                    "SET_DATA


method SET_SCREEN_STATUS.
  call method super->set_screen_status( report = report   "Y6DK041343
                                        pfstatus = pfstatus
                                        set_functions = set_functions ).

endmethod.
ENDCLASS.