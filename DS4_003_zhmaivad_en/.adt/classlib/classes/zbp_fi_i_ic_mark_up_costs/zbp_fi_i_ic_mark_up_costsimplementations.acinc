************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
* BOBRULA      | 10.03.2023 | 18926: IC Mark-up costs maintenance App  *
*              |            |        - Select All workaround           *
*              |            | DS4K946491                               *
*----------------------------------------------------------------------*
* BOBRULA      | 04.04.2023 | 19591: [UPGRADE] [MB] General issues     *
*              |            | DS4K949041                               *
*----------------------------------------------------------------------*
* BOBRULA      | 25.05.2023 | 21186: IC Mark-up costs maintenance App  *
*              |            |        - Select All workaround           *
*              |            | DS4K952055                               *
*----------------------------------------------------------------------*
* BOBRULA      | 19.03.2024 | 30234: Application job creation for      *
*              |            |        IC Mark Up daily job run          *
*              |            | DS4K984140                               *
*----------------------------------------------------------------------*
CLASS lcl_buffer DEFINITION CREATE PRIVATE FINAL.

  PUBLIC SECTION.

    CLASS-METHODS get_instance
      RETURNING VALUE(ro_instance) TYPE REF TO lcl_buffer.

    METHODS set_buffer_data
      IMPORTING it_buffer_data TYPE zfi_tt_posted_mark_up_value.

    METHODS get_buffer_data
      RETURNING
        VALUE(rt_buffer_data) TYPE zfi_tt_posted_mark_up_value.

  PRIVATE SECTION.

    CLASS-DATA: go_instance TYPE REF TO lcl_buffer.

    DATA: mt_buffer_data TYPE zfi_tt_posted_mark_up_value.

ENDCLASS.

CLASS lcl_buffer IMPLEMENTATION.

  METHOD get_instance.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 13.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    IF go_instance IS NOT BOUND.
      go_instance = NEW #( ).
    ENDIF.

    ro_instance = go_instance.

  ENDMETHOD.

  METHOD set_buffer_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 13.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
     mt_buffer_data = it_buffer_data.
  ENDMETHOD.

  METHOD get_buffer_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 13.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    rt_buffer_data = mt_buffer_data.
  ENDMETHOD.

ENDCLASS.


CLASS lhc_InterCompanyMarkUpCostsCal DEFINITION INHERITING FROM cl_abap_behavior_handler FINAL.
  PRIVATE SECTION.

    METHODS get_instance_authorizations FOR INSTANCE AUTHORIZATION
      IMPORTING keys REQUEST requested_authorizations FOR MarkUpCosts RESULT result ##NEEDED.

    METHODS read FOR READ
      IMPORTING keys FOR READ MarkUpCosts RESULT result.

    METHODS lock FOR LOCK
      IMPORTING keys FOR LOCK MarkUpCosts ##NEEDED.

    METHODS PostCosts FOR MODIFY
      IMPORTING keys FOR ACTION MarkUpCosts~PostCosts.

    METHODS PostAllCosts FOR MODIFY
      IMPORTING keys FOR ACTION MarkUpCosts~PostAllCosts.

ENDCLASS.

CLASS lhc_InterCompanyMarkUpCostsCal IMPLEMENTATION.

  METHOD get_instance_authorizations.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.

  METHOD read.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
* BOBRULA      | 04.04.2023 | 19591: [UPGRADE] [MB] General issues     *
*              |            | DS4K949041                               *
*----------------------------------------------------------------------*
    SELECT *
      FROM ZFI_I_IC_Mark_Up_Costs as MarkUpCosts
      INNER JOIN @keys            As keys
              ON keys~CostingVariant = MarkUpCosts~CostingVariant
             AND keys~Article        = MarkUpCosts~Article
             AND keys~Plant          = MarkUpCosts~Plant
             AND keys~CostingVersion = MarkUpCosts~CostingVersion
             AND keys~ValidFrom      = MarkUpCosts~ValidFrom
             AND keys~ValidTo        = MarkUpCosts~ValidTo
      INTO CORRESPONDING FIELDS OF TABLE @result.

    IF sy-subrc > 0.
      CLEAR result.
    ENDIF.

  ENDMETHOD.

  METHOD lock.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.

  METHOD PostCosts.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    READ ENTITIES OF ZFI_I_IC_Mark_Up_Costs IN LOCAL MODE
      ENTITY MarkUpCosts
        ALL FIELDS
        WITH CORRESPONDING #( keys )
      RESULT DATA(lt_MarkUp_Cost).

    lcl_buffer=>get_instance(
      )->set_buffer_data(
        CORRESPONDING zfi_tt_posted_mark_up_value(
          lt_MarkUp_Cost
            MAPPING
              klvar  = CostingVariant
              matnr  = Article
              werks  = Plant
              losgr  = CostingLotSize
              tvers  = CostingVersion
              kadat  = ValidFrom
              bidat  = ValidTo
              meeht  = ValuationQuantityUnit
              kst001 = StandardFOB
              kst003 = BuyingCommission
              kst005 = StandardFreight
              kst007 = StandardCustoms
              kst009 = MarkUpValue ) ).

  ENDMETHOD.

  METHOD PostAllCosts.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 10.03.2023 | 18926: IC Mark-up costs maintenance App  *
*              |            |        - Select All workaround           *
*              |            | DS4K946491                               *
*----------------------------------------------------------------------*
* BOBRULA      | 19.03.2024 | 30234: Application job creation for      *
*              |            |        IC Mark Up daily job run          *
*              |            | DS4K984140                               *
*----------------------------------------------------------------------*

    DATA(ls_filters_fe) = VALUE zcl_fi_ic_mark_up_costs_filter=>gtyp_s_filters_fe(  ).

    /ui2/cl_json=>deserialize(
      EXPORTING
        json        = TO_UPPER(  VALUE #( keys[ 1 ]-%param-filters OPTIONAL ) )
        pretty_name = /ui2/cl_json=>pretty_mode-camel_case
      CHANGING
        data = ls_filters_fe ).

    TRY.
        DATA(ls_filters_be) = NEW zcl_fi_ic_mark_up_costs_filter(
          )->get_filters_be( ls_filters_fe ).
      CATCH zcx_fi_ic_mark_up_costs into DATA(lo_exception).
        lo_exception->if_abap_behv_message~m_severity = if_abap_behv_message=>severity-error.
        reported-markupcosts = VALUE #(
            BASE reported-markupcosts
          ( VALUE #( %msg =  lo_exception ) ) ).
        RETURN.
      CATCH zcx_pe_fe2be_conversor ##NO_HANDLER.
    ENDTRY.

    DATA(lt_MarkUp_Cost) = VALUE zfi_tt_posted_mark_up_value( ).
    SELECT
        CostingVariant        as klvar,
        Article               as matnr,
        Plant                 as werks,
        CostingLotSize        as losgr,
        CostingVersion        as tvers,
        ValidFrom             as kadat ,
        ValidTo               as bidat,
        ValuationQuantityUnit as meeht,
        StandardFOB           as kst001,
        BuyingCommission      as kst003,
        StandardFreight       as kst005,
        StandardCustoms       as kst007,
        MarkUpValue           as kst009
      FROM ZFI_C_IC_Mark_Up_Costs as MarkUpCosts
      WHERE ControllingArea          IN  @ls_filters_be-ControllingArea
        AND CostingVariant           IN  @ls_filters_be-CostingVariant
        AND CompanyCode              IN  @ls_filters_be-CompanyCode
        AND Plant                    IN  @ls_filters_be-Plant
        AND CostingDate              IN  @ls_filters_be-CostingDate
        AND UPPER( Status )          IN  @ls_filters_be-Status
        AND Article                  IN  @ls_filters_be-Article
        AND BuyingCommission         IN  @ls_filters_be-BuyingCommission
        AND Currency                 IN  @ls_filters_be-Currency
        AND ExistingMarkUpPercentage IN  @ls_filters_be-ExistingMarkUpPercentage
        AND ExistingMarkUpValue      IN  @ls_filters_be-ExistingMarkUpValue
        AND MarkUpPercentage         IN  @ls_filters_be-MarkUpPercentage
        AND MarkUpValue              IN  @ls_filters_be-MarkUpValue
        AND StandardCustoms          IN  @ls_filters_be-StandardCustoms
        AND StandardFOB              IN  @ls_filters_be-StandardFOB
        AND StandardFreight          IN  @ls_filters_be-StandardFreight
        AND ValidFrom                IN  @ls_filters_be-ValidFrom
      INTO  TABLE @lt_MarkUp_Cost.

    IF sy-subrc > 0.
      RETURN.
    ENDIF.

    lcl_buffer=>get_instance( )->set_buffer_data( lt_MarkUp_Cost ).

  ENDMETHOD.

ENDCLASS.

CLASS lsc_ZFI_I_IC_MARK_UP_COSTS DEFINITION INHERITING FROM cl_abap_behavior_saver FINAL.
  PROTECTED SECTION.

    METHODS finalize REDEFINITION.

    METHODS check_before_save REDEFINITION.

    METHODS save REDEFINITION.

    METHODS cleanup REDEFINITION.

    METHODS cleanup_finalize REDEFINITION.

ENDCLASS.

CLASS lsc_ZFI_I_IC_MARK_UP_COSTS IMPLEMENTATION.

  METHOD finalize.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.

  METHOD check_before_save.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.

  METHOD save.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    CONSTANTS:
      lc_destination_name TYPE bgrfc_dest_name_inbound VALUE 'IC_MARK_UP_VALUE'.

    DATA(lt_posted_mark_up_value) = lcl_buffer=>get_instance( )->get_buffer_data( ).

    IF lt_posted_mark_up_value IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lif_my_unit) = CAST if_trfc_unit_inbound(
      cl_bgrfc_destination_inbound=>create( lc_destination_name
        )->create_trfc_unit( ) ).

    lif_my_unit->if_bgrfc_unit~disable_commit_checks( ).

    CALL FUNCTION 'ZFI_FM_MARK_UP_VALUE_UPD' IN BACKGROUND UNIT lif_my_unit
      TABLES ct_posted_mark_up_value = lt_posted_mark_up_value.

  ENDMETHOD.

  METHOD cleanup.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.

  METHOD cleanup_finalize.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.01.2023 | 16604: IC Mark-up costs maintenance      *
*              |            |        program                           *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.

ENDCLASS.