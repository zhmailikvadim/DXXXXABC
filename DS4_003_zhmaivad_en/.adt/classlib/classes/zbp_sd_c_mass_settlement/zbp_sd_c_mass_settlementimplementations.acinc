************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
CLASS lhc_MassSettlement DEFINITION INHERITING FROM cl_abap_behavior_handler.
  PRIVATE SECTION.

    METHODS get_instance_authorizations FOR INSTANCE AUTHORIZATION
      IMPORTING keys REQUEST requested_authorizations FOR MassSettlement RESULT result.

    METHODS read FOR READ
      IMPORTING keys FOR READ MassSettlement RESULT result.

    METHODS lock FOR LOCK
      IMPORTING keys FOR LOCK MassSettlement.
    METHODS PostCondContracts FOR READ
      IMPORTING keys FOR FUNCTION MassSettlement~PostCondContracts RESULT result.
    METHODS CheckCondContracts FOR READ
      IMPORTING keys FOR FUNCTION MassSettlement~CheckCondContracts RESULT result.
    METHODS CheckAllCondContracts FOR READ
      IMPORTING keys FOR FUNCTION MassSettlement~CheckAllCondContracts RESULT result.
    METHODS PostAllCondContracts FOR READ
      IMPORTING keys FOR FUNCTION MassSettlement~PostAllCondContracts RESULT result.

ENDCLASS.

CLASS lhc_MassSettlement IMPLEMENTATION.

  METHOD get_instance_authorizations.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
    " Dummy statement to avoid ATC check errors on non-used variables.
    IF keys                     IS NOT INITIAL OR
       result                   IS INITIAL     OR
       requested_authorizations IS NOT INITIAL.
      RETURN.
    ENDIF.
  ENDMETHOD.

  METHOD read.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
    " Dummy statement to avoid ATC check errors on non-used variables.
    IF keys   IS NOT INITIAL OR
       result IS INITIAL.
      RETURN.
    ENDIF.
  ENDMETHOD.

  METHOD lock.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
    " Dummy statement to avoid ATC check errors on non-used variables.
    IF keys IS NOT INITIAL.
      RETURN.
    ENDIF.
  ENDMETHOD.

  METHOD CheckAllCondContracts.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 24.11.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*

    DATA(ls_key) = VALUE #( keys[ 1 ] OPTIONAL ).

    DATA(lo_mass_settlement) = NEW zcl_sd_mass_settlement( ).

    TRY.
       lo_mass_settlement->set_be_filter( ls_key-%param-filters ).

      CATCH zcx_sd_mass_settlement into DATA(lo_exception).

        lo_exception->if_abap_behv_message~m_severity = if_abap_behv_message=>severity-error.

        failed-masssettlement   = VALUE #( BASE failed-masssettlement
                                              ( VALUE #( %tky = ls_key-%cid ) ) ).
        reported-masssettlement = VALUE #( BASE reported-masssettlement
                                              ( VALUE #( %tky = ls_key-%cid
                                                         %msg = lo_exception ) ) ).
    ENDTRY.

    IF failed-masssettlement IS NOT INITIAL.
      RETURN.
    ENDIF.

    lo_mass_settlement->get_settlement_wo_business_vol(
      IMPORTING
        et_settl_wo_bus_vol = DATA(lt_mass_settlement) ).

    READ ENTITIES OF ZSD_C_Mass_Settlement IN LOCAL MODE
      ENTITY MassSettlement
      EXECUTE CheckCondContracts
      FROM CORRESPONDING #( lt_mass_settlement )

      RESULT DATA(lt_result)
      FAILED DATA(ls_failed).

    result = VALUE #( FOR <ls_result> IN lt_result
                        ( %cid   = <ls_result>-ConditionContract
                          %param = <ls_result>-%param ) ).

    IF ls_failed-masssettlement IS NOT INITIAL.

      lo_exception = NEW zcx_sd_mass_settlement(
                           not_posted_lines = LINES( ls_failed-masssettlement )
                           textid           = zcx_sd_mass_settlement=>not_posted_lines
                           severity         = if_abap_behv_message=>severity-information ).

      result = VALUE #( BASE result
                           ( %param-severity   = lo_exception->if_abap_behv_message~m_severity ##TYPE
                             %param-text       = lo_exception->get_text( ) ) ).
    ENDIF.

  ENDMETHOD.

  METHOD PostAllCondContracts.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 14.11.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*

    DATA(ls_key) = VALUE #( keys[ 1 ] OPTIONAL ).

    DATA(lo_mass_settlement) = NEW zcl_sd_mass_settlement( ).

    TRY.
       lo_mass_settlement->set_be_filter( ls_key-%param-filters ).

      CATCH zcx_sd_mass_settlement into DATA(lo_exception).

        lo_exception->if_abap_behv_message~m_severity = if_abap_behv_message=>severity-error.

        failed-masssettlement   = VALUE #( BASE failed-masssettlement
                                              ( VALUE #( %tky = ls_key-%cid ) ) ).
        reported-masssettlement = VALUE #( BASE reported-masssettlement
                                              ( VALUE #( %tky = ls_key-%cid
                                                         %msg = lo_exception ) ) ).
    ENDTRY.

    IF failed-masssettlement IS NOT INITIAL.
      RETURN.
    ENDIF.

    lo_mass_settlement->get_settlement_wo_business_vol(
      IMPORTING
        et_settl_wo_bus_vol = DATA(lt_mass_settlement) ).

    READ ENTITIES OF ZSD_C_Mass_Settlement IN LOCAL MODE
      ENTITY MassSettlement
      EXECUTE PostCondContracts
      FROM VALUE #( FOR <ls_mass_settlement> IN lt_mass_settlement
                    LET lv_BusinessVolume2Date = lo_mass_settlement->get_be_filter( )-business_volume_to_date IN
                      ( ConditionContract   = <ls_mass_settlement>-conditioncontract
                        StatusID            = <ls_mass_settlement>-statusid
                        BusinessVolume2Date = lv_BusinessVolume2Date
                        %param              = VALUE #( SettlementDate = ls_key-%param-SettlementDate(4) &&
                                                                        ls_key-%param-SettlementDate+5(2) &&
                                                                        ls_key-%param-SettlementDate+8(2)
                                                       SettlementType = ls_key-%param-SettlementType ) ) )
      RESULT   DATA(lt_result)
      FAILED   DATA(ls_failed)
      REPORTED DATA(ls_reported).

    result = VALUE #( FOR <ls_result> IN lt_result
                        ( %cid   = <ls_result>-ConditionContract
                          %param = <ls_result>-%param ) ).

    failed-masssettlement   = VALUE #( BASE failed-masssettlement
                                          ( LINES OF ls_failed-masssettlement ) ).
    reported-masssettlement = VALUE #( BASE reported-masssettlement
                                          ( LINES OF ls_reported-masssettlement ) ).

  ENDMETHOD.

  METHOD PostCondContracts.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 30.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*

    READ ENTITIES OF ZSD_C_Mass_Settlement IN LOCAL MODE
      ENTITY MassSettlement
      EXECUTE CheckCondContracts FROM CORRESPONDING #( keys )

      RESULT   DATA(lt_result)
      FAILED   DATA(ls_failed)
      REPORTED DATA(ls_reported).

    result = VALUE #( FOR <ls_result> IN lt_result
                        ( ConditionContract = <ls_result>-ConditionContract
                          %param-severity   = <ls_result>-%param-severity
                          %param-text       = <ls_result>-%param-text ) ).

    IF ls_failed-masssettlement IS NOT INITIAL.

      DATA(lo_exception) = NEW zcx_sd_mass_settlement(
                                 not_posted_lines = LINES( ls_failed-masssettlement )
                                 textid           = zcx_sd_mass_settlement=>not_posted_lines
                                 severity         = if_abap_behv_message=>severity-information ).

      result = VALUE #( BASE result
                           ( %param-severity   = lo_exception->if_abap_behv_message~m_severity ##TYPE
                             %param-text       = lo_exception->get_text( ) ) ).
    ENDIF.

    DATA(lt_cond_contract_post) = VALUE wcb_coco_num_tab( ).
    LOOP AT keys ASSIGNING FIELD-SYMBOL(<ls_key>).

      CHECK xsdbool( line_exists(
            ls_failed-masssettlement[ key ID
                                      ConditionContract = <ls_key>-ConditionContract ] ) ) = abap_false.

      lt_cond_contract_post = VALUE wcb_coco_num_tab(
                                BASE lt_cond_contract_post
                                   ( <ls_key>-ConditionContract ) ).
    ENDLOOP.

    IF lt_cond_contract_post IS INITIAL.

      lo_exception = NEW zcx_sd_mass_settlement(
                           condition_contract = <ls_key>-ConditionContract
                           textid             = zcx_sd_mass_settlement=>no_lines_for_posting
                           severity           = if_abap_behv_message=>severity-error ).

      failed-masssettlement   = VALUE #( BASE failed-masssettlement
                                            ( %tky = space ) ).
      reported-masssettlement = VALUE #( BASE reported-masssettlement
                                            ( %tky = space
                                              %msg = lo_exception ) ).

      RETURN.
    ENDIF.

    DATA(ls_key) = VALUE #( keys[ 1 ] OPTIONAL ).
    CALL FUNCTION 'ZSD_FM_MASS_SETTLEMENT_POST' DESTINATION 'NONE'
      EXPORTING
        it_cond_contract        = lt_cond_contract_post
        iv_settlement_type      = ls_key-%param-SettlementType
        iv_settlement_date      = COND #(
                                    WHEN ls_key-%param-SettlementDate IS INITIAL THEN
                                         sy-datum
                                    ELSE ls_key-%param-SettlementDate )
        iv_business_vol_to_date = ls_key-BusinessVolume2Date
      EXCEPTIONS
        system_failure        = 1
        communication_failure = 2
        OTHERS                = 3.

    IF sy-subrc > 0.

      lo_exception = NEW zcx_sd_mass_settlement(
                           condition_contract = <ls_key>-ConditionContract
                           textid             = zcx_sd_mass_settlement=>posting_failure
                           severity           = if_abap_behv_message=>severity-error ).

      failed-masssettlement   = VALUE #( BASE failed-masssettlement
                                            ( %tky = <ls_key>-ConditionContract ) ).
      reported-masssettlement = VALUE #( BASE reported-masssettlement
                                            ( %tky = <ls_key>-ConditionContract
                                              %msg = lo_exception ) ).
    ELSE.

      lo_exception = NEW zcx_sd_mass_settlement(
                           textid   = zcx_sd_mass_settlement=>posted
                           severity = if_abap_behv_message=>severity-information ).

      reported-masssettlement = VALUE #( BASE reported-masssettlement
                                              ( %msg = lo_exception ) ).
    ENDIF.

  ENDMETHOD.

  METHOD CheckCondContracts.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 30.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*

   DATA:
     lo_exception TYPE REF TO zcx_sd_mass_settlement.

    LOOP AT keys ASSIGNING FIELD-SYMBOL(<ls_key>).

      FREE lo_exception.
      CASE <ls_key>-statusID.

        WHEN '3'.
          lo_exception = NEW zcx_sd_mass_settlement(
                               condition_contract = <ls_key>-ConditionContract
                               textid             = zcx_sd_mass_settlement=>negative_or_zero_value
                               severity           = if_abap_behv_message=>severity-error ).

          failed-masssettlement   = VALUE #( BASE failed-masssettlement
                                                ( %tky = <ls_key>-ConditionContract  ) ).
          reported-masssettlement = VALUE #( BASE reported-masssettlement
                                                ( %tky = <ls_key>-ConditionContract
                                                  %msg = lo_exception  ) ).
      ENDCASE.

      IF lo_exception IS NOT BOUND.
        CONTINUE.
      ENDIF.

      result = VALUE #( BASE result
                           ( ConditionContract = <ls_key>-ConditionContract
                             %param-severity   = lo_exception->if_abap_behv_message~m_severity ##TYPE
                             %param-text       = lo_exception->get_text( ) ) ).
    ENDLOOP.

  ENDMETHOD.

ENDCLASS.

CLASS lsc_ZSD_C_MASS_SETTLEMENT DEFINITION INHERITING FROM cl_abap_behavior_saver.
  PROTECTED SECTION.

    METHODS finalize REDEFINITION.

    METHODS check_before_save REDEFINITION.

    METHODS save REDEFINITION.

    METHODS cleanup REDEFINITION.

    METHODS cleanup_finalize REDEFINITION.

ENDCLASS.

CLASS lsc_ZSD_C_MASS_SETTLEMENT IMPLEMENTATION.

  METHOD finalize.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
  RETURN.
  ENDMETHOD.

  METHOD check_before_save.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
  RETURN.
  ENDMETHOD.

  METHOD save.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
  RETURN.
  ENDMETHOD.

  METHOD cleanup.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
  RETURN.
  ENDMETHOD.

  METHOD cleanup_finalize.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 27.10.2023 | 26154: Multiple Settlement Workbench –   *
*              |            |        Mass process of contracts         *
*              |            | DS4K967807                               *
*----------------------------------------------------------------------*
  RETURN.
  ENDMETHOD.

ENDCLASS.