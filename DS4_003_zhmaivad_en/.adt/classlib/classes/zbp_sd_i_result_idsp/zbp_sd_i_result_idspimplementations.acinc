CLASS lcl_ResultIDSP DEFINITION INHERITING FROM cl_abap_behavior_handler FINAL.
  PRIVATE SECTION.

    METHODS get_instance_authorizations FOR INSTANCE AUTHORIZATION
      IMPORTING it_keys
                  REQUEST   it_requested_authorizations ##NEEDED
                  FOR ResultIDSP
      RESULT    ct_result ##NEEDED.

    METHODS set_run_id FOR DETERMINE ON SAVE
      IMPORTING it_keys
                  FOR ResultIDSP~setRunId.

    METHODS get_instance_features FOR INSTANCE FEATURES
      IMPORTING it_keys
                  REQUEST   it_requested_features ##NEEDED
                  FOR ResultIDSP
      RESULT    ct_result.

ENDCLASS.

CLASS lcl_ResultIDSP IMPLEMENTATION.

  METHOD get_instance_authorizations ##NEEDED.
  ENDMETHOD.

  METHOD set_run_id.
    READ ENTITIES OF zsd_i_result_idsp IN LOCAL MODE
    ENTITY ResultIDSP
    FIELDS ( RunId ) WITH CORRESPONDING #( it_keys )
    RESULT DATA(lt_result_idsp).

    DELETE lt_result_idsp WHERE RunId IS NOT INITIAL.    "#EC CI_STDSEQ

    IF lt_result_idsp IS INITIAL.
      RETURN.
    ENDIF.

    TRY.
        cl_numberrange_runtime=>number_get(
            EXPORTING nr_range_nr = CONV #( zcl_pe_hm_basic=>get_value( iv_id = '318' ) )
                      object      = CONV #( zcl_pe_hm_basic=>get_value( iv_id = '317' ) )
            IMPORTING number      = DATA(lv_number) ).

      CATCH cx_nr_object_not_found
            cx_number_ranges
            zcx_pe_hm.
        RETURN.
    ENDTRY.
    DATA(lv_next_run_id) =  |{ lv_number ALPHA = OUT }|.

    MODIFY ENTITIES OF zsd_i_result_idsp IN LOCAL MODE
    ENTITY ResultIDSP
      UPDATE
        FROM VALUE #( FOR ls_result_idsp IN lt_result_idsp INDEX INTO lv_i (
          %tky              = ls_result_idsp-%tky
          RunId             = lv_next_run_id + lv_i - 1
          %control-RunId = if_abap_behv=>mk-on ) )
    REPORTED DATA(ls_update_reported).

    reported = CORRESPONDING #( DEEP ls_update_reported ).
  ENDMETHOD.

  METHOD get_instance_features.

    READ ENTITIES OF zsd_i_result_idsp IN LOCAL MODE
      ENTITY ResultIDSP
         FIELDS (  RunTypeId )
         WITH CORRESPONDING #( it_keys )
       RESULT DATA(lt_results)
       FAILED      failed.


    TRY.
        ct_result = VALUE #( FOR ls_result IN lt_results
                           ( %tky                           = ls_result-%tky
                             %assoc-_resultibds             = COND #( WHEN ls_result-runtypeid = zcl_pe_hm_basic=>get_value( iv_id = '320' iv_sequence = 1 ) OR
                                                                           ls_result-runtypeid = zcl_pe_hm_basic=>get_value( iv_id = '320' iv_sequence = 2 )
                                                                      THEN if_abap_behv=>fc-o-enabled ELSE if_abap_behv=>fc-o-disabled   )
                             %assoc-_resultpack             = COND #( WHEN ls_result-runtypeid = zcl_pe_hm_basic=>get_value( iv_id = '320' iv_sequence = 3 )
                                                                      THEN if_abap_behv=>fc-o-enabled ELSE if_abap_behv=>fc-o-disabled   )
                          ) ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.
  ENDMETHOD.

ENDCLASS.