************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SUFIYKON     | 26.09.2023 | 25482 Recount Application                *
*              |            | DS4K965210                               *
************************************************************************

CLASS lhc_scanned_header
  DEFINITION
    INHERITING FROM cl_abap_behavior_handler
    FINAL.

  PRIVATE SECTION.
    METHODS get_instance_authorizations FOR INSTANCE AUTHORIZATION
      IMPORTING
        keys   REQUEST requested_authorizations FOR scanned_header
      RESULT
        result ##NEEDED.

    METHODS read FOR READ
      IMPORTING
        keys   FOR READ scanned_header
      RESULT
        result ##NEEDED.

    METHODS lock FOR LOCK
      IMPORTING
        keys FOR LOCK scanned_header ##NEEDED.

    METHODS rba_items FOR READ
      IMPORTING
        keys_rba FOR READ scanned_header\_items FULL result_requested
      RESULT
        result LINK association_links ##NEEDED.

    METHODS get_recount_status FOR READ
      IMPORTING
        keys   FOR FUNCTION scanned_header~get_recount_status
      RESULT
        result.

    METHODS finish_recount FOR MODIFY
      IMPORTING
        keys   FOR ACTION scanned_header~finish_recount
      RESULT
        result.
ENDCLASS.

CLASS lhc_scanned_items
  DEFINITION
    INHERITING FROM cl_abap_behavior_handler
    FINAL.

  PRIVATE SECTION.
    METHODS read FOR READ
      IMPORTING
        keys   FOR READ scanned_items
      RESULT
        result ##NEEDED.

    METHODS rba_header FOR READ
      IMPORTING
        keys_rba FOR READ scanned_items\_header FULL result_requested
      RESULT
        result LINK association_links ##NEEDED.

    METHODS process_scanned_item FOR MODIFY
      IMPORTING
        keys   FOR ACTION scanned_items~process_scanned_item
      RESULT
        result.
ENDCLASS.

CLASS lsc_scanned_header
  DEFINITION
    INHERITING FROM cl_abap_behavior_saver
    FINAL.

  PROTECTED SECTION.
    METHODS finalize REDEFINITION.
    METHODS check_before_save REDEFINITION.
    METHODS save REDEFINITION.
    METHODS cleanup REDEFINITION.
    METHODS cleanup_finalize REDEFINITION.
ENDCLASS.

CLASS lhc_scanned_header IMPLEMENTATION.
  METHOD get_instance_authorizations.
    RETURN.
  ENDMETHOD.

  METHOD read.
    RETURN.
  ENDMETHOD.

  METHOD lock.
    RETURN.
  ENDMETHOD.

  METHOD rba_items.
    RETURN.
  ENDMETHOD.

  METHOD get_recount_status.
    DATA lv_status_text TYPE itex132.

    DATA(ls_key) = keys[ 1 ].

    CALL FUNCTION 'ZSD_FM_GET_RECOUNT_STATUS'
      EXPORTING
        iv_venum       = ls_key-%param-handlingunitinternalid
      IMPORTING
        ev_status_text = lv_status_text.

    INSERT VALUE #(
      %cid             = ls_key-%cid
      %param-recstatus = lv_status_text ) INTO TABLE result.
  ENDMETHOD.

  METHOD finish_recount.
    DATA lt_return TYPE bapiret2_t.

    DATA(ls_key) = keys[ 1 ].

    CALL FUNCTION 'ZSD_FM_RUN_RECOUNT' DESTINATION 'NONE'
      EXPORTING
        iv_venum              = ls_key-%param-handlingunitinternalid
      CHANGING
        ct_return             = lt_return
      EXCEPTIONS
        communication_failure = 1
        system_failure        = 2
        OTHERS                = 3.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_message).
      INSERT VALUE #(
        type       = sy-msgty
        id         = sy-msgid
        number     = sy-msgno
        message    = lv_message
        message_v1 = sy-msgv1
        message_v2 = sy-msgv2
        message_v3 = sy-msgv3
        message_v4 = sy-msgv4 ) INTO TABLE lt_return.
    ENDIF.

    LOOP AT lt_return ASSIGNING FIELD-SYMBOL(<ls_return>).
      INSERT VALUE #(
        %cid               = ls_key-%cid
        %param-messagetype = <ls_return>-type
        %param-message     = <ls_return>-message ) INTO TABLE result.
    ENDLOOP.
  ENDMETHOD.
ENDCLASS.

CLASS lhc_scanned_items IMPLEMENTATION.
  METHOD read.
    RETURN.
  ENDMETHOD.

  METHOD rba_header.
    RETURN.
  ENDMETHOD.

  METHOD process_scanned_item.
    DATA ls_return TYPE bapiret2.

    DATA(ls_key) = keys[ 1 ].

    CALL FUNCTION 'ZSD_FM_PROCESS_SCANNED_ITEM' DESTINATION 'NONE'
      EXPORTING
        iv_venum              = ls_key-%param-handlingunitinternalid
        iv_ean11              = ls_key-%param-productstandardid
      IMPORTING
        es_return             = ls_return
      EXCEPTIONS
        communication_failure = 1
        system_failure        = 2
        OTHERS                = 3.

    IF sy-subrc <> 0.
      ls_return-type = sy-msgty.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO ls_return-message.
    ENDIF.

    result = VALUE #(
      ( %cid               = keys[ 1 ]-%cid
        %param-messagetype = ls_return-type
        %param-message     = ls_return-message ) ).
  ENDMETHOD.
ENDCLASS.

CLASS lsc_scanned_header IMPLEMENTATION.
  METHOD finalize.
    RETURN.
  ENDMETHOD.

  METHOD check_before_save.
    RETURN.
  ENDMETHOD.

  METHOD save.
    RETURN.
  ENDMETHOD.

  METHOD cleanup.
    RETURN.
  ENDMETHOD.

  METHOD cleanup_finalize.
    RETURN.
  ENDMETHOD.
ENDCLASS.