************************************************************************
* R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.07.2023 | 23764   : SO Mass Change further improvements*
*              |            | DS4K959588                               *
*----------------------------------------------------------------------*
CLASS lsc_zsd_i_sohdrrddredeterm DEFINITION INHERITING FROM cl_abap_behavior_saver.
  PUBLIC SECTION.
    INTERFACES zif_sd_i_sohdrrddredeterminati.
  PROTECTED SECTION.

    METHODS save REDEFINITION.
ENDCLASS.

CLASS lsc_zsd_i_sohdrrddredeterm IMPLEMENTATION.

  METHOD save.
    DATA(lo_so_rdd_redetermination) = zcl_sd_so_rdd_redetermination=>get_instance( ).
    lo_so_rdd_redetermination->save(
      EXPORTING
        iif_handler  = me
      CHANGING
        cs_reported = reported ).

  ENDMETHOD.

  METHOD zif_sd_i_sohdrrddredeterminati~create_message_object.
    rif_message = new_message(
      id       = is_bapiret2-id
      number   = is_bapiret2-number
      severity = CONV #( is_bapiret2-type )
      v1       = is_bapiret2-message_v1
      v2       = is_bapiret2-message_v2
      v3       = is_bapiret2-message_v3
      v4       = is_bapiret2-message_v4 ).
  ENDMETHOD.
ENDCLASS.


CLASS lhc_soreredetermination DEFINITION INHERITING FROM cl_abap_behavior_handler.
  PRIVATE SECTION.
    METHODS get_instance_authorizations FOR INSTANCE AUTHORIZATION
      IMPORTING keys REQUEST requested_authorizations FOR soreredetermination RESULT result ##NEEDED.

    METHODS massupdate FOR MODIFY
      IMPORTING keys FOR ACTION soreredetermination~massupdate.

    METHODS read FOR READ
      IMPORTING keys FOR READ soreredetermination RESULT result ##NEEDED.

    METHODS lock FOR LOCK
      IMPORTING keys FOR LOCK soreredetermination ##NEEDED.

ENDCLASS.

CLASS lhc_soreredetermination IMPLEMENTATION.

  METHOD get_instance_authorizations.
    READ ENTITIES OF zsd_i_sohdrrddredetermination IN LOCAL MODE
      ENTITY soreredetermination
        FIELDS ( salesdocument )
        WITH CORRESPONDING #( keys )
      RESULT DATA(lt_result)
      FAILED failed.

    LOOP AT lt_result INTO DATA(ls_result).
      APPEND VALUE #( %tky = ls_result-%tky
                      %action-massupdate = if_abap_behv=>auth-allowed ) TO result.
    ENDLOOP.
  ENDMETHOD.

  METHOD massupdate.
    IF keys IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lo_so_rdd_redetermination) = zcl_sd_so_rdd_redetermination=>get_instance( ).
    lo_so_rdd_redetermination->mass_update( it_keys = keys ).
  ENDMETHOD.

  METHOD read.
    RETURN.
  ENDMETHOD.

  METHOD lock.
    RETURN.
  ENDMETHOD.
ENDCLASS.