************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 21.02.2023 | 18064     : [Build] - PO Delivery Date   *
*              |            | DS4K943975                               *
*----------------------------------------------------------------------*
* KHRYSAND     | 27.04.2023 | 20667     :  [Investigation/CR] [Build]  *
*              |            | DS4K950868                               *
*----------------------------------------------------------------------*
* ISAICSVI     | 24.10.2023 | 24999     :  Feature-[CR] Update AB conf *
*              |            | DS4K963491                               *
*----------------------------------------------------------------------*
* PEREDSER     | 31.01.2024 | DS4K978048   FB_28588_SP_Feature -       *
*              |            | CR-[Build] - Manual Delivery date update *
*----------------------------------------------------------------------*/

CLASS lhc_podeldate DEFINITION INHERITING FROM cl_abap_behavior_handler FINAL.
  PRIVATE SECTION.

    METHODS changedeliverydate FOR MODIFY
      IMPORTING it_keys FOR ACTION podeldate~changedeliverydate RESULT ct_result.
    METHODS get_instance_authorizations FOR INSTANCE AUTHORIZATION
      IMPORTING keys REQUEST requested_authorizations FOR podeldate RESULT result ##NEEDED.

ENDCLASS.

CLASS lhc_podeldate IMPLEMENTATION.


  METHOD changedeliverydate.
    DATA: lt_message    TYPE bal_t_msg,
          lt_input_data TYPE zsp_tt_po_del_date_mass.

    READ ENTITIES OF zsp_i_po_del_date_mass IN LOCAL MODE
    ENTITY podeldate
    ALL FIELDS WITH CORRESPONDING #( it_keys )
    RESULT DATA(lt_del_dates).

    IF it_keys[ 1 ]-%param-deliverydate < sy-datum.
      APPEND VALUE #( %msg = NEW zcx_sp_po_del_date_mass( severity = if_abap_behv_message=>severity-information
                                                          textid   = zcx_sp_po_del_date_mass=>invalid_future_del_date ) )
        TO reported-podeldate.

    ELSE.
      LOOP AT lt_del_dates ASSIGNING FIELD-SYMBOL(<ls_del_dates>).
        lt_message = VALUE #( ( msgid = zcl_sp_po_del_date_mass=>gc_log_constant-mess_class
                                msgno = zcl_sp_po_del_date_mass=>gc_log_constant-mess_start_process
                                msgty = if_abap_behv_message=>severity-information ) ##TYPE
                              ( msgid = zcl_sp_po_del_date_mass=>gc_log_constant-mess_class
                                msgno = zcl_sp_po_del_date_mass=>gc_log_constant-mess_po_parameters
                                msgty = if_abap_behv_message=>severity-information
                                msgv1 = <ls_del_dates>-purchaseorder
                                msgv2 = <ls_del_dates>-purchaseorderitem
                                msgv3 = <ls_del_dates>-purchaseorderscheduleline ) ).

        TRY.
            DATA(lo_log) = NEW zcl_sd_create_ball_log( iv_extnumber = CONV #( |{ <ls_del_dates>-purchaseorder }{ <ls_del_dates>-purchaseorderitem }{ <ls_del_dates>-purchaseorderscheduleline }| )
                                                       iv_object    = zcl_sp_po_del_date_mass=>gc_log_constant-object
                                                       iv_subobject = zcl_sp_po_del_date_mass=>gc_log_constant-subobject ).
            lo_log->add_item_log( lt_message ).
            lo_log->save_log( ).
            <ls_del_dates>-processingstatus = 'P'.
          CATCH zcx_sd_ball_log.
            CONTINUE.
        ENDTRY.
      ENDLOOP.

      lt_input_data = CORRESPONDING #( lt_del_dates ).

      APPEND VALUE #( %msg = NEW zcx_sp_po_del_date_mass( severity = if_abap_behv_message=>severity-information
                                                          textid   = zcx_sp_po_del_date_mass=>process_start ) )
      TO reported-podeldate.

      ct_result = VALUE #( FOR ls_del_dates IN lt_del_dates ( %key   = ls_del_dates-%key
                                                              %param = ls_del_dates ) ).

      WAIT UP TO 1 SECONDS.

      CALL FUNCTION 'ZSP_FM_PO_DEL_DATE_MASS'
        STARTING NEW TASK 'UPDATE'
        EXPORTING
          it_input_data         = lt_input_data
          iv_future_date        = it_keys[ 1 ]-%param-deliverydate
        EXCEPTIONS
          communication_failure = 1
          system_failure        = 2 ##FM_SUBRC_OK. "#EC CI_SUBRC

    ENDIF.
  ENDMETHOD.

  METHOD get_instance_authorizations.
    RETURN.
  ENDMETHOD.

ENDCLASS.