CLASS zcl_adjust_stock_hlp DEFINITION
PUBLIC
  CREATE PUBLIC .

  PUBLIC SECTION.

    CLASS-METHODS get_instance
      RETURNING
        VALUE(ro_instance) TYPE REF TO zcl_adjust_stock_hlp .
    METHODS read_logo
      IMPORTING
        !iv_object     TYPE tdobjectgr
        !iv_name       TYPE tdobname
        !iv_id         TYPE tdidgr
        !iv_btype      TYPE tdbtype
      RETURNING
        VALUE(rv_logo) TYPE xstring .
    METHODS fetch_language
      IMPORTING
        !iv_sending_site   TYPE werks_d
      RETURNING
        VALUE(rv_language) TYPE spras .
    METHODS fetch_user_details
      IMPORTING
        !iv_usnam          TYPE usnam
      RETURNING
        VALUE(rv_operator) TYPE char100 .
  PROTECTED SECTION.

    TYPES:
      BEGIN OF mtyp_s_user_details,
        user_id  TYPE uname,
        operator TYPE char100,
      END OF mtyp_s_user_details .
    TYPES:
      mtyp_ts_user_details TYPE SORTED TABLE OF mtyp_s_user_details
        WITH UNIQUE KEY user_id .

    CLASS-DATA go_instance TYPE REF TO zcl_adjust_stock_hlp .
    DATA mt_s_user_details TYPE mtyp_ts_user_details .
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_ADJUST_STOCK_HLP IMPLEMENTATION.


  METHOD fetch_language.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                    *
*          |          | TRANSPORT REQUESTS                             *
************************************************************************
* BIGVATAM |03.10.2022| xxxxx: Initial version.
*          |          | xxxxxxxxxxx
************************************************************************
* As both tables are buffered, separate SELECTs are used
    SELECT SINGLE land1
      FROM t001w
      WHERE werks = @iv_sending_site
      INTO @DATA(lv_land1).

    IF sy-subrc = 0.
      SELECT SINGLE spras
        FROM t005
        WHERE land1 = @lv_land1
        INTO @rv_language.

      IF sy-subrc <> 0.
        CLEAR rv_language.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD fetch_user_details.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                    *
*          |          | TRANSPORT REQUESTS                             *
************************************************************************
* BIGVATAM |03.10.2022| xxxxx: Initial version.
*          |          | xxxxxxxxxxx
************************************************************************
    DATA:
      ls_address TYPE bapiaddr3,
      lt_return  TYPE STANDARD TABLE OF bapiret2.

    ASSIGN mt_s_user_details[ user_id = iv_usnam ]
      TO FIELD-SYMBOL(<ls_user_details>).

    IF sy-subrc = 0.
      rv_operator = <ls_user_details>-operator.

    ELSE.

      CALL FUNCTION 'BAPI_USER_GET_DETAIL'
        EXPORTING
          username = iv_usnam
        IMPORTING
          address  = ls_address
        TABLES
          return   = lt_return.

      IF NOT ls_address IS INITIAL.
        rv_operator = |{ ls_address-lastname }, { ls_address-firstname }|.

        INSERT VALUE #( user_id = iv_usnam
                        operator = rv_operator )
          INTO TABLE mt_s_user_details.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD get_instance.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                    *
*          |          | TRANSPORT REQUESTS                             *
************************************************************************
* BIGVATAM |03.10.2022| xxxxx: Initial version.
*          |          | xxxxxxxxxxx
************************************************************************
    IF NOT go_instance IS BOUND.
      go_instance = NEW zcl_adjust_stock_hlp( ).
    ENDIF.

    ro_instance = go_instance.
  ENDMETHOD.


  METHOD read_logo.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                    *
*          |          | TRANSPORT REQUESTS                             *
************************************************************************
* BIGVATAM |03.10.2022| xxxxx: Initial version.
*          |          | xxxxxxxxxxx
************************************************************************
    cl_ssf_xsf_utilities=>get_bds_graphic_as_bmp(
      EXPORTING
        p_object       = iv_object
        p_name         = iv_name
        p_id           = iv_id
        p_btype        = iv_btype
      RECEIVING
        p_bmp          = rv_logo
      EXCEPTIONS
        not_found      = 1
        internal_error = 2
        OTHERS         = 3 ).

    IF sy-subrc <> 0.
      CLEAR rv_logo.
    ENDIF.
  ENDMETHOD.
ENDCLASS.