class ZCL_BD_PO_INTERFACE definition
  public
  final
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IS_POHEADER type BAPIMEPOHEADER
      !IT_POITEM type BAPIMEPOITEM_TP
      !IT_POADDRDELIVERY type BAPIMEPOADDRDELIVERY_TP
      !IT_POSCHEDULE type BAPIMEPOSCHEDULE_TP
      !IT_POACCOUNT type BAPIMEPOACCOUNT_TP
      !IT_POCONDHEADER type BAPIMEPOCONDHEADER_TP
      !IT_POCOND type BAPIMEPOCOND_TP
      !IT_POHISTORY_TOTALS type BAPIEKBES_TP
      !IT_POPARTNER type BAPIEKKOP_TP .
  methods ASSIGN_HEADER_DATA
    changing
      !CS_HEADER_ATTR type ZZZ_ZSP_S_HEADER .
  methods SELECT_PURCH_DOC .
  methods ASSIGN_ITEM_TRADECO_DATA
    importing
      !IV_EBELP type EBELP
    changing
      !CS_TRADECO_DATA type ZZZ_ZSP_S_TRADECO_DATA .
  methods PRELOAD_ITEM_DATA
    importing
      !IV_EBELP type EBELP
      !IV_MATNR type MATNR .
  methods SELECT_SALES_DATA
    importing
      !IV_EBELP type EBELP .
  methods ASSIGN_ITEM_DATA
    importing
      !IV_EBELP type EBELP
    changing
      !CS_ITEM_DATA type ZZZ_ZSP_S_ITEM_DIRECT_DATA .
  methods ASSIGN_ITEM_PRICE_DATA
    importing
      !IV_EBELP type EBELP
    changing
      !CS_ITEM_PR_DATA type ZZZ_ZSP_S_ITEM_PRICE_DATA .
  methods SELECT_MODEL_PART
    returning
      value(RV_MODELS) type STRING .
  methods ASSIGN_FSH_VASM_DATA
    importing
      !IV_EBELP type EBELP
    changing
      !CT_FSH_VALS type ZZZ_ZSP_S_FSH_VASM_TL_TAB .
  methods SELECT_CONDT_TYPES
    importing
      !IV_KSCHL type KSCHA
    returning
      value(RT_COND_DATA) type ZZZ_ZSP_S_CONDITION_TAB .
  methods ASSIGN_PARTY_DATA
    changing
      !CS_PARTY_SECTION type ZZZ_ZSP_S_PARTY_SECTION .
  methods ASSIGN_TARIF_DATA
    importing
      !IS_ITEM_DATA type ZZZ_ZSP_S_ITEM_DIRECT_DATA
    changing
      !CT_TARIF type ZZZ_ZSP_S_TARIFF_TAB .
  methods PRELOAD_PARTY_DATA .
  methods ASSIGN_CASELOT_DATA
    importing
      !IV_EBELP type EBELP
    changing
      !CT_CASELOT type ZZZ_ZSP_S_CASE_LOT_TAB .
  methods ASSIGN_ITEM_CONTRACT_DETAILS
    importing
      !IS_ITEM type BAPIMEPOITEM
    changing
      !CS_CONTRACT_DETAILS type ZZSP_S_CONTRACT_DETAILS_ENH .
  methods ASSIGN_ITEM_PARTY_FS
    importing
      !IV_EBELP type EBELP
    changing
      !CS_PARTY_FS type ZZSP_S_POITEM_PARTY_FS .
  methods ASSIGN_ITEM_SHIP2L_ADDRESS
    changing
      !CS_ADDRESS type SAPPLCO_ADDRESS .
  PROTECTED SECTION.
private section.

  types:
    BEGIN OF mtyp_s_purchitem,
      zz1_tradeco_prec_no_pdi    TYPE zz1_tradeco_prec_no,
      zz1_tradeco_prec_item_pdi  TYPE zz1_tradeco_prec_item,
      zz_vkgrp                   TYPE vkgrp,
      zz_vtweg                   TYPE vtweg,
      zz1_postat_pdi             TYPE zz1_postat,
      zz1_adi_m_leadtimeplan_pdi TYPE zz1_adi_m_leadtimeplan,
      zz1_adi_m_planned_pdi      TYPE zz1_adi_m_planned,
      zz1_mm_exfactorydt_pdi     TYPE zz1_mm_exfactorydt,
      zz_vdatu_ana               TYPE edatu_vbak,
      zz_customer                TYPE ekunnr,
      kna1_country               TYPE land1_gp,
      plant                      TYPE ewerk,
      t001w_country              TYPE land1,
      zz1_fexfct_da_pdi          TYPE zz1_fexfct_da,
      zz1_lexfct_da_pdi          TYPE zz1_lexfct_da,
      zz1_mm_scmsegment_pdi      TYPE zz1_mm_scmsegment,
      zz1_ori_eligibility_pdi    TYPE zz1_ori_eligibility,
      zz_evers                   TYPE evers,
      zz_zcust_model_part        TYPE zpd_t_mmcustom-zcust_model_part,
      zz1_mm_vascutoffdate_pdi   TYPE zz1_mm_vascutoffdate,
      zz1_custin_pdi             TYPE zz1_custin,
      zz1_mdd_da_pdi             TYPE zz1_mdd_da,
      zz1_packpln_da_pdi         TYPE zz1_packpln_da,
      zz1_delay_conf_pdi         TYPE zz1_delay_conf,
      zz1_delay_po_pdi           TYPE zz1_delay_po,
      zz1_delay_pln_conf_pdi     TYPE zz1_delay_pln_conf,
      zz1_delay_pln_wip_pdi      TYPE zz1_delay_pln_wip,
      zz1_custom_stat_pdi        TYPE zz1_custom_stat,
      zz1_ordprio_pdi            TYPE zz1_ordprio,
      zz1_adi_m_promoflag_pdi    TYPE zz1_adi_m_promoflag,
      zz1_prior_ind_pdi          TYPE zz1_prior_ind,
      zz1_idd_da_pdi             TYPE zz1_idd_da,
      zz1_load_typ_pdi           TYPE zz1_load_typ,
      zz1_fprod_da_pdi           TYPE zz1_fprod_da,
      zz1_lprod_da_pdi           TYPE zz1_lprod_da,
      zz1_hts_eligibility_pdi    TYPE zz1_hts_eligibility,
      zz1_form_pdi               TYPE zz1_form,
      zz1_hts_curr_pdi           TYPE zz1_hts_curr,
      zz1_hts_scope_pdi          TYPE zz1_hts_scope,
      zz_wrf_charstc2            TYPE wrf_charstc2,
      zz_netwr                   TYPE bwert,
      zz_satnr                   TYPE satnr,
      zz_vdatu                   TYPE edatu_vbak,
      zz1_mm_siden_pdi           TYPE zz1_mm_siden,
      zz1_rerout_ind_pdi         TYPE zz1_rerout_ind,
      zz1_l2_group_pdi           TYPE zz1_l2_group,
      zz1_customer_mrg_pdi       TYPE zz1_customer_mrg,
      zz1_cut_off_da_pdi         TYPE zz1_cut_off_da,
      zz1_oc0_po_pdi             TYPE zz1_oc0_po,
      zz_rfm_ref_doc             TYPE rfm_ref_doc,
      zz_rfm_ref_item            TYPE rfm_ref_item,
      elikz                      TYPE elikz,
      loekz                      TYPE eloek,
      eindt                      TYPE eindt,
      soldto_kunnr               TYPE kunag,
      shipto_kunnr               TYPE kunnr,
      zz1_adi_req_avail_pdi      TYPE zz1_adi_req_avail,
      ebelp                      TYPE ebelp,
      menge                      TYPE bstmg,
      meins                      TYPE bstme,
      matnr                      TYPE matnr,
      konnr                      TYPE konnr,
      ktpnr                      TYPE ktpnr,
      kunnr_wrk                  TYPE kunnr_wk,
      vtweg_wrk                  TYPE vtwiv,
      spart_wrk                  TYPE spaiv,
      vkorg_wrk                  TYPE vkoiv,
    END OF mtyp_s_purchitem .
  types:
    BEGIN OF mtyp_s_party,
      zz_partner           TYPE i_bpusrexternalid-businesspartner,
      bpidentificationtype TYPE i_bpusrexternalid-bpidentificationtype,
      zz_partner_number    TYPE i_bpusrexternalid-bpidentificationnumber,
      zz_name              TYPE name1_gp,
      zz_name_2            TYPE name2_gp,
      zz_name_3            TYPE mcdk1,
      zz_name_4            TYPE mcdk2,
      zz_telephone         TYPE telf1,
      zz_street            TYPE stras_gp,
      zz_street_2          TYPE ad_strspp1,
      zz_city              TYPE ort01_gp,
      zz_region            TYPE regio,
      zz_postal_code       TYPE pstlz,
      zz_country_code      TYPE land1_gp,
      adrnr                TYPE adrnr,
    END OF mtyp_s_party .
  types:
    BEGIN OF mtyp_s_partner,
      partnerfunction TYPE parvw,
      buspart         TYPE lifn2,
    END OF mtyp_s_partner .
  types:
    mtyp_t_party TYPE STANDARD TABLE OF mtyp_s_party WITH DEFAULT KEY .
  types:
    BEGIN OF gtyp_s_bsart,
      bsart TYPE bsart,
    END OF gtyp_s_bsart .
  types:
    BEGIN OF mtyp_s_ekkn_vbak,
      ebelp TYPE i_purordaccountassignmentapi01-purchaseorderitem,
      vbeln TYPE vbeln_va,
      bstnk TYPE bstnk,
      vdatu TYPE edatu_vbak,
      kunnr TYPE kunag,
      vtweg TYPE vtweg,
      spart TYPE spart,
      vkorg TYPE vkorg,
    END OF mtyp_s_ekkn_vbak .
  types:
    BEGIN OF mtyp_s_prcd_elem_data,
      kbetr    TYPE vfprc_element_amount,
      sign     TYPE zzz_zsp_s_condition-zz_cond_amount_sign,
      not_null TYPE abap_bool,
    END OF mtyp_s_prcd_elem_data .
  types:
    BEGIN OF mtyp_s_parvw,
      good_issuer TYPE parvw,
      vendor      TYPE parvw,
    END OF mtyp_s_parvw .
  types:
    BEGIN OF mtyp_s_dec_tab_sent_id,
      kalsm       TYPE kalsm_d,
      mwskz       TYPE mwskz,
      land1       TYPE zz1_country_from,
      begda       TYPE begda,
      endda       TYPE endda,
      tax_sent_id TYPE zfi_tax_sentence_id,
    END OF mtyp_s_dec_tab_sent_id .
  types:
    BEGIN OF mtyp_s_dec_tab_txt,
      zfi_tax_sentence TYPE if_fdt_types=>element_text,
    END OF mtyp_s_dec_tab_txt .
  types:
    mtyp_tt_dec_tab_txt TYPE STANDARD TABLE OF mtyp_s_dec_tab_txt WITH DEFAULT KEY .
  types:
    BEGIN OF mtyp_s_indexconv_data,
      zzsdesc   TYPE zpd_t_indexconv-zzsdesc,
      zzmti2    TYPE zpd_t_indexconv-zzmti2,
      zzprindex TYPE zpd_t_indexconv-zzprindex,
      zzsize    TYPE zpd_t_sizepageas-zzsize,
    END OF mtyp_s_indexconv_data .
  types:
    BEGIN OF mtyp_s_hidehts_country,
      land1 TYPE land1,
    END OF mtyp_s_hidehts_country .
  types:
    BEGIN OF mtyp_s_ekpo_st,
      zz1_tradeco_prec_no_pdi   TYPE ekpo-ebeln,
      zz1_tradeco_prec_item_pdi TYPE ekpo-ebelp,
    END OF mtyp_s_ekpo_st .
  types:
    mtyp_t_ekpo_st TYPE SORTED TABLE OF mtyp_s_ekpo_st WITH NON-UNIQUE KEY zz1_tradeco_prec_no_pdi zz1_tradeco_prec_item_pdi .

  constants GC_NO type STRING value 'N' ##NO_TEXT.
  constants GC_YES type STRING value 'Y' ##NO_TEXT.
  constants GC_LANG_EN type SPRAS value 'E' ##NO_TEXT.
  constants GC_ERP_S4 type CHAR2 value 'S4' ##NO_TEXT.
  constants GC_KAPPL_M type STRING value 'M' ##NO_TEXT.
  constants:
    BEGIN OF gc_bool_str,
      true  TYPE string VALUE 'true' ##NO_TEXT,
      false TYPE string VALUE 'false' ##NO_TEXT,
    END OF gc_bool_str .
  constants:
    BEGIN OF gc_barcode_type,
      u TYPE char2 VALUE 'U' ##NO_TEXT,
      e TYPE char2 VALUE 'E' ##NO_TEXT,
    END OF gc_barcode_type .
  constants:
    BEGIN OF gc_eantp,
      uc TYPE numtp VALUE 'UC' ##NO_TEXT,
      he TYPE numtp VALUE 'HE' ##NO_TEXT,
    END OF gc_eantp .
  data MS_PARVW type MTYP_S_PARVW .
  data:
    mts_ekkn_vbak TYPE SORTED TABLE OF mtyp_s_ekkn_vbak WITH NON-UNIQUE KEY ebelp .
  data:
    ms_ekkn_vbak  LIKE LINE OF mts_ekkn_vbak .
  data MS_POHEADER type BAPIMEPOHEADER  ##NEEDED.
  data MT_POITEM type BAPIMEPOITEM_TP  ##NEEDED.
  data MT_POADDRDELIVERY type BAPIMEPOADDRDELIVERY_TP  ##NEEDED.
  data MT_POSCHEDULE type BAPIMEPOSCHEDULE_TP  ##NEEDED.
  data MT_POACCOUNT type BAPIMEPOACCOUNT_TP  ##NEEDED.
  data MT_POCONDHEADER type BAPIMEPOCONDHEADER_TP  ##NEEDED.
  data MT_POCOND type BAPIMEPOCOND_TP  ##NEEDED.
  data MT_POHISTORY_TOTALS type BAPIEKBES_TP  ##NEEDED.
  data MT_POPARTNER type BAPIEKKOP_TP  ##NEEDED.
  data:
    mt_patners TYPE STANDARD TABLE OF mtyp_s_partner .
  data:
    BEGIN OF ms_purchdoc,
      zz_lastchangedatetime   TYPE i_purchasingdocument-lastchangedatetime,
      zz_supplier             TYPE i_purchasingdocument-supplier,
      zz1_mm_tradecomodel_pdh TYPE e_purchasingdocument-zz1_mm_tradecomodel_pdh,
      zz_land1                TYPE land1,
      zz_stceg                TYPE stceg,
      zz_knumv                TYPE knumv,
    END OF ms_purchdoc .
  data:
    BEGIN OF ms_control_const,
      tradeco_model_value TYPE zz1_e44c75c98012-code,
      tradeco_model_valid TYPE abap_bool,
    END OF ms_control_const .
  data:
    BEGIN OF ms_material,
      material      TYPE i_material-material,
      zzmbc         TYPE e_product-zzmbc,
      zz_fsh_mg_at1 TYPE fsh_mg_attribute1,
      zzworking     TYPE zpd_working,
      zz_size1      TYPE wrf_size1,
      zz_fsh_sc_mid TYPE fsh_sc_mid,
      zzgen         TYPE zpd_gen,
      zz_wgbez      TYPE wgbez,
      zzmc1         TYPE zpd_zzmc1,
      zzmc2         TYPE zpd_zzmc2,
      zzmc3         TYPE zpd_zzmc3,
      zzmc4         TYPE zpd_zzmc4,
      zz_mra        TYPE zpd_t_mara_seas-zzmra,
      zzmsc         TYPE zpd_zzmsc,
      zz_ntgew      TYPE ntgew,
      zz_gewei      TYPE gewei,
      zz_brgew      TYPE brgew,
      zz_volum      TYPE volum,
      zz_voleh      TYPE voleh,
      zz_zzworking  TYPE zpd_working,
    END OF ms_material .
  data:
    mts_purchitem TYPE SORTED TABLE OF mtyp_s_purchitem WITH UNIQUE KEY ebelp
                                      WITH NON-UNIQUE SORTED KEY lkz COMPONENTS loekz .
  data MS_PURCHITEM type MTYP_S_PURCHITEM .
  data:
    mt_rn_condtypes TYPE RANGE OF kschl .
  data:
    mt_brf_bsart TYPE STANDARD TABLE OF gtyp_s_bsart .
  data:
    ms_poaccount       LIKE LINE OF mt_poaccount .
  data:
    mt_hidehts_country TYPE STANDARD TABLE OF mtyp_s_hidehts_country .
  data:
    mr_type_code       TYPE RANGE OF zsd_doctype .
  data MV_TAXNUM type ZZZ_ZSP_S_HEADER-ZZ_TAXNUM .
  data:
    BEGIN OF ms_kschl,
      fob_price TYPE kscha,
      tax_rate  TYPE kscha,
    END OF ms_kschl .
  data MV_LAND_SS type ZPD_T_SIZEPAGEAS-LAND1 .
  data MV_PARVW_4_TAXNUM type PARVW value 'LF' ##NO_TEXT.
  data MV_S4GPS_ACTIVE type ZPE_HM_ACTIVE .
  data MV_TRADECO_V15 type ZPE_HM_ACTIVE .
  data MV_ERP_TYPE type CHAR05 .
  data MV_PARVW_4FS type PARVW .
  data MV_PARVW_4BP type PARVW .
  data MV_PARVW_4SH type PARVW .
  data MV_TRADECO_ID_TYPE type BU_ID_TYPE .
  data MV_TRADECO_V3 type ZZ1_MM_TRADECOMODEL .
  data MTS_EKPO_ST type MTYP_T_EKPO_ST .

  methods READ_HARDCODE_VALUES .
  methods GET_LFA1_PARTY_DATA
    importing
      !IS_BPPARTNER type MTYP_S_PARTNER
    returning
      value(RT_BPARTNER) type MTYP_T_PARTY .
  methods GET_KNA1_PARTY_DATA
    importing
      !IS_BPPARTNER type MTYP_S_PARTNER
    returning
      value(RT_BPARTNER) type MTYP_T_PARTY .
  methods SELECT_EKPO_DATA_FOR_PO .
  methods READ_TEXT_FOR_CUR_ITEM
    importing
      !IV_ID type TDID
    returning
      value(RV_TEXT) type STRING .
  methods GET_BUKRS_PARTY_DATA
    returning
      value(RS_PARTY) type ZZZ_ZSP_S_PARTY .
  methods SELECT_EKKN_DATA_FOR_PO .
  methods GET_MATNR_COLOR_DESCR
    returning
      value(RV_ZMMCC) type STRING .
  methods GET_COND_AMOUNT_SIGN_IN_PRCD
    importing
      !IV_KSCHL type KSCHA
    returning
      value(RS_PRCD_ELEM) type MTYP_S_PRCD_ELEM_DATA .
  methods GET_ITEM_INVOICE_TEXT
    returning
      value(RV_INV_TXT) type ZZZ_ZSP_S_ITEM_DIRECT_DATA-ZZ_INVOICE_TEXT .
  methods GET_TAX_SENTENCE_FROM_BRF
    importing
      !IS_BRF_INP type MTYP_S_DEC_TAB_SENT_ID
    returning
      value(RV_TAX_SENT_TXT) type ZFI_TAX_SENTENCE
    raising
      CX_FDT_INPUT
      CX_SY_MOVE_CAST_ERROR
      CX_FDT_CONFIG
      CX_FDT .
  methods GET_BARCODE_TYPE
    returning
      value(RV_BARCODE_TYPE) type ZZZ_ZSP_S_ITEM_DIRECT_DATA-ZZ_BARCODE_TYPE .
  methods GET_EAN11
    importing
      !IV_BARCODE_TYPE type ZZZ_ZSP_S_ITEM_DIRECT_DATA-ZZ_BARCODE_TYPE
    returning
      value(RV_EAN11) type ZZZ_ZSP_S_ITEM_DIRECT_DATA-ZZ_EAN11 .
  methods GET_INDEXCONV_N_SIZE_DATA
    importing
      !IV_LAND type LAND1
    returning
      value(RS_INDEXCONV) type MTYP_S_INDEXCONV_DATA .
  methods GET_BUYER_LAND
    returning
      value(RV_FSH_SC_CID) type FSH_SC_CID .
  methods GET_STATUS_FROM_HM .
  methods ASSIGN_PARTY_IP
    returning
      value(RS_PARTY_IP) type ZZSP_S_PO_PARTY_ITEM .
  methods ASSIGN_PARTY_BP
    returning
      value(RS_PARTY_BP) type ZZSP_S_PO_PARTY_ITEM .
  methods SELECT_TRADECO_PO_DATA .
ENDCLASS.



CLASS ZCL_BD_PO_INTERFACE IMPLEMENTATION.


  METHOD ASSIGN_CASELOT_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    TYPES: BEGIN OF ltyp_s_size_cnt,
             size TYPE i_material-productcharacteristic2,
             cnt  TYPE i,
           END OF ltyp_s_size_cnt.
    DATA: lts_size TYPE SORTED TABLE OF ltyp_s_size_cnt WITH UNIQUE KEY size.

    CLEAR: ct_caselot.
    SELECT  h~caseloth_uuid  AS uuid,
            caselot_no       AS zz_caselot_no,
            caselot_text     AS zz_caselot_text,
            caselot_pkmode   AS zz_caselot_pkmode,
            no_caselot_cnt   AS zz_no_caselot_cnt,
            no_maspb_ctn     AS zz_no_maspb_ctn,
            no_caselot_units AS zz_no_caselot_units,
            total_qty_ctn    AS zz_total_qty_ctn,
            i~caselot_cl_pb  AS zz_caselot_cl_pb,
            i~matnr          AS matnr,
            m~productcharacteristic2 AS size
          FROM zsd_t_case_lot_h AS h
          LEFT OUTER JOIN zsd_t_case_lot_i AS i
                ON i~caseloth_uuid = h~caseloth_uuid
          LEFT OUTER JOIN i_material AS m
                ON m~material = i~matnr
          WHERE h~vbeln = @ms_ekkn_vbak-vbeln    AND
                h~matnr = @ms_purchitem-zz_satnr AND
                h~edatu = @ms_ekkn_vbak-vdatu
          INTO TABLE @DATA(lt_cl).
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
    DATA(ls_cl_init) = VALUE zzz_zsp_s_case_lot( zz_order_qty     = ms_purchitem-menge
                                                 zz_order_qty_uom = ms_purchitem-meins
                                                 zz_size          = ms_purchitem-zz_wrf_charstc2 ).
    LOOP AT lt_cl ASSIGNING FIELD-SYMBOL(<ls_cl_gr>)
          GROUP BY <ls_cl_gr>-uuid.
      INSERT CORRESPONDING #( BASE ( ls_cl_init ) <ls_cl_gr> EXCEPT zz_caselot_cl_pb )
            INTO TABLE ct_caselot ASSIGNING FIELD-SYMBOL(<ls_caselot>).
      LOOP AT GROUP <ls_cl_gr> ASSIGNING FIELD-SYMBOL(<ls_cl>).
        COLLECT VALUE ltyp_s_size_cnt( size = <ls_cl>-size  cnt = 1 ) INTO lts_size.
        IF <ls_caselot>-zz_caselot_cl_pb IS INITIAL AND <ls_cl>-matnr = ms_purchitem-matnr.
          <ls_caselot>-zz_caselot_cl_pb = <ls_cl>-zz_caselot_cl_pb.
        ENDIF.
      ENDLOOP.
      <ls_caselot>-zz_caselot_summary1 = |{ 'Case Lot No'(004) }: { <ls_caselot>-zz_caselot_no }|.
      <ls_caselot>-zz_caselot_summary2 = |{ <ls_caselot>-zz_no_caselot_cnt } { 'times'(005)
                                          }, { <ls_caselot>-zz_no_caselot_units
                                          } { <ls_caselot>-zz_order_qty_uom } { 'each'(006) }|.
      <ls_caselot>-zz_caselot_summary3 = REDUCE #( INIT lv_r = `` lv_sep = `` FOR ls_s IN lts_size
                                                   NEXT lv_r = |{ lv_r }{ lv_sep }{ ls_s-cnt }x{ ls_s-size }|
                                                        lv_sep = `; ` ) && '.'.
      <ls_caselot>-zz_caselot_summary4 = |{ 'No of Master Polybag'(007) }: { <ls_caselot>-zz_no_maspb_ctn }|.
      <ls_caselot>-zz_caselot_summary5 = |{ 'No of qty per Carton'(008) }: { <ls_caselot>-zz_total_qty_ctn }|.
    ENDLOOP.
  ENDMETHOD.


  METHOD ASSIGN_FSH_VASM_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*

    SELECT
      valueaddedsubservicetype AS zz_fsh_vas_sub_ser,
      valueaddedservicetext1 AS zz_fsh_cust_field1,
      valueaddedservicetext2 AS zz_fsh_cust_field2,
      valueaddedservicetext3 AS zz_fsh_cust_field3
      FROM i_valaddedsrvcmmbasic
      WHERE valaddedsrvcmmrefdocnmbr EQ @ms_poheader-po_number
        AND valaddedsrvcmmrefdocitem EQ @iv_ebelp
      INTO TABLE @DATA(lt_data).
    IF sy-subrc EQ 0.
      ct_fsh_vals = CORRESPONDING #( lt_data ).
    ENDIF.

  ENDMETHOD.


  METHOD ASSIGN_HEADER_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 02.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*

    select_purch_doc( ).
    select_ekpo_data_for_po( ).
    select_ekkn_data_for_po( ).
    get_status_from_hm( ).

    TRY.
        cl_gdt_conversion=>date_time_outbound(
          EXPORTING
            im_value_long = ms_purchdoc-zz_lastchangedatetime
          IMPORTING
            ex_value      = cs_header_attr-zz_lastchangedatetime ).
      CATCH cx_gdt_conversion .
        CLEAR cs_header_attr-zz_lastchangedatetime.
    ENDTRY.
    CONVERT TIME STAMP ms_purchdoc-zz_lastchangedatetime TIME ZONE sy-zonlo
          INTO DATE cs_header_attr-zz_lastchangedate.
    cs_header_attr-zz_current_date = sy-datlo.
    cs_header_attr-zz_bukrs = ms_poheader-comp_code.
    cs_header_attr-zz_zterm = ms_poheader-pmnttrms.
    cs_header_attr-zz1_mm_tradecomodel_pdh = ms_purchdoc-zz1_mm_tradecomodel_pdh.
    SELECT SINGLE
          FROM i_supplier WITH PRIVILEGED ACCESS
          FIELDS country AS zz_country
          WHERE supplier EQ @ms_purchdoc-zz_supplier
          INTO @cs_header_attr-zz_vendor_country.         "#EC CI_SUBRC

    SELECT                                              "#EC CI_NOORDER
          FROM i_countrysalestax WITH PRIVILEGED ACCESS
          FIELDS
          vatregistrationcountry,
          companyvatregistration
          WHERE companycode EQ @ms_poheader-comp_code
          AND vatregistrationcountry EQ @ms_poheader-vat_cntry
          INTO ( @ms_purchdoc-zz_land1, @ms_purchdoc-zz_stceg ) UP TO 1 ROWS.
    ENDSELECT.
    IF sy-subrc EQ 0.
      cs_header_attr-zz_stceg = ms_purchdoc-zz_stceg.
    ENDIF.

    cs_header_attr-zz_erp = mv_erp_type.
    cs_header_attr-zz_reallocated_po = REDUCE #(
            INIT lv_r1 = gc_no
            FOR ls_i1 IN mts_purchitem
                WHERE ( zz_rfm_ref_doc IS NOT INITIAL AND zz_rfm_ref_doc <> ms_poheader-po_number ) "#EC CI_SORTSEQ
            NEXT lv_r1 = gc_yes ).
    cs_header_attr-zz_total_ord_val = REDUCE #( INIT lv_r2 TYPE zzz_zsp_s_header-zz_total_ord_val
                                                FOR ls_i2 IN mts_purchitem
                                                USING KEY lkz WHERE ( loekz = space )
                                                NEXT lv_r2 = lv_r2 + ls_i2-zz_netwr ).
    SELECT  lf~supplier,
            lf~country && '0' AS taxtype
          FROM i_purchaseorderpartnerapi01 WITH PRIVILEGED ACCESS AS pa
          INNER JOIN i_supplier WITH PRIVILEGED ACCESS AS lf
                ON lf~supplier = pa~supplier
          WHERE pa~purchaseorder   = @ms_poheader-po_number AND
                pa~partnerfunction = @mv_parvw_4_taxnum
          INTO ( @DATA(lv_lifnr), @DATA(lv_taxtype) )
          UP TO 1 ROWS.
    ENDSELECT.
    IF sy-subrc = 0.
      SELECT SINGLE taxnum
            FROM dfkkbptaxnum
            INTO @cs_header_attr-zz_taxnum
            WHERE partner = @lv_lifnr AND
                  taxtype = @lv_taxtype.
      IF sy-subrc <> 0.
        CLEAR: cs_header_attr-zz_taxnum.
      ENDIF.
      mv_taxnum = cs_header_attr-zz_taxnum.
    ENDIF.
    preload_party_data( ).

    IF mv_s4gps_active EQ abap_true.
      SELECT SINGLE so~zz1_bstkd_sdh
        FROM i_salesdocument WITH PRIVILEGED ACCESS AS so
        INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS po
        ON so~salesdocument = po~salesorder
        WHERE purchaseorder = @ms_poheader-po_number
        INTO @cs_header_attr-zz_afspono.
    ENDIF.

    IF mv_tradeco_v15 EQ abap_true.
      DATA(ls_ekpo) = mts_purchitem[ ebelp = '00010' ].

      SELECT SINGLE companycode AS zz_market_pocompany_code,
                    purchaseordertype AS zz_market_podoc_type
        FROM i_purchaseorderapi01
        WHERE purchaseorder = @ls_ekpo-zz1_tradeco_prec_no_pdi
        INTO CORRESPONDING FIELDS OF @cs_header_attr.
    ENDIF.

    cs_header_attr-zz_contract_validity_from  = ms_poheader-vper_start.
    cs_header_attr-zz_contract_validity_to  = ms_poheader-vper_end.

  ENDMETHOD.


  METHOD ASSIGN_ITEM_CONTRACT_DETAILS.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DADABBEK     | 05.07.2023 | 22955: PO interface adaptions            *
*              |            | DS4K957434                               *
*----------------------------------------------------------------------*
* DADABBEK     | 15.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------

    DATA lv_qaunt TYPE ekpo-ktmng.

    CALL FUNCTION 'ME_READ_RELEASE_QUANTITY'
      EXPORTING
        i_konnr = ms_poheader-po_number
        i_ktpnr = is_item-po_item
      IMPORTING
        e_abmng = lv_qaunt.

    cs_contract_details-zz_open_quantity = is_item-quantity - lv_qaunt.
    cs_contract_details-zz_l2group = ms_purchitem-zz1_l2_group_pdi.
    cs_contract_details-zz_customer_market_region = ms_purchitem-zz1_customer_mrg_pdi.
    cs_contract_details-zz_contract_cut_off_date = ms_purchitem-zz1_cut_off_da_pdi.

  ENDMETHOD.


  METHOD ASSIGN_ITEM_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 03.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*

    cs_item_data = CORRESPONDING #( ms_material ) ##ENH_OK .
    cs_item_data = CORRESPONDING #( BASE ( cs_item_data )  ms_purchitem ) ##ENH_OK .
    IF ms_purchitem-kna1_country IS NOT INITIAL.
      cs_item_data-zz_country =  ms_purchitem-kna1_country.
    ELSEIF ms_purchitem-t001w_country IS NOT INITIAL.
      cs_item_data-zz_country =  ms_purchitem-t001w_country.
    ENDIF.
    cs_item_data-zz_zcust_model_part = select_model_part( ).
    IF  ms_poheader-po_number IS NOT INITIAL AND ms_material-material IS NOT INITIAL AND iv_ebelp  IS NOT INITIAL.
      cs_item_data-zz_ebeln_matrnr_ebelp = |{ ms_poheader-po_number }-{ ms_material-material }-{ iv_ebelp }|.
    ENDIF.
    IF ms_material-zzmc1 IS NOT INITIAL AND ms_material-zzmc2 IS NOT INITIAL AND ms_material-zzmc4 IS NOT INITIAL
    AND  ms_material-zzmc3 IS NOT INITIAL.
      cs_item_data-zzmds = |{ ms_material-zzmc1 }/{ ms_material-zzmc2 }/{ ms_material-zzmc3 }/{ ms_material-zzmc4 }|.
    ENDIF.

    LOOP AT mt_poschedule ASSIGNING FIELD-SYMBOL(<ls_shed>) "#EC CI_STDSEQ
                          WHERE po_item EQ iv_ebelp.
      CALL FUNCTION 'DATE_CONV_EXT_TO_INT'
        EXPORTING
          i_date_ext = <ls_shed>-delivery_date
        IMPORTING
          e_date_int = cs_item_data-zz_eindt
        EXCEPTIONS
          error      = 0.
      cs_item_data-zz_slfdt = <ls_shed>-stat_date.
      ms_purchitem-eindt = cs_item_data-zz_eindt.
    ENDLOOP.

    IF cs_item_data-zz_customer_final_ship_to IS INITIAL.
      LOOP AT mt_patners ASSIGNING FIELD-SYMBOL(<ls_partner>)
                           WHERE partnerfunction EQ 'WE' .
        cs_item_data-zz_customer_final_ship_to = <ls_partner>-buspart.
      ENDLOOP.
    ENDIF.

    cs_item_data-zz_order_info_nexus          = read_text_for_cur_item( 'F11' ).
    cs_item_data-zz_shipping_instruction_text = read_text_for_cur_item( 'F12' ).
    cs_item_data-zz_hide_hts = COND #( WHEN line_exists( mt_hidehts_country[ land1 = cs_item_data-zz_country ] ) "#EC CI_STDSEQ
                                       THEN gc_yes ELSE gc_no ).
    cs_item_data-zz_customer_ref_po = ms_ekkn_vbak-bstnk.
    " It seems to be obsolete because there is the same field (TAXNUM) on header level.
    " For now analyst decided to left this field on item level too and fill with the same value as header one.
    cs_item_data-zz_vendor_vat_number = mv_taxnum.
    cs_item_data-zzmds = get_matnr_color_descr( ).
    cs_item_data-zz_invoice_text = get_item_invoice_text( ).
    cs_item_data-zz_barcode_type = get_barcode_type( ).
    cs_item_data-zz_ean11        = get_ean11( cs_item_data-zz_barcode_type ).
    cs_item_data-zz_tax_rate     = get_cond_amount_sign_in_prcd( ms_kschl-tax_rate )-kbetr.

    DATA(ls_indexconv) = get_indexconv_n_size_data( mv_land_ss ).
    cs_item_data-zz_manufacturing_local_size = ls_indexconv-zzsdesc.
    cs_item_data-zzmti2                      = ls_indexconv-zzmti2.
    cs_item_data-zz_technical_print_index    = ls_indexconv-zzprindex.

    DATA(lv_buyer_land) = get_buyer_land( ).
    DATA(ls_buyer_size) = get_indexconv_n_size_data( CONV #( lv_buyer_land ) ).
    cs_item_data-zz_buyer_customer_size = ls_buyer_size-zzsdesc.
    cs_item_data-zz_zsize               = ls_buyer_size-zzsize.

    IF mv_tradeco_v15 EQ abap_true.
      SELECT SINGLE PurchasingDocumentItemCategory AS zz_market_poitem_category,
                    AccountAssignmentCategory AS zz_market_poaccnt_assmnt_cat,
                    Plant AS zz_market_podc
      FROM i_purchasingdocumentitem WITH PRIVILEGED ACCESS
      WHERE purchasingdocument = @ms_purchitem-zz1_tradeco_prec_no_pdi
        AND purchasingdocumentitem = @ms_purchitem-zz1_tradeco_prec_item_pdi
       INTO CORRESPONDING FIELDS OF @cs_item_data.

      SELECT SINGLE cust~Country AS zz_country,
                    cust~FreeDefinedAttribute04 AS zz_barcode_type
        FROM i_customer AS cust
        INNER JOIN i_salesdocumentpartner WITH PRIVILEGED ACCESS AS sdpr
        ON cust~customer = sdpr~customer
        INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
        ON sdpr~salesdocument = poas~salesorder
        WHERE poas~purchaseorder = @ms_purchitem-zz1_tradeco_prec_no_pdi
        AND poas~purchaseorderitem =  @ms_purchitem-zz1_tradeco_prec_item_pdi
        INTO CORRESPONDING FIELDS OF @cs_item_data.

      cs_item_data-zz_vdatu = ms_purchitem-zz_vdatu.

      IF ms_purchitem-zz_customer IS INITIAL.
        SELECT SINGLE sdpr~customer
          FROM i_salesdocumentpartner AS sdpr
          INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
          ON sdpr~salesdocument = poas~salesorder
          WHERE poas~purchaseorder = @ms_purchitem-zz1_tradeco_prec_no_pdi
          AND poas~purchaseorderitem = @ms_purchitem-zz1_tradeco_prec_item_pdi
          AND sdpr~partnerfunction = @mv_parvw_4sh
         INTO @cs_item_data-zz_customer_final_ship_to.
      ENDIF.

      cs_item_data-zz1_postat_pdi = ms_ekkn_vbak-bstnk. " Need to check BSTNK
    ELSE.
      cs_item_data-zz_customer_final_ship_to = ms_purchitem-soldto_kunnr.
      cs_item_data-zz1_postat_pdi = ms_purchitem-zz1_postat_pdi.
    ENDIF.

    IF mv_s4gps_active EQ abap_true AND mv_tradeco_v15 EQ abap_true.
      cs_item_data-zz1_adi_req_avail_pdi = VALUE #( mts_ekkn_vbak[ ebelp = iv_ebelp ]-vdatu OPTIONAL ).
    ENDIF.

    SELECT SINGLE Brand
     FROM i_product
     WHERE i_product~product  = @ms_purchitem-matnr
     INTO @cs_item_data-zzmbc.

    cs_item_data-zz_contract_out_agreement_no = ms_purchitem-konnr.
    cs_item_data-zz_contract_agrmnt_line_item_n = ms_purchitem-ktpnr.

    IF mv_s4gps_active EQ abap_true.
      SELECT SINGLE zz1_size
        FROM zsd_t_afsdatarec
        WHERE zz1_bskd = @ms_ekkn_vbak-bstnk
        INTO @cs_item_data-zz_zsize.

      cs_item_data-zz_order_classification = ms_purchitem-zz1_oc0_po_pdi.
    ENDIF.

  ENDMETHOD.


  METHOD ASSIGN_ITEM_PARTY_FS.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DADABBEK     | 05.07.2023 | 22955: PO interface adaptions            *
*              |            | DS4K957434                               *
*----------------------------------------------------------------------*
* DADABBEK     | 15.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------

    SELECT SINGLE sdpr~customer
      FROM i_salesdocumentpartner AS sdpr
      INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
      ON sdpr~salesdocument = poas~salesorder
      WHERE poas~purchaseorder = @ms_poheader-po_number
      AND poas~purchaseorderitem = @iv_ebelp
      AND sdpr~partnerfunction = @mv_parvw_4fs
      INTO @cs_party_fs-zz_business_partner_id.

    SELECT SINGLE cust~organizationbpname1 AS zz_bpname1,
                  cust~organizationbpname2 AS zz_bpname2,
                  cust~zz_organizationbpname3 AS zz_bpname3,
                  cust~zz_organizationbpname4 AS zz_bpname4,
                  cust~telephonenumber1 AS zz_bptelephone,
                  cust~faxnumber AS zz_bpfax,
                  cust~streetname AS zz_bpstreet,
                  adrs~StreetPrefixName1 AS zz_bpstreet2,
                  cust~CustomerFullName AS zz_bpcity,
                  cust~cityname AS zz_bpregion,
                  cust~postalcode AS zz_bppostal_code,
                  cust~Country AS zz_bpcountry_code,
                  but0id~idnumber AS zz_gpscustomer_no
      FROM i_customer AS cust
      INNER JOIN i_salesdocumentpartner WITH PRIVILEGED ACCESS AS sdpr
      ON cust~customer = sdpr~customer
      LEFT OUTER JOIN but0id
      ON but0id~partner = sdpr~customer
      LEFT OUTER JOIN i_addrorgnamepostaladdress AS adrs
      ON adrs~addressid = cust~addressid
      INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
      ON sdpr~salesdocument = poas~salesorder
      WHERE poas~purchaseorder = @ms_purchitem-zz1_tradeco_prec_no_pdi
      AND poas~purchaseorderitem =  @ms_purchitem-zz1_tradeco_prec_item_pdi
      AND sdpr~partnerfunction = @mv_parvw_4sh
      AND but0id~type = @mv_tradeco_id_type
      INTO CORRESPONDING FIELDS OF @cs_party_fs.


  ENDMETHOD.


  METHOD ASSIGN_ITEM_PRICE_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 07.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    cs_item_pr_data-zz_fob_price = get_cond_amount_sign_in_prcd( ms_kschl-fob_price )-kbetr.
  ENDMETHOD.


  METHOD ASSIGN_ITEM_SHIP2L_ADDRESS.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DADABBEK     | 05.07.2023 | 22955: PO interface adaptions            *
*              |            | DS4K957434                               *
*----------------------------------------------------------------------*
* DADABBEK     | 15.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------

    IF mv_tradeco_v15 EQ abap_true.

      IF ms_purchitem-zz_customer IS INITIAL.
        SELECT SINGLE sdpr~customer
          FROM i_salesdocumentpartner AS sdpr
          INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
          ON sdpr~salesdocument = poas~salesorder
          WHERE poas~purchaseorder = @ms_purchitem-zz1_tradeco_prec_no_pdi
          AND poas~purchaseorderitem = @ms_purchitem-zz1_tradeco_prec_item_pdi
          AND sdpr~partnerfunction = @mv_parvw_4sh
         INTO @cs_address-zz_ship_to_customer.
      ELSE.
        IF mv_s4gps_active EQ abap_true.
          SELECT SINGLE sdpr~customer
            FROM i_salesdocumentpartner AS sdpr
            INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
            ON sdpr~salesdocument = poas~salesorder
            WHERE poas~purchaseorder = @ms_poheader-po_number
            AND poas~purchaseorderitem = @ms_purchitem-ebelp
            AND sdpr~partnerfunction = @mv_parvw_4sh
           INTO @cs_address-zz_ship_to_customer.
        ENDIF.
      ENDIF.
    ELSE.
      cs_address-zz_ship_to_customer = ms_purchitem-zz_customer.
    ENDIF.

    IF mv_s4gps_active EQ abap_true.

      SELECT SINGLE sdpr~customer
        FROM i_salesdocumentpartner AS sdpr
        INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
        ON sdpr~salesdocument = poas~salesorder
        WHERE poas~purchaseorder = @ms_purchitem-zz1_tradeco_prec_no_pdi
        AND poas~purchaseorderitem = @ms_purchitem-zz1_tradeco_prec_item_pdi
        AND sdpr~partnerfunction = @mv_parvw_4fs
        INTO @DATA(lv_kunnr).

      SELECT SINGLE idnumber
        FROM but0id
        WHERE partner = @lv_kunnr
        AND type = @mv_tradeco_id_type
        INTO @DATA(lv_idnumber).

      cs_address-zz_gpscustomer_no = |{ lv_idnumber ALPHA = OUT }|.

    ENDIF.

  ENDMETHOD.


  METHOD ASSIGN_ITEM_TRADECO_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 15.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    IF ms_purchitem-shipto_kunnr IS NOT INITIAL
    AND ms_poheader-doc_type IS NOT INITIAL
    AND ms_material-zzmbc IS NOT INITIAL
    AND iv_ebelp IS NOT INITIAL.
      cs_tradeco_data-zz_kunnr_bsart_zzmbc_ebeln = |{ ms_purchitem-shipto_kunnr }-{ ms_poheader-doc_type }-{ ms_material-zzmbc }-{ iv_ebelp }|.
    ENDIF.

    cs_tradeco_data = CORRESPONDING #( BASE ( cs_tradeco_data  ) ms_purchitem
                      EXCEPT zz1_tradco_prec_no_pdi zz1_tradco_prec_item_pdi ) ##ENH_OK .

    IF mv_tradeco_v15 EQ abap_true.
      cs_tradeco_data-zz1_tradco_prec_no_pdi = ms_purchitem-zz1_tradeco_prec_no_pdi.
      cs_tradeco_data-zz1_tradco_prec_item_pdi = ms_purchitem-zz1_tradeco_prec_item_pdi.
    ENDIF.

  ENDMETHOD.


  METHOD ASSIGN_PARTY_BP.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DADABBEK     | 05.07.2023 | 22955: PO interface adaptions            *
*              |            | DS4K957434                               *
*----------------------------------------------------------------------*
* DADABBEK     | 15.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------

    TYPES:
      BEGIN OF ltyp_bp_address,
        ad_city1     TYPE ad_city1,
        ad_fxnmbr1   TYPE ad_fxnmbr1,
        ad_pstcd1    TYPE ad_pstcd1,
        ad_street    TYPE ad_street,
        ad_tlnmbr1   TYPE ad_tlnmbr1,
        bukrs        TYPE bukrs,
        bu_id_number TYPE bu_id_number,
        lland        TYPE lland,
        name1_gp     TYPE name1_gp,
        name2_gp     TYPE name2_gp,
        regio        TYPE regio,
      END OF ltyp_bp_address.

    DATA:
      lt_bp_address TYPE STANDARD TABLE OF ltyp_bp_address.

    IF ms_purchdoc-zz1_mm_tradecomodel_pdh EQ mv_tradeco_v3.
      TRY.
          zcl_pe_hm_complex=>get_table(
            EXPORTING
              iv_id           = '024F85009E261EEE89C08DEACF3F80D2'
              is_filter       = ms_purchdoc-zz_land1
            IMPORTING
              et_table        = lt_bp_address ).
        CATCH zcx_pe_hm.
          RETURN.
      ENDTRY.

      READ TABLE lt_bp_address ASSIGNING FIELD-SYMBOL(<ls_bp_address>) INDEX 1.
      CHECK sy-subrc EQ 0.
      rs_party_bp = VALUE #( zz_business_partner_id = <ls_bp_address>-bukrs
                             zz_bpidentification_type = <ls_bp_address>-bu_id_number
                             zz_bpname1 = <ls_bp_address>-name1_gp
                             zz_bpname2 = <ls_bp_address>-name2_gp
                             zz_bpcountry_code = <ls_bp_address>-lland
                             zz_bpregion = <ls_bp_address>-regio
                             zz_bppostal_code = <ls_bp_address>-ad_pstcd1
                             zz_bpcity = <ls_bp_address>-ad_city1
                             zz_bpstreet = <ls_bp_address>-ad_street
                             zz_bptelephone = <ls_bp_address>-ad_tlnmbr1
                             zz_bpfax = <ls_bp_address>-ad_fxnmbr1 ).
    ELSE.
      SELECT SINGLE sdpr~customer, but0id~idnumber
        FROM i_salesdocumentpartner AS sdpr
        INNER JOIN i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS poas
        ON sdpr~salesdocument = poas~salesorder
        LEFT JOIN but0id
        ON but0id~partner = sdpr~customer
        WHERE poas~purchaseorder = @ms_poheader-po_number
        AND sdpr~partnerfunction = @mv_parvw_4bp
        INTO ( @rs_party_bp-zz_business_partner_id, @DATA(lv_idnumber) ).

    ENDIF.

    rs_party_bp-zz_bpidentification_type = |{ lv_idnumber ALPHA = OUT }|.

    SELECT SINGLE adrs~AddresseeName1 AS zz_bpname1,
                  adrs~AddresseeName2 AS zz_bpname2,
                  adrs~Country AS zz_bpcountry_code,
                  adrs~Region AS zz_bpregion,
                  adrs~PostalCode AS zz_bppostal_code,
                  adrs~CityName AS zz_bpcity,
                  adrs~StreetName AS zz_bpstreet,
                  adrs~ZZ_TelNumber AS zz_bptelephone,
                  adrs~ZZ_FaxNumber AS zz_bpfax
      FROM i_addrorgnamepostaladdress AS adrs
      INNER JOIN i_customer AS cust
      ON adrs~AddressID = cust~AddressID
      WHERE cust~Customer = @rs_party_bp-zz_business_partner_id
      INTO CORRESPONDING FIELDS OF @rs_party_bp.

  ENDMETHOD.


  METHOD ASSIGN_PARTY_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 03.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
* NIELOOLE     | 12.05.2023 | 21264 PO Interface - replace hardcoding  *
*              |            |       with HM ID                         *
*              |            | DS4K952219                               *
*----------------------------------------------------------------------*
* DADABBEK     | 05.07.2023 | 22955: PO interface adaptions            *
*              |            | DS4K957434                               *
*----------------------------------------------------------------------*
* DADABBEK     | 15.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*-----------------------------------------------------------------------

    FIELD-SYMBOLS: <ls_party> LIKE cs_party_section-zz_s_party_am.

    TRY.
        DATA(lt_r_type_vendor) = zcl_pe_hm_basic=>get_range( '1126' ).
      CATCH zcx_pe_hm INTO DATA(lo_exc).
        MESSAGE lo_exc.
    ENDTRY.

    LOOP AT mt_patners ASSIGNING FIELD-SYMBOL(<ls_partner>).
      IF <ls_partner>-partnerfunction EQ 'WE'.
        DATA(lt_bpartner) = get_kna1_party_data( <ls_partner> ).
      ELSE.
        lt_bpartner = get_lfa1_party_data( is_bppartner = <ls_partner> ).
      ENDIF.
      LOOP AT lt_bpartner ASSIGNING FIELD-SYMBOL(<ls_bpartner>). "#EC CI_NESTED
        CASE <ls_partner>-partnerfunction.
          WHEN 'AM'.
            IF <ls_bpartner>-bpidentificationtype IN lt_r_type_vendor
            OR <ls_bpartner>-bpidentificationtype IS INITIAL.
              ASSIGN cs_party_section-zz_s_party_am TO <ls_party>.
            ENDIF.
          WHEN 'LF'.
            IF <ls_bpartner>-bpidentificationtype IN lt_r_type_vendor
            OR <ls_bpartner>-bpidentificationtype IS INITIAL.
              ASSIGN cs_party_section-zz_s_party_lf TO <ls_party>.
            ENDIF.
          WHEN 'RS' OR 'PI'.
            IF <ls_bpartner>-bpidentificationtype IN lt_r_type_vendor
            OR <ls_bpartner>-bpidentificationtype IS INITIAL.
              ASSIGN cs_party_section-zz_s_party_pi TO <ls_party>.
            ENDIF.
          WHEN 'BA' OR 'OA'.
            IF <ls_bpartner>-bpidentificationtype IN lt_r_type_vendor
            OR <ls_bpartner>-bpidentificationtype IS INITIAL.
              ASSIGN cs_party_section-zz_s_party_oa TO <ls_party>.
            ENDIF.
          WHEN 'WL'.
            IF <ls_bpartner>-bpidentificationtype IN lt_r_type_vendor
            OR <ls_bpartner>-bpidentificationtype IS INITIAL.
              ASSIGN cs_party_section-zz_s_party_gs TO <ls_party>.
            ENDIF.
          WHEN 'PF'.
            IF <ls_bpartner>-bpidentificationtype IN lt_r_type_vendor
            OR <ls_bpartner>-bpidentificationtype IS INITIAL.
              ASSIGN cs_party_section-zz_s_party_pf TO <ls_party>.
            ENDIF.
          WHEN 'LO'.
            IF <ls_bpartner>-bpidentificationtype IN lt_r_type_vendor
            OR <ls_bpartner>-bpidentificationtype IS INITIAL.
              ASSIGN cs_party_section-zz_s_party_lo TO <ls_party>.
            ENDIF.
        ENDCASE.
        IF <ls_party> IS ASSIGNED.
          <ls_party> = CORRESPONDING #( <ls_bpartner> ) ##ENH_OK.
          UNASSIGN <ls_party>.
        ENDIF.
      ENDLOOP.
    ENDLOOP.

    IF cs_party_section-zz_s_party_lf IS NOT INITIAL AND
       cs_party_section-zz_s_party_gs IS INITIAL.
      cs_party_section-zz_s_party_gs = cs_party_section-zz_s_party_lf.
    ENDIF.

    cs_party_section-zz_s_party_cc = get_bukrs_party_data(  ).

    cs_party_section-zz_s_party_bp = assign_party_bp( ).

    cs_party_section-zz_s_party_ip = assign_party_ip( ).

  ENDMETHOD.


  METHOD ASSIGN_PARTY_IP.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DADABBEK     | 05.07.2023 | 22955: PO interface adaptions            *
*              |            | DS4K957434                               *
*----------------------------------------------------------------------*
* DADABBEK     | 15.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------

    SELECT SINGLE adrs~AddresseeName1 AS zz_bpname1,
                  adrs~AddresseeName2 AS zz_bpname2,
                  adrs~Country AS zz_bpcountry_code,
                  adrs~Region AS zz_bpregion,
                  adrs~PostalCode AS zz_bppostal_code,
                  adrs~CityName AS zz_bpcity,
                  adrs~StreetName AS zz_bpstreet,
                  adrs~ZZ_TelNumber AS zz_bptelephone,
                  adrs~ZZ_FaxNumber AS zz_bpfax
      FROM i_addrorgnamepostaladdress AS adrs
      INNER JOIN i_companycode AS ccode
      ON adrs~AddressID = ccode~addressid
      WHERE ccode~companycode = @ms_poheader-comp_code
      INTO CORRESPONDING FIELDS OF @rs_party_ip.
    IF sy-subrc EQ 0.
      rs_party_ip-zz_business_partner_id = ms_poheader-comp_code.
      rs_party_ip-zz_bpidentification_type = ms_parvw-vendor.
    ENDIF.

  ENDMETHOD.


  METHOD ASSIGN_TARIF_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 07.10.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    CHECK line_exists( mt_brf_bsart[ bsart = ms_poheader-doc_type ] ).

    SELECT zcust_qualifier INTO @DATA(lv_qualifier)
          FROM zpd_t_mmcustom UP TO 1 ROWS
          WHERE matnr            = @ms_purchitem-zz_satnr   AND
                country          = @is_item_data-zz_country AND
                zcust_qualifier >= @ms_purchitem-zz_wrf_charstc2.
    ENDSELECT.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
    DATA(lv_date) = COND #( WHEN ms_purchitem-zz1_mdd_da_pdi IS INITIAL THEN is_item_data-zz_eindt
                            ELSE ms_purchitem-zz1_mdd_da_pdi ).
    SELECT  zcust_tariff      AS zz_hts_code,
            zcust_tariffdescr AS zz_hts_code_description,
            zcust_model_part  AS zz_model_part_number
          INTO CORRESPONDING FIELDS OF TABLE @ct_tarif    ##TOO_MANY_ITAB_FIELDS
          FROM zpd_t_mmcustom UP TO 1 ROWS
          WHERE matnr            = @ms_purchitem-zz_satnr   AND
                country          = @is_item_data-zz_country AND
                zcust_qualifier  = @lv_qualifier            AND
                lkenz            = @space                   AND
                zcust_date_from <= @lv_date                 AND
                zcust_date_to   >= @lv_date.              "#EC CI_SUBRC
  ENDMETHOD.


  METHOD CONSTRUCTOR.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 02.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*

    ms_poheader  = is_poheader.
    mt_poitem = it_poitem.
    mt_poaddrdelivery = it_poaddrdelivery.
    mt_poschedule = it_poschedule.
    mt_poaccount = it_poaccount.
    mt_pocondheader  =  it_pocondheader.
    mt_pocond = it_pocond.
    mt_pohistory_totals = it_pohistory_totals.
    mt_popartner = it_popartner.

    read_hardcode_values( ).

    TRY.
        zcl_pe_hm_complex=>get_table( EXPORTING iv_id           = '0239FC3B8D041EDD949149E3EC42BF59'
                                                iv_mapping_type = zcl_pe_hm_complex=>gc_s_mapping_type-names
                                      IMPORTING et_table        = mt_brf_bsart ).
      CATCH zcx_pe_hm.
        CLEAR mt_brf_bsart.
    ENDTRY.

    TRY.
        zcl_pe_hm_complex=>get_table( EXPORTING iv_id    = '0239FC3B8D041EDDA8AA93C7D8CD3F59'
                                      IMPORTING et_table = mt_hidehts_country ).
      CATCH zcx_pe_hm   ##NO_HANDLER.
    ENDTRY.

    TRY.
        mr_type_code = zcl_pe_hm_basic=>get_range( '1302' ).
      CATCH zcx_pe_hm   ##NO_HANDLER.
    ENDTRY.

    TRY.
        ms_parvw-good_issuer = zcl_pe_hm_basic=>get_value( '509' ).
      CATCH zcx_pe_hm   ##NO_HANDLER.
    ENDTRY.

    TRY.
        ms_parvw-vendor = zcl_pe_hm_basic=>get_value( '610' ).
      CATCH zcx_pe_hm   ##NO_HANDLER.
    ENDTRY.

    TRY.
        DATA(lt_cust) = zcl_pe_hm_basic=>get_table( '1348' ).
        DATA(ls_cust) = lt_cust[ 1 ].
        ms_kschl = CORRESPONDING #( ls_cust MAPPING fob_price = field1 tax_rate = field2 ).
        mv_land_ss = ls_cust-field3.
        mv_parvw_4_taxnum = ls_cust-field4.
      CATCH cx_sy_itab_line_not_found  zcx_pe_hm   ##NO_HANDLER.
    ENDTRY.

  ENDMETHOD.


  METHOD GET_BARCODE_TYPE.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 09.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    DO 3 TIMES.
      CASE sy-index.
        WHEN 1.
          DATA(lv_kunnr) = ms_purchitem-zz_customer.
        WHEN 2.
          TRY.
              lv_kunnr = ms_ekkn_vbak-kunnr.
            CATCH cx_sy_itab_line_not_found.
              CONTINUE.
          ENDTRY.
        WHEN 3.
          TRY.
              lv_kunnr = mt_poitem[ po_item = ms_purchitem-ebelp ]-plant.
            CATCH cx_sy_itab_line_not_found.
              CONTINUE.
          ENDTRY.
      ENDCASE.
      SELECT SINGLE katr4 FROM kna1 INTO @rv_barcode_type
            WHERE kunnr = @lv_kunnr.                 "#EC CI_SEL_NESTED
      IF sy-subrc = 0.
        EXIT.
      ENDIF.
    ENDDO.
  ENDMETHOD.


  METHOD GET_BUKRS_PARTY_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    SELECT SINGLE addressid
          FROM i_companycode WITH PRIVILEGED ACCESS
          WHERE companycode = @ms_poheader-comp_code
          INTO @DATA(lv_adrnr).
    IF sy-subrc = 0.
*          There is no fields for tel_number, fax_number in I_AddrOrgNamePostalAddress
      SELECT  name1 AS zz_name,
              name2 AS zz_name_2,
              country AS zz_country_code,
              region AS zz_region,
              post_code1 AS zz_postal_code,
              city1 AS zz_city,
              street AS zz_street,
              tel_number AS zz_telephone,
              fax_number AS zz_fax
            INTO CORRESPONDING FIELDS OF @rs_party
            FROM adrc UP TO 1 ROWS
            WHERE addrnumber = @lv_adrnr  AND
                  date_from <= @sy-datum  AND
                  date_to   >= @sy-datum
            ORDER BY nation.
      ENDSELECT.
      IF sy-subrc <> 0.
        CLEAR: rs_party.
      ENDIF.
    ENDIF.
    rs_party-zz_partner = ms_poheader-comp_code.
  ENDMETHOD.


  METHOD GET_BUYER_LAND.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 14.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    DO 2 TIMES.
      CASE sy-index.
        WHEN 1.
          DATA(ls_sales_data) = ms_ekkn_vbak.
        WHEN 2.
          ls_sales_data = CORRESPONDING #( ms_purchitem
                                MAPPING kunnr = kunnr_wrk vtweg = vtweg_wrk
                                        spart = spart_wrk vkorg = vkorg_wrk ).
      ENDCASE.
      SELECT SINGLE fsh_sc_cid FROM knvv             "#EC CI_SEL_NESTED
            INTO @rv_fsh_sc_cid
            WHERE kunnr = @ls_sales_data-kunnr AND
                  vkorg = @ls_sales_data-vkorg AND
                  vtweg = @ls_sales_data-vtweg AND
                  spart = @ls_sales_data-spart.
      IF sy-subrc = 0 AND rv_fsh_sc_cid IS NOT INITIAL.
        EXIT.
      ENDIF.
    ENDDO.
  ENDMETHOD.


  METHOD GET_COND_AMOUNT_SIGN_IN_PRCD.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 07.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*

    SELECT  kbetr,
            CASE WHEN kbetr < 0 THEN 'D'
                 ELSE 'C' END AS sign,
            'X' AS not_null
          INTO @rs_prcd_elem
          FROM prcd_elements UP TO 1 ROWS
          WHERE knumv = @ms_purchdoc-zz_knumv AND
                kschl = @iv_kschl             AND
                kappl = @gc_kappl_m           AND
                kposn = @ms_purchitem-ebelp.         "#EC CI_SEL_NESTED
    ENDSELECT.                                            "#EC CI_SUBRC
  ENDMETHOD.


  METHOD GET_EAN11.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 14.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    DATA(lv_eantp) = SWITCH #( iv_barcode_type WHEN gc_barcode_type-u THEN gc_eantp-uc
                                               ELSE gc_eantp-he ).
    SELECT ean11 FROM mean INTO @rv_ean11 UP TO 1 ROWS
          WHERE matnr = @ms_purchitem-matnr AND
                eantp = @lv_eantp.
    ENDSELECT.
    IF sy-subrc = 0.
      DATA(lv_ean11) = CONV char40( |0000000000000{ rv_ean11 }| ). "zero 13 times + EAN11
      DATA(lv_len) = strlen( rv_ean11 ).
      rv_ean11 = lv_ean11+lv_len(13).
      rv_ean11 = COND #( WHEN iv_barcode_type <> gc_barcode_type-u THEN rv_ean11
                         ELSE shift_left( val = rv_ean11  places = 1 ) ).
    ENDIF.
  ENDMETHOD.


  METHOD GET_INDEXCONV_N_SIZE_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 14.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    SELECT i~zzsdesc, i~zzmti2, i~zzprindex, s~zzsize
          FROM mara AS m
          INNER JOIN zpd_t_sizepageas AS s
                ON s~zzlabtabno = m~fsh_sc_mid AND
                   s~land1      = @iv_land
          INNER JOIN zpd_t_indexconv AS i
                ON i~zzsize = s~zzsize AND
                   i~zzmti3 = @ms_purchitem-zz_wrf_charstc2
          WHERE m~matnr = @ms_purchitem-matnr
          INTO @rs_indexconv
          UP TO 1 ROWS.
    ENDSELECT.                                            "#EC CI_SUBRC
  ENDMETHOD.


  METHOD GET_ITEM_INVOICE_TEXT.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 07.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    DATA: ls_lfa1    TYPE lfa1,
          ls_brf_inp TYPE mtyp_s_dec_tab_sent_id.

    SELECT parvw, lifn2 FROM ekpa INTO TABLE @DATA(lt_ekpa)
          WHERE ebeln  = @ms_poheader-po_number AND
                parvw IN (@ms_parvw-good_issuer, @ms_parvw-vendor). "#EC CI_SUBRC
    READ TABLE lt_ekpa ASSIGNING FIELD-SYMBOL(<ls_ekpa>) WITH KEY parvw = ms_parvw-good_issuer.
    IF sy-subrc <> 0.
      READ TABLE lt_ekpa ASSIGNING <ls_ekpa> WITH KEY parvw = ms_parvw-vendor.
    ENDIF.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
    CALL FUNCTION 'LFA1_SINGLE_READ'
      EXPORTING
        lfa1_lifnr   = <ls_ekpa>-lifn2
        cvp_behavior = '2'
      IMPORTING
        wlfa1        = ls_lfa1
      EXCEPTIONS
        OTHERS       = 4.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
    ls_brf_inp-land1 = ls_lfa1-land1.
    ls_brf_inp-begda = sy-datum.
    ls_brf_inp-endda = sy-datum.
    TRY.
        ls_brf_inp-mwskz = mt_poitem[ po_item = ms_purchitem-ebelp ]-tax_code.
      CATCH cx_sy_itab_line_not_found.
        RETURN.
    ENDTRY.
    SELECT SINGLE land1 FROM t001 INTO @DATA(lv_bu_land1)
          WHERE bukrs = @ms_poheader-comp_code.
    IF sy-subrc = 0.
      SELECT SINGLE kalsm FROM t005 INTO @ls_brf_inp-kalsm
            WHERE land1 = @lv_bu_land1.
    ENDIF.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
    TRY.
        rv_inv_txt = get_tax_sentence_from_brf( ls_brf_inp ).
      CATCH cx_fdt_input  cx_sy_move_cast_error  cx_fdt_config  cx_fdt   ##NO_HANDLER.
    ENDTRY.
  ENDMETHOD.


  METHOD GET_KNA1_PARTY_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 30.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
    SELECT ext~businesspartner AS zz_partner,        "#EC CI_SEL_NESTED
     ext~bpidentificationtype,
     ext~bpidentificationnumber AS zz_partner_number,
     kn~name1 AS zz_name,
     kn~name2 AS zz_name_2,
     kn~mcod1 AS zz_name_3,
     kn~mcod2 AS zz_name_4,
     kn~telf1 AS zz_telephone,
     kn~stras AS zz_street,
     adrc~str_suppl1 AS zz_street_2,
     kn~ort01 AS zz_city,
     kn~regio AS zz_region,
     kn~pstlz AS zz_postal_code,
     kn~land1 AS zz_country_code,
     kn~adrnr
  FROM ekpa AS ek
  LEFT OUTER JOIN i_bpusrexternalid AS ext
  ON ext~businesspartner EQ ek~lifn2
 LEFT OUTER JOIN kna1 AS kn
 ON kn~lifnr EQ ek~lifn2
 LEFT OUTER JOIN adrc
 ON adrc~addrnumber EQ kn~adrnr
 WHERE ext~businesspartner EQ @is_bppartner-buspart
 AND ek~parvw EQ @is_bppartner-partnerfunction
 AND ek~ebeln EQ @ms_poheader-po_number
 INTO CORRESPONDING FIELDS OF TABLE @rt_bpartner.
    IF sy-subrc NE 0.
      SELECT ek~lifn2 AS zz_partner,                 "#EC CI_SEL_NESTED
      ext~bpidentificationtype,
      ext~bpidentificationnumber AS zz_partner_number,
      kn~name1 AS zz_name,
      kn~name2 AS zz_name_2,
      kn~mcod1 AS zz_name_3,
      kn~mcod2 AS zz_name_4,
      kn~telf1 AS zz_telephone,
      kn~stras AS zz_street,
      adrc~str_suppl1 AS zz_street_2,
      kn~ort01 AS zz_city,
      kn~regio AS zz_region,
      kn~pstlz AS zz_postal_code,
      kn~land1 AS zz_country_code,
      kn~adrnr
   FROM ekpa AS ek
   LEFT OUTER JOIN i_bpusrexternalid AS ext
   ON ext~businesspartner EQ ek~lifn2
  LEFT OUTER JOIN kna1 AS kn
  ON kn~lifnr EQ ek~lifn2
  LEFT OUTER JOIN adrc
  ON adrc~addrnumber EQ kn~adrnr
  WHERE ek~parvw EQ @is_bppartner-partnerfunction
  AND ek~ebeln EQ @ms_poheader-po_number
  INTO CORRESPONDING FIELDS OF TABLE @rt_bpartner.
      IF sy-subrc NE 0.
        CLEAR rt_bpartner.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD GET_LFA1_PARTY_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 30.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*

    SELECT ext~businesspartner AS zz_partner,        "#EC CI_SEL_NESTED
             ext~bpidentificationtype,
             ext~bpidentificationnumber AS zz_partner_number,
             lf~name1 AS zz_name,
             lf~name2 AS zz_name_2,
             lf~mcod1 AS zz_name_3,
             lf~mcod2 AS zz_name_4,
             lf~telf1 AS zz_telephone,
             lf~stras AS zz_street,
             adrc~str_suppl1 AS zz_street_2,
             lf~ort01 AS zz_city,
             lf~regio AS zz_region,
             lf~pstlz AS zz_postal_code,
             lf~land1 AS zz_country_code,
             lf~adrnr
        FROM ekpa AS ek
        LEFT OUTER JOIN i_bpusrexternalid AS ext
        ON ext~businesspartner EQ ek~lifn2
        LEFT OUTER JOIN lfa1 AS lf
        ON lf~lifnr EQ ek~lifn2
        LEFT OUTER JOIN adrc
        ON adrc~addrnumber EQ lf~adrnr
        WHERE ext~businesspartner EQ @is_bppartner-buspart
        AND ek~parvw EQ @is_bppartner-partnerfunction
        AND ek~ebeln EQ @ms_poheader-po_number
        INTO CORRESPONDING FIELDS OF TABLE @rt_bpartner.
    IF sy-subrc NE 0.
      SELECT ek~lifn2 AS zz_partner,                 "#EC CI_SEL_NESTED
             ext~bpidentificationtype,
             ext~bpidentificationnumber AS zz_partner_number,
             lf~name1 AS zz_name,
             lf~name2 AS zz_name_2,
             lf~mcod1 AS zz_name_3,
             lf~mcod2 AS zz_name_4,
             lf~telf1 AS zz_telephone,
             lf~stras AS zz_street,
             adrc~str_suppl1 AS zz_street_2,
             lf~ort01 AS zz_city,
             lf~regio AS zz_region,
             lf~pstlz AS zz_postal_code,
             lf~land1 AS zz_country_code,
             lf~adrnr
        FROM ekpa AS ek
        LEFT OUTER JOIN i_bpusrexternalid AS ext
        ON ext~businesspartner EQ ek~lifn2
        LEFT OUTER JOIN lfa1 AS lf
        ON lf~lifnr EQ ek~lifn2
        LEFT OUTER JOIN adrc
        ON adrc~addrnumber EQ lf~adrnr
        WHERE ek~parvw EQ @is_bppartner-partnerfunction
        AND ek~ebeln EQ @ms_poheader-po_number
        INTO CORRESPONDING FIELDS OF TABLE @rt_bpartner.
      IF sy-subrc NE 0.
        CLEAR rt_bpartner.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD GET_MATNR_COLOR_DESCR.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
* RAPANKON     | 05.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------*
* there is no released CDS for ZZMC* field
    SELECT SINGLE i_product~zzmc1, i_product~zzmc2, i_product~zzmc3, i_product~zzmc4
      FROM i_product
     WHERE i_product~product  = @ms_purchitem-matnr
      INTO @DATA(ls_mara).
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    SELECT zpd_t_color~zzcolor,
           zpd_t_color~color_short_abbr
      FROM zpd_t_color
     WHERE zpd_t_color~zzcolor IN ( @ls_mara-zzmc1, @ls_mara-zzmc2, @ls_mara-zzmc3, @ls_mara-zzmc4 )
      INTO TABLE @DATA(lt_color).
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    TRY.
        DATA(lv_color1) = lt_color[ zzcolor = ls_mara-zzmc1 ]-color_short_abbr. "#EC CI_STDSEQ
      CATCH cx_sy_itab_line_not_found.
        CLEAR lv_color1.
    ENDTRY.
    TRY.
        DATA(lv_color2) = lt_color[ zzcolor = ls_mara-zzmc2 ]-color_short_abbr. "#EC CI_STDSEQ
      CATCH cx_sy_itab_line_not_found.
        CLEAR lv_color2.
    ENDTRY.
    TRY.
        DATA(lv_color3) = lt_color[ zzcolor = ls_mara-zzmc3 ]-color_short_abbr. "#EC CI_STDSEQ
      CATCH cx_sy_itab_line_not_found.
        CLEAR lv_color3.
    ENDTRY.
    TRY.
        DATA(lv_color4) = lt_color[ zzcolor = ls_mara-zzmc4 ]-color_short_abbr. "#EC CI_STDSEQ
      CATCH cx_sy_itab_line_not_found.
        CLEAR lv_color4.
    ENDTRY.

    rv_zmmcc = |{ lv_color1 }/{ lv_color2 }/{ lv_color3 }/{ lv_color4 }|.

  ENDMETHOD.


  METHOD GET_STATUS_FROM_HM.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DADABBEK     | 05.07.2023 | 22955: PO interface adaptions            *
*              |            | DS4K957434                               *
*----------------------------------------------------------------------*
* DADABBEK     | 15.07.2023 | 20957_SP_[Feature] [PF&E] Create custom  *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------

    TYPES:
      BEGIN OF ltyp_s_s4gps_filter,
        ekorg TYPE ekorg,
        esart TYPE esart,
      END OF ltyp_s_s4gps_filter,

      BEGIN OF ltyp_s_tradeco_filter,
        zz1_mm_tradecomodel TYPE zz1_mm_tradecomodel,
        bsart               TYPE bsart,
      END OF ltyp_s_tradeco_filter.


    TRY.
        mv_s4gps_active = zcl_pe_hm_complex=>is_active( iv_id     = '0239FC3B8D041EDE87958DE7099DDF59'
                                                        is_filter = VALUE ltyp_s_s4gps_filter( ekorg = ms_poheader-comp_code
                                                                                               esart = ms_poheader-doc_type  ) ).
        mv_tradeco_v15 = zcl_pe_hm_complex=>is_active( iv_id     = '024F85009E261EEE87F5AE805DA180D2'
                                                       is_filter = VALUE ltyp_s_tradeco_filter( zz1_mm_tradecomodel = ms_purchdoc-zz1_mm_tradecomodel_pdh
                                                                                                bsart = ms_poheader-doc_type ) ).
        IF mv_s4gps_active EQ abap_false.
          mv_erp_type = zcl_pe_hm_basic=>get_value( iv_id = '1947' iv_sequence = '001' ).
        ELSE.
          mv_erp_type = zcl_pe_hm_basic=>get_value( iv_id = '1947' iv_sequence = '002' ).
        ENDIF.

        mv_parvw_4sh  = zcl_pe_hm_basic=>get_value( iv_id = '1279' iv_sequence = '001' ).
        mv_parvw_4bp  = zcl_pe_hm_basic=>get_value( iv_id = '1969' iv_sequence = '001' ).
        mv_parvw_4fs  = zcl_pe_hm_basic=>get_value( iv_id = '1969' iv_sequence = '002' ).
        mv_tradeco_v3 = zcl_pe_hm_basic=>get_value( iv_id = '2031' iv_sequence = '002' ).
        mv_tradeco_id_type = zcl_pe_hm_basic=>get_value( iv_id = '1893' iv_sequence = '003' ).

      CATCH zcx_pe_hm   ##NO_HANDLER.
    ENDTRY.

  ENDMETHOD.


  METHOD GET_TAX_SENTENCE_FROM_BRF.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 07.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    DATA: lts_true TYPE SORTED TABLE OF i WITH UNIQUE KEY table_line.
    FIELD-SYMBOLS: <ls_tpnt> TYPE if_fdt_types=>element_timepoint.
    FIELD-SYMBOLS: <lt_result> TYPE mtyp_tt_dec_tab_txt.

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " For the first function we forced to get full BRF+ decision table content
    " and search manually. We can't use function call, because we need
    " to compare using LE, GE compare operators while in decision table all operators is EQ.
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

    " get content of first decision table  (ZFI_T_D_TAX_SENTENCE)
    DATA(lif_fact) = cl_fdt_factory=>get_instance( )->get_expression( '024F85009E261EEDA39E29D61C0EC0D2' ).
    DATA(lo_tab) = CAST cl_fdt_decision_table( lif_fact ).
    lo_tab->if_fdt_decision_table~get_table_data( IMPORTING ets_data = DATA(lts_dec_id) ).

    " search suitable column by column, first building and than refining tab with suitable row_no
    DO 5 TIMES.
      ASSIGN COMPONENT sy-index OF STRUCTURE is_brf_inp TO FIELD-SYMBOL(<lv_comp>).
      CHECK sy-subrc = 0.
      LOOP AT lts_dec_id ASSIGNING FIELD-SYMBOL(<ls_dec_id>) WHERE col_no = sy-index.
        CHECK <ls_dec_id>-ts_range IS NOT INITIAL AND <ls_dec_id>-ts_range[ 1 ]-r_low_value IS BOUND.
        ASSIGN <ls_dec_id>-ts_range[ 1 ]-r_low_value->* TO FIELD-SYMBOL(<lv_dec_comp>).
        CHECK sy-subrc = 0.
        CASE <ls_dec_id>-col_no.
          WHEN 4.
            ASSIGN <lv_dec_comp> TO <ls_tpnt> CASTING.
            DATA(lv_cond) = xsdbool( <lv_comp> >= <ls_tpnt>-date ).
          WHEN 5.
            ASSIGN <lv_dec_comp> TO <ls_tpnt> CASTING.
            lv_cond = xsdbool( <lv_comp> <= <ls_tpnt>-date ).
          WHEN OTHERS.
            lv_cond = xsdbool( <lv_comp> = <lv_dec_comp> ).
        ENDCASE.
        IF <ls_dec_id>-col_no = 1 AND lv_cond = abap_true.
          INSERT <ls_dec_id>-row_no INTO TABLE lts_true.
        ELSEIF <ls_dec_id>-col_no > 1 AND lv_cond IS INITIAL.
          DELETE lts_true WHERE table_line = <ls_dec_id>-row_no.
          IF lts_true IS INITIAL.
            EXIT.
          ENDIF.
        ENDIF.
      ENDLOOP.
      IF lts_true IS INITIAL.
        EXIT.
      ENDIF.
    ENDDO.
    " if there is suitable rows, use first
    IF lts_true IS NOT INITIAL.
      DATA(lo_value_ref) = lts_dec_id[ col_no = 6  row_no = lts_true[ 1 ] ]-r_value.
      IF lo_value_ref IS BOUND.
        ASSIGN lo_value_ref->* TO FIELD-SYMBOL(<lv_tax_sent_id>).
      ENDIF.
    ENDIF.
    IF <lv_tax_sent_id> IS NOT ASSIGNED.
      RETURN.
    ENDIF.

    " Second decision table (ZFI_T_D_TAX_SENTENCE_TEXT) is correct,
    " so we can call function (GET_TAX_SENTENCE_TEXT)
    DATA(lif_factory)  = cl_fdt_factory=>get_instance( '024F85009E261EEDA39E340BCBC100D2' ).
    DATA(lif_function) = lif_factory->get_function( '024F85009E261EEDA39E36750E6D00D2' ).
    DATA(lif_context)  = lif_function->get_process_context( ).
    lif_context->set_value( iv_id = '024F85009E261EEDA39E34BEE7DD00D2'
                            ia_value = <lv_tax_sent_id> ).
    lif_function->process( EXPORTING io_context = lif_context
                           IMPORTING eo_result  = DATA(lif_result) ).
    IF lif_result IS BOUND.
      DATA(lif_data_obj) = lif_result->get_data_object( ).
      lif_data_obj->create_data_reference( IMPORTING er_data = DATA(lo_result_ref) ).
      ASSIGN lo_result_ref->* TO <lt_result> CASTING.
      lif_result->get_value( IMPORTING ea_value = <lt_result> ).
      rv_tax_sent_txt = VALUE #( <lt_result>[ 1 ]-zfi_tax_sentence OPTIONAL ).
    ENDIF.
  ENDMETHOD.


  METHOD preload_item_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 02.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*

    CLEAR: ms_purchitem, ms_material, ms_poaccount.

    SELECT  mm~material,
            zzmbc ,
            freedefinedproductattribute01 AS zz_fsh_mg_at1,
            zzworking AS zz_zzworking,
            productcharacteristic2 AS zz_size1,
            vci~fsh_sc_mid AS zz_fsh_sc_mid,
            zzgen,
            zseasd~zzmra AS zz_mra,
            ntgew AS zz_ntgew,
            gewei AS zz_gewei,
            brgew AS zz_brgew,
            volum AS zz_volum,
            voleh AS zz_voleh
      FROM i_material WITH PRIVILEGED ACCESS AS mm
      INNER JOIN e_product WITH PRIVILEGED ACCESS AS ext
            ON ext~product EQ mm~material
      INNER JOIN i_supdmndmaterialplant WITH PRIVILEGED ACCESS AS mc
            ON mc~material EQ mm~material
      LEFT OUTER JOIN vc_integration_mara WITH PRIVILEGED ACCESS AS vci
            ON vci~matnr EQ mm~material
      LEFT OUTER  JOIN zsc_i_zpd_t_mara_seasd_ex WITH PRIVILEGED ACCESS AS zseasd
            ON zseasd~matnr EQ mm~material
      WHERE mm~material EQ @iv_matnr
      INTO CORRESPONDING FIELDS OF @ms_material UP TO 1 ROWS.
    ENDSELECT.                                            "#EC CI_SUBRC

    SELECT  wgbez AS zz_wgbez,                         "#EC CI_BUFFJOIN
            zzmc1,
            zzmc2,
            zzmc3,
            zzmc4,
            zzmsc
       FROM mara
       LEFT OUTER JOIN t023t
            ON t023t~matkl EQ mara~matkl
       WHERE matnr EQ @iv_matnr
         AND t023t~spras EQ @gc_lang_en
       INTO CORRESPONDING FIELDS OF @ms_material UP TO 1 ROWS.
    ENDSELECT.                                            "#EC CI_SUBRC

    ms_purchitem = VALUE #( mts_purchitem[ ebelp = iv_ebelp ] OPTIONAL ).
    ms_ekkn_vbak = VALUE #( mts_ekkn_vbak[ ebelp = iv_ebelp ] OPTIONAL ).

    READ TABLE mt_poaccount INTO ms_poaccount
          WITH KEY  po_item = iv_ebelp.                  "#EC CI_STDSEQ
    IF sy-subrc EQ 0.
      SELECT SINGLE soldtoparty,
                    shiptoparty
            FROM i_rfm_salesdocument
            WHERE salesdocument EQ @ms_poaccount-sd_doc
            INTO ( @ms_purchitem-soldto_kunnr, @ms_purchitem-shipto_kunnr ). "#EC CI_SUBRC
    ENDIF.

    select_sales_data( iv_ebelp ).

  ENDMETHOD.


  METHOD PRELOAD_PARTY_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 07.10.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*

    SELECT
      partnerfunction,
      supplier AS buspart
     FROM i_purchasingdocumentpartner WITH PRIVILEGED ACCESS
     WHERE purchasingdocument EQ @ms_poheader-po_number
     INTO TABLE @mt_patners.
    IF sy-subrc NE 0.
      CLEAR mt_patners.
    ENDIF.
  ENDMETHOD.


  METHOD READ_HARDCODE_VALUES.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*

    TRY.
        DATA(lt_hardcode) = zcl_pe_hm_basic=>get_table( '433' ).

      CATCH zcx_pe_hm INTO DATA(lo_exc_006).
        MESSAGE lo_exc_006.
    ENDTRY.

    LOOP AT lt_hardcode ASSIGNING FIELD-SYMBOL(<ls_line>).
      CASE <ls_line>-sequence.
        WHEN 1.
          ms_control_const-tradeco_model_value = <ls_line>-field2.
        WHEN OTHERS.
          DATA ls_cond_type LIKE LINE OF mt_rn_condtypes.
          CLEAR ls_cond_type.
          IF <ls_line>-field3 IS NOT INITIAL.
            ls_cond_type-sign = 'I'.
            ls_cond_type-option = 'EQ'.
            ls_cond_type-low = <ls_line>-field3.
            INSERT  ls_cond_type INTO TABLE mt_rn_condtypes.
          ENDIF.
      ENDCASE.
    ENDLOOP.
  ENDMETHOD.


  METHOD READ_TEXT_FOR_CUR_ITEM.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 03.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    DATA: lt_line TYPE STANDARD TABLE OF tline.

    CALL FUNCTION 'READ_TEXT'
      EXPORTING
        id       = iv_id
        language = gc_lang_en
        name     = CONV tdobname( ms_poheader-po_number && ms_purchitem-ebelp )
        object   = 'EKPO'
      TABLES
        lines    = lt_line
      EXCEPTIONS
        OTHERS   = 0.
    rv_text = REDUCE #( INIT lv_res = `` lv_sep = `` FOR ls_wa IN lt_line
                        NEXT lv_res = |{ lv_res }{ lv_sep }{ ls_wa-tdline }|  lv_sep = |\n| ).
  ENDMETHOD.


  METHOD SELECT_CONDT_TYPES.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*

    CHECK iv_kschl IN mr_type_code.

    SELECT knega, vtext, krech           "#EC CI_BUFFJOIN "#EC CI_SUBRC
          INTO TABLE @DATA(lt_data)
          FROM t685a AS a
          LEFT OUTER JOIN t685t AS t
                ON t~kappl = a~kappl AND
                   t~kschl = a~kschl
          WHERE a~kappl = @gc_kappl_m AND
                a~kschl = @iv_kschl   AND
                t~spras = @gc_lang_en.
    LOOP AT lt_data ASSIGNING FIELD-SYMBOL(<ls_data>).
      INSERT VALUE #( LET ls_prcd = get_cond_amount_sign_in_prcd( iv_kschl ) IN
                      zz_allow_cond_type  = iv_kschl
                      zz_cond_type_descr  = <ls_data>-vtext
                      zz_allow_charge_val = COND #( WHEN <ls_data>-krech CO 'BA' THEN ls_prcd-kbetr )
                      zz_per_unit         = COND #( WHEN <ls_data>-krech = 'C' THEN ls_prcd-kbetr )
                      zz_flat_rate = COND #( WHEN <ls_data>-krech  = 'B'  THEN gc_bool_str-true
                                             WHEN <ls_data>-krech CO 'AC' THEN gc_bool_str-false )
                      zz_cond_amount_sign = SWITCH #( <ls_data>-knega WHEN 'A' THEN 'C'
                                                                      WHEN 'X' THEN 'D'
                                                                      WHEN ' ' THEN ls_prcd-sign )
            ) INTO TABLE rt_cond_data.
    ENDLOOP.
  ENDMETHOD.


  METHOD SELECT_EKKN_DATA_FOR_PO.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    " there is no field BSTNK in any released CDSes
    SELECT  po~purchaseorderitem AS ebelp,
            vb~vbeln, vb~bstnk, vb~vdatu, vb~kunnr, vb~vtweg, vb~spart, vb~vkorg
          FROM i_purordaccountassignmentapi01 WITH PRIVILEGED ACCESS AS po
          INNER JOIN vbak AS vb
                ON vb~vbeln = po~salesorder
          WHERE po~purchaseorder = @ms_poheader-po_number
          INTO TABLE @mts_ekkn_vbak.
    IF sy-subrc <> 0.
      CLEAR: mts_ekkn_vbak.
    ENDIF.
  ENDMETHOD.


  METHOD select_ekpo_data_for_po.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
************************************************************************
* IVANOKON     | 22.05.2023 | 20957     : [Feature] [PF&E] Create cust *
*              |            | DS4K951468                               *
*----------------------------------------------------------------------*

    SELECT
            pi~purchasingdocumentitem AS ebelp,
            ext~zz1_tradeco_prec_no_pdi,
            ext~zz1_tradeco_prec_item_pdi,
            ext~zz1_postat_pdi,
            ext~zz1_adi_m_leadtimeplan_pdi,
            ext~zz1_adi_m_planned_pdi,
            pi~zz1_mm_exfactorydt_pdi,
            pi~customer AS zz_customer,
            cst~country AS kna1_country,
            pi~plant,
            add~country AS t001w_country,
            pi~zz1_fexfct_da_pdi,
            pi~zz1_lexfct_da_pdi,
            ext~zz1_mm_scmsegment_pdi,
            ext~zz1_ori_eligibility_pdi,
            pi~shippinginstruction AS zz_evers,
            pi~zz1_mm_vascutoffdate_pdi,
            pi~zz1_custin_pdi,
            pi~zz1_mdd_da_pdi,
            ext~zz1_packpln_da_pdi,
            ext~zz1_delay_conf_pdi,
            ext~zz1_delay_po_pdi,
            ext~zz1_delay_pln_conf_pdi,
            ext~zz1_delay_pln_wip_pdi,
            ext~zz1_custom_stat_pdi,
            pi~zz1_ordprio_pdi,
            ext~zz1_adi_m_promoflag_pdi,
            ext~zz1_prior_ind_pdi,
            ext~zz1_idd_da_pdi,
            ext~zz1_load_typ_pdi,
            ext~zz1_fprod_da_pdi,
            ext~zz1_lprod_da_pdi,
            ext~zz1_hts_eligibility_pdi,
            ext~zz1_form_pdi  ,
            ext~zz1_hts_curr_pdi,
            ext~zz1_hts_scope_pdi,
            ext~zz1_adi_req_avail_pdi,
            pi~iscompletelydelivered AS elikz,
            pi~purchasingdocumentdeletioncode AS loekz,
            pi~productcharacteristic2 AS zz_wrf_charstc2  ,
            pi~netamount AS zz_netwr   ,
            pi~crossplantconfigurableproduct AS zz_satnr,
            ext~zz1_mm_siden_pdi,
            ext~zz1_rerout_ind_pdi,
            ext~zz1_l2_group_pdi,
            ext~zz1_customer_mrg_pdi,
            ext~zz1_cut_off_da_pdi,
            ext~zz1_oc0_po_pdi,
            pd~referencedocumentnumber AS zz_rfm_ref_doc,
            pd~referencedocumentitem   AS zz_rfm_ref_item,
            pi~orderquantity           AS menge,
            pi~orderquantityunit       AS meins,
            pi~material                AS matnr,
            pi~PurchaseContract        AS konnr,
            pi~PurchaseContractItem    AS ktpnr,
            plt~plantcustomer          AS kunnr_wrk,
            plt~distributionchannel    AS vtweg_wrk,
            plt~division               AS spart_wrk,
            plt~salesorganization      AS vkorg_wrk
        FROM i_purchasingdocumentitem WITH PRIVILEGED ACCESS AS pi
        INNER JOIN e_purchasingdocumentitem WITH PRIVILEGED ACCESS AS ext
              ON ext~purchasingdocument EQ pi~purchasingdocument
             AND ext~purchasingdocumentitem EQ pi~purchasingdocumentitem
        LEFT OUTER JOIN i_customer WITH PRIVILEGED ACCESS AS cst
              ON cst~customer EQ pi~customer
        LEFT OUTER JOIN i_plant WITH PRIVILEGED ACCESS AS plt
              ON plt~plant EQ pi~plant
        LEFT OUTER JOIN i_address WITH PRIVILEGED ACCESS AS add
              ON add~addressid EQ plt~addressid
        LEFT OUTER JOIN i_purchasingdocumentitem WITH PRIVILEGED ACCESS AS pd
              ON pd~purchasingdocument EQ pi~purchasingdocument
             AND pd~purchasingdocumentitem EQ pi~purchasingdocumentitem
        WHERE pi~purchasingdocument EQ @ms_poheader-po_number
        INTO CORRESPONDING FIELDS OF TABLE @mts_purchitem  ##TOO_MANY_ITAB_FIELDS. "#EC CI_SUBRC

    mts_ekpo_st = CORRESPONDING #( mts_purchitem  ).

    SELECT pi~purchasingdocument AS ebeln,
           pi~purchasingdocumentitem AS ebelp,
           pi~PurchasingDocumentItemCategory AS zz_market_poitem_category,
           pi~AccountAssignmentCategory AS zz_market_poaccnt_assmnt_cat,
           pi~Plant AS zz_market_podc,
           pi~crossplantconfigurableproduct AS satnr,
           pi~\_PurchasingDocument-PurchasingDocumentType AS zz_market_podoc_type,
           pi~\_PurchasingDocument-CompanyCode AS zz_market_pocompany_code
      FROM i_purchasingdocumentitem WITH PRIVILEGED ACCESS AS pi
      INNER JOIN @mts_ekpo_st AS pitm
      ON pi~purchasingdocument = pitm~zz1_tradeco_prec_no_pdi
      AND pi~purchasingdocumentitem = pitm~zz1_tradEco_prec_item_pdi
      INTO TABLE @DATA(lt_data).


    SELECT poas~purchaseorder AS ebeln,
           poas~purchaseorderitem AS ebelp,
           sdtm~Plant AS werks,
           sdtm~\_ShipToParty-customer AS kunnr,
           sdtm~\_ShipToParty-Country AS zz_country,
           sdtm~\_ShipToParty-FreeDefinedAttribute04 AS zz_barcode_type,
           sdtm~\_SalesDocument-zz_purchaseorderbycustomer AS bstnk,
           sdtm~\_SalesDocument-requesteddeliverydate AS vdatu,
           sdpr~customer AS zz_customer_final_ship_to,
           sdpr~partnerfunction AS parvw
      FROM i_salesdocumentitem AS sdtm
      INNER JOIN i_purordaccountassignmentapi01 AS poas
      ON poas~purchaseorder = sdtm~SalesDocument
      AND poas~purchaseorderitem = sdtm~SalesDocumentItem
      INNER JOIN @mts_ekpo_st AS pitm
      ON poas~purchaseorder = pitm~zz1_tradeco_prec_no_pdi
      AND poas~purchaseorderitem = pitm~zz1_tradEco_prec_item_pdi
      LEFT OUTER JOIN i_salesdocumentpartner WITH PRIVILEGED ACCESS AS sdpr
      ON sdpr~salesdocument = poas~salesorder
      INTO TABLE @DATA(lt_vbap).


  ENDMETHOD.


  METHOD SELECT_MODEL_PART.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*

    SELECT zcust_model_part
     FROM zpd_t_mmcustom
     WHERE matnr EQ @ms_material-material
       AND ( country EQ @ms_purchitem-kna1_country OR country EQ @ms_purchitem-t001w_country )
       AND zcust_qualifier GE @ms_material-zz_size1
       AND zcust_date_from LE @sy-datlo
     INTO TABLE @DATA(lt_models).
    IF sy-subrc EQ 0.
      LOOP AT lt_models ASSIGNING FIELD-SYMBOL(<ls_item>).
        IF rv_models IS INITIAL.
          rv_models = <ls_item>-zcust_model_part.
        ELSE.
          rv_models = rv_models && ` ` && <ls_item>-zcust_model_part.
        ENDIF.
      ENDLOOP.
    ENDIF.
  ENDMETHOD.


  METHOD SELECT_PURCH_DOC.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 02.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    SELECT                                              "#EC CI_NOORDER
       lastchangedatetime AS zz_lastchangedatetime,
       supplier AS zz_supplier,
       zz1_mm_tradecomodel_pdh,
       purchasingdocumentcondition AS zz_knumv
       FROM i_purchasingdocument WITH PRIVILEGED ACCESS
       INNER JOIN e_purchasingdocument WITH PRIVILEGED ACCESS
       ON e_purchasingdocument~purchasingdocument EQ i_purchasingdocument~purchasingdocument
       WHERE  i_purchasingdocument~purchasingdocument EQ @ms_poheader-po_number
       INTO CORRESPONDING FIELDS OF @ms_purchdoc UP TO 1 ROWS .
    ENDSELECT.
    IF sy-subrc EQ 0
    AND ( ms_purchdoc-zz1_mm_tradecomodel_pdh EQ ms_control_const-tradeco_model_value
         OR
         ms_purchdoc-zz1_mm_tradecomodel_pdh IS INITIAL
        ).

      ms_control_const-tradeco_model_valid = abap_true.
    ENDIF.

  ENDMETHOD.


  METHOD SELECT_SALES_DATA.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 04.09.2022 | 13264     : PO from S/4 to P2            *
*              |            | DS4K928751                               *
*----------------------------------------------------------------------*
* TARUSVIT     | 06.02.2023 | 16876 Extension &  Data fe               *
*              |            | DS4K940329                               *
*----------------------------------------------------------------------*
    CHECK ms_poaccount IS NOT INITIAL.

    SELECT                          "#EC CI_SEL_NESTED  "#EC CI_NOORDER
        so~salesgroup,
        so~distributionchannel,
        si~requesteddeliverydate,
        so~requesteddeliverydate AS zz_vdatu
      FROM i_salesdocument WITH PRIVILEGED ACCESS AS so
      INNER JOIN i_salesdocumentitem WITH PRIVILEGED ACCESS AS si
      ON si~salesdocument EQ so~salesdocument
      WHERE so~salesdocument EQ @ms_poaccount-sd_doc
       AND salesdocumentitem EQ @ms_poaccount-itm_number
      INTO ( @ms_purchitem-zz_vkgrp, @ms_purchitem-zz_vtweg, @ms_purchitem-zz_vdatu_ana, @ms_purchitem-zz_vdatu )  UP TO 1 ROWS.
    ENDSELECT.                            "#EC CI_SUBRC "#EC CI_NOORDER
  ENDMETHOD.


  METHOD select_tradeco_po_data.
    TYPES:
      BEGIN OF ltyp_s_temp_tradeco,
        zz1_tradeco_prec_no_pdi   TYPE ekpo-ebeln,
        zz1_tradeco_prec_item_pdi TYPE ekpo-ebelp,
      END OF ltyp_s_temp_tradeco.

    TYPES:
      BEGIN OF mtyp_s_tradeco_POdoc,
        ebeln TYPE ebeln,
        ebelp TYPE ebelp,
        pstyp TYPE pstyp,
        knttp TYPE knttp,
        werks TYPE ewerk,
        satnr TYPE satnr,
        bsart TYPE esart,
        bukrs TYPE bukrs,
      END OF mtyp_s_tradeco_POdoc.

    TYPES:
      BEGIN OF mtyp_s_tradeco_SDdoc,
        ebeln                     TYPE ebeln,
        ebelp                     TYPE ebelp,
        parvw                     TYPE parvw,
        werks                     TYPE ewerk,
        kunnr                     TYPE kunnr,
        land1                     TYPE land1_gp,
        katr4                     TYPE katr4,
        bstnk                     TYPE bstnk,
        vdatu                     TYPE edatu_vbak,
        zz_customer_final_ship_to TYPE kunnr,
      END OF mtyp_s_tradeco_SDdoc.




    DATA:
      lt_temp_tradeco TYPE SORTED TABLE OF ltyp_s_temp_tradeco WITH NON-UNIQUE KEY zz1_tradeco_prec_no_pdi zz1_tradeco_prec_item_pdi.

    lt_temp_tradeco = CORRESPONDING #( mts_purchitem ).

    SELECT pi~purchasingdocument AS ebeln,
           pi~purchasingdocumentitem AS ebelp,
           pi~PurchasingDocumentItemCategory AS pstyp,
           pi~AccountAssignmentCategory AS knttp,
           pi~Plant AS werks,
           pi~crossplantconfigurableproduct AS satnr,
           pi~\_PurchasingDocument-PurchasingDocumentType AS bsart,  " zz_market_podoc_type,
           pi~\_PurchasingDocument-CompanyCode AS bukrs " zz_market_pocompany_code
      FROM i_purchasingdocumentitem WITH PRIVILEGED ACCESS AS pi
      INNER JOIN @lt_temp_tradeco AS pitm
      ON pi~purchasingdocument = pitm~zz1_tradeco_prec_no_pdi
      AND pi~purchasingdocumentitem = pitm~zz1_tradEco_prec_item_pdi
      INTO TABLE @DATA(lt_data).


    SELECT poas~purchaseorder AS ebeln,
           poas~purchaseorderitem AS ebelp,
           sdtm~Plant AS werks,
           sdtm~\_ShipToParty-customer AS kunnr,
           sdtm~\_ShipToParty-Country AS land1, " zz_country,
           sdtm~\_ShipToParty-FreeDefinedAttribute04 AS katr4, "zz_barcode_type,
           sdtm~\_SalesDocument-zz_purchaseorderbycustomer AS bstnk,
           sdtm~\_SalesDocument-requesteddeliverydate AS vdatu,
           sdpr~customer AS zz_customer_final_ship_to,
           sdpr~partnerfunction AS parvw
      FROM i_salesdocumentitem AS sdtm
      INNER JOIN i_purordaccountassignmentapi01 AS poas
      ON poas~purchaseorder = sdtm~SalesDocument
      AND poas~purchaseorderitem = sdtm~SalesDocumentItem
      INNER JOIN @lt_temp_tradeco AS pitm
      ON poas~purchaseorder = pitm~zz1_tradeco_prec_no_pdi
      AND poas~purchaseorderitem = pitm~zz1_tradEco_prec_item_pdi
      LEFT OUTER JOIN i_salesdocumentpartner WITH PRIVILEGED ACCESS AS sdpr
      ON sdpr~salesdocument = poas~salesorder
      INTO TABLE @DATA(lt_vbap).




  ENDMETHOD.
ENDCLASS.