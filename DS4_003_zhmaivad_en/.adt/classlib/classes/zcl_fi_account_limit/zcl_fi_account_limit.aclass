CLASS zcl_fi_account_limit DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES:
      if_sadl_exit_calc_element_read.
  PROTECTED SECTION.
private section.

  constants MC_TEXT_ID type TDID value 'UKM' ##NO_TEXT.
  constants MC_APPL_OBJECT type TDOBJECT value 'BUT000' ##NO_TEXT.
ENDCLASS.



CLASS ZCL_FI_ACCOUNT_LIMIT IMPLEMENTATION.


  METHOD if_sadl_exit_calc_element_read~get_calculation_info.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* PISHCALI     | 13.09.2022 | 13391: FI_BE BUILD - Limit Custom Report *
*              |            | DS4K929386                               *
*----------------------------------------------------------------------*
    " do nothing
    CLEAR et_requested_orig_elements.
  ENDMETHOD.


  METHOD if_sadl_exit_calc_element_read~calculate.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* PISHCALI     | 13.09.2022 | 13391: FI_BE BUILD - Limit Custom Report *
*              |            | DS4K929386                               *
*----------------------------------------------------------------------*

    TYPES:
      BEGIN OF ltyp_partner_buf,
        partner_number TYPE zfi_i_lim_rep-businesspartnernumber,
        limit_value    TYPE zfi_i_lim_rep-accountlimit,
      END OF ltyp_partner_buf.

    DATA:
      ls_partner         TYPE ltyp_partner_buf,
      lts_partner_buf    TYPE SORTED TABLE OF ltyp_partner_buf WITH UNIQUE KEY partner_number,
      lv_partner_number  TYPE thead-tdname,
      lt_lines           TYPE TABLE OF tline,
      lt_calculated_data TYPE STANDARD TABLE OF zfi_i_lim_rep.

    lt_calculated_data = CORRESPONDING #( it_original_data ).

    LOOP AT lt_calculated_data ASSIGNING FIELD-SYMBOL(<ls_calculated_data>).

      READ TABLE lts_partner_buf ASSIGNING FIELD-SYMBOL(<ls_partner_buf>)
        WITH TABLE KEY partner_number = <ls_calculated_data>-businesspartnernumber.

      IF sy-subrc = 0.
        <ls_calculated_data>-accountlimit = <ls_partner_buf>-limit_value.
      ELSE.

        lv_partner_number = <ls_calculated_data>-businesspartnernumber.

        CALL FUNCTION 'READ_TEXT' ##FM_SUBRC_OK
          EXPORTING
            client                  = sy-mandt
            id                      = mc_text_id
            language                = sy-langu
            name                    = lv_partner_number
            object                  = mc_appl_object
          TABLES
            lines                   = lt_lines
          EXCEPTIONS
            id                      = 1
            language                = 2
            name                    = 3
            not_found               = 4
            object                  = 5
            reference_check         = 6
            wrong_access_to_archive = 7
            OTHERS                  = 8. "#EC CI_SUBRC

        " If there is no data, we still save it to the buffer to avoid multiple FM calls
        ls_partner-partner_number = <ls_calculated_data>-businesspartnernumber.
        ls_partner-limit_value    = VALUE #( lt_lines[ 1 ]-tdline OPTIONAL ).

        <ls_calculated_data>-accountlimit =  ls_partner-limit_value.

        INSERT ls_partner INTO TABLE lts_partner_buf.

      ENDIF.

      CLEAR ls_partner.

    ENDLOOP.

    ct_calculated_data = CORRESPONDING #( lt_calculated_data ).

  ENDMETHOD.
ENDCLASS.