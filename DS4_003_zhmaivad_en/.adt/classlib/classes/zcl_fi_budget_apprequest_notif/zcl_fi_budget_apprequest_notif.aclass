class ZCL_FI_BUDGET_APPREQUEST_NOTIF definition
  public
  inheriting from /IWNGW/CL_NOTIF_PROVIDER_ABS
  final
  create public .

public section.

  class-methods SEND_NOTIFICATION
    importing
      !IV_APPREQUEST type IMA_POSID
      !IT_RECIPIENT type /IWNGW/IF_NOTIF_PROVIDER=>TY_T_NOTIFICATION_RECIPIENT .

  methods /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_PARAMETERS
    redefinition .
  methods /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_TYPE
    redefinition .
  methods /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_TYPE_TEXT
    redefinition .
protected section.
private section.
ENDCLASS.



CLASS ZCL_FI_BUDGET_APPREQUEST_NOTIF IMPLEMENTATION.


  METHOD /iwngw/if_notif_provider_ext~get_notification_parameters.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* IAPRYALE     | 12.12.2022 | 15964 : Budget data to be copied directly*
*              |            | from appropriation request approval      *
*              |            | DS4K941606                               *
*----------------------------------------------------------------------*

    RETURN.

  ENDMETHOD.


  METHOD /iwngw/if_notif_provider_ext~get_notification_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* IAPRYALE     | 12.12.2022 | 15964 : Budget data to be copied directly*
*              |            | from appropriation request approval      *
*              |            | DS4K941606                               *
*----------------------------------------------------------------------*

    es_notification_type-is_groupable = abap_true.
    es_notification_type-type_key     = iv_type_key.
    es_notification_type-version      = iv_type_version.

  ENDMETHOD.


  METHOD /iwngw/if_notif_provider_ext~get_notification_type_text.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* IAPRYALE     | 12.12.2022 | 15964 : Budget data to be copied directly*
*              |            | from appropriation request approval      *
*              |            | DS4K941606                               *
*----------------------------------------------------------------------*

    DATA:
      lv_text     TYPE string,
      lv_subtitle TYPE string.

    CLEAR et_type_text.

    lv_text     = TEXT-001.
    lv_subtitle = TEXT-002.

    REPLACE '&1' WITH '{APPREQUEST}' INTO lv_subtitle.

    et_type_text = VALUE #(
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_public     value = lv_text )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_sensitive  value = lv_text )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_grouped    value = lv_text )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-subtitle            value = lv_subtitle ) ).

  ENDMETHOD.


  METHOD send_notification.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* IAPRYALE     | 12.12.2022 | 15964 : Budget data to be copied directly*
*              |            | from appropriation request approval      *
*              |            | DS4K941606                               *
*----------------------------------------------------------------------*

    CONSTANTS:
      lc_type_key_budget    TYPE /iwngw/if_notif_provider=>ty_s_notification_id-type_key VALUE 'ZFI_BUDGET_APPREQUEST_NOTIF',
      lc_provider_id_budget TYPE /iwngw/if_notif_provider=>ty_s_provider-id VALUE 'ZFI_BUDGET_APPREQUEST_NOTIF'.

    DATA:
      lt_notif          TYPE /iwngw/if_notif_provider=>ty_t_notification,
      lt_param_bundle   TYPE /iwngw/if_notif_provider=>ty_t_notification_param_bundle,
      lt_cont_parameter TYPE /iwngw/if_notif_provider=>ty_t_notification_parameter.

    TRY.
        DATA(lif_system_uuid) = cl_uuid_factory=>create_system_uuid( ).

        lt_cont_parameter =  VALUE #( ( name  = 'APPREQUEST'
                                        type  = /iwngw/if_notif_provider=>gcs_parameter_types-type_string
                                        value = iv_apprequest ) ).

        lt_param_bundle =  VALUE #( ( language   = sy-langu
                                      parameters = lt_cont_parameter ) ).

        lt_notif =  VALUE #( ( id                 = lif_system_uuid->create_uuid_x16( )
                               type_key           = lc_type_key_budget
                               type_version       = '7'
                               priority           = /iwngw/if_notif_provider=>gcs_priorities-high
                               recipients         = it_recipient
                               parameters         = lt_param_bundle ) ).


        /iwngw/cl_notification_api=>create_notifications( iv_provider_id  = lc_provider_id_budget
                                                          it_notification = lt_notif ).
      CATCH cx_uuid_error /iwngw/cx_notification_api. ##NO_HANDLER.
        RETURN.
    ENDTRY.

  ENDMETHOD.
ENDCLASS.