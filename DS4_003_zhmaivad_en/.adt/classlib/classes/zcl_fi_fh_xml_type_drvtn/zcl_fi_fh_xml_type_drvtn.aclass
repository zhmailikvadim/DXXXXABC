CLASS zcl_fi_fh_xml_type_drvtn DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_badi_interface .
    INTERFACES /pf1/if_badi_fh_xml_type_drvtn .
  PROTECTED SECTION.

  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_fi_fh_xml_type_drvtn IMPLEMENTATION.


  METHOD /pf1/if_badi_fh_xml_type_drvtn~get_po_type_drvtn_ind.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* PRATAMAN     | 21.11.2023 | 26964 : APM PO derivation for TMS        *
*                             internal payments                        *
*              |            | DS4K971232                               *
*----------------------------------------------------------------------*
    CONSTANTS: lc_internal_ind TYPE char3 VALUE 'INT',
               lc_external_ind TYPE char3 VALUE 'EXT',
               lc_external_val TYPE char1 VALUE 'E',
               lc_internal_val TYPE char1 VALUE 'I',
               lc_tag1         TYPE string VALUE 'E1IDKU3',
               lc_tag2         TYPE string VALUE 'PAIRZAWE',
               lc_object       TYPE cl_bali_header_setter=>ty_object  VALUE 'ZTREASURY',
               lc_subobject    TYPE cl_bali_header_setter=>ty_subobject  VALUE 'ZTREASURY'.

    TRY.
        ASSIGN it_order_tags[ 1 ] TO FIELD-SYMBOL(<ls_ordertag>).

        IF <ls_ordertag> IS ASSIGNED.
          DATA(lt_child_tags) = <ls_ordertag>-xml_tag->t_child_tags.
        ELSE.
          RETURN.
        ENDIF.

        LOOP AT lt_child_tags ASSIGNING FIELD-SYMBOL(<lo_tag>) .
          IF <lo_tag>->tag = lc_tag1 AND <lo_tag> IS ASSIGNED.
            ASSIGN <lo_tag>->t_child_tags TO FIELD-SYMBOL(<lt_child>) .
          ENDIF.
        ENDLOOP.

        IF <lt_child> IS ASSIGNED.
          LOOP AT <lt_child> ASSIGNING FIELD-SYMBOL(<lo_child>).
            IF <lo_child>->tag = lc_tag2 AND <lo_child> IS ASSIGNED.
              DATA(lv_value) = <lo_child>->value .
            ENDIF.
          ENDLOOP.
        ELSE.
          RETURN.
        ENDIF.

        CASE lv_value.
          WHEN lc_external_val.
            cv_derivation_indicator = lc_external_ind.
          WHEN lc_internal_val.
            cv_derivation_indicator = lc_internal_ind.
          WHEN OTHERS.
            CLEAR cv_derivation_indicator.
        ENDCASE.

      CATCH cx_sy_itab_line_not_found.
        TRY.
            DATA(lif_log) = cl_bali_log=>create_with_header( cl_bali_header_setter=>create( object    = lc_object
                                                                                            subobject = lc_subobject ) ).

            DATA(lif_message) = cl_bali_message_setter=>create( severity = if_bali_constants=>c_severity_error
                                                                id       = 'Z_TREASURY'
                                                                number   = '000' ).
            lif_log->add_item( item = lif_message ).

            cl_bali_log_db=>get_instance( )->save_log( log = lif_log ).
          CATCH cx_bali_runtime ##NO_HANDLER.
        ENDTRY.
    ENDTRY.

  ENDMETHOD.
ENDCLASS.