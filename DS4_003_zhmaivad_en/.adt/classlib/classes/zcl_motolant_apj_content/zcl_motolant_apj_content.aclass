CLASS zcl_motolant_apj_content DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

  PRIVATE SECTION.

    METHODS create_apj_content
      IMPORTING
        io_apj_dt_handler     TYPE REF TO cl_apj_dt_create_content
        iv_transport_request  TYPE cl_apj_dt_create_content=>ty_transport_request
        iv_dev_package        TYPE cl_apj_dt_create_content=>ty_package
        iv_apj_catalog_name   TYPE cl_apj_dt_create_content=>ty_catalog_name
        iv_apj_catalog_text   TYPE cl_apj_dt_create_content=>ty_text
        iv_apj_catalog_class  TYPE cl_apj_dt_create_content=>ty_class_name
        iv_apj_template_name  TYPE cl_apj_dt_create_content=>ty_template_name
        iv_apj_template_text  TYPE cl_apj_dt_create_content=>ty_text
      RETURNING
        VALUE(rv_successfull) TYPE abap_bool
      RAISING
        cx_apj_dt_content.
    METHODS delete_apj_content
      IMPORTING
        io_apj_dt_handler     TYPE REF TO cl_apj_dt_create_content
        iv_transport_request  TYPE cl_apj_dt_create_content=>ty_transport_request
        iv_apj_catalog_name   TYPE cl_apj_dt_create_content=>ty_catalog_name
        iv_apj_template_name  TYPE cl_apj_dt_create_content=>ty_template_name
      RETURNING
        VALUE(rv_successfull) TYPE abap_bool
      RAISING
        cx_apj_dt_content.

ENDCLASS.



CLASS zcl_motolant_apj_content IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    CONSTANTS:
      " General data
      lc_request       TYPE cl_apj_dt_create_content=>ty_transport_request VALUE 'DS4K949140',
      lc_package       TYPE cl_apj_dt_create_content=>ty_package VALUE 'Z_SO_CONVERSION_TO_DEDICATED',
      " Job Catalog data
      lc_catalog_name  TYPE cl_apj_dt_create_content=>ty_catalog_name VALUE 'ZSD_JC_SO_CONVERSION_FIELD_CLC',
      lc_catalog_text  TYPE cl_apj_dt_create_content=>ty_text VALUE 'SO Conversion Tool: Fields Calculation',
      lc_catalog_class TYPE cl_apj_dt_create_content=>ty_class_name VALUE 'ZCL_SD_SO_CONV_CALC_FIELDS_APJ',
      " Job Template data
      lc_template_name TYPE cl_apj_dt_create_content=>ty_template_name VALUE 'ZSD_JT_SO_CONVERSION_FIELD_CLC',
      lc_template_text TYPE cl_apj_dt_create_content=>ty_text VALUE 'SO Conversion Tool: Fields Calculation'.


    DATA:
      lo_apj_dt_handler   TYPE REF TO cl_apj_dt_create_content,
      lo_apj_dt_exception TYPE REF TO cx_apj_dt_content,
      lv_successfull      TYPE abap_bool.

    DATA:
      lv_action TYPE c LENGTH 1 VALUE 'C'.


    lo_apj_dt_handler = cl_apj_dt_create_content=>get_instance( ).


    CASE lv_action.
      WHEN 'C'.
        TRY.
            lv_successfull = create_apj_content(
              io_apj_dt_handler    = lo_apj_dt_handler
              iv_transport_request = lc_request
              iv_dev_package       = lc_package
              iv_apj_catalog_name  = lc_catalog_name
              iv_apj_catalog_text  = lc_catalog_text
              iv_apj_catalog_class = lc_catalog_class
              iv_apj_template_name = lc_template_name
              iv_apj_template_text = lc_template_text
            ).
          CATCH cx_apj_dt_content INTO lo_apj_dt_exception.
            out->write( lo_apj_dt_exception->get_longtext( ) ).
            out->write( 'APJ content creation failed' ).
            RETURN.
        ENDTRY.

      WHEN 'D'.
        TRY.
            lv_successfull = delete_apj_content(
              io_apj_dt_handler    = lo_apj_dt_handler
              iv_transport_request = lc_request
              iv_apj_catalog_name  = lc_catalog_name
              iv_apj_template_name = lc_template_name
            ).
          CATCH cx_apj_dt_content INTO lo_apj_dt_exception.
            out->write( lo_apj_dt_exception->get_longtext( ) ).
            out->write( 'APJ content deletion failed' ).
            RETURN.
        ENDTRY.

      WHEN OTHERS.
        lv_successfull = abap_false.
    ENDCASE.

    IF lv_successfull = abap_true.
      out->write( 'Success' ).
    ELSE.
      out->write( 'Error' ).
    ENDIF.


  ENDMETHOD.

  METHOD create_apj_content.

    rv_successfull = io_apj_dt_handler->create_job_cat_entry(
      iv_catalog_name      = iv_apj_catalog_name
      iv_text              = iv_apj_catalog_text
      iv_class_name        = iv_apj_catalog_class
      iv_transport_request = iv_transport_request
      iv_package           = iv_dev_package
    ).

    rv_successfull = io_apj_dt_handler->create_job_template_entry(
      iv_template_name     = iv_apj_template_name
      iv_text              = iv_apj_template_text
      iv_catalog_name      = iv_apj_catalog_name
      iv_transport_request = iv_transport_request
      iv_package           = iv_dev_package
    ).

  ENDMETHOD.

  METHOD delete_apj_content.

    rv_successfull = io_apj_dt_handler->delete_job_template_entry(
      iv_template_name     = iv_apj_template_name
      iv_transport_request = iv_transport_request
    ).

    rv_successfull = io_apj_dt_handler->delete_job_cat_entry(
      iv_catalog_name      = iv_apj_catalog_name
      iv_transport_request = iv_transport_request
    ).

  ENDMETHOD.

ENDCLASS.