class ZCL_PD_COMMODITY_CODE_PROC definition
  public
  create public .

public section.

  constants:
    BEGIN OF gc_log_attributes,
      message_id_cust      TYPE symsgid VALUE 'ZPD_COMMOD_CODE',
      message_id_aif       TYPE symsgid VALUE 'BAPI',
      message_type_success TYPE symsgty VALUE 'S',
      message_type_error   TYPE symsgty VALUE 'E',
      message_no_0         TYPE symsgno VALUE '000',
      message_no_1         TYPE symsgno VALUE '1',
      message_no_2         TYPE symsgno VALUE '2',
      message_no_3         TYPE symsgno VALUE '3',
    END OF gc_log_attributes .

  methods CONSTRUCTOR
    importing
      !IT_INPUT_TABLE type ZPD_TT_COMMOD_CODE_SAP .
  methods COMM_CODE_PROCCESSING .
  methods GET_LOG_MESS
    returning
      value(RT_LOG) type BAPIRET2_T .
protected section.
private section.

  types:
    GTYP_TT_MMCUSTOM TYPE STANDARD TABLE OF zpd_t_mmcustom .

  data MT_MESSAGES_LOG type BAPIRET2_T .
  data MT_PROCESS_DATA type ZPD_TT_COMMOD_CODE_SAP .
  data MT_MMCUSTOM type GTYP_TT_MMCUSTOM .

  methods CHECK_LINE_FOR_DELETION
    importing
      !IS_LINE type ZPD_S_COMMOD_CODE_SAP
    returning
      value(RV_RESULT) type BOOLEAN .
  methods ADD_LOG_MESS
    importing
      !IV_MESS_TYPE type SYMSGTY
      !IV_MESS_NO type SYMSGNO .
ENDCLASS.



CLASS ZCL_PD_COMMODITY_CODE_PROC IMPLEMENTATION.


  METHOD ADD_LOG_MESS.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 10.10.2022 | 14034     : [Platform] INTGR             *
*              |            | DS4K931670                               *
*----------------------------------------------------------------------*

    /aif/cl_appl_log_writer=>convert_msg_to_bapiret(
      EXPORTING
        iv_msgty      = iv_mess_type
        iv_msgid      = gc_log_attributes-message_id_cust
        iv_msgno      = iv_mess_no
      IMPORTING
        et_return_tab = DATA(lt_return_tab_log_data) ).

    APPEND LINES OF lt_return_tab_log_data TO mt_messages_log.

  ENDMETHOD.


  METHOD comm_code_proccessing.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 10.10.2022 | 14034     : [Platform] INTGR             *
*              |            | DS4K931670                               *
*----------------------------------------------------------------------*
* KHRYSAND     | 18.10.2022 | 14034     : [Platform] INTGR             *
*              |            | DS4K931670                               *
*----------------------------------------------------------------------*
* KHRYSAND     | 10.01.2023 | 16789     : CR - Deletion fla            *
*              |            | DS4K940027                               *
*----------------------------------------------------------------------*
* RAPANKON     | 27.01.2023 | 17453     : [Build] [CR] - Commodity Codes interface*
*              |            | DS4K942057                               *
*----------------------------------------------------------------------*
* PEREDSER     | 16-AUG-2023| DS4K961001   FB_24359_PD_PDM-M BUILD     *
*              |            | Fix Material S4 Standard Out             *
*----------------------------------------------------------------------*
* KHRYSAND     | 25.10.2023 | 26266     : PD_Performance issues in RS4 *
*              |            | DS4K968222                               *
*----------------------------------------------------------------------*
* NEVMYOLE     | 01.11.2023 | 26291 IFI Number and Quota to be include *
*              |            | DS4K968767                               *
*----------------------------------------------------------------------*


    DATA: lt_mmcustom_new  TYPE TABLE OF zpd_t_mmcustom,
          lt_mmcustom_upd  TYPE TABLE OF zpd_t_mmcustom,
          ls_comm_code_new TYPE zpd_t_mmcustom.

    IF mt_process_data IS INITIAL.
      add_log_mess( iv_mess_type = gc_log_attributes-message_type_error
                    iv_mess_no   = gc_log_attributes-message_no_2 ).

      MESSAGE i002(zpd_commod_code) INTO DATA(lv_msg)        ##NEEDED.
      RETURN.
    ENDIF.

    LOOP AT mt_process_data ASSIGNING FIELD-SYMBOL(<ls_process_data>).
      IF check_line_for_deletion( <ls_process_data> ) = abap_true.
        CONTINUE.
      ENDIF.

      ASSIGN mt_mmcustom[ matnr            = <ls_process_data>-matnr
                          country          = <ls_process_data>-country
                          zcust_model_part = <ls_process_data>-zcust_model_part
                          zcust_qualifier  = <ls_process_data>-zcust_qualifier
                          zcust_date_from  = <ls_process_data>-zcust_date_from
                          zcust_date_to    = <ls_process_data>-zcust_date_to ] TO FIELD-SYMBOL(<ls_mmcustom>). "#EC CI_STDSEQ

      IF sy-subrc = 0.
        <ls_mmcustom> = VALUE #( BASE <ls_mmcustom> zcust_tariff      = <ls_process_data>-zcust_tariff
                                                    zcust_tariffdescr = <ls_process_data>-zcust_tariffdescr
                                                    zcust_ifinumber   = <ls_process_data>-zcust_ifinumber
                                                    zcust_quota       = <ls_process_data>-zcust_quota
                                                    udate             = sy-datum
                                                    aenam             = sy-uname
                                                    lkenz             = abap_false ).
        APPEND <ls_mmcustom> TO lt_mmcustom_upd[].
      ELSE.
        ls_comm_code_new = CORRESPONDING #( <ls_process_data> )  ##ENH_OK.
        ls_comm_code_new = VALUE #( BASE ls_comm_code_new ctnam         = sy-uname
                                                          aenam         = sy-uname
                                                          ersda         = sy-datum
                                                          zcust_groupid = |{ ls_comm_code_new-matnr }{ ls_comm_code_new-country }|
                                                          udate         = sy-datum ).
        APPEND ls_comm_code_new TO lt_mmcustom_new.
      ENDIF.
    ENDLOOP.

    LOOP AT mt_mmcustom ASSIGNING <ls_mmcustom>.
      ASSIGN mt_process_data[ matnr            = <ls_mmcustom>-matnr
                              country          = <ls_mmcustom>-country
                              zcust_model_part = <ls_mmcustom>-zcust_model_part
                              zcust_qualifier  = <ls_mmcustom>-zcust_qualifier
                              zcust_date_from  = <ls_mmcustom>-zcust_date_from
                              zcust_date_to    = <ls_mmcustom>-zcust_date_to ] TO <ls_process_data>. "#EC CI_STDSEQ

      IF sy-subrc <> 0 AND
         line_exists( mt_process_data[ matnr = <ls_mmcustom>-matnr ] ) AND
         <ls_mmcustom>-lkenz = abap_false.
        <ls_mmcustom> = VALUE #( BASE <ls_mmcustom> lkenz = abap_true
                                                    udate = sy-datum
                                                    aenam = sy-uname ). "#EC CI_STDSEQ
        APPEND <ls_mmcustom> TO lt_mmcustom_upd[].
      ENDIF.
    ENDLOOP.

    IF lines( lt_mmcustom_upd ) > 0.
      UPDATE zpd_t_mmcustom FROM TABLE lt_mmcustom_upd.
      IF sy-subrc <> 0.
        add_log_mess( iv_mess_type = gc_log_attributes-message_type_error
                      iv_mess_no   = gc_log_attributes-message_no_3 ).

        MESSAGE i003(zpd_commod_code) INTO lv_msg        ##NEEDED.
        RETURN.
      ENDIF.
    ENDIF.

    IF lines( lt_mmcustom_new ) > 0.
      INSERT zpd_t_mmcustom FROM TABLE lt_mmcustom_new.   "#EC CI_SUBRC
      IF sy-subrc IS NOT INITIAL.
        ROLLBACK WORK.
        add_log_mess( iv_mess_type = gc_log_attributes-message_type_error
                      iv_mess_no   = gc_log_attributes-message_no_3 ).
        zcl_pd_drftrig_4custables=>process_mmcustom( it_mmcustom_1 = lt_mmcustom_upd ).
        RETURN.
      ENDIF.
    ENDIF.

    IF lt_mmcustom_upd[] IS NOT INITIAL.
      zcl_pd_drftrig_4custables=>process_mmcustom( it_mmcustom_1 = lt_mmcustom_new it_mmcustom_2 = lt_mmcustom_upd ).
    ELSE.
      zcl_pd_drftrig_4custables=>process_mmcustom( it_mmcustom_1 = lt_mmcustom_new ).
    ENDIF.

    add_log_mess( iv_mess_type = gc_log_attributes-message_type_success
                    iv_mess_no   = gc_log_attributes-message_no_1 ).

    MESSAGE i001(zpd_commod_code) INTO lv_msg        ##NEEDED.
    COMMIT WORK AND WAIT.

  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 10.10.2022 | 14034     : [Platform] INTGR             *
*              |            | DS4K931670                               *
* KHRYSAND     | 25.10.2023 | 26266     : PD_Performance issues in RS4 *
*              |            | DS4K968222                               *
*----------------------------------------------------------------------*

    CHECK lines( it_input_table ) > 0.

    mt_process_data = it_input_table.

    SELECT *
    FROM zpd_t_mmcustom AS mmcustom
    FOR ALL ENTRIES IN @it_input_table
    WHERE  mmcustom~matnr = @it_input_table-matnr
    INTO TABLE @mt_mmcustom.   "#EC CI_FAE_LINES_ENSURED  "#EC CI_SUBRC

  ENDMETHOD.


  method GET_LOG_MESS.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 10.10.2022 | 14034     : [Platform] INTGR             *
*              |            | DS4K931670                               *
*----------------------------------------------------------------------*

    rt_log = mt_messages_log.

  endmethod.


  METHOD check_line_for_deletion.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 27.01.2023 | 17453     : [Build] [CR] - Commodity Codes interface*
*              |            | DS4K942057                               *
*----------------------------------------------------------------------*

    IF is_line IS INITIAL.
      RETURN.
    ENDIF.

    IF is_line-matnr            IS NOT INITIAL AND
       is_line-country          IS INITIAL AND
       is_line-zcust_model_part IS INITIAL AND
       is_line-zcust_qualifier  IS INITIAL AND
       is_line-zcust_date_from  IS INITIAL AND
       is_line-zcust_date_to    IS INITIAL.
      rv_result = abap_true.
    ENDIF.

  ENDMETHOD.
ENDCLASS.