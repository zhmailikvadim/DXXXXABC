CLASS zcl_pd_plant_type_determine DEFINITION
  PUBLIC
  FINAL
  CREATE PRIVATE.

  PUBLIC SECTION.
    DATA mv_plant TYPE werks_d READ-ONLY.

    DATA mv_valuation_area TYPE bwkey READ-ONLY.

    DATA mv_plant_type TYPE zpd_zvlfkz READ-ONLY.

    DATA mv_local_reference_store TYPE werks_d READ-ONLY.

    CONSTANTS: BEGIN OF gc_plant_type,
                 real_store       TYPE zpd_zvlfkz VALUE 'A',
                 local_ref_store  TYPE zpd_zvlfkz VALUE 'AL',
                 global_ref_store TYPE zpd_zvlfkz VALUE 'AG',
                 real_dc          TYPE zpd_zvlfkz VALUE 'B',
                 global_ref_dc    TYPE zpd_zvlfkz VALUE 'BG',
               END OF gc_plant_type.
    CLASS-DATA: BEGIN OF gs_global_reference READ-ONLY,
                  store               TYPE zpd_i_globalretailsettings-referencestore,
                  distribution_center TYPE zpd_i_globalretailsettings-referencedistributioncenter,
                END OF gs_global_reference.

    CLASS-METHODS class_constructor.

    CLASS-METHODS get_instance_by_plant IMPORTING iv_plant         TYPE werks_d
                                        RETURNING VALUE(ro_result) TYPE REF TO zcl_pd_plant_type_determine.

    CLASS-METHODS get_instance_by_valuation_area IMPORTING iv_valuation_area TYPE bwkey
                                                 RETURNING VALUE(ro_result)  TYPE REF TO zcl_pd_plant_type_determine.

    METHODS get_highier_instance RETURNING VALUE(ro_result) TYPE REF TO zcl_pd_plant_type_determine.

    METHODS get_plant_by_type IMPORTING iv_type          TYPE zpd_zvlfkz
                              RETURNING VALUE(rv_result) TYPE werks_d.

    METHODS get_valuation_area_by_type IMPORTING iv_type          TYPE zpd_zvlfkz
                                       RETURNING VALUE(rv_result) TYPE bwkey.

    METHODS is_reference RETURNING VALUE(rv_result) TYPE abap_bool.

  PROTECTED SECTION.

  PRIVATE SECTION.
    TYPES: BEGIN OF gtyp_s_instance_buffer,
             plant          TYPE werks_d,
             valuation_area TYPE bwkey,
             instance       TYPE REF TO zcl_pd_plant_type_determine,
           END OF gtyp_s_instance_buffer.
    CLASS-DATA: gth_buffer_instances TYPE HASHED TABLE OF gtyp_s_instance_buffer WITH UNIQUE KEY plant
                                                                                 WITH UNIQUE HASHED KEY valuation_area COMPONENTS valuation_area.

    METHODS set_plant IMPORTING iv_plant TYPE werks_d.

    METHODS set_valuation_area IMPORTING iv_valuation_area TYPE bwkey.

    METHODS get_plant_type IMPORTING iv_type_in_db    TYPE i_plant-plantcategory
                           RETURNING VALUE(rv_result) TYPE zpd_zvlfkz.
ENDCLASS.



CLASS zcl_pd_plant_type_determine IMPLEMENTATION.


  METHOD class_constructor.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    SELECT FROM zpd_i_globalretailsettings
           FIELDS referencestore AS store,
                  referencedistributioncenter AS distribution_center
           INTO CORRESPONDING FIELDS OF @gs_global_reference
           UP TO 1 ROWS.
    ENDSELECT.
    IF sy-subrc <> 0.
      CLEAR gs_global_reference.
    ENDIF.
  ENDMETHOD.


  METHOD get_instance_by_plant.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    READ TABLE gth_buffer_instances ASSIGNING FIELD-SYMBOL(<ls_buffer>)
         WITH KEY plant = iv_plant.
    IF sy-subrc <> 0.
      IF lines( gth_buffer_instances ) > 100.
        CLEAR gth_buffer_instances.
      ENDIF.

      DATA(lo_new_instance) = NEW zcl_pd_plant_type_determine( ).
      lo_new_instance->set_plant( iv_plant ).

      INSERT VALUE #( plant          = lo_new_instance->mv_plant
                      valuation_area = lo_new_instance->mv_valuation_area
                      instance       = lo_new_instance )
             INTO TABLE gth_buffer_instances
             ASSIGNING <ls_buffer>.
    ENDIF.
    ro_result = <ls_buffer>-instance.
  ENDMETHOD.


  METHOD get_instance_by_valuation_area.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    READ TABLE gth_buffer_instances ASSIGNING FIELD-SYMBOL(<ls_buffer>)
         WITH TABLE KEY valuation_area
         COMPONENTS valuation_area = iv_valuation_area.
    IF sy-subrc <> 0.
      IF lines( gth_buffer_instances ) > 100.
        CLEAR gth_buffer_instances.
      ENDIF.

      DATA(lo_new_instance) = NEW zcl_pd_plant_type_determine( ).
      lo_new_instance->set_valuation_area( iv_valuation_area ).

      INSERT VALUE #( plant          = lo_new_instance->mv_plant
                      valuation_area = lo_new_instance->mv_valuation_area
                      instance       = lo_new_instance )
             INTO TABLE gth_buffer_instances
             ASSIGNING <ls_buffer>.
    ENDIF.
    ro_result = <ls_buffer>-instance.
  ENDMETHOD.


  METHOD get_plant_type.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    CASE mv_plant.
      WHEN gs_global_reference-distribution_center.
        rv_result = gc_plant_type-global_ref_dc.
      WHEN gs_global_reference-store.
        rv_result = gc_plant_type-global_ref_store.
      WHEN mv_local_reference_store.
        rv_result = gc_plant_type-local_ref_store.
      WHEN OTHERS.
        rv_result = iv_type_in_db.
    ENDCASE.
  ENDMETHOD.


  METHOD get_plant_by_type.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    rv_result = SWITCH #( iv_type
                          WHEN gc_plant_type-real_store       THEN mv_plant
                          WHEN gc_plant_type-local_ref_store  THEN mv_local_reference_store
                          WHEN gc_plant_type-global_ref_store THEN gs_global_reference-store
                          WHEN gc_plant_type-real_dc          THEN mv_plant
                          WHEN gc_plant_type-global_ref_dc    THEN gs_global_reference-distribution_center ).
  ENDMETHOD.


  METHOD get_valuation_area_by_type.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    DATA(lv_plant) = get_plant_by_type( iv_type ).

    DATA(lo_plant_data) = get_instance_by_plant( lv_plant ).

    rv_result = lo_plant_data->mv_valuation_area.
  ENDMETHOD.


  METHOD set_plant.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    mv_plant = iv_plant.

    SELECT SINGLE valuationarea,
                  salesorganization,
                  plantcategory,
                  distributionchannel
           FROM i_plant
           WHERE plant = @mv_plant
           INTO @DATA(ls_t001w).
    IF sy-subrc = 0.
      mv_valuation_area = ls_t001w-valuationarea.

      SELECT SINGLE referencestore
             FROM i_slsorganizationdistrchnl
             WHERE salesorganization   = @ls_t001w-salesorganization
               AND distributionchannel = @ls_t001w-distributionchannel
             INTO @mv_local_reference_store.
      IF sy-subrc <> 0.
        CLEAR mv_local_reference_store.
      ENDIF.

      mv_plant_type = get_plant_type( ls_t001w-plantcategory ).
    ENDIF.

  ENDMETHOD.


  METHOD set_valuation_area.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    mv_valuation_area = iv_valuation_area.

    SELECT SINGLE plant,
                  salesorganization,
                  plantcategory,
                  distributionchannel
           FROM i_plant
           WHERE valuationarea = @iv_valuation_area
           INTO @DATA(ls_t001w) ##WARN_OK.
    IF sy-subrc = 0.
      mv_plant = ls_t001w-plant.

      SELECT SINGLE referencestore
             FROM i_slsorganizationdistrchnl
             WHERE salesorganization   = @ls_t001w-salesorganization
               AND distributionchannel = @ls_t001w-distributionchannel
             INTO @mv_local_reference_store.
      IF sy-subrc <> 0.
        CLEAR mv_local_reference_store.
      ENDIF.

      mv_plant_type = get_plant_type( ls_t001w-plantcategory ).
    ENDIF.

  ENDMETHOD.


  METHOD is_reference.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*
    rv_result = SWITCH #( mv_plant_type
                          WHEN zcl_pd_plant_type_determine=>gc_plant_type-global_ref_dc
                            OR zcl_pd_plant_type_determine=>gc_plant_type-global_ref_store
                            OR zcl_pd_plant_type_determine=>gc_plant_type-local_ref_store
                          THEN abap_true
                          ELSE abap_false ).
  ENDMETHOD.


  METHOD get_highier_instance.
********************************************************************************
*  R E V I S I O N   H I S T O R Y                                             *
********************************************************************************
* AUTHOR   | DATE     | CHANGE NUMBER & DESCRIPTION                            *
*          |          | TRANSPORT REQUESTS                                     *
********************************************************************************
* BESMADMI |18.12.2023| FB_27668_PD_[FEATURE] PDM-M BUILD Ref. Material Deve
*          |          | DS4K974145
*------------------------------------------------------------------------------*

    DATA(lv_highier_type) = SWITCH zpd_zvlfkz( mv_plant_type
                                               WHEN gc_plant_type-real_dc         THEN gc_plant_type-global_ref_dc
                                               WHEN gc_plant_type-real_store      THEN gc_plant_type-local_ref_store
                                               WHEN gc_plant_type-local_ref_store THEN gc_plant_type-global_ref_store
                                               ELSE space ).
    IF lv_highier_type IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lv_highier_plant) = get_plant_by_type( lv_highier_type ).

    ro_result = get_instance_by_plant( lv_highier_plant ).
  ENDMETHOD.
ENDCLASS.