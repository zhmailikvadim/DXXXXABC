CLASS zcl_pe_atc_abap_naming_new DEFINITION
  PUBLIC
  INHERITING FROM cl_ci_test_abap_naming_new
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    METHODS constructor.

  PROTECTED SECTION.
    METHODS check_methods REDEFINITION.
    METHODS raise_message REDEFINITION.
    METHODS check_name REDEFINITION.
    METHODS check_function_signature REDEFINITION.
    METHODS check_data_type REDEFINITION.

  PRIVATE SECTION.

    CONSTANTS: BEGIN OF mc_s_class_category,
                 abap_unit       TYPE seocategry VALUE 05,
                 exception_class TYPE seocategry VALUE 40,
               END OF mc_s_class_category.

    DATA: mv_rfc_destination TYPE rfcdest.

    METHODS is_exception_class IMPORTING iv_program       TYPE program
                               RETURNING VALUE(rv_result) TYPE boolean.

    METHODS is_search_help_exits RETURNING VALUE(rv_result) TYPE boolean.
    METHODS is_wizard_global_data RETURNING VALUE(rv_result) TYPE boolean.
    METHODS is_wizard_form IMPORTING iv_name          TYPE string
                                     iv_full_name     TYPE string
                           RETURNING VALUE(rv_result) TYPE boolean.

ENDCLASS.



CLASS ZCL_PE_ATC_ABAP_NAMING_NEW IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    super->constructor( ).
    description         = 'Extended Naming Conventions for Programs'(000).
    category            = zcl_pe_atc_category_001=>gc_my_name.
    mv_rfc_destination  = zcl_pe_atc_aoc_super=>get_rfc_destination( ).

  ENDMETHOD.


  METHOD check_data_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

*-- Exclude constants in exception classes
    IF is_exception_class( statement_wa->include ) AND p_code = c_code-clas_constant.
      RETURN.
    ENDIF.

*-- Exclude wizard global data
    IF is_wizard_global_data( ).
      RETURN.
    ENDIF.

    super->check_data_type( p_ids    = p_ids
                            p_code   = p_code
                            p_ids_2  = p_ids_2
                            p_code_2 = p_code_2
                            p_tag    = p_tag ).

  ENDMETHOD.


  METHOD raise_message.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 11.01.2023 | 16320     : Wrong std raise_message exec *
*              |            | DS4K938346                               *
************************************************************************


*-- Discard standard programs
    IF zcl_pe_atc_aoc_super=>is_relevant( iv_program = p_include iv_destination = mv_rfc_destination ) = abap_false.
      RETURN.
    ENDIF.


*-- Different logic ran by standard if parameters are supplied
    IF p_name IS SUPPLIED AND p_finding_origins IS SUPPLIED.
      super->raise_message( p_include         = p_include
                            p_full_name       = p_full_name
                            p_line            = p_line
                            p_column          = p_column
                            p_code            = p_code
                            p_param_1         = p_param_1
                            p_name            = p_name
                            p_finding_origins = p_finding_origins
                            p_ids             = p_ids ).

    ELSEIF p_name IS SUPPLIED AND p_finding_origins IS NOT SUPPLIED.
      super->raise_message( p_include         = p_include
                            p_full_name       = p_full_name
                            p_line            = p_line
                            p_column          = p_column
                            p_code            = p_code
                            p_param_1         = p_param_1
                            p_name            = p_name
                            p_ids             = p_ids ).

    ELSEIF p_name IS NOT SUPPLIED AND p_finding_origins IS SUPPLIED.
      super->raise_message( p_include         = p_include
                            p_full_name       = p_full_name
                            p_line            = p_line
                            p_column          = p_column
                            p_code            = p_code
                            p_param_1         = p_param_1
                            p_finding_origins = p_finding_origins
                            p_ids             = p_ids ).

    ELSE.
      super->raise_message( p_include         = p_include
                            p_full_name       = p_full_name
                            p_line            = p_line
                            p_column          = p_column
                            p_code            = p_code
                            p_param_1         = p_param_1
                            p_ids             = p_ids ).
    ENDIF.

  ENDMETHOD.


  METHOD is_search_help_exits.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    DATA: ls_sh_info TYPE dd30l.


*   Is it really a function? Just in case
    IF get_token_rel( 1 ) <> 'FUNCTION'.
      RETURN.
    ENDIF.

*   FM Available in destination?
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_SEARCH_HELP' iv_destination = mv_rfc_destination ) = abap_false.
      RETURN.
    ENDIF.

*   FM used as search help exit?
    CALL FUNCTION 'ZPE_FM_GET_SEARCH_HELP' DESTINATION mv_rfc_destination
      EXPORTING
        iv_exit               = CONV ddshselext( token_wa->str )
      IMPORTING
        es_info               = ls_sh_info
      EXCEPTIONS
        not_authorized        = 1
        not_found             = 2
        action_not_supported  = 3
        communication_failure = 4
        system_failure        = 5
        OTHERS                = 6.
    IF sy-subrc = 0 AND ls_sh_info IS NOT INITIAL.
      rv_result = abap_true.
    ENDIF.

  ENDMETHOD.


  METHOD check_function_signature.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

*   Exclude FMs used as search help exits
    IF is_search_help_exits( ).
      RETURN.
    ENDIF.

    super->check_function_signature( ).

  ENDMETHOD.


  METHOD check_methods.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 09.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

*-- Exclude CONSTRUCTOR in exception classes
    IF is_exception_class( statement_wa->include ) AND get_token_rel( 2 ) = 'CONSTRUCTOR'.
      RETURN.
    ENDIF.

    super->check_methods( p_ids  = p_ids
                          p_code = p_code ).

  ENDMETHOD.


  METHOD check_name.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

*-- Exclude Wizard forms
    IF is_wizard_form( iv_name = p_name iv_full_name = p_full_name ).
      p_result = abap_true.
      RETURN.
    ENDIF.

    p_result = super->check_name( EXPORTING p_name      = p_name
                                            p_ids       = p_ids
                                            p_tag       = p_tag
                                            p_full_name = p_full_name
                                  IMPORTING p_eff_ids   = p_eff_ids ).

  ENDMETHOD.


  METHOD is_exception_class.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 09.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
    DATA lv_category TYPE seocategry.


*   FM Available in destination?
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_CLASS_DATA' iv_destination = mv_rfc_destination ) = abap_false.
      RETURN.
    ENDIF.

*   Exception class?
    CALL FUNCTION 'ZPE_FM_GET_CLASS_DATA' DESTINATION mv_rfc_destination
      EXPORTING
        iv_classname           = cl_oo_classname_service=>get_clsname_by_include( iv_program )
      IMPORTING
        ev_category            = lv_category
      EXCEPTIONS
        not_found              = 1
        not_authorized         = 3
        communication_failure_ = 4
        system_failure         = 5
        OTHERS                 = 6.
    IF sy-subrc = 0 AND lv_category = mc_s_class_category-exception_class.
      rv_result = abap_true.
    ENDIF.

  ENDMETHOD.


  METHOD is_wizard_form.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    IF  iv_name = 'CONTAINER'       OR
        iv_name = 'COMMAND'         OR
        iv_name = 'P_WIZARD_STEPS'  OR
        iv_name = 'P_SUBRC'         OR
        iv_name = 'L_WIZARD'        OR
        iv_name = 'L_WIZARD_STEP'.

      DATA(lv_form_name) = substring_before( val = substring_after( val = iv_full_name sub = 'FO:' ) sub = '\DA' ).
      IF lv_form_name CP 'CB_PROCESS*' OR lv_form_name CP 'WZ*'.
        rv_result = abap_true.
      ENDIF.

    ENDIF.

  ENDMETHOD.


  METHOD is_wizard_global_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    DATA(lv_name) = get_token_rel( 2 ).
    IF lv_name = 'G_ERROR' OR lv_name = 'G_OK_CODE' OR lv_name EQ 'G_SAVE_OK_CODE'.
      READ TABLE proc_def->stmts TRANSPORTING NO FIELDS WITH KEY keyword = 'DEFINE'
                                                                 include = '<WIZARD>'.
      IF sy-subrc = 0.
        rv_result = abap_true.
      ENDIF.
    ENDIF.

  ENDMETHOD.
ENDCLASS.