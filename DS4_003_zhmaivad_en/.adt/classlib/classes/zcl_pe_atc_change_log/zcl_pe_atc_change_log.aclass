CLASS zcl_pe_atc_change_log DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.

    CLASS-METHODS check_unified_header_data
      IMPORTING iv_trkorr       TYPE trkorr
                iv_cm_ref_value TYPE string
                it_header       TYPE string_table OPTIONAL
                iv_header       TYPE string OPTIONAL
      RAISING   zcx_pe_atc_error.

    CLASS-METHODS get_tr_change_management_ref
      IMPORTING iv_trkorr      TYPE trkorr
                iv_destination TYPE rfcdest
      RETURNING VALUE(rv_cn)   TYPE string
      RAISING   zcx_pe_trv_xx.

  PRIVATE SECTION.

ENDCLASS.



CLASS ZCL_PE_ATC_CHANGE_LOG IMPLEMENTATION.


  METHOD check_unified_header_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

*   Get source code in string format
    DATA(lv_source_code) = iv_header.
    IF lv_source_code IS INITIAL.
      lv_source_code = concat_lines_of( table = it_header sep = space ).
    ENDIF.

*   Check, if transport number was added to the change history log
    FIND FIRST OCCURRENCE OF iv_trkorr IN lv_source_code.
    IF sy-subrc <> 0.
      DATA(lv_tr_missing) = abap_true.
    ENDIF.

*   Check, if RFC/RTI number was added to the change history log
    FIND FIRST OCCURRENCE OF iv_cm_ref_value IN lv_source_code.
    IF sy-subrc <> 0.
      DATA(lv_cn_missing) = abap_true.
    ENDIF.

    IF lv_tr_missing = abap_true AND lv_cn_missing = abap_true.
      RAISE EXCEPTION TYPE zcx_pe_atc_error MESSAGE e002(zpe_atc) WITH iv_trkorr iv_cm_ref_value.
    ELSEIF lv_tr_missing = abap_true.
      RAISE EXCEPTION TYPE zcx_pe_atc_error MESSAGE e000(zpe_atc) WITH iv_trkorr.
    ELSEIF lv_cn_missing = abap_true.
      RAISE EXCEPTION TYPE zcx_pe_atc_error MESSAGE e001(zpe_atc) WITH iv_cm_ref_value.
    ENDIF.

  ENDMETHOD.


  METHOD get_tr_change_management_ref.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
    DATA: lt_blocks      TYPE zpe_tt_trv_descr_blocks_atc,
          lv_eval_result TYPE abap_bool.

    TEST-SEAM change_number.
    END-TEST-SEAM.

    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_RUN_TR_VALIDATION' iv_destination = iv_destination ) = abap_false.
      RETURN.
    ENDIF.
    CALL FUNCTION 'ZPE_FM_RUN_TR_VALIDATION' DESTINATION iv_destination
      EXPORTING
        iv_trkorr              = iv_trkorr
      IMPORTING
        ev_eval_result         = lv_eval_result
      TABLES
        t_blocks               = lt_blocks ##ENH_OK
      EXCEPTIONS
        not_found              = 1
        action_not_supported   = 2
        not_authorized         = 3
        communication_failure_ = 4
        system_failure         = 5
        OTHERS                 = 6.
    IF sy-subrc <> 0.
      MESSAGE e011(zpe_atc) WITH 'ZPE_FM_RUN_TR_VALIDATION' INTO DATA(lv_msg)   ##NEEDED.
      zcl_pe_atc_logger=>log_message( ).
      RETURN.
    ELSE.
      IF lv_eval_result = abap_false.
        RAISE EXCEPTION TYPE zcx_pe_trv_xx.
      ENDIF.
      READ TABLE lt_blocks ASSIGNING FIELD-SYMBOL(<ls_wi>) WITH KEY blockid = zcl_pe_trv=>gc_blid_work_item.
      IF sy-subrc EQ 0.
        rv_cn = <ls_wi>-text.
      ENDIF.
    ENDIF.

  ENDMETHOD.
ENDCLASS.