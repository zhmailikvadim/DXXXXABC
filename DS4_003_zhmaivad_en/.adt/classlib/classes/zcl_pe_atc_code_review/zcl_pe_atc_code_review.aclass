CLASS zcl_pe_atc_code_review DEFINITION
  PUBLIC
  INHERITING FROM cl_ci_test_root
  FINAL
  CREATE PUBLIC .


  PUBLIC SECTION.

    CONSTANTS: gc_name TYPE sci_chk VALUE 'ZCL_PE_ATC_CODE_REVIEW'.

    METHODS constructor.
    METHODS get_attributes REDEFINITION.
    METHODS get_result_node REDEFINITION.
    CLASS-METHODS get_tm_rfc_destination IMPORTING iv_transport              TYPE trkorr
                                         RETURNING VALUE(rv_rfc_destination) TYPE rfcdest.

    METHODS if_ci_test~query_attributes REDEFINITION.
    METHODS if_ci_test~display_documentation REDEFINITION.

    METHODS put_attributes REDEFINITION.
    METHODS run REDEFINITION.


  PRIVATE SECTION.

    TYPES: BEGIN OF mtyp_s_checksum,
             object_name LIKE object_name,
             object_type LIKE object_type,
             checksum    TYPE int4,
           END OF mtyp_s_checksum,
           mtyp_t_checksum TYPE STANDARD TABLE OF mtyp_s_checksum WITH DEFAULT KEY.

    DATA: mt_trusted         TYPE zpe_tt_atc_trusted_developer,
          mv_rfc_destination TYPE rfcdest,
          mv_task            TYPE trkorr.


    CONSTANTS: BEGIN OF mc_s_class_category,
                 abap_unit       TYPE seocategry VALUE 05,
                 exception_class TYPE seocategry VALUE 40,
               END OF mc_s_class_category.




    METHODS check_object RETURNING VALUE(rs_atc_error) TYPE mtyp_s_checksum
                         RAISING   zcx_pe_atc_not_relevant.

    METHODS check_object_w_vc RETURNING VALUE(rs_atc_error) TYPE mtyp_s_checksum
                              RAISING   zcx_pe_atc_not_relevant.

    METHODS get_custom_includes_fugr IMPORTING iv_program                TYPE sobj_name
                                     RETURNING VALUE(rt_custom_includes) TYPE zpe_tt_include
                                     RAISING   zcx_pe_atc_not_relevant.

    METHODS get_custom_includes_prog IMPORTING iv_program                TYPE sobj_name
                                     RETURNING VALUE(rt_custom_includes) TYPE zpe_tt_include
                                     RAISING   zcx_pe_atc_not_relevant.


    METHODS get_development_objects IMPORTING iv_object_name TYPE sobj_name
                                              iv_object_type TYPE trobjtype
                                    EXPORTING et_dev_objects TYPE mtyp_t_checksum
                                              ev_object_name TYPE sobj_name
                                              ev_object_type TYPE trobjtype
                                    RAISING   zcx_pe_atc_not_relevant.

    METHODS get_dev_fugr IMPORTING iv_object_name        TYPE sobj_name
                         RETURNING VALUE(rt_dev_objects) TYPE mtyp_t_checksum
                         RAISING   zcx_pe_atc_not_relevant.

    METHODS get_dev_clas IMPORTING iv_object_name TYPE sobj_name
                         EXPORTING et_dev_objects TYPE mtyp_t_checksum
                                   ev_object_name TYPE sobj_name
                                   ev_object_type TYPE trobjtype
                         RAISING   zcx_pe_atc_not_relevant.



    METHODS get_dev_prog IMPORTING iv_object_name        TYPE sobj_name
                         RETURNING VALUE(rt_dev_objects) TYPE mtyp_t_checksum
                         RAISING   zcx_pe_atc_not_relevant.
    METHODS is_exempted
      RETURNING
        VALUE(rv_result) TYPE abap_bool.









ENDCLASS.



CLASS zcl_pe_atc_code_review IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 01.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 25.07.2022 | 12489: Code Review check - ENHO          *
*              |            | DS4K925252                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 19.01.2023 | 16832: New message                       *
*              |            | DS4K940184                               *
*----------------------------------------------------------------------*
* CANOCAR      | 01.03.2023 | 18548: Code review for Dev.Class         *
*              |            | DS4K945437                               *
*----------------------------------------------------------------------*
* CANOCAR      | 09.03.2023 | 18481: Dev.Class checked by other ATC    *
*              |            | DS4K945173                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.03.2024 | 30001: CFE Code Review                   *
*              |            | DS4K983213                               *
************************************************************************

    super->constructor( ).

    description         = 'Code review'(000).
    category            = zcl_pe_atc_category_002=>gc_my_name.
    position            = '100'.
    version             = '001'.
    has_attributes      = abap_true.
    has_documentation   = abap_true.
    remote_rfc_enabled  = abap_true.
    uses_checksum       = abap_true.
    mv_rfc_destination  = zcl_pe_atc_aoc_super=>get_rfc_destination( ).

    add_obj_type( 'DDLS' ).
    add_obj_type( 'PROG' ).
    add_obj_type( 'FUGR' ).
    add_obj_type( 'CLAS' ).
    add_obj_type( 'WAPA' ).
    add_obj_type( 'WAPD' ).
    add_obj_type( 'WAPP' ).
    add_obj_type( 'ENHO' ).
    add_obj_type( 'ADVC' ).

    fill_message '0002' 'E' 'Code review required by PE Team - &1'(005) c_exceptn_imposibl.

  ENDMETHOD.


  METHOD get_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 01.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
************************************************************************

    EXPORT trusted = mt_trusted TO DATA BUFFER p_attributes.

  ENDMETHOD.


  METHOD if_ci_test~query_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 01.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
************************************************************************

*-- the grey coding is always needed
    DATA:
      lt_attributes  TYPE sci_atttab,
      ls_attribute   LIKE LINE OF lt_attributes,
      lv_message(72) TYPE c.


    CLASS cl_ci_query_attributes DEFINITION LOAD.
    fill_att mt_trusted    'Trusted developers'(002)               ' '.

    IF cl_ci_query_attributes=>generic( p_name       = myname
                                        p_title      = 'Settings'(003)
                                        p_attributes = lt_attributes
                                        p_message    = lv_message
                                        p_display    = p_display ) = 'X'.
      RETURN.
    ENDIF.

    attributes_ok = 'X'.

  ENDMETHOD.


  METHOD put_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 01.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
************************************************************************

    IMPORT trusted = mt_trusted FROM DATA BUFFER p_attributes. "#EC CI_SUBRC

  ENDMETHOD.


  METHOD get_dev_prog.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 04.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 19.01.2023 | 16832: Consider changed includes         *
*              |            | DS4K940184                               *
************************************************************************

*   Get changed include programs
    DATA(lt_includes) = get_custom_includes_prog( iv_object_name ).
    LOOP AT lt_includes ASSIGNING FIELD-SYMBOL(<ls_include>).
      INSERT VALUE mtyp_s_checksum( object_name = <ls_include>-include object_type = 'INCL' ) INTO TABLE rt_dev_objects.
    ENDLOOP.


  ENDMETHOD.


  METHOD run.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 04.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 25.07.2022 | 12489: Code Review check - ENHO          *
*              |            | DS4K925252                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 02.11.2022 | 14981: Restrict to transport release     *
*              |            | DS4K934555                               *
*----------------------------------------------------------------------*
* CANOCAR      | 19.01.2023 | 16832: ATC error per main object         *
*              |            | considering changed subobjects only      *
*              |            | DS4K940184                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2023 | 26348: Exempted objects e.g. BW extract  *
*              |            | DS4K968500                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.03.2024 | 30001: CFE Code Review                   *
*              |            | DS4K983213                               *
************************************************************************

*   Trusted developers
    IF line_exists( mt_trusted[ table_line = sy-uname ] )   ##USER_OK.
      RETURN.
    ENDIF.

*   ATC transport request is required for code review analysis
    zcl_pe_atc_aoc_super=>get_atc_task( IMPORTING ev_transport_request = DATA(lv_current_trkorr)
                                                  ev_task              = mv_task ).
    IF lv_current_trkorr IS INITIAL.
      RETURN.
    ENDIF.

*   Some objects or packages are exempted
    IF is_exempted( ).
      RETURN.
    ENDIF.

*   Get checksum for all changed sub-objects
    TRY.
        DATA(ls_atc_error) = check_object( ).
      CATCH zcx_pe_atc_not_relevant.
        RETURN.
    ENDTRY.

*   ATC error
    inform( p_sub_obj_name = ls_atc_error-object_name
            p_sub_obj_type = ls_atc_error-object_type
            p_line         = 0
            p_checksum_1   = ls_atc_error-checksum
            p_kind         = c_error
            p_test         = myname
            p_code         = '0002'
            p_param_1      = lv_current_trkorr ).

  ENDMETHOD.


  METHOD check_object.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 19.01.2023 | 16832: ATC error per main object         *
*              |            | considering changed subobjects only      *
*              |            | DS4K940184                               *
*----------------------------------------------------------------------*
* CANOCAR      | 01.03.2023 | 18548: Code review for Dev.Class         *
*              |            | DS4K945437                               *
*----------------------------------------------------------------------*
* CANOCAR      | 11.05.2023 | 21224: Navigation to objects bug fix     *
*              |            | DS4K952142                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.03.2024 | 30001: CFE Code Review                   *
*              |            | DS4K983213                               *
************************************************************************

    IF object_type = 'ADVC'.
      rs_atc_error = VALUE #( object_name = object_name
                              object_type = object_type
                              checksum    = zcl_pe_atc_aoc_super=>generate_checksum_from_string( CONV #( mv_task ) ) ).
    ELSE.
      rs_atc_error = check_object_w_vc( ).   "With version control
    ENDIF.

  ENDMETHOD.


  METHOD check_object_w_vc.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 12.03.2024 | 30001: CFE Code Review                   *
*              |            | DS4K983213                               *
************************************************************************

    DATA: lv_source_code TYPE string.


*-- Get all changed development objects
    get_development_objects( EXPORTING iv_object_name = object_name
                                       iv_object_type = object_type
                             IMPORTING et_dev_objects = DATA(lt_dev_objects)
                                       ev_object_name = DATA(lv_atc_object)
                                       ev_object_type = DATA(lv_atc_type) ).
    IF lt_dev_objects IS INITIAL.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.


*-- Collect source code
    LOOP AT lt_dev_objects ASSIGNING FIELD-SYMBOL(<ls_dev_object>).
      lv_source_code = lv_source_code && zcl_pe_atc_aoc_super=>get_source_code( iv_destination = mv_rfc_destination
                                                                                iv_object_name = <ls_dev_object>-object_name
                                                                                iv_object_type = <ls_dev_object>-object_type ).
    ENDLOOP.
    IF lv_source_code IS INITIAL.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.


*-- Add checksum
    rs_atc_error = VALUE #( object_name = COND #( WHEN lv_atc_object IS NOT INITIAL THEN lv_atc_object ELSE object_name )
                            object_type = COND #( WHEN lv_atc_type IS NOT INITIAL THEN lv_atc_type ELSE object_type )
                            checksum    = zcl_pe_atc_aoc_super=>generate_checksum_from_string( lv_source_code ) ).

  ENDMETHOD.

  METHOD get_custom_includes_fugr.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 19.01.2023 | 16832: ATC error per main object         *
*              |            | considering changed subobjects only      *
*              |            | DS4K940184                               *
************************************************************************

    DATA: lt_includes    TYPE zpe_tt_include.


*-- Get function group include programs
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_ALL_INCLUDES' iv_destination = mv_rfc_destination ) = abap_false.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.
    CALL FUNCTION 'ZPE_FM_GET_ALL_INCLUDES' DESTINATION mv_rfc_destination
      EXPORTING
        iv_program            = iv_program
      TABLES
        t_includes            = lt_includes
      EXCEPTIONS
        not_found             = 1
        action_not_supported  = 2
        not_authorized        = 3
        communication_failure = 4
        system_failure        = 5
        OTHERS                = 6.
    IF sy-subrc <> 0.
      MESSAGE e011(zpe_atc) WITH 'ZPE_FM_GET_ALL_INCLUDES' INTO DATA(lv_msg)    ##NEEDED.
      zcl_pe_atc_logger=>log_message( ).
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.


*-- Get ATC transport request
    DATA(lv_transport) = zcl_pe_atc_aoc_super=>get_atc_transport_request( ).


*-- Return only changed ones
    LOOP AT lt_includes ASSIGNING FIELD-SYMBOL(<ls_include>).

      IF zcl_pe_atc_aoc_super=>is_relevant( iv_program = <ls_include>-include iv_destination = mv_rfc_destination ) = abap_false.
        CONTINUE.
      ENDIF.

      zcl_pe_atc_aoc_super=>get_vm_ids( EXPORTING iv_destination = mv_rfc_destination
                                                  is_object      = VALUE scir_rest( objtype = 'FUGR' sobjname = <ls_include>-include )
                                        IMPORTING ev_object_type = DATA(lv_object_type)
                                                  ev_object_name = DATA(lv_object_name) ).

      IF zcl_pe_atc_aoc_super=>is_changed_on_transport( iv_destination = mv_rfc_destination
                                                        iv_object_type = lv_object_type
                                                        iv_object_name = lv_object_name
                                                        iv_transport   = lv_transport ).
        INSERT <ls_include> INTO TABLE rt_custom_includes.
        CONTINUE.
      ENDIF.

    ENDLOOP.

  ENDMETHOD.


  METHOD get_custom_includes_prog.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 19.01.2023 | 16832: Changed objects                   *
*              |            | DS4K940184                               *
************************************************************************

    DATA: lt_includes    TYPE zpe_tt_include.


*-- Add include programs
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_ALL_INCLUDES' iv_destination = mv_rfc_destination ) = abap_false.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.
    CALL FUNCTION 'ZPE_FM_GET_ALL_INCLUDES' DESTINATION mv_rfc_destination
      EXPORTING
        iv_program            = iv_program
      TABLES
        t_includes            = lt_includes
      EXCEPTIONS
        not_found             = 1
        action_not_supported  = 2
        not_authorized        = 3
        communication_failure = 4
        system_failure        = 5
        OTHERS                = 6.
    IF sy-subrc <> 0.
      MESSAGE e011(zpe_atc) WITH 'ZPE_FM_GET_ALL_INCLUDES' INTO DATA(lv_msg)    ##NEEDED.
      zcl_pe_atc_logger=>log_message( ).
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.


*-- Add main program
    INSERT VALUE #( include = iv_program ) INTO TABLE lt_includes.


*-- Discard not relevant or not changed in the transport
    LOOP AT lt_includes ASSIGNING FIELD-SYMBOL(<ls_include>).

      IF zcl_pe_atc_aoc_super=>is_relevant( iv_program = <ls_include>-include iv_destination = mv_rfc_destination ) = abap_false.
        CONTINUE.
      ENDIF.

      zcl_pe_atc_aoc_super=>get_vm_ids( EXPORTING iv_destination = mv_rfc_destination
                                                  is_object      = VALUE scir_rest( objtype = 'PROG' sobjname = <ls_include>-include )
                                        IMPORTING ev_object_type = DATA(lv_object_type)
                                                  ev_object_name = DATA(lv_object_name) ).

      IF zcl_pe_atc_aoc_super=>is_changed_on_transport( iv_destination = mv_rfc_destination
                                                        iv_object_type = lv_object_type
                                                        iv_object_name = lv_object_name
                                                        iv_transport   = zcl_pe_atc_aoc_super=>get_atc_transport_request( ) ).
        INSERT <ls_include> INTO TABLE rt_custom_includes.
      ENDIF.

    ENDLOOP.

  ENDMETHOD.


  METHOD get_development_objects.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 25.07.2022 | 12489: Code Review check - ENHO          *
*              |            | DS4K925252                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 19.01.2023 | 16832: ATC error per main object         *
*              |            | considering changed subobjects only      *
*              |            | DS4K940184                               *
*----------------------------------------------------------------------*
* CANOCAR      | 31.05.2023 | 21866: Inconsistent checksum due to      *
*              |            | undefined object sorting                 *
*              |            | DS4K953824                               *
*----------------------------------------------------------------------*
* CANOCAR      | 20.09.2023 | 23927: Implict enhancements              *
*              |            | DS4K959800                               *
************************************************************************

    CLEAR:  et_dev_objects, ev_object_name, ev_object_type.


    CASE iv_object_type.

      WHEN 'FUGR'.
        et_dev_objects = get_dev_fugr( iv_object_name ).

      WHEN 'CLAS'.
        get_dev_clas( EXPORTING iv_object_name = iv_object_name
                      IMPORTING et_dev_objects = et_dev_objects
                                ev_object_name = ev_object_name
                                ev_object_type = ev_object_type ).

*     BSP Application: BSP Implementing class is transformed into WAPA object in CL_CI_TESTS->RUN
*     and it is not relevant for frontend code review
      WHEN 'WAPA'.
        IF program_name IS NOT INITIAL.
          RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
        ENDIF.
        INSERT VALUE mtyp_s_checksum( object_name = iv_object_name object_type = iv_object_type ) INTO TABLE et_dev_objects.

*     Implicit enhancements
      WHEN 'ENHO'.
        et_dev_objects = VALUE #( ( object_name = iv_object_name object_type = iv_object_type ) ).

*     Programs
      WHEN 'PROG'.
        et_dev_objects = get_dev_prog( iv_object_name ).

      WHEN OTHERS.
        INSERT VALUE mtyp_s_checksum( object_name = iv_object_name object_type = iv_object_type ) INTO TABLE et_dev_objects.

    ENDCASE.

    SORT et_dev_objects BY object_type object_name.

  ENDMETHOD.


  METHOD get_dev_clas.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 04.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 19.01.2023 | 16832: ATC error per main object         *
*              |            | considering changed subobjects only      *
*              |            | DS4K940184                               *
************************************************************************

    DATA: lv_class_category TYPE seocategry,
          lv_program        TYPE program,
          lt_includes       TYPE zpe_tt_global_class_methods.



    CLEAR: et_dev_objects, ev_object_name, ev_object_type.


*-- Get class data
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_CLASS_DATA' iv_destination = mv_rfc_destination ) = abap_false.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.

    CALL FUNCTION 'ZPE_FM_GET_CLASS_DATA' DESTINATION mv_rfc_destination
      EXPORTING
        iv_classname           = CONV seoclsname( iv_object_name )
      IMPORTING
        ev_program             = lv_program
        ev_category            = lv_class_category
        et_method_includes     = lt_includes
      EXCEPTIONS
        not_found              = 1
        not_authorized         = 3
        communication_failure_ = 4
        system_failure         = 5
        OTHERS                 = 6.
    IF sy-subrc NE 0.
      MESSAGE e011(zpe_atc) WITH 'ZPE_FM_GET_CLASS_DATA' INTO DATA(lv_msg)  ##NEEDED.
      zcl_pe_atc_logger=>log_message( ).
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.


*-- Discard test and exception classes
    IF lv_class_category = mc_s_class_category-abap_unit OR lv_class_category = mc_s_class_category-exception_class.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.


*-- ATC error object is the class main program
    ev_object_name = lv_program.
    ev_object_type = 'PROG'.


*-- Return only changed sub-objects
    LOOP AT lt_includes ASSIGNING FIELD-SYMBOL(<ls_include>).

      IF zcl_pe_atc_aoc_super=>is_relevant( iv_program = <ls_include>-incname iv_destination = mv_rfc_destination ) = abap_false.
        CONTINUE.
      ENDIF.

*     Different object type and name for Methods & Local Implementation
      DATA(lv_object_type) = COND versobjtyp( WHEN <ls_include>-cpdkey IS INITIAL THEN 'CINC' ELSE 'METH' ).
      DATA(lv_object_name) = COND versobjnam( WHEN <ls_include>-cpdkey IS INITIAL THEN <ls_include>-incname ELSE <ls_include>-cpdkey ).

      IF zcl_pe_atc_aoc_super=>is_changed_on_transport( iv_destination = mv_rfc_destination
                                                        iv_object_type = lv_object_type
                                                        iv_object_name = lv_object_name
                                                        iv_transport   = zcl_pe_atc_aoc_super=>get_atc_transport_request( ) ).
        INSERT VALUE mtyp_s_checksum( object_name = <ls_include>-incname object_type = 'INCL' ) INTO TABLE et_dev_objects.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.


  METHOD get_dev_fugr.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 04.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 19.01.2023 | 16832: Consider changed FMs              *
*              |            | DS4K940184                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.2023    | 27103: Housekeeping SLG1                 *
*              |            | DS4K971853                               *
************************************************************************

    DATA: ls_tm_info TYPE tvdir.


*   Discard first FG for table maintenance
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_TABLE_MAINTENANCE' iv_destination = mv_rfc_destination ) = abap_false.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.

    CALL FUNCTION 'ZPE_FM_GET_TABLE_MAINTENANCE' DESTINATION mv_rfc_destination
      EXPORTING
        iv_area               = CONV funct_pool( iv_object_name )
      IMPORTING
        es_info               = ls_tm_info
      EXCEPTIONS
        not_authorized        = 1
        not_found             = 2
        action_not_supported  = 3
        communication_failure = 4
        system_failure        = 5
        OTHERS                = 6.
    IF sy-subrc = 0 AND ls_tm_info IS NOT INITIAL.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.


*   Consider changed subobjects e.g. FM
    DATA(lt_includes) = get_custom_includes_fugr( insert( val = iv_object_name sub = 'SAPL' off = find( val = iv_object_name sub = '/' occ = 2 ) + 1 ) ).
    LOOP AT lt_includes ASSIGNING FIELD-SYMBOL(<ls_include>).
      INSERT VALUE mtyp_s_checksum( object_name = <ls_include>-include object_type = 'INCL' ) INTO TABLE rt_dev_objects.
    ENDLOOP.

  ENDMETHOD.


  METHOD get_result_node.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 01.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
************************************************************************

    p_result = NEW zcl_pe_atc_code_review_result( p_kind = p_kind ).

  ENDMETHOD.


  METHOD get_tm_rfc_destination.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 27.10.2022 | 14170     : Code review analysis tools   *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 02.11.2022 | 14981: Pretty printer                    *
*              |            | DS4K934555                               *
************************************************************************

    TRY.
        zcl_pe_hm_complex=>get_value( EXPORTING iv_id           = '024F85009E261EED948CC5EC9D55A0D2'
                                                is_filter       = iv_transport
                                      IMPORTING ev_value        = rv_rfc_destination ).
      CATCH zcx_pe_hm.
        rv_rfc_destination = 'NONE'.
    ENDTRY.

  ENDMETHOD.


  METHOD if_ci_test~display_documentation.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 01.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14170: Analysis tools                    *
*              |            | DS4K932069                               *
************************************************************************

    zcl_pe_atc_aoc_super=>display_documentation( ).

  ENDMETHOD.


  METHOD is_exempted.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 27.10.2023 | 26348: Exempted objects e.g. BW extract  *
*              |            | DS4K968500                               *
************************************************************************

    TYPES: BEGIN OF ltyp_s_filter,
             development_class TYPE devclass,
             object_name       TYPE so_obj_nam,
             object_type       TYPE trobjtype,
           END OF ltyp_s_filter.


    rv_result = abap_false.


*   Get development class
    TRY.
        DATA(ls_tadir) = zcl_pe_atc_aoc_super=>get_tadir( iv_object_type = object_type
                                                          iv_object_name = object_name
                                                          iv_destination = mv_rfc_destination ).
      CATCH zcx_pe_atc_not_relevant.
        RETURN.
    ENDTRY.

*   Is exempted?
    IF zcl_pe_hm_complex=>is_active( iv_id           = 'F9431F5D8B571EDE9D964CBD3C5F9F5C'
                                     is_filter       = VALUE ltyp_s_filter( development_class = ls_tadir-devclass
                                                                            object_name       = object_name
                                                                            object_type       = object_type )
                                     iv_mapping_type = zcl_pe_hm=>gc_s_mapping_type-data_elements ).
      rv_result = abap_true.
    ENDIF.

  ENDMETHOD.
ENDCLASS.