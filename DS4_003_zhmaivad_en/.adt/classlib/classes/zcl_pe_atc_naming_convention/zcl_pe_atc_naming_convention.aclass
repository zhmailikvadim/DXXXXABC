CLASS zcl_pe_atc_naming_convention DEFINITION
  PUBLIC
  INHERITING FROM cl_ci_test_ddic
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    CONSTANTS: gc_my_name TYPE seoclsname VALUE 'ZCL_PE_ATC_NAMING_CONVENTION'.

    METHODS constructor.
    METHODS if_ci_test~display_documentation REDEFINITION.
    METHODS get_attributes REDEFINITION.

    METHODS is_object_correct IMPORTING iv_object_type TYPE trobjtype
                                        iv_object_name TYPE sobj_name
                              EXPORTING ev_pattern     TYPE string
                              RAISING   zcx_pe_atc.

    METHODS put_attributes REDEFINITION.
    METHODS run REDEFINITION.
    METHODS run_begin REDEFINITION.
    METHODS if_ci_test~query_attributes REDEFINITION.

  PROTECTED SECTION.

  PRIVATE SECTION.

    CONSTANTS: mc_error_naming_convention TYPE sci_errc   VALUE '0001',
               mc_table                   TYPE trobjtype  VALUE 'TABL',
               mc_ddls                    TYPE trobjtype  VALUE 'DDLS',
               mc_srvb                    TYPE trobjtype  VALUE 'SRVB',
               mc_ddlx                    TYPE trobjtype  VALUE 'DDLX',
               mc_ttyp                    TYPE trobjtype  VALUE 'TTYP'.

*   ATC Check configuration on ATC transaction
    DATA: mv_doma        TYPE zpe_atc_domain,
          mv_dtel        TYPE zpe_atc_data_element,
          mv_shlp        TYPE zpe_atc_search_help,
          mt_ttyp        TYPE zpe_tt_atc_table_type,
          mv_view        TYPE zpe_atc_view,
          mt_tabl_transp TYPE zpe_tt_atc_table,
          mt_tabl_inttab TYPE zpe_tt_atc_structure,
          mt_tabl_append TYPE zpe_tt_atc_append,
          mt_ddls        TYPE zpe_tt_atc_cds,
          mv_srvd        TYPE zpe_atc_srvd,
          mt_srvb        TYPE zpe_tt_atc_srvb,
          mt_ddlx        TYPE zpe_tt_atc_ddlx,
          mv_msag        TYPE zpe_atc_message_class,
          mv_sfpi        TYPE zpe_atc_adobe_interface,
          mv_sfpf        TYPE zpe_atc_adobe_form,
          mv_enhs        TYPE zpe_atc_enh_spot,
          mv_enho        TYPE zpe_atc_enh_impl,
          mt_sap_area    TYPE zpe_tt_atc_sap_area,
          mv_destination TYPE rfcdest.

    DATA: mo_data_provider TYPE REF TO lcl_data_provider.

    METHODS get_data_provider IMPORTING io_data_provider TYPE REF TO lcl_data_provider OPTIONAL.

    METHODS match_pattern
      IMPORTING
        iv_object_name  TYPE sobj_name
        iv_pattern      TYPE string
      RETURNING
        VALUE(rv_match) TYPE boolean.

    METHODS discard_auto_generated_objects
      IMPORTING iv_destination TYPE rfcdest
                iv_object_type TYPE trobjtype
                iv_object_name TYPE sobj_name
      RAISING   zcx_pe_atc_not_relevant
                zcx_pe_atc.

ENDCLASS.



CLASS ZCL_PE_ATC_NAMING_CONVENTION IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    super->constructor( ).

    description         = 'Naming Conventions - Development objects'(000).
    category            = zcl_pe_atc_category_001=>gc_my_name.
    position            = '100'.
    version             = '001'.
    has_attributes      = abap_true.
    has_documentation   = abap_true.
    remote_rfc_enabled  = abap_true.

    add_obj_type( 'SRVD' ).
    add_obj_type( 'DDLS' ).
    add_obj_type( 'SRVB' ).
    add_obj_type( 'DDLX' ).
    add_obj_type( 'DOMA' ).
    add_obj_type( 'DTEL' ).
    add_obj_type( 'TABL' ).

    fill_message mc_error_naming_convention c_error 'Wrong naming convention: &1'(004) c_exceptn_imposibl.

  ENDMETHOD.


  METHOD get_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    EXPORT  doma        = mv_doma
            dtel        = mv_dtel
            shlp        = mv_shlp
            ttyp        = mt_ttyp
            view        = mv_view
            tabl_transp = mt_tabl_transp
            tabl_inttab = mt_tabl_inttab
            tabl_append = mt_tabl_append
            ddls        = mt_ddls
            srvd        = mv_srvd
            srvb        = mt_srvb
            ddlx        = mt_ddlx
            msag        = mv_msag
            sfpi        = mv_sfpi
            sfpf        = mv_sfpf
            enhs        = mv_enhs
            enho        = mv_enho
            sap_area    = mt_sap_area
            TO DATA BUFFER p_attributes.

  ENDMETHOD.


  METHOD get_data_provider.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    IF io_data_provider IS BOUND.
      mo_data_provider = io_data_provider.
    ELSE.
      mo_data_provider = NEW lcl_data_provider( ).
    ENDIF.

  ENDMETHOD.


  METHOD if_ci_test~query_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    DATA:
      lt_attributes  TYPE sci_atttab,
      ls_attribute   LIKE LINE OF lt_attributes,
      lv_message(72) TYPE c.


    CLASS cl_ci_query_attributes DEFINITION LOAD.
*----------------------------------------------

*-- fill attribute table # depends on the attributes of your check
    fill_att sy-index       'ABAP Dictionary'(014)      'G'.
    fill_att mv_doma        'Domain'(006)               ' '.
    fill_att mv_dtel        'Data Element'(007)         ' '.
    fill_att mv_shlp        'Search Help'(008)          ' '.
    fill_att mt_ttyp        'Table Type'(011)           ' '.
    fill_att mv_view        'View'(012)                 ' '.
    fill_att mt_tabl_transp 'Table'(009)                ' '.
    fill_att mt_tabl_inttab 'Structure'(016)            ' '.
    fill_att mt_tabl_append 'Append'(017)               ' '.

    fill_att sy-index       'Classic ABAP'(018)         'G'.
    fill_att mv_msag        'Message class'(023)        ' '.
    fill_att mv_sfpi        'Adobe Interface'(024)      ' '.
    fill_att mv_sfpf        'Adobe Form'(025)           ' '.
    fill_att mv_enhs        'Enhancement spot'(026)     ' '.
    fill_att mv_enho        'Enhancement impl'(027)     ' '.

    fill_att sy-index       'ABAP RESTful Application Progr. Model'(028)         'G'.
    fill_att mt_ddls        'CDS View'(021)             ' '.
    fill_att mv_srvd        'Service Definition'(029)   ' '.
    fill_att mt_srvb        'Service Binding'(030)      ' '.
    fill_att mt_ddlx        'Metadata Extension'(031)   ' '.


    fill_att sy-index       'Configuration'(015)        'G'.
    fill_att mt_sap_area    '<AREA>'(013)               ' '.

    IF cl_ci_query_attributes=>generic( p_name       = myname
                                        p_title      = 'Settings'(005)
                                        p_attributes = lt_attributes
                                        p_message    = lv_message
                                        p_display    = p_display ) = 'X'.
      RETURN.
    ENDIF.

    attributes_ok = 'X'.

  ENDMETHOD.


  METHOD is_object_correct.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    CLEAR: ev_pattern.


*-- Is this object being created?
*-----------------------------------------------------------------
*   Get object type for version management access
    DATA(lv_object_type) = mo_data_provider->get_object_type_vm( iv_object_type ).

*   Check Object history and evaluate if it is object creation
    mo_data_provider->check_object_creation( iv_object_type = lv_object_type
                                             iv_object_name = iv_object_name
                                             iv_destination = mv_destination ).



*-- Auto-generated objects
*--------------------------------
    discard_auto_generated_objects( iv_destination = mv_destination
                                    iv_object_type = lv_object_type
                                    iv_object_name = iv_object_name ).



*-- Get naming convention for object type from Class Attribute
*---------------------------------------------------------------
*   For TABL, we have to distinguish between tables, views,...
    DATA(lv_attribute) = |MV_{ iv_object_type }|.
    IF iv_object_type = mc_table.
      mo_data_provider->get_table_category( EXPORTING iv_object_name = iv_object_name
                                                      iv_destination = mv_destination
                                            IMPORTING ev_tab_class   = DATA(lv_tab_class) ).
      lv_attribute = |MT_{ iv_object_type }_{ lv_tab_class }|.
    ELSEIF iv_object_type = mc_ddls OR iv_object_type = mc_srvb OR iv_object_type = mc_ddlx OR iv_object_type = mc_ttyp.
      lv_attribute = |MT_{ iv_object_type }|.
    ENDIF.

*   Get class attribute with pattern
    ASSIGN (lv_attribute) TO FIELD-SYMBOL(<ls_pattern>).
    IF sy-subrc NE 0.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.         " There is no attribute, no naming convention
    ENDIF.




*-- Check if object has been created according to adidas guidelines
*----------------------------------------------------------------------
    DATA(lo_desc) = cl_abap_typedescr=>describe_by_data( <ls_pattern> ).
    IF lo_desc->kind = cl_abap_typedescr=>kind_table.
      ev_pattern = TEXT-001.
      LOOP AT <ls_pattern> ASSIGNING FIELD-SYMBOL(<ls_pattern_line>).
        IF match_pattern( iv_object_name = iv_object_name iv_pattern  = to_upper( CONV string( <ls_pattern_line> ) ) ).
          RETURN.
        ENDIF.
      ENDLOOP.
    ELSE.
      ev_pattern = to_upper( CONV string( <ls_pattern> ) ).
      IF match_pattern( iv_object_name = iv_object_name iv_pattern  = to_upper( CONV string( <ls_pattern> ) ) ).
        RETURN.
      ENDIF.
    ENDIF.

*   Not matching
    RAISE EXCEPTION TYPE zcx_pe_atc_error.

  ENDMETHOD.


  METHOD put_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    IMPORT  doma        = mv_doma
            dtel        = mv_dtel
            shlp        = mv_shlp
            ttyp        = mt_ttyp
            view        = mv_view
            tabl_transp = mt_tabl_transp
            tabl_inttab = mt_tabl_inttab
            tabl_append = mt_tabl_append
            ddls        = mt_ddls
            srvd        = mv_srvd
            srvb        = mt_srvb
            ddlx        = mt_ddlx
            msag        = mv_msag
            sfpi        = mv_sfpi
            sfpf        = mv_sfpf
            enhs        = mv_enhs
            enho        = mv_enho
            sap_area    = mt_sap_area
            FROM DATA BUFFER p_attributes.                "#EC CI_SUBRC

  ENDMETHOD.


  METHOD run.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    mv_destination = zcl_pe_atc_aoc_super=>get_rfc_destination( ).

    get_data_provider( ).

    TRY.
        is_object_correct( EXPORTING iv_object_type = object_type
                                     iv_object_name = object_name
                           IMPORTING ev_pattern     = DATA(lv_pattern) ).
      CATCH zcx_pe_atc_error.
        inform( p_sub_obj_type = object_type
                p_sub_obj_name = object_name
                p_kind         = c_error
                p_test         = myname
                p_code         = mc_error_naming_convention
                p_param_1      = lv_pattern ).

      CATCH zcx_pe_atc_not_relevant.
        RETURN.
      CATCH zcx_pe_atc.
        RETURN.
    ENDTRY.

  ENDMETHOD.


  METHOD run_begin.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    RETURN.
  ENDMETHOD.


  METHOD match_pattern.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    IF iv_pattern CS `<AREA>`.
      LOOP AT mt_sap_area ASSIGNING FIELD-SYMBOL(<lv_area>).
        DATA(lv_pattern) = iv_pattern.
        REPLACE `<AREA>` IN lv_pattern WITH <lv_area>.
        IF iv_object_name CP lv_pattern.
          rv_match = abap_true.
          RETURN.
        ENDIF.
      ENDLOOP.
    ELSEIF iv_object_name CP iv_pattern.
      rv_match = abap_true.
    ENDIF.

  ENDMETHOD.


  METHOD discard_auto_generated_objects.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.2023    | 27103: Housekeeping SLG1                 *
*              |            | DS4K971853                               *
************************************************************************

    DATA: ls_ddl_source TYPE ddddlsrcv,
          lv_msg        TYPE string ##NEEDED.

    CASE iv_object_type.

      WHEN mc_ddls.

        IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_DDL_SOURCE' iv_destination = iv_destination ) = abap_false.
          RAISE EXCEPTION TYPE zcx_pe_atc.
        ENDIF.

        CALL FUNCTION 'ZPE_FM_GET_DDL_SOURCE' DESTINATION iv_destination
          EXPORTING
            iv_object_name        = iv_object_name
          IMPORTING
            es_ddl_source         = ls_ddl_source
          EXCEPTIONS
            not_found             = 1
            not_authorized        = 2
            communication_failure = 3
            system_failure        = 4
            OTHERS                = 5.

        IF sy-subrc = 1 OR ls_ddl_source-source_origin NE 0.
          RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
        ELSEIF sy-subrc = 2.
          MESSAGE e012(zpe_atc) WITH 'ZPE_FM_GET_DDL_SOURCE' iv_destination INTO lv_msg.
          zcl_pe_atc_logger=>log_message( ).
          RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
        ELSEIF sy-subrc > 2.
          MESSAGE e017(zpe_atc) WITH 'ZPE_FM_GET_DDL_SOURCE' iv_destination INTO lv_msg.
          zcl_pe_atc_logger=>log_message( ).
          RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
        ENDIF.

    ENDCASE.

  ENDMETHOD.


  METHOD if_ci_test~display_documentation.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.07.2022 | 12279: AIF Objects                       *
*              |            | DS4K924515                               *
*----------------------------------------------------------------------*

    zcl_pe_atc_aoc_super=>display_documentation( ).

  ENDMETHOD.
ENDCLASS.