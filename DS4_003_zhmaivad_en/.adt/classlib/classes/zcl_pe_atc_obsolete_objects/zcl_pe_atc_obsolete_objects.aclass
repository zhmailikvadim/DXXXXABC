CLASS zcl_pe_atc_obsolete_objects DEFINITION
  PUBLIC
  INHERITING FROM cl_ci_test_ddic
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    CONSTANTS: gc_my_name TYPE seoclsname VALUE 'ZCL_PE_ATC_OBSOLETE_OBJECTS'.
    DATA: mt_obsoletes TYPE zpe_tt_atc_objecttype.

    METHODS constructor.
    METHODS get_attributes REDEFINITION.
    METHODS if_ci_test~query_attributes REDEFINITION.
    METHODS put_attributes REDEFINITION.
    METHODS run REDEFINITION.
    METHODS is_object_obsolete IMPORTING iv_object_type TYPE trobjtype
                                         iv_object_name TYPE sobj_name OPTIONAL
                               RAISING   zcx_pe_atc_error
                                         zcx_pe_atc_not_relevant.
    METHODS if_ci_test~display_documentation REDEFINITION .

  PRIVATE SECTION.
    CONSTANTS: mc_errornum TYPE sci_errc   VALUE '0001'.
    DATA mo_data_provider TYPE REF TO lcl_data_provider.
    METHODS is_relevant
      IMPORTING
                iv_object_type TYPE trobjtype
                iv_object_name TYPE sobj_name OPTIONAL
      RAISING   zcx_pe_atc_not_relevant.
    METHODS get_data_provider IMPORTING
                                io_data_provider TYPE REF TO lcl_data_provider OPTIONAL.




ENDCLASS.



CLASS ZCL_PE_ATC_OBSOLETE_OBJECTS IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
    super->constructor( ).
    description         = 'Forbidden/obsolete development objects'(001).
    category            = zcl_pe_atc_category_002=>gc_my_name.
    has_documentation   = abap_true.
    remote_rfc_enabled  = abap_true.
    has_attributes      = abap_true.


    fill_message mc_errornum c_error 'This Object Type is not allowed anymore. Please check our guidelines'(002) c_exceptn_imposibl.

  ENDMETHOD.


  METHOD get_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    EXPORT obsolete = mt_obsoletes TO DATA BUFFER p_attributes.
  ENDMETHOD.


  METHOD is_object_obsolete.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    is_relevant( iv_object_type = iv_object_type iv_object_name = iv_object_name ).
    IF line_exists( mt_obsoletes[ table_line = iv_object_type ] )   .
      RAISE EXCEPTION TYPE zcx_pe_atc_error.
    ELSE.
      RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
    ENDIF.
  ENDMETHOD.


  METHOD run.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.08.2022 | 12913: Source system check not needed    *
*              |            | DS4K926986                               *
************************************************************************

    TRY.
        get_data_provider(  ).
        is_object_obsolete( iv_object_type = object_type iv_object_name = object_name ).

      CATCH zcx_pe_atc_error.
        inform( p_sub_obj_type = object_type
                        p_sub_obj_name = object_name
                        p_kind         = c_error
                        p_test         = myname
                        p_code         = mc_errornum ).
      CATCH zcx_pe_atc_not_relevant.
        RETURN.
    ENDTRY.
  ENDMETHOD.


  METHOD if_ci_test~query_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
    DATA:
      lt_attributes  TYPE sci_atttab,
      ls_attribute   LIKE LINE OF lt_attributes,
      lv_message(72) TYPE c.


    CLASS cl_ci_query_attributes DEFINITION LOAD.
    fill_att mt_obsoletes    'Obsolete Objects'(003)               ' '.

    IF cl_ci_query_attributes=>generic( p_name       = gc_my_name
                                        p_title      = 'Settings'(004)
                                        p_attributes = lt_attributes
                                        p_message    = lv_message
                                        p_display    = p_display ) = 'X'.
      RETURN.
    ENDIF.

    attributes_ok = 'X'.

  ENDMETHOD.


  METHOD if_ci_test~display_documentation.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 22.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    zcl_pe_atc_aoc_super=>display_documentation( ).

  ENDMETHOD.


  METHOD get_data_provider.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
    IF io_data_provider IS BOUND.
      mo_data_provider = io_data_provider.
    ELSE.
      mo_data_provider = NEW lcl_data_provider( ).
    ENDIF.

  ENDMETHOD.


  METHOD is_relevant.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
    CASE iv_object_type.
      WHEN 'IWSG'.
        IF mo_data_provider->is_relevant_service( iv_object_name ) = abap_false.
          RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
        ENDIF.
      WHEN 'IWOM'.
        DATA(lv_service_name) = mo_data_provider->get_service( iv_object_name ).
        IF lv_service_name IS NOT INITIAL AND mo_data_provider->is_relevant_service( iv_object_name ) = abap_false.
          RAISE EXCEPTION TYPE zcx_pe_atc_not_relevant.
        ENDIF.
    ENDCASE.
  ENDMETHOD.


  METHOD put_attributes.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 15.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    IMPORT obsolete = mt_obsoletes FROM DATA BUFFER p_attributes. "#EC CI_SUBRC

  ENDMETHOD.
ENDCLASS.