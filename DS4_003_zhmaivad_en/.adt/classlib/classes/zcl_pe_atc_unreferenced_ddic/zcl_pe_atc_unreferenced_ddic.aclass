CLASS zcl_pe_atc_unreferenced_ddic DEFINITION
  PUBLIC
  INHERITING FROM cl_ci_test_ddic
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    METHODS if_ci_test~display_documentation REDEFINITION .
    METHODS run REDEFINITION.
    METHODS constructor .

  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS ZCL_PE_ATC_UNREFERENCED_DDIC IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 21.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    super->constructor( ).
    description         = 'Unreferenced DDIC Object'(001).
    category            = zcl_pe_atc_category_002=>gc_my_name.
    remote_rfc_enabled  = abap_true.
    has_documentation   = abap_true.

    add_obj_type( 'DOMA' ).
    add_obj_type( 'DTEL' ).
    add_obj_type( 'TABL' ).
    add_obj_type( 'TTYP' ).
    add_obj_type( 'SHLP' ).
    add_obj_type( 'VIEW' ).

    fill_message '001' c_error 'Unreferenced DDIC object'(m01) c_exceptn_imposibl.

  ENDMETHOD.


  METHOD run.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 21.02.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.08.2022 | 12913: Source system check not needed    *
*              |            | DS4K926986                               *
************************************************************************

    DATA(lv_destination) = zcl_pe_atc_aoc_super=>get_rfc_destination( ).


*-- Is FM available in satellite system?
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_DDIC_WHERE_USED' iv_destination = lv_destination ) = abap_false.
      RETURN.
    ENDIF.

*-- Get Where used list
    CALL FUNCTION 'ZPE_FM_GET_DDIC_WHERE_USED' DESTINATION lv_destination
      EXPORTING
        iv_object_name        = CONV ddsymtab-name( object_name )
        iv_object_type        = object_type
      EXCEPTIONS
        not_found             = 1
        not_authorized        = 2
        internal_error        = 3
        communication_failure = 2
        system_failure        = 3
        OTHERS                = 4.
    IF sy-subrc = 1.
      inform( p_test = myname
              p_kind = c_error
              p_code = '001' ).
    ELSEIF sy-subrc > 1.
      MESSAGE e011(zpe_atc) WITH 'ZPE_FM_GET_DDIC_WHERE_USED' INTO DATA(lv_msg) ##NEEDED.
      zcl_pe_atc_logger=>log_message( ).
    ENDIF.

  ENDMETHOD.


  METHOD if_ci_test~display_documentation.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 30.01.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    zcl_pe_atc_aoc_super=>display_documentation( ).

  ENDMETHOD.
ENDCLASS.