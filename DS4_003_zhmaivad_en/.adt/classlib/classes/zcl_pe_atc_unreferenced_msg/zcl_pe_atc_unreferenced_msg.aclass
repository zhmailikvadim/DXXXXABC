CLASS zcl_pe_atc_unreferenced_msg DEFINITION
  PUBLIC
  INHERITING FROM zcl_aoc_super_root
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    METHODS constructor .
    METHODS run REDEFINITION .
    METHODS if_ci_test~display_documentation REDEFINITION.

  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS ZCL_PE_ATC_UNREFERENCED_MSG IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHADDFAD     | 14.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
    super->constructor( ).

    version        = '001'.
    position       = '035'.
    category       = zcl_pe_atc_category_002=>gc_my_name.

    has_documentation = abap_true.
    has_attributes = abap_true.
    attributes_ok  = abap_true.
    remote_rfc_enabled  = abap_true.

    add_obj_type( 'MSAG' ).

    insert_scimessage(
      iv_code = '001'
      iv_text = 'Message &1 not in use, statically unrefereed'(m01) ).

  ENDMETHOD.


  METHOD run.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHADDFAD     | 14.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*
* CANOCAR      | 12.08.2022 | 12913: Source system check not needed    *
*              |            | DS4K926986                               *
*----------------------------------------------------------------------*
* CANOCAR      | 19.01.2023 | 16832: ATC error per main object         *
*              |            | considering changed subobjects only      *
*              |            | DS4K940184                               *
************************************************************************
    DATA lt_msg TYPE zpe_tt_msg.


    DATA(lv_destination) = zcl_pe_atc_aoc_super=>get_rfc_destination( ).

    DATA(lv_atc_transport) = zcl_pe_atc_aoc_super=>get_atc_transport_request( lv_destination ).
    IF lv_atc_transport IS INITIAL.
      RETURN.
    ENDIF.

*-- Is FM available in satellite system?
    IF zcl_pe_atc_aoc_super=>is_function_in_destination( iv_function = 'ZPE_FM_GET_MSG_DATA' iv_destination = lv_destination ) = abap_false.
      RETURN.
    ENDIF.

    CALL FUNCTION 'ZPE_FM_GET_MSG_DATA' DESTINATION lv_destination
      EXPORTING
        iv_object_name         = CONV ddsymtab-name( object_name )
        iv_object_type         = object_type
      IMPORTING
        et_msg                 = lt_msg
      EXCEPTIONS
        not_authorized         = 1
        communication_failure_ = 2
        system_failure         = 3
        OTHERS                 = 4.

    IF sy-subrc = 0 AND lt_msg[] IS NOT INITIAL.
      LOOP AT lt_msg ASSIGNING FIELD-SYMBOL(<ls_not_used_msg>) WHERE active IS INITIAL.
*       Is object really changed in the transport request?
        IF zcl_pe_atc_aoc_super=>is_changed_on_transport( iv_destination = lv_destination
                                                          iv_object_type = 'MESS'
                                                          iv_object_name = |{ object_name }{ <ls_not_used_msg>-msgnr }|
                                                          iv_transport   = lv_atc_transport ).
          inform( p_sub_obj_type = object_type
                  p_sub_obj_name = object_name
                  p_test         = myname
                  p_kind         = mv_errty
                  p_code         = '001'
                  p_param_1      = <ls_not_used_msg>-msgnr ).
        ENDIF.
      ENDLOOP.
    ENDIF.

  ENDMETHOD.


  METHOD if_ci_test~display_documentation.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 15.03.2022 | 9784      : ATC Development              *
*              |            | DS4K913083                               *
*----------------------------------------------------------------------*

    zcl_pe_atc_aoc_super=>display_documentation( ).

  ENDMETHOD.
ENDCLASS.