CLASS zcl_pe_change_log DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    METHODS get_change_log_new IMPORTING iv_object_type       TYPE versobjtyp OPTIONAL
                                         iv_object_name       TYPE sobj_name OPTIONAL
                                         iv_transport_request TYPE trkorr OPTIONAL
                                         iv_change_number     TYPE string OPTIONAL
                                         iv_rfc_destination   TYPE rfcdest OPTIONAL
                               RETURNING VALUE(rt_change_log) TYPE rswsourcet.
    METHODS get_change_log_upd IMPORTING iv_object_type       TYPE versobjtyp OPTIONAL
                                         iv_object_name       TYPE sobj_name OPTIONAL
                               RETURNING VALUE(rt_change_log) TYPE rswsourcet.

  PRIVATE SECTION.

    CONSTANTS: mc_line1 TYPE LINE OF rswsourcet VALUE '************************************************************************',
               mc_line2 TYPE LINE OF rswsourcet VALUE '*----------------------------------------------------------------------*'.

    METHODS get_dynamic_data IMPORTING iv_object_type       TYPE versobjtyp OPTIONAL
                                       iv_object_name       TYPE sobj_name OPTIONAL
                                       iv_transport_request TYPE trkorr OPTIONAL
                                       iv_change_number     TYPE string OPTIONAL
                                       iv_rfc_destination   TYPE rfcdest OPTIONAL
                             EXPORTING ev_line1             TYPE string
                                       ev_line2             TYPE string.
    METHODS get_template_new RETURNING VALUE(rt_result) TYPE rswsourcet.

ENDCLASS.



CLASS zcl_pe_change_log IMPLEMENTATION.


  METHOD get_template_new.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 29.11.2021 | 9390      : ABAP Templates               *
*              |            | DS4K900415                               *
*----------------------------------------------------------------------*

    APPEND mc_line1 TO rt_result.
    APPEND `*  R E V I S I O N   H I S T O R Y                                     *` TO rt_result.
    APPEND mc_line1 TO rt_result.
    APPEND `* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *` TO rt_result.
    APPEND `*              |            | TRANSPORT REQUESTS                       *` TO rt_result.
    APPEND mc_line1 TO rt_result.

  ENDMETHOD.


  METHOD get_dynamic_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 29.11.2021 | 9390      : ABAP Templates               *
*              |            | DS4K900415                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14815      : ATC Quickfix bug            *
*              |            | DS4K934149                               *
*----------------------------------------------------------------------*
* CANOCAR      | 18.05.2023 | 21486: Remove depndncy bw ATC & Template *
*              |            | DS4K952779                               *
************************************************************************

    CONSTANTS: lc_object_type TYPE string VALUE '(SAPLLOCAL_EDT1)TRANSPORT_KEY-OBJ_TYPE',
               lc_object_name TYPE string VALUE '(SAPLLOCAL_EDT1)TRANSPORT_KEY-OBJ_NAME'.

    DATA: lv_tr_description TYPE e07t-as4text,
          lv_description    TYPE zpe_trv_bltxt.


*-- Read development objects
    DATA(lv_object_type) = iv_object_type.
    IF lv_object_type IS INITIAL.
      ASSIGN (lc_object_type) TO FIELD-SYMBOL(<lv_object_type>).
      IF sy-subrc EQ 0.
        lv_object_type = <lv_object_type>.
      ENDIF.
    ENDIF.
    DATA(lv_object_name) = iv_object_name.
    IF lv_object_name IS INITIAL.
      ASSIGN (lc_object_name) TO FIELD-SYMBOL(<lv_object_name>).
      IF sy-subrc EQ 0.
        lv_object_name = <lv_object_name>.
      ENDIF.
    ENDIF.


*-- Get transport request and description
    DATA(lv_transport_request) = iv_transport_request.
    IF lv_object_type IS NOT INITIAL AND lv_object_name IS NOT INITIAL AND lv_transport_request IS INITIAL.
      SELECT e070~strkorr, e07t~as4text  INTO (@lv_transport_request, @lv_tr_description) UP TO 1 ROWS
         FROM e071 INNER JOIN e070 ON e071~trkorr      = e070~trkorr AND
                                      e070~trfunction  = 'S'         AND
                                      e070~as4user     = @sy-uname
                   INNER JOIN e07t ON e07t~trkorr      = e070~strkorr AND
                                      e07t~langu       = @sy-langu
         WHERE e071~object   = @lv_object_type AND
               e071~obj_name = @lv_object_name
         ORDER BY e070~strkorr.
        EXIT.
      ENDSELECT.
      IF sy-subrc NE 0.
        RETURN.
      ENDIF.
    ELSEIF lv_transport_request IS NOT INITIAL.
      CALL FUNCTION 'ZPE_FM_GET_TR' DESTINATION iv_rfc_destination
        EXPORTING
          iv_trkorr              = iv_transport_request
        IMPORTING
          ev_text                = lv_tr_description
        EXCEPTIONS
          not_found              = 1
          action_not_supported   = 2
          not_authorized         = 3
          communication_failure_ = 4
          system_failure         = 5
          OTHERS                 = 6.
      IF sy-subrc NE 0.
        RETURN.
      ENDIF.
    ELSE.
      RETURN.
    ENDIF.


*-- Get change number and description
    TRY.
        DATA(lv_change_number) = CONV zpe_trv_bltxt( iv_change_number ).
        DATA(lt_blocks)        = zcl_pe_trv=>get_description_blocks( iv_description = lv_tr_description ).
        IF lv_change_number IS INITIAL.
          READ TABLE lt_blocks ASSIGNING FIELD-SYMBOL(<ls_wi>) WITH KEY blockid = zcl_pe_trv=>gc_blid_work_item.
          IF sy-subrc EQ 0.
            lv_change_number = <ls_wi>-text.
          ENDIF.
        ENDIF.
        READ TABLE lt_blocks ASSIGNING FIELD-SYMBOL(<ls_desc>) WITH KEY blockid = zcl_pe_trv=>gc_blid_descr.
        IF sy-subrc EQ 0.
          lv_description = <ls_desc>-text.
        ENDIF.
      CATCH zcx_pe_trv_xx ##NO_HANDLER.
        " Empty then
    ENDTRY.


*-- Build lines
    ev_line1 = |* { sy-uname WIDTH = 12 } \| { sy-datum DATE = ENVIRONMENT } \| { lv_change_number WIDTH = 10 }: { lv_description(28) WIDTH = 28 } *|.
    ev_line2 = |*              \|            \| { lv_transport_request WIDTH = 40 } *|.


  ENDMETHOD.


  METHOD get_change_log_new.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 29.11.2021 | 9390      : ABAP Templates               *
*              |            | DS4K900415                               *
*----------------------------------------------------------------------*
* CANOCAR      | 27.10.2022 | 14815      : ATC Quickfix bug            *
*              |            | DS4K934149                               *
************************************************************************


*   Change log template
    rt_change_log = get_template_new( ).

*   Dynamic content
    get_dynamic_data( EXPORTING iv_object_type       = iv_object_type
                                iv_object_name       = iv_object_name
                                iv_transport_request = iv_transport_request
                                iv_change_number     = iv_change_number
                                iv_rfc_destination   = iv_rfc_destination
                      IMPORTING ev_line1             = DATA(lv_line1)
                                ev_line2             = DATA(lv_line2) ).

    APPEND lv_line1 TO rt_change_log.
    APPEND lv_line2 TO rt_change_log.

*   Closing
    APPEND mc_line2 TO rt_change_log.

  ENDMETHOD.


  METHOD get_change_log_upd.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 29.11.2021 | 9390      : ABAP Templates               *
*              |            | DS4K900415                               *
*----------------------------------------------------------------------*

*   Dynamic content
    get_dynamic_data( EXPORTING iv_object_type = iv_object_type
                                iv_object_name = iv_object_name
                      IMPORTING ev_line1       = DATA(lv_line1)
                                ev_line2       = DATA(lv_line2) ).

    APPEND lv_line1 TO rt_change_log.
    APPEND lv_line2 TO rt_change_log.

*   Closing
    APPEND mc_line2 TO rt_change_log.

  ENDMETHOD.
ENDCLASS.