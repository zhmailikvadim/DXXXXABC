CLASS zcl_pe_epss_sapevent_handle DEFINITION
  PUBLIC
  INHERITING FROM cl_epss_sapevent
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    METHODS resolve_link REDEFINITION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_PE_EPSS_SAPEVENT_HANDLE IMPLEMENTATION.


  METHOD resolve_link.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 27.10.2022 | 14170     : Code review analysis tools   *
*              |            | DS4K932069                               *
*----------------------------------------------------------------------*
* CANOCAR      | 18.01.2023 | 17080: Prevent runtime error             *
*              |            | DS4K940925                               *
************************************************************************

    DATA: lv_class    TYPE seoclname,
          lif_handler TYPE REF TO zif_pe_epss_sapevent_handle.


    TRY.
        zcl_pe_hm_complex=>get_value( EXPORTING iv_id           = '024F85009E261EED91C424217FE3A0D2'
                                                is_filter       = CONV zpe_doc_link_type( doc_link+3(8) )
                                      IMPORTING ev_value        = lv_class ).

        CREATE OBJECT lif_handler TYPE (lv_class).
        IF lif_handler IS BOUND.
          lif_handler->resolve_link( iv_doc_link           = doc_link
                                     io_epss_html_viewer   = epss_html_viewer
                                     is_help_info          = help_info ).
        ENDIF.

      CATCH zcx_pe_hm.
        super->resolve_link( doc_link           = doc_link
                             epss_html_viewer   = epss_html_viewer
                             help_info          = help_info ).

      CATCH cx_sy_create_object_error.
        MESSAGE i010(ad) WITH 'Navigation not supported'(001).

    ENDTRY.

  ENDMETHOD.
ENDCLASS.