CLASS zcl_pe_fe2be_conversor DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    DATA: mt_datefilters_v2 TYPE if_rap_query_filter=>tt_name_range_pairs.
    CLASS-METHODS get_instance
      RETURNING
        VALUE(ro_instance) TYPE REF TO zcl_pe_fe2be_conversor.
    METHODS constructor.
    METHODS get_be_range IMPORTING it_fe_filter       TYPE zpe_tt_selectall_fe_filter
                         RETURNING VALUE(rt_be_range) TYPE if_rap_query_filter=>tt_range_option
                         RAISING   zcx_pe_fe2be_conversor.
    METHODS get_be_daterange IMPORTING it_fe_filter       TYPE zpe_tt_selectall_fe_filter
                             RETURNING VALUE(rt_be_range) TYPE if_rap_query_filter=>tt_range_option
                             RAISING   zcx_pe_fe2be_conversor.
    METHODS get_be_range_v2 IMPORTING is_fe_filters      TYPE any
                            RETURNING VALUE(rt_be_range) TYPE if_rap_query_filter=>tt_name_range_pairs
                            RAISING   zcx_pe_fe2be_conversor.
    METHODS adjust_string_v2 CHANGING cv_fe_filter_string TYPE string.
    METHODS set_daterange_v2 IMPORTING iv_filtername TYPE string.
    METHODS convert_daterange_v2 CHANGING cv_low  TYPE string
                                          cv_high TYPE string OPTIONAL.
  PROTECTED SECTION.
  PRIVATE SECTION.
    CLASS-DATA go_instance TYPE REF TO zcl_pe_fe2be_conversor .
ENDCLASS.



CLASS zcl_pe_fe2be_conversor IMPLEMENTATION.


  METHOD constructor  ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 19369     : Select all solution -        *
*              |            | FE/BE conv DS4K948557                    *
*----------------------------------------------------------------------*
  ENDMETHOD.


  METHOD get_instance.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 19369     : Select all solution -        *
*              |            | FE/BE conv DS4K948557                    *
*----------------------------------------------------------------------*
* RIOSNAT      | 24.03.2023 | 23915     : Select all solution OData V2 *
*              |            | FE/BE conv DS4K959768                    *
*----------------------------------------------------------------------*
    IF go_instance IS NOT BOUND.
      go_instance = NEW #( ).
    ENDIF.

    ro_instance = go_instance.
    CLEAR go_instance->mt_datefilters_v2.
  ENDMETHOD.


  METHOD get_be_range.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 19369     : Select all solution -        *
*              |            | FE/BE conv DS4K948557                    *
*----------------------------------------------------------------------*

    TRY.
        LOOP AT it_fe_filter ASSIGNING FIELD-SYMBOL(<ls_fe_filter>).
          APPEND INITIAL LINE TO rt_be_range ASSIGNING FIELD-SYMBOL(<ls_be_range>).
          IF <ls_fe_filter>-operator(1) = 'N'.
            <ls_be_range>-sign = 'E'.
            IF <ls_fe_filter>-operator = 'NOTBT' OR
               <ls_fe_filter>-operator = 'NOTLT' OR
               <ls_fe_filter>-operator = 'NOTLE' OR
               <ls_fe_filter>-operator = 'NOTGT' OR
               <ls_fe_filter>-operator = 'NOTGE'.
              <ls_be_range>-option = <ls_fe_filter>-operator+3.
            ENDIF.
          ELSE.
            <ls_be_range>-sign = 'I'.
          ENDIF.
          <ls_be_range>-low = <ls_fe_filter>-values[ 1 ].

          IF <ls_fe_filter>-operator = 'BT' OR <ls_fe_filter>-operator = 'NOTBT'.
            READ TABLE <ls_fe_filter>-values ASSIGNING FIELD-SYMBOL(<lv_value>) INDEX 2.
            IF sy-subrc <> 0.
              <ls_be_range>-high = <ls_be_range>-low.
            ELSE.
              <ls_be_range>-high = <lv_value>.
            ENDIF.
          ENDIF.

          CASE <ls_fe_filter>-operator.
            WHEN 'Contains' OR 'NotContains' OR 'StartsWith' OR 'NotStartsWith' OR 'EndsWith' OR 'NotEndsWith'.
              <ls_be_range>-option = 'CP'.

              CASE <ls_fe_filter>-operator.
                WHEN 'Contains' OR 'NotContains'.
                  <ls_be_range>-low = |*{ <ls_be_range>-low }*|.
                WHEN 'StartsWith' OR 'NotStartsWith'.
                  <ls_be_range>-low = |{ <ls_be_range>-low }*|.
                WHEN 'EndsWith' OR 'NotEndsWith'.
                  <ls_be_range>-low = |*{ <ls_be_range>-low }|.
              ENDCASE.
            WHEN 'NE' OR 'Empty' OR 'NotEmpty'.
              <ls_be_range>-option = 'EQ'.
            WHEN 'EQ' OR 'BT' OR 'LT' OR 'LE' OR 'GT' OR 'GE'.
              <ls_be_range>-option = <ls_fe_filter>-operator.
          ENDCASE.
        ENDLOOP.
      CATCH cx_sy_itab_line_not_found.
        RAISE EXCEPTION TYPE zcx_pe_fe2be_conversor.
    ENDTRY.
  ENDMETHOD.


  METHOD get_be_daterange.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 19369     : Select all solution -        *
*              |            | FE/BE conv DS4K948557                    *
*----------------------------------------------------------------------*
    LOOP AT it_fe_filter ASSIGNING FIELD-SYMBOL(<ls_fe_filter>).
      APPEND INITIAL LINE TO rt_be_range ASSIGNING FIELD-SYMBOL(<ls_be_range>).
      <ls_be_range>-sign = 'I'.
      TRY .
          DATA(lv_value) = <ls_fe_filter>-values[ 1 ].
          <ls_be_range>-low = lv_value(4) && lv_value+5(2) && lv_value+8(2).
          TRY.
              lv_value = <ls_fe_filter>-values[ 2 ].
              <ls_be_range>-high = lv_value(4) && lv_value+5(2) && lv_value+8(2).
              <ls_be_range>-option = 'BT'.
            CATCH cx_sy_itab_line_not_found.
              <ls_be_range>-option = CONV #( <ls_fe_filter>-operator ).
          ENDTRY.
        CATCH cx_sy_itab_line_not_found.
          RAISE EXCEPTION TYPE zcx_pe_fe2be_conversor.
      ENDTRY.
    ENDLOOP.

  ENDMETHOD.
  METHOD get_be_range_v2.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 23915     : Select all solution OData V2 *
*              |            | FE/BE conv DS4K959768                    *
*----------------------------------------------------------------------*
    FIELD-SYMBOLS: <lt_filter_ranges> TYPE zpe_tt_selectall_fe_range_v2,
                   <lt_filter_items>  TYPE zpe_tt_selectall_fe_item_v2.

    DATA(lo_objdescr) = cl_abap_typedescr=>describe_by_data( is_fe_filters ).

    IF lo_objdescr->kind <> lo_objdescr->kind_struct.
      RAISE EXCEPTION TYPE zcx_pe_fe2be_conversor.
    ENDIF.

    DATA(lt_components) = CAST cl_abap_structdescr( lo_objdescr )->get_components( ).

    DO.
      ASSIGN COMPONENT sy-index OF STRUCTURE is_fe_filters TO FIELD-SYMBOL(<ls_filter>).

      IF sy-subrc <> 0.
        EXIT.
      ENDIF.

      DATA(ls_component) = lt_components[ sy-index ].

      CASE ls_component-type->kind.
        WHEN ls_component-type->kind_elem.
          INSERT VALUE #( name = ls_component-name range = VALUE #( ( sign = 'I' option = 'EQ' low = <ls_filter> ) ) ) INTO TABLE rt_be_range.
        WHEN ls_component-type->kind_struct.
          ASSIGN COMPONENT 'RANGES' OF STRUCTURE <ls_filter> TO <lt_filter_ranges>. "#EC CI_SUBRC
          ASSIGN COMPONENT 'ITEMS'  OF STRUCTURE <ls_filter> TO <lt_filter_items>. "#EC CI_SUBRC

          INSERT VALUE #( name = ls_component-name ) INTO TABLE rt_be_range ASSIGNING FIELD-SYMBOL(<ls_be_range>).

          IF <lt_filter_ranges> IS NOT INITIAL.
            LOOP AT <lt_filter_ranges> ASSIGNING FIELD-SYMBOL(<ls_filter_range>).
              INSERT VALUE #(
                sign = COND #( WHEN <ls_filter_range>-exclude = `true`  THEN 'E'
                               WHEN <ls_filter_range>-exclude = `false` THEN 'I' ) )
                INTO TABLE <ls_be_range>-range ASSIGNING FIELD-SYMBOL(<ls_range>) ##OPERATOR[OPTION].

              CASE <ls_filter_range>-operation.
                WHEN `EQ` OR `BT` OR `LT` OR `LE` OR `GT` OR `GE`.
                  <ls_range>-option = <ls_filter_range>-operation.
                  <ls_range>-low    = <ls_filter_range>-value1.

                  IF <ls_filter_range>-operation = `BT`.
                    <ls_range>-high = <ls_filter_range>-value2.
                  ENDIF.
                WHEN `Contains` OR `StartsWith` OR `EndsWith`.
                  <ls_range>-option = 'CP'.

                  CASE <ls_filter_range>-operation.
                    WHEN `Contains`.
                      <ls_range>-low = |*{ <ls_filter_range>-value1 }*|.
                    WHEN `StartsWith`.
                      <ls_range>-low = |{ <ls_filter_range>-value1 }*|.
                    WHEN `EndsWith`.
                      <ls_range>-low = |*{ <ls_filter_range>-value1 }|.
                  ENDCASE.
                WHEN `Empty`.
                  <ls_range>-option = 'EQ'.
              ENDCASE.
              ASSIGN mt_datefilters_v2[ name = ls_component-name ] TO FIELD-SYMBOL(<ls_datefilter>).
              IF sy-subrc = 0.
                convert_daterange_v2( CHANGING cv_low = <ls_range>-low
                                               cv_high = <ls_range>-high  ).
              ENDIF.
            ENDLOOP.
          ENDIF.

          IF <lt_filter_items> IS NOT INITIAL.
            LOOP AT <lt_filter_items> ASSIGNING FIELD-SYMBOL(<ls_filter_item>).
              ASSIGN mt_datefilters_v2[ name = ls_component-name ] TO <ls_datefilter>.
              IF sy-subrc = 0.
                convert_daterange_v2( CHANGING cv_low = <ls_filter_item>-key  ).
              ENDIF.
              INSERT VALUE #( sign = 'I' option = 'EQ' low = <ls_filter_item>-key )
                INTO TABLE <ls_be_range>-range.
            ENDLOOP.

          ENDIF.
      ENDCASE.
    ENDDO.
  ENDMETHOD.

  METHOD adjust_string_v2.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 23915     : Select all solution OData V2 *
*              |            | adjust json DS4K959768                   *
*----------------------------------------------------------------------*
    cv_fe_filter_string = replace( val  = cv_fe_filter_string
                                   sub  = `"exclude":false,`
                                   with = `"exclude":"false",`
                                   occ  = 0 ).

    cv_fe_filter_string = replace( val  = cv_fe_filter_string
                                   sub  = `"exclude":true,`
                                   with = `"exclude":"true",`
                                   occ  = 0 ).
  ENDMETHOD.

  METHOD convert_daterange_v2.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 23915     : Select all solution OData V2 *
*              |            | adjust json DS4K959768                   *
*----------------------------------------------------------------------*
    cv_low = cv_low(4) && cv_low+5(2) && cv_low+8(2).
    IF cv_high IS NOT INITIAL.
      cv_high = cv_high(4) && cv_high+5(2) && cv_high+8(2).
    ENDIF.
  ENDMETHOD.

  METHOD set_daterange_v2.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RIOSNAT      | 24.03.2023 | 23915     : Select all solution OData V2 *
*              |            | adjust json DS4K959768                   *
*----------------------------------------------------------------------*
    APPEND VALUE #( name = iv_filtername )  TO mt_datefilters_v2.
  ENDMETHOD.

ENDCLASS.