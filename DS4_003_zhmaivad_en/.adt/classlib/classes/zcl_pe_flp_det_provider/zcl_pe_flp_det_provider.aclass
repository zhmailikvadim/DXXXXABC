CLASS zcl_pe_flp_det_provider DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_rap_query_provider .

    TYPES:
      BEGIN OF gtyp_s_request_params,
        offset   TYPE int8,
        top      TYPE int8,
        fields   TYPE if_rap_query_request=>tt_requested_elements,
        sort     TYPE if_rap_query_request=>tt_sort_elements,
        search   TYPE string,
        o_filter TYPE REF TO if_rap_query_filter,
      END OF gtyp_s_request_params .
    TYPES:
      BEGIN OF gtyp_s_app_details,
        id               TYPE /ui2/chip_id_pbc,
        domainid         TYPE /ui2/page_id,
        app_type         TYPE /ui2/pb_app_type,
        ui5_component_id TYPE /ui2/pb_descr_id,
        wd_appl_id       TYPE  wdy_application_name,
      END OF gtyp_s_app_details .
    TYPES:
      BEGIN OF gtyp_s_req_params_query,
        offset TYPE int8,
        top    TYPE int8,
        fields TYPE string,
        sort   TYPE string,
        filter TYPE string,
        search TYPE string,
      END OF gtyp_s_req_params_query .
    TYPES:
      BEGIN OF gtyp_s_app_variants,
        app_variant       TYPE string,
        app_id            TYPE string,
        var_orig_resource TYPE string,
      END OF gtyp_s_app_variants.
    TYPES:
      gtyp_t_app_details TYPE SORTED TABLE OF gtyp_s_app_details WITH NON-UNIQUE KEY id.
    TYPES:
      gtyp_t_icf_nodes   TYPE SORTED TABLE OF zpe_i_icf_nodes WITH UNIQUE KEY domainid icf_node system_alias .
    TYPES:
      gtyp_t_odata_srv   TYPE SORTED TABLE OF zpe_i_odata_services WITH UNIQUE KEY domainid service_path namespace version .
    TYPES:
      gtyp_t_tms_cont_man   TYPE SORTED TABLE OF zpe_i_all_tm_cont_manager WITH UNIQUE KEY target_mapping_id tile_orig_id .
    TYPES:
      gtyp_t_app_variants TYPE SORTED TABLE OF gtyp_s_app_variants WITH UNIQUE KEY app_variant.


    CONSTANTS:
      BEGIN OF gc_entity_names,
        icf_node         TYPE string VALUE 'ZPE_I_ICF_NODES',
        odata_services   TYPE string VALUE 'ZPE_I_ODATA_SERVICES',
        cont_manager_tms TYPE string VALUE 'ZPE_I_ALL_TM_CONT_MANAGER',
      END OF gc_entity_names .
    CONSTANTS:
      BEGIN OF gc_sql_parameters,
        descending TYPE char20 VALUE 'DESCENDING',
        ascending  TYPE char20 VALUE 'ASCENDING',
        all_fields TYPE char1  VALUE '*',
        comma      TYPE char1 VALUE ',',
        slash      TYPE char1 VALUE '/',
        dot        TYPE char1 VALUE '.',
      END OF gc_sql_parameters .

    METHODS constructor .
  PROTECTED SECTION.
  PRIVATE SECTION.

    DATA mv_req_entity TYPE string .
    DATA mif_service_utils TYPE REF TO /ui2/if_fcm_service_status .

    METHODS get_params_from_req
      IMPORTING
        !iif_request     TYPE REF TO if_rap_query_request
      RETURNING
        VALUE(rs_params) TYPE gtyp_s_request_params .
    METHODS read_services
      IMPORTING
        !iif_request  TYPE REF TO if_rap_query_request
        !iif_response TYPE REF TO if_rap_query_response .
    METHODS read_tms_via_cont_man_api
      IMPORTING
        !iif_request  TYPE REF TO if_rap_query_request
        !iif_response TYPE REF TO if_rap_query_response
      RAISING
        zcx_pe_flp_det_provider .
    METHODS get_appl_to_check_services_db
      IMPORTING
        !iif_filter             TYPE REF TO if_rap_query_filter
        !iv_mask                TYPE string OPTIONAL
      RETURNING
        VALUE(rts_applications) TYPE gtyp_t_app_details
      RAISING
        cx_rap_query_filter_no_range .
    METHODS get_services_for_apps
      IMPORTING
        !its_applications      TYPE gtyp_t_app_details
      EXPORTING
        !ets_converted_app_fdm TYPE /ui2/if_fdm=>tt_tm_sorted
      RETURNING
        VALUE(rth_services)    TYPE /ui2/if_fcm_service_status=>tth_item_service_status .
    METHODS map_icf_to_app
      IMPORTING
        !its_conv_app_fdm    TYPE /ui2/if_fdm=>tt_tm_sorted
        !ith_services        TYPE /ui2/if_fcm_service_status=>tth_item_service_status
      RETURNING
        VALUE(rts_icf_nodes) TYPE gtyp_t_icf_nodes .
    METHODS conv_req_props_tostr_query
      IMPORTING
        !is_params             TYPE gtyp_s_request_params
        !iv_default_val_order  TYPE string
      RETURNING
        VALUE(rs_params_query) TYPE gtyp_s_req_params_query .
    METHODS map_odata_to_app
      IMPORTING
        !its_conv_app_fdm    TYPE /ui2/if_fdm=>tt_tm_sorted
        !ith_services        TYPE /ui2/if_fcm_service_status=>tth_item_service_status
      RETURNING
        VALUE(rts_odata_srv) TYPE gtyp_t_odata_srv .
    METHODS get_app_variants
      RETURNING
        VALUE(rts_addons) TYPE gtyp_t_app_variants .
ENDCLASS.



CLASS ZCL_PE_FLP_DET_PROVIDER IMPLEMENTATION.


  METHOD if_rap_query_provider~select.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*
    mv_req_entity = io_request->get_entity_id( ).

    IF mv_req_entity = gc_entity_names-icf_node
        OR mv_req_entity = gc_entity_names-odata_services.
      read_services(
          iif_request  = io_request
          iif_response = io_response
      ).
    ELSEIF mv_req_entity = gc_entity_names-cont_manager_tms.
      read_tms_via_cont_man_api(
          iif_request  = io_request
          iif_response = io_response
      ).
    ENDIF.
  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*
    mif_service_utils = NEW /ui2/cl_fcm_service_status( ).
  ENDMETHOD.


  METHOD get_params_from_req.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*
    rs_params-offset = iif_request->get_paging( )->get_offset( ).
    IF iif_request->get_paging( )->get_page_size( ) = iif_request->get_paging( )->page_size_unlimited.
      rs_params-top = 1000.
    ELSE.
      rs_params-top = iif_request->get_paging( )->get_page_size( ).
    ENDIF.
    rs_params-fields = iif_request->get_requested_elements( ).
    rs_params-sort = iif_request->get_sort_elements( ).
    rs_params-o_filter = iif_request->get_filter( ).
    rs_params-search = iif_request->get_search_expression( ).
  ENDMETHOD.


  METHOD read_services.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*
    DATA: lts_converted_apps TYPE /ui2/if_fdm=>tt_tm_sorted,
          lv_data_counter    TYPE int8 VALUE 0.

    FIELD-SYMBOLS: <lt_data> TYPE ANY TABLE.

* Fetch services related data for applications

    TRY.

        DATA(ls_req_params) = get_params_from_req( iif_request ).
        DATA(ls_req_params_query) = conv_req_props_tostr_query(
                                      is_params            = ls_req_params
                                      iv_default_val_order = 'DOMAINID'
                                    ).

        DATA(lts_apps_to_check) = get_appl_to_check_services_db( iif_filter = ls_req_params-o_filter ). "#EC CI_CONV_OK
        DATA(lth_services) = get_services_for_apps(
          EXPORTING
            its_applications = lts_apps_to_check
          IMPORTING
            ets_converted_app_fdm = lts_converted_apps  ). "#EC CI_CONV_OK

** Map fetched information for output and apply request query parameters

        IF mv_req_entity = gc_entity_names-icf_node.
          DATA(lts_icf_nodes) = map_icf_to_app(
                                                its_conv_app_fdm = lts_converted_apps
                                                ith_services = lth_services ). "#EC CI_CONV_OK

          SELECT * FROM @lts_icf_nodes AS icf_nodes "#EC CI_SUBRC "#EC CI_DYNWHERE
            WHERE (ls_req_params_query-filter)
            INTO TABLE @DATA(lt_filtered_icf_nodes).

          lv_data_counter = lines( lt_filtered_icf_nodes ).

          SELECT * FROM @lt_filtered_icf_nodes AS icf_nodes "#EC CI_SUBRC "#EC CI_DYNWHERE
            ORDER BY (ls_req_params_query-sort)
            INTO TABLE @DATA(lt_ordered_res)
            UP TO @ls_req_params_query-top ROWS
            OFFSET @ls_req_params_query-offset.

          ASSIGN lt_ordered_res TO <lt_data>.
        ELSEIF mv_req_entity = gc_entity_names-odata_services.
          DATA(lts_odata_services) = map_odata_to_app(
                                      its_conv_app_fdm = lts_converted_apps
                                      ith_services     = lth_services
                                    ).
          SELECT * FROM @lts_odata_services AS icf_nodes "#EC CI_SUBRC "#EC CI_DYNWHERE
            WHERE (ls_req_params_query-filter)
            INTO TABLE @DATA(lt_filtered_odata_srv).

          lv_data_counter = lines( lt_filtered_odata_srv ).

          SELECT * FROM @lt_filtered_odata_srv AS icf_nodes "#EC CI_SUBRC "#EC CI_DYNWHERE
            ORDER BY (ls_req_params_query-sort)
            INTO TABLE @DATA(lt_ordered_odata)
            UP TO @ls_req_params_query-top ROWS
            OFFSET @ls_req_params_query-offset.

          ASSIGN lt_ordered_odata TO <lt_data>.
        ENDIF.

        IF iif_request->is_total_numb_of_rec_requested( ) = abap_true.
          iif_response->set_total_number_of_records( lv_data_counter ).
        ENDIF.
        iif_response->set_data( <lt_data> ).
      CATCH cx_rap_query_response_set_twic ##NO_HANDLER. " RAP query response set twice
      CATCH cx_rap_query_filter_no_range ##NO_HANDLER.
    ENDTRY.
  ENDMETHOD.


  METHOD get_services_for_apps.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*

*Collect all component ids for application and find service-related info
    DATA: lts_apps_to_check TYPE /ui2/if_fdm=>tt_tm_sorted.
    LOOP AT its_applications ASSIGNING FIELD-SYMBOL(<ls_app_details>).
      TRY.
          IF <ls_app_details>-app_type = /ui2/if_fdm=>gc_app_type-sapui5 AND <ls_app_details>-ui5_component_id IS NOT INITIAL.
            INSERT VALUE /ui2/if_fdm=>ts_tm( key = /ui2/cl_fdm_pb_adapter=>cnv_chip_id_to_tm_key( CONV #( <ls_app_details>-id ) )
                                             ref_key-id = <ls_app_details>-domainid
                                             application-target-ui5-component = <ls_app_details>-ui5_component_id
                                             application-type = /ui2/if_fdm=>gc_app_type-sapui5
                                            ) INTO TABLE lts_apps_to_check.
          ELSEIF <ls_app_details>-app_type = /ui2/if_fdm=>gc_app_type-web_dynpro.
            INSERT VALUE /ui2/if_fdm=>ts_tm( key = /ui2/cl_fdm_pb_adapter=>cnv_chip_id_to_tm_key( CONV #( <ls_app_details>-id ) )
                                             ref_key-id = <ls_app_details>-domainid
                                             application-target-wda-application = <ls_app_details>-wd_appl_id
                                             application-type = /ui2/if_fdm=>gc_app_type-web_dynpro
                                            ) INTO TABLE lts_apps_to_check.
          ENDIF.
        CATCH /ui2/cx_fdm_pb_adapter ##NO_HANDLER."No need to handle ->move to next iteration
      ENDTRY.
    ENDLOOP.

    DATA(lo_catalog_items) = NEW /ui2/cl_fdm_catalog_items(
          it_target_mapping       = lts_apps_to_check
        ).
    ets_converted_app_fdm = lts_apps_to_check.          "#EC CI_CONV_OK
    rth_services = mif_service_utils->get_item_service_status( io_catalog_items = lo_catalog_items ). "#EC CI_CONV_OK

  ENDMETHOD.


  METHOD map_icf_to_app.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*

    LOOP AT its_conv_app_fdm ASSIGNING FIELD-SYMBOL(<ls_data_to_check>).
      LOOP AT ith_services[ item_key = <ls_data_to_check>-key ]-icf_service_status ASSIGNING FIELD-SYMBOL(<ls_icf_node>). "#EC CI_NESTED
        INSERT VALUE #(
                        domainid = <ls_data_to_check>-ref_key
                        icf_node = <ls_icf_node>-url
                        system_alias = <ls_icf_node>-system_alias
                        is_active = SWITCH #( <ls_icf_node>-status_icon
                                               WHEN /ui2/if_fcm_service_status=>gc_traffic_light-green THEN abap_true
                                               ELSE abap_false
                                             )
                        status_txt = <ls_icf_node>-status_text
                        status_add_info = <ls_icf_node>-status_add_info
                      ) INTO TABLE rts_icf_nodes.
      ENDLOOP.
    ENDLOOP.
  ENDMETHOD.


  METHOD conv_req_props_tostr_query.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*

* Get request parameters

    rs_params_query-top = is_params-top.
    rs_params_query-offset = is_params-offset.

    LOOP AT is_params-sort ASSIGNING FIELD-SYMBOL(<ls_sort>).
      rs_params_query-sort = |{ rs_params_query-sort } { <ls_sort>-element_name }{ gc_sql_parameters-comma }|.
    ENDLOOP.
    IF <ls_sort> IS NOT ASSIGNED.
      rs_params_query-sort = |{ iv_default_val_order } { gc_sql_parameters-ascending }|.
    ELSE.
      rs_params_query-sort = substring( val = rs_params_query-sort off = 0 len = strlen( rs_params_query-sort ) - 1 ).
      IF <ls_sort>-descending = abap_true.
        rs_params_query-sort = |{ rs_params_query-sort } { gc_sql_parameters-descending }|.
      ELSE.
        rs_params_query-sort = |{ rs_params_query-sort } { gc_sql_parameters-ascending }|.
      ENDIF.
    ENDIF.

    IF is_params-fields IS NOT INITIAL.
      rs_params_query-fields = concat_lines_of( table = is_params-fields sep = gc_sql_parameters-comma ).
    ELSE.
      rs_params_query-fields = gc_sql_parameters-all_fields.
    ENDIF.

    rs_params_query-filter = is_params-o_filter->get_as_sql_string( ).

  ENDMETHOD.


  METHOD map_odata_to_app.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*
    LOOP AT its_conv_app_fdm ASSIGNING FIELD-SYMBOL(<ls_data_to_check>).
      LOOP AT ith_services[ item_key = <ls_data_to_check>-key ]-odata_service_status ASSIGNING FIELD-SYMBOL(<ls_odata_service>). "#EC CI_NESTED
        INSERT VALUE #(
                        domainid = <ls_data_to_check>-ref_key
                        service_path = <ls_odata_service>-ext_service_name
                        namespace = <ls_odata_service>-namespace
                        version = <ls_odata_service>-version
                        backend_system = syst-sysid
                        is_active = <ls_odata_service>-service_is_active
                        status_txt = <ls_odata_service>-status_text
                        status_add_info = <ls_odata_service>-status_add_info
                        path = |{ <ls_odata_service>-namespace }{ <ls_odata_service>-ext_service_name }/{ <ls_odata_service>-version }/|
                      ) INTO TABLE rts_odata_srv.
      ENDLOOP.
    ENDLOOP.
  ENDMETHOD.


  METHOD read_tms_via_cont_man_api.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
* RUTKOVIA     | 21.08.2023 | 23424     : adidas Fiori Library - Add Technical Cat  *
*              |            | DS4K958550
*----------------------------------------------------------------------*
    DATA: lif_flp_cont_mgr TYPE REF TO /ui2/if_flp_cont_mgr,
          lif_messaging    TYPE REF TO /ui2/if_fcm_messaging.

    DATA: lts_ttms        TYPE gtyp_t_tms_cont_man,
          lv_data_counter TYPE int8 VALUE 0,
          ls_catalog_key  TYPE /ui2/if_fdm=>ts_catalog_key,
          lt_catalogs     TYPE /ui2/if_fdm=>tt_catalog_key.


* Get data for apps the same way as in content manager report.

    TRY.
        DATA(ls_req_params) = get_params_from_req( iif_request ).
        DATA(ls_req_params_query) = conv_req_props_tostr_query(
                                      is_params            = ls_req_params
                                      iv_default_val_order = 'TARGET_MAPPING_ID'
                                    ).



        DATA(lo_catalog_api) = NEW /ui2/cl_fdm_catalog_api(
          iv_scope          = /ui2/if_fdm=>gc_scope-customization
          iv_use_cache      = abap_true ).

        lif_messaging = NEW /ui2/cl_fcm_messaging( ).

        lif_flp_cont_mgr = NEW /ui2/cl_flp_cont_mgr( iv_scope = /ui2/if_fdm=>gc_scope-customization
                                    io_catalog_api = lo_catalog_api
                                    io_messaging = lif_messaging ).

        DATA(lt_r_filter) = ls_req_params-o_filter->get_as_ranges( ). "#EC CI_CONV_OK
        ASSIGN lt_r_filter[ name = 'TARGET_MAPPING_ID' ] TO FIELD-SYMBOL(<ls_r_filter>). "#EC CI_STDSEQ
        IF sy-subrc = 0.
          SELECT tm_det~parentid FROM zpe_i_tm_details  AS tm_det
            INNER JOIN   zpe_i_all_business_catalogs AS bus_cat ON tm_det~parentid = bus_cat~id
          WHERE tm_det~domainid IN @<ls_r_filter>-range
          INTO TABLE @DATA(lt_page_cats).
          IF sy-subrc = 0.
            LOOP AT lt_page_cats ASSIGNING FIELD-SYMBOL(<ls_page_catalog>).
              CLEAR: ls_catalog_key.

              TRY.
                  ls_catalog_key = /ui2/cl_fdm_pb_adapter=>cnv_catpage_id_to_catalog_key( iv_catalogpage_id = CONV #( <ls_page_catalog> ) ).
                  APPEND ls_catalog_key TO lt_catalogs.
                CATCH /ui2/cx_fdm_pb_adapter  ##NO_HANDLER.
              ENDTRY.
            ENDLOOP.
          ENDIF.

        ELSE.
          lt_catalogs = lo_catalog_api->/ui2/if_fdm_catalog_api~get_all_catalog_keys( ).
        ENDIF.

        lif_flp_cont_mgr->initialize_tables_for_catalogs(
             it_catalog_key = lt_catalogs
         ).

        DATA(lts_app_variants) = get_app_variants( ).

        LOOP AT lif_flp_cont_mgr->get_all_tiles_tms( ) ASSIGNING FIELD-SYMBOL(<ls_ttm>) WHERE tm_orig_id IS NOT INITIAL. "#EC CI_SORTSEQ
          DATA(ls_tms) = VALUE zpe_i_all_tm_cont_manager(
                                                   target_mapping_id = <ls_ttm>-tm_orig_id
                                                   tile_orig_id = <ls_ttm>-tile_orig_id
                                                   fiori_id = <ls_ttm>-fiori_id
                                                   semantic_action = <ls_ttm>-semantic_action
                                                   semantic_object = <ls_ttm>-semantic_object
                                                   trans = <ls_ttm>-transaction
                                                   application_detail = <ls_ttm>-application_detail
                                                   application_type = <ls_ttm>-application_type
                                                   ttm_title = <ls_ttm>-ttm_title
                                                   tm_match_type = <ls_ttm>-tile_tm_matching_text
                                                   tm_orig_catalog_id = <ls_ttm>-tm_orig_catalog_id
                                                   tile_keywords = <ls_ttm>-tile_keywords
                                                 ).
          IF ls_tms-application_detail IS NOT INITIAL.
**            Check if application has variant
            ASSIGN lts_app_variants[ app_variant = ls_tms-application_detail ] TO FIELD-SYMBOL(<ls_app_variant>).
            IF sy-subrc = 0.
              IF ls_tms-fiori_id IS INITIAL.
                ls_tms-fiori_id = <ls_app_variant>-app_id.
              ENDIF.
              ls_tms-variant_orig_resource = <ls_app_variant>-var_orig_resource.
              ls_tms-is_app_variant = abap_true.
            ENDIF.
          ENDIF.
          INSERT ls_tms INTO TABLE lts_ttms.
        ENDLOOP.

        SELECT * FROM @lts_ttms AS ttms_cont_man "#EC CI_SUBRC "#EC CI_DYNWHERE
          WHERE (ls_req_params_query-filter)
          INTO TABLE @DATA(lt_filtered_ttms).

        lv_data_counter = lines( lt_filtered_ttms ).

        SELECT * FROM @lt_filtered_ttms AS ttms_cont_man "#EC CI_SUBRC "#EC CI_DYNWHERE
            ORDER BY (ls_req_params_query-sort)
            INTO TABLE @DATA(lt_ttms_ordered)
            UP TO @ls_req_params_query-top ROWS
            OFFSET @ls_req_params_query-offset.



        IF iif_request->is_total_numb_of_rec_requested( ) = abap_true.
          iif_response->set_total_number_of_records( lv_data_counter ).
        ENDIF.
        iif_response->set_data( lt_ttms_ordered ).
      CATCH /ui2/cx_fdm_cache_invalid INTO DATA(lo_err). " FLP Data Model: Exception Cache for Read is invalid
        RAISE EXCEPTION TYPE zcx_pe_flp_det_provider
          EXPORTING
            textid   = zcx_pe_flp_det_provider=>flp_cache_update
            previous = lo_err.
      CATCH /ui2/cx_fdm_unexpected.    " FLP Data Model: Unexpected fail
        RAISE EXCEPTION TYPE zcx_pe_flp_det_provider
          EXPORTING
            textid = zcx_pe_flp_det_provider=>flp_unexpected_error.

      CATCH cx_rap_query_response_set_twic ##NO_HANDLER. " RAP query response set twice
      CATCH cx_rap_query_filter_no_range cx_rap_query_provider_contract cx_sy_no_handler."
        RAISE EXCEPTION TYPE zcx_pe_flp_det_provider
          EXPORTING
            textid = zcx_pe_flp_det_provider=>flp_unexpected_error.
    ENDTRY.
  ENDMETHOD.


  METHOD get_app_variants.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 05.09.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*

    DATA: ls_app_variant TYPE zcl_pe_flp_det_provider=>gtyp_s_app_variants.

*Fetching app variants info

    SELECT namespace FROM /uif/lrepdcontcd
      WHERE trobjtype = 'ADVT'
         OR trobjtype = 'ADVD'
    INTO TABLE @DATA(lt_unparsed_varinats).

    IF sy-subrc = 0.

      LOOP AT lt_unparsed_varinats ASSIGNING FIELD-SYMBOL(<ls_unparsed_variant>).
        CLEAR: ls_app_variant.
        SPLIT <ls_unparsed_variant> AT zcl_pe_flp_det_provider=>gc_sql_parameters-slash INTO TABLE DATA(lt_parsed_variant).
        ASSIGN lt_parsed_variant[ table_line = 'apps' ]  TO FIELD-SYMBOL(<lv_parsed_variant>) ##NO_TEXT. "#EC CI_STDSEQ
        IF sy-subrc = 0.
          DATA(lv_app_id_index) = sy-tabix + 1.
          ASSIGN lt_parsed_variant[ lv_app_id_index ] TO FIELD-SYMBOL(<lv_app_name_with_namespace>).
          IF sy-subrc = 0.
            ls_app_variant-var_orig_resource = <lv_app_name_with_namespace>.
            SPLIT <lv_app_name_with_namespace> AT zcl_pe_flp_det_provider=>gc_sql_parameters-dot INTO TABLE DATA(lt_app_name_data).
**      Get last element -> name of application
            ASSIGN lt_app_name_data[ lines( lt_app_name_data ) ] TO FIELD-SYMBOL(<lv_app_name>).
            IF sy-subrc = 0 AND to_lower( <lv_app_name>+0(1) ) = 'z'.
              ls_app_variant-app_id = <lv_app_name>.
            ENDIF.
**    Find variant
            ASSIGN lt_parsed_variant[ table_line = 'appVariants' ]  TO <lv_parsed_variant>. "#EC CI_STDSEQ
            IF sy-subrc = 0.
              DATA(lv_variant_index) = sy-tabix + 1.
              ASSIGN lt_parsed_variant[ lv_variant_index ] TO <lv_app_name_with_namespace>.
              IF sy-subrc = 0.
                ls_app_variant-app_variant = <lv_app_name_with_namespace>.
                INSERT ls_app_variant INTO TABLE rts_addons.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDLOOP.
    ENDIF.

  ENDMETHOD.


  METHOD get_appl_to_check_services_db.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RUTKOVIA     | 18.07.2022 | 12218     : [Fiori Launchpad Documentation] AppDocu  *
*              |            | DS4K924286                               *
*----------------------------------------------------------------------*
    DATA: lv_mask TYPE string.
    TRY.
        IF iv_mask IS NOT SUPPLIED.
          lv_mask = '%:Z%'.
        ENDIF.
        DATA(lv_filter_sql) = iif_filter->get_as_sql_string( ).
        DATA(lt_r_filter) = iif_filter->get_as_ranges( ). "#EC CI_CONV_OK
        ASSIGN lt_r_filter[ name = 'DOMAINID' ] TO FIELD-SYMBOL(<ls_r_filter>). "#EC CI_STDSEQ
        IF sy-subrc = 0.
          TRY.
              SELECT id, domainid, app_type, ui5_component_id, wd_appl_id FROM zpe_i_tm_details "#EC CI_SUBRC "#EC CI_DYNWHERE
                WHERE (lv_filter_sql)
              INTO TABLE @rts_applications.
            CATCH  cx_sy_dynamic_osql_semantics.
              SELECT id, domainid, app_type, ui5_component_id, wd_appl_id FROM zpe_i_tm_details "#EC CI_SUBRC
                WHERE domainid IN @<ls_r_filter>-range
              INTO TABLE @rts_applications.
          ENDTRY.
        ELSE.
          SELECT id, domainid, app_type, ui5_component_id, wd_appl_id FROM zpe_i_tm_details "#EC CI_SUBRC
                  WHERE parentid LIKE @lv_mask
                 INTO TABLE @rts_applications.
        ENDIF.
      CATCH cx_rap_query_filter_no_range.
        SELECT id, domainid, app_type, ui5_component_id, wd_appl_id FROM zpe_i_tm_details "#EC CI_SUBRC "#EC CI_DYNWHERE
          WHERE (lv_filter_sql)
        INTO TABLE @rts_applications."If fetch domain id is null -> no ranges exists
    ENDTRY.
  ENDMETHOD.
ENDCLASS.