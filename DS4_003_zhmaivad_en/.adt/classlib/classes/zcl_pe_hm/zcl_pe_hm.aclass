CLASS zcl_pe_hm DEFINITION
  PUBLIC
  CREATE PROTECTED .

  PUBLIC SECTION.

    CONSTANTS:
      BEGIN OF gc_s_mapping_type,
        names         TYPE zpe_hm_mapping_type VALUE 'N',
        data_elements TYPE zpe_hm_mapping_type VALUE 'D',
        position      TYPE zpe_hm_mapping_type VALUE 'P',
      END OF gc_s_mapping_type .
    CONSTANTS:
      BEGIN OF gc_s_active,
        yes TYPE zpe_hm_active_icon VALUE icon_green_light,
        no  TYPE zpe_hm_active_icon VALUE icon_light_out,
      END OF gc_s_active .
    CONSTANTS:
      BEGIN OF gc_s_md_type,
        in  TYPE zpe_hm_type_metadata VALUE icon_wd_inbound_plug,
        out TYPE zpe_hm_type_metadata VALUE icon_wd_outbound_plug,
      END OF gc_s_md_type .
    CONSTANTS:
      BEGIN OF gc_s_in_use,
        yes TYPE zpe_hm_used VALUE icon_green_light,
        no  TYPE zpe_hm_used VALUE icon_light_out,
      END OF gc_s_in_use .
    CONSTANTS:
      BEGIN OF gc_s_wu_type,
        abap TYPE rsobj_cls  VALUE icon_debugger_step_into,
        cds  TYPE rsobj_cls  VALUE icon_database_table_ina,
        amdp TYPE rsobj_cls  VALUE icon_database_table,
        rest TYPE rsobj_cls  VALUE icon_biw_source_sys_ext,
      END OF gc_s_wu_type.

    CLASS-METHODS get_id
      RETURNING
        VALUE(rv_id) TYPE zpe_hm_id
      RAISING
        zcx_pe_hm .

    CLASS-METHODS get_metadata
      RETURNING
        VALUE(rt_data) TYPE zpe_tt_hm_metadata
      RAISING
        zcx_pe_hm .

    CLASS-METHODS get_type IMPORTING iv_id           TYPE zpe_hm_id_generic
                           RETURNING VALUE(rv_value) TYPE zpe_hm_type
                           RAISING   /iwbep/cx_mgw_busi_exception.

  PROTECTED SECTION.

  PRIVATE SECTION.

    CONSTANTS: mc_nr_object   TYPE nrobj VALUE 'ZHM',
               mc_nr_interval TYPE nrnr VALUE '01'.

ENDCLASS.



CLASS ZCL_PE_HM IMPLEMENTATION.


  METHOD get_id.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 20.01.2022 | 9407      : Hardcode Manager             *
*              |            | DS4K900411                               *
*----------------------------------------------------------------------*
* CANOCAR      | 22.06.2022 | 11751: Feature pack 22-2-6               *
*              |            | DS4K921477                               *
*----------------------------------------------------------------------*
* CANOCAR      | 20.04.2023 | 20248: Technical debt                    *
*              |            | DS4K949969                               *
*----------------------------------------------------------------------*

    TRY.
*       Interval 01 for productive usage, 02 for testing
        cl_numberrange_runtime=>number_get( EXPORTING nr_range_nr = mc_nr_interval
                                                      object      = mc_nr_object
                                            IMPORTING number      = DATA(lv_id)   ).
        rv_id = shift_left( val = lv_id places = 10 ).
      CATCH cx_number_ranges.
        RAISE EXCEPTION TYPE zcx_pe_hm MESSAGE e004(zpe_hm) WITH 'ZHM'.
    ENDTRY.

  ENDMETHOD.


  METHOD get_metadata.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 20.01.2022 | 9407      : Hardcode Manager             *
*              |            | DS4K900411                               *
*----------------------------------------------------------------------*

    SELECT metadata
      FROM zpe_t_hm_md
      INTO TABLE rt_data.
    IF sy-subrc NE 0.
      RAISE EXCEPTION TYPE zcx_pe_hm MESSAGE e013(zpe_hm).
    ENDIF.

  ENDMETHOD.


  METHOD get_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 20.04.2023 | 20248      : aHM ODATA Service           *
*              |            | DS4K949969                               *
*----------------------------------------------------------------------*
* CANOCAR      | 13.09.2023 | 24554: Encrypted hardcode                *
*              |            | DS4K961739                               *
************************************************************************

    DATA(lv_id) = |{ CONV zpe_hm_id( iv_id ) ALPHA = IN }|.

*   Is it basic?
    SELECT SINGLE id INTO @DATA(lv_basic_id)    ##NEEDED
        FROM zpe_t_hm_h
       WHERE id = @lv_id.
    IF sy-subrc = 0.
      rv_value = zcl_pe_hm_basic=>gc_type.
      RETURN.
    ENDIF.

*   Is it a switch?
    SELECT SINGLE id INTO @DATA(lv_switch_id)   ##NEEDED
        FROM zpe_t_hm_bs01
       WHERE id = @lv_id.
    IF sy-subrc = 0.
      rv_value = zcl_pe_hm_switch=>gc_type.
      RETURN.
    ENDIF.

*   Is it complex?
    SELECT SINGLE id INTO @DATA(lv_brf_id)      ##NEEDED
        FROM fdt_admn_0000
       WHERE id = @iv_id.
    IF sy-subrc = 0.
      rv_value = zcl_pe_hm_complex=>gc_type.
      RETURN.
    ENDIF.

*   Is it encrypted?
    SELECT SINGLE id INTO @DATA(lv_enc_id)      ##NEEDED
        FROM zpe_t_hm_e01
       WHERE id = @lv_id.
    IF sy-subrc = 0.
      rv_value = zcl_pe_hm_encrypted=>gc_type.
      RETURN.
    ENDIF.


*   Hardcode ID not found
    MESSAGE e011(zpe_hm) WITH iv_id INTO DATA(lv_text).
    RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
      EXPORTING
        textid            = /iwbep/cx_mgw_busi_exception=>resource_not_found
        http_status_code  = /iwbep/cx_mgw_busi_exception=>gcs_http_status_codes-not_found
        message_unlimited = lv_text.

  ENDMETHOD.
ENDCLASS.