CLASS zcl_pe_hm_demo DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_amdp_marker_hdb.

    TYPES: BEGIN OF gtyp_s_company_code,
            bukrs TYPE t001-bukrs,
            butxt TYPE t001-butxt,
           END OF gtyp_s_company_code,
           gtyp_t_company_codes TYPE STANDARD TABLE OF gtyp_s_company_code.


    CLASS-METHODS search AMDP OPTIONS CDS SESSION CLIENT current
      EXPORTING VALUE(et_trs) TYPE zpe_tt_hm_output_trs
      RAISING   cx_amdp_execution_failed.

    CLASS-METHODS search_with_join AMDP OPTIONS CDS SESSION CLIENT current
      EXPORTING VALUE(et_trs) TYPE zpe_tt_hm_output_trs
      RAISING   cx_amdp_execution_failed.

    CLASS-METHODS search_switch AMDP OPTIONS CDS SESSION CLIENT current
      EXPORTING VALUE(et_company_codes) TYPE gtyp_t_company_codes
      RAISING   cx_amdp_execution_failed.

  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS ZCL_PE_HM_DEMO IMPLEMENTATION.


  METHOD search BY DATABASE PROCEDURE FOR HDB LANGUAGE SQLSCRIPT USING e071k e07t zpe_i_hm.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 14.02.2022 | 9407      : Hardcode Manager             *
*              |            | DS4K900411                               *
*----------------------------------------------------------------------*
* CANOCAR      | 22.06.2022 | 11751: Feature pack 22-2-6               *
*              |            | DS4K921477                               *
*----------------------------------------------------------------------*

    lt_hm  = select field1 from zpe_i_hm('0000000008') where sequence = '001';

    et_trs = select DISTINCT '' as id, tr.trkorr as transport_request, 'W' as type, tx.as4text as description
               from e071k as tr inner join :lt_hm as hm on hm.field1 = tr.mastername
                                inner join e07t   as tx on tr.trkorr = tx.trkorr  and
                                                           tx.langu  = session_context('LOCALE_SAP');
  ENDMETHOD.


  METHOD search_with_join BY DATABASE PROCEDURE FOR HDB LANGUAGE SQLSCRIPT USING e071k e07t zpe_i_hm.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 14.02.2022 | 9407      : Hardcode Manager             *
*              |            | DS4K900411                               *
*----------------------------------------------------------------------*
* CANOCAR      | 22.06.2022 | 11751: Feature pack 22-2-6               *
*              |            | DS4K921477                               *
*----------------------------------------------------------------------*

    et_trs = select DISTINCT '' as id, tr.trkorr as transport_request, 'W' as type, tx.as4text as description
               from e071k as tr inner join zpe_i_hm('0000000008') as hm on hm.field1   = tr.mastername and
                                                                  hm.sequence = '002'
                                inner join e07t          as tx on tr.trkorr   = tx.trkorr     and
                                                                  tx.langu    = session_context('LOCALE_SAP');
  ENDMETHOD.


  METHOD search_switch BY DATABASE PROCEDURE FOR HDB LANGUAGE SQLSCRIPT USING t001 zpe_i_hm_bs.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 14.02.2022 | 9407      : Hardcode Manager             *
*              |            | DS4K900411                               *
*----------------------------------------------------------------------*

    lt_hm            = select field1 from zpe_i_hm_bs('0000000015');
    et_company_codes = select t001.bukrs, t001.butxt
                         from t001 as t001 inner join :lt_hm as hm on hm.field1 = t001.bukrs
                        where mandt = session_context('CLIENT');

  ENDMETHOD.
ENDCLASS.