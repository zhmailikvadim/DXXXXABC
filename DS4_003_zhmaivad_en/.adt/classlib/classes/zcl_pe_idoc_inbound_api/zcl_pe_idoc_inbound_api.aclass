CLASS zcl_pe_idoc_inbound_api DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_http_extension .
  PROTECTED SECTION.
  PRIVATE SECTION.
    CONSTANTS: mc_body TYPE string VALUE '<html><body><p>?</p></body></html>'.

ENDCLASS.



CLASS zcl_pe_idoc_inbound_api IMPLEMENTATION.


  METHOD if_http_extension~handle_request.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 15.03.2023 | 25223: Creation                          *
*              |            | DS4K964360                               *
*----------------------------------------------------------------------*
    "e.g. https://ds4.adidas-group.com/adidas/idoc?file=ad-local-sync-ftp-dev/DS4/ORDERS/0000000000927352

*-- Get file
    DATA(lv_file) = server->request->get_form_field( 'file' ).
    IF lv_file IS INITIAL.
      server->response->set_cdata( replace( val = mc_body sub = '?' with = 'Field query parameter is missing'(001) ) ).
      server->response->set_status( code = '400' reason = CONV string( 'Field query parameter is missing'(001) ) ).
      RETURN.
    ENDIF.


*-- Get file
    TRY.
        DATA(lo_idoc) = zcl_pe_idoc_inbound=>factory( lv_file ).
        DATA(lv_idoc) = lo_idoc->create_from_aws( ).

        server->response->set_cdata( replace( val = mc_body sub = '?' with = |IDOC { lv_idoc } has been created| ) ).

      CATCH zcx_pe_idoc INTO DATA(lo_exc).
        server->response->set_cdata( replace( val = mc_body sub = '?' with = lo_exc->get_longtext( ) ) ).
        server->response->set_status( code = '400' reason = lo_exc->get_text( ) ).

    ENDTRY.

  ENDMETHOD.
ENDCLASS.