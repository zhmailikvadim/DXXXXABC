class ZCL_PE_TEMPLATE_PROVIDER definition
  public
  create public .

public section.

  methods GET_CONTENT
    importing
      !IV_APPLICATION_ID type APPLID
    exporting
      !EV_FILE_NAME type FILE_NAME
      !EV_CONTENT type XSTRING .
protected section.

  types:
    BEGIN OF gtyp_s_style_changer,
      header            TYPE REF TO zif_excel_style_changer,
      field_group       TYPE REF TO zif_excel_style_changer,
      field_description TYPE REF TO zif_excel_style_changer,
      field_tech_name   TYPE REF TO zif_excel_style_changer,
    END OF gtyp_s_style_changer .
  types:
    BEGIN OF gtyp_s_sheet_config,
      fld_name_raw      TYPE /aif/lfa_fldname_raw_e,
      excel_sheet       TYPE /aif/lfa_excel_sheet_name_e,
      row_fld_tech_name TYPE /aif/lfa_excel_name_row_e,
      start_row_data    TYPE /aif/lfa_excel_start_row_e,
      last_row_data     TYPE /aif/lfa_excel_last_row_e,
    END OF gtyp_s_sheet_config .
  types:
    gtyp_t_sheet_config TYPE STANDARD TABLE OF gtyp_s_sheet_config
                             WITH KEY primary_key
                             COMPONENTS fld_name_raw .
  types:
    BEGIN OF gtyp_s_field_mapping,
      excel_sheet TYPE /aif/lfa_excel_sheet_name_e,
      fld_name    TYPE /aif/lfa_target_fldname_e,
      excel_col   TYPE /aif/lfa_excel_col_e,
    END OF gtyp_s_field_mapping .
  types:
    gtyp_ts_field_mapping TYPE SORTED TABLE OF gtyp_s_field_mapping
                              WITH NON-UNIQUE KEY excel_sheet fld_name
                              WITH NON-UNIQUE SORTED KEY key_field
                                   COMPONENTS fld_name .
  types:
    BEGIN OF gtyp_s_tab,
      ddic_name         TYPE /aif/if_ddic_struct,
      rtts_class        TYPE REF TO cl_abap_tabledescr,
      description       TYPE as4text,
      sheet_name        TYPE /aif/lfa_excel_sheet_name_e,
      row_fld_tech_name TYPE /aif/lfa_excel_name_row_e,
      start_row_data    TYPE /aif/lfa_excel_start_row_e,
      last_row_data     TYPE /aif/lfa_excel_last_row_e,
    END OF gtyp_s_tab .
  types:
    gtyp_t_tab TYPE STANDARD TABLE OF gtyp_s_tab
                             WITH KEY primary_key
                             COMPONENTS ddic_name .
  types:
    BEGIN OF gtyp_s_tab_field,
      ddic_tab_name TYPE /aif/if_ddic_struct,
      column_int    TYPE zexcel_cell_column,
      ddic_fld_name TYPE /aif/lfa_target_fldname_e,
      description   TYPE as4text,
      column        TYPE /aif/lfa_excel_col_e,
      data_type     TYPE inttype,
    END OF gtyp_s_tab_field .
  types:
    gtyp_ts_tab_field TYPE SORTED TABLE OF gtyp_s_tab_field
                           WITH NON-UNIQUE KEY  ddic_tab_name column_int .
  types:
    BEGIN OF gtyp_s_mandatory_field,
      tab_name TYPE tabname,
      fld_name TYPE fieldname,
    END OF gtyp_s_mandatory_field .
  types:
    gtyp_t_mandatory_field TYPE STANDARD TABLE OF gtyp_s_mandatory_field
                                WITH NON-UNIQUE SORTED KEY tab_fld
                                                COMPONENTS tab_name fld_name .
  types:
    BEGIN OF gtyp_s_field_group,
      category_name   TYPE char30,
      field_from      TYPE zpe_template_field_from,
      field_to        TYPE zpe_template_field_to,
      group_color     TYPE zpe_tmplt_field_group_color,
      field_color     TYPE zpe_tmplt_field_color,
      column_from_int TYPE int2,
      column_to_int   TYPE int2,
    END OF gtyp_s_field_group .
  types:
    gtyp_t_field_group TYPE STANDARD TABLE OF gtyp_s_field_group
                             WITH KEY primary_key
                             COMPONENTS category_name .

  data GV_MAX_ROW type INT2 .
  data MO_EXCEL type ref to ZCL_EXCEL .
  data MS_STYLE_CHANGER type GTYP_S_STYLE_CHANGER .
  data MT_SHEET_CONFIG type GTYP_T_SHEET_CONFIG .
  data MT_FIELD_GROUP type GTYP_T_FIELD_GROUP .
  data MTS_FIELD_MAPPING type GTYP_TS_FIELD_MAPPING .
  data MT_MANDATORY_FIELD type GTYP_T_MANDATORY_FIELD .
  constants:
    BEGIN OF mc_s_default_color,
      header            TYPE zexcel_style_color_argb VALUE 'FF004C99',
      field_group       TYPE zexcel_style_color_argb VALUE 'FF8CAADC',
      field_description TYPE zexcel_style_color_argb VALUE 'FF8CAADC',
      field_tech_name   TYPE zexcel_style_color_argb VALUE 'FFEDF1F9',
    END OF mc_s_default_color .
  constants MC_DEFAULT_COLUMN_WIDTH type F value 27 ##NO_TEXT.

  methods GENERATE_EXCEL_FROM_AIF
    importing
      !IV_APPLICATION_ID type APPLID
      !IV_FILE_CONFIG type /AIF/LFA_FILECONF
      !IIF_EXCEL_WRITER type ref to ZIF_EXCEL_WRITER
    returning
      value(RV_CONTENT) type XSTRING
    raising
      ZCX_EXCEL .
  methods GET_SHEET_CONFIG
    importing
      !IV_APPLICATION_ID type APPLID
      !IV_FILE_CONFIG type /AIF/LFA_FILECONF .
  methods GET_MANDATORY_FIELD
    importing
      !IV_APPLICATION_ID type APPLID .
  methods GET_TABS
    importing
      !IV_APPLICATION_ID type APPLID
    returning
      value(RT_TAB) type GTYP_T_TAB .
  methods GET_FIELD_MAPPING
    importing
      !IV_APPLICATION_ID type APPLID
      !IV_FILE_CONFIG type /AIF/LFA_FILECONF
      !IT_TAB type GTYP_T_TAB
    raising
      ZCX_EXCEL .
  methods GET_DEFAULT_FIELD_MAPPING
    importing
      !IT_TAB type GTYP_T_TAB
    raising
      ZCX_EXCEL .
  methods GET_TAB_FIELDS
    importing
      !IS_TAB type GTYP_S_TAB
    returning
      value(RTS_TAB_FIELD) type GTYP_TS_TAB_FIELD
    raising
      ZCX_EXCEL .
  methods GET_STYLE_CHANGERS
    raising
      ZCX_EXCEL .
  methods SET_HEADER
    importing
      !IS_TAB type GTYP_S_TAB
    raising
      ZCX_EXCEL .
  methods SET_FIELD_GROUPS
    importing
      !IS_TAB type GTYP_S_TAB
    raising
      ZCX_EXCEL .
  methods SET_FIELD_DESCRIPTION
    importing
      !IS_TAB type GTYP_S_TAB
      !IV_COLUMN type SIMPLE
      !IV_VALUE type SIMPLE
    raising
      ZCX_EXCEL .
  methods SET_FIELD_TECH_NAME
    importing
      !IS_TAB type GTYP_S_TAB
      !IV_COLUMN type SIMPLE
      !IV_VALUE type SIMPLE
    raising
      ZCX_EXCEL .
  methods SET_COLUMN_DATA_TYPE
    importing
      !IS_TAB type GTYP_S_TAB
      !IV_COLUMN type SIMPLE
      !IV_ABAP_TYPE type ABAP_TYPEKIND
    raising
      ZCX_EXCEL .
  methods CHANGE_FIELD_COLOR
    importing
      !IS_TAB type GTYP_S_TAB
    raising
      ZCX_EXCEL .
private section.
ENDCLASS.



CLASS ZCL_PE_TEMPLATE_PROVIDER IMPLEMENTATION.


  METHOD set_field_tech_name.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*

    DATA(lo_worksheet) = mo_excel->get_active_worksheet( ).

    lo_worksheet->set_cell(
      ip_row    = is_tab-row_fld_tech_name
      ip_column = iv_column
      ip_value  = iv_value  ).

    lo_worksheet->change_area_style(
      ip_row           = is_tab-row_fld_tech_name
      ip_row_to        = is_tab-start_row_data - 1
      ip_column_start  = iv_column
      ip_column_end    = iv_column
      ip_style_changer = ms_style_changer-field_tech_name ).

    lo_worksheet->change_cell_style(
      ip_row                     = is_tab-start_row_data - 1
      ip_column                  = iv_column
      ip_borders_down_color_rgb  = zcl_excel_style_color=>c_black
      ip_borders_down_style      = zcl_excel_style_border=>c_border_thin ).

  ENDMETHOD.


  METHOD get_style_changers.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*

    "Worksheet Header
    ms_style_changer-header = zcl_excel_style_changer=>create( mo_excel ).

    ms_style_changer-header->set_font_name( zcl_excel_style_font=>c_name_calibri ).
    ms_style_changer-header->set_font_scheme( zcl_excel_style_font=>c_scheme_none ).
    ms_style_changer-header->set_font_size( 22 ).
    ms_style_changer-header->set_font_bold( abap_true ).
    ms_style_changer-header->set_font_italic( abap_true ).
    ms_style_changer-header->set_font_color_rgb( zcl_excel_style_color=>c_white ).

    ms_style_changer-header->set_fill_filltype( zcl_excel_style_fill=>c_fill_solid ).
    ms_style_changer-header->set_fill_fgcolor_rgb( mc_s_default_color-header ).

    "Field Group
    ms_style_changer-field_group = zcl_excel_style_changer=>create( mo_excel ).

    ms_style_changer-field_group->set_font_name( zcl_excel_style_font=>c_name_calibri ).
    ms_style_changer-field_group->set_font_scheme( zcl_excel_style_font=>c_scheme_none ).
    ms_style_changer-field_group->set_font_size( 11 ).
    ms_style_changer-field_group->set_font_bold( abap_true ).

    ms_style_changer-field_group->set_fill_filltype( zcl_excel_style_fill=>c_fill_solid ).
    ms_style_changer-field_group->set_fill_fgcolor_rgb( mc_s_default_color-field_group ).

    ms_style_changer-field_group->set_borders_allbo_color_rgb( zcl_excel_style_color=>c_black ).
    ms_style_changer-field_group->set_borders_allborders_style( zcl_excel_style_border=>c_border_thin ).

    "Field Description
    ms_style_changer-field_description = zcl_excel_style_changer=>create( mo_excel ).

    ms_style_changer-field_description->set_font_name( zcl_excel_style_font=>c_name_calibri ).
    ms_style_changer-field_description->set_font_scheme( zcl_excel_style_font=>c_scheme_none ).
    ms_style_changer-field_description->set_font_size( 11 ).
    ms_style_changer-field_description->set_font_bold( abap_true ).

    ms_style_changer-field_description->set_fill_filltype( zcl_excel_style_fill=>c_fill_solid ).
    ms_style_changer-field_description->set_fill_fgcolor_rgb( mc_s_default_color-field_description ).

    ms_style_changer-field_description->set_borders_allbo_color_rgb( zcl_excel_style_color=>c_black ).
    ms_style_changer-field_description->set_borders_allborders_style( zcl_excel_style_border=>c_border_thin ).

    "Field Technical Name
    ms_style_changer-field_tech_name = zcl_excel_style_changer=>create( mo_excel ).
    DATA(lif_style_changer) = ms_style_changer-field_tech_name.

    lif_style_changer->set_font_name( zcl_excel_style_font=>c_name_calibri ).
    lif_style_changer->set_font_scheme( zcl_excel_style_font=>c_scheme_none ).
    lif_style_changer->set_font_size( 11 ).
    lif_style_changer->set_font_bold( abap_true ).

    lif_style_changer->set_fill_filltype( zcl_excel_style_fill=>c_fill_solid ).
    lif_style_changer->set_fill_fgcolor_rgb( mc_s_default_color-field_tech_name ).

    lif_style_changer->set_borders_left_color_rgb( zcl_excel_style_color=>c_black ).
    lif_style_changer->set_borders_left_style( zcl_excel_style_border=>c_border_thin ).
    lif_style_changer->set_borders_right_color_rgb( zcl_excel_style_color=>c_black ).
    lif_style_changer->set_borders_right_style( zcl_excel_style_border=>c_border_thin ).

  ENDMETHOD.


  METHOD change_field_color.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*

    CHECK mt_field_group IS NOT INITIAL
      AND is_tab-row_fld_tech_name > 2.

    DATA(lo_worksheet) = mo_excel->get_active_worksheet( ).

    LOOP AT mt_field_group ASSIGNING FIELD-SYMBOL(<ls_field_group>).

      IF <ls_field_group>-group_color IS NOT INITIAL.

        DATA(lif_field_description_sc) = zcl_excel_style_changer=>create( mo_excel ).
        lif_field_description_sc->set_fill_filltype( zcl_excel_style_fill=>c_fill_solid ).
        lif_field_description_sc->set_fill_fgcolor_rgb( <ls_field_group>-group_color ).

        lo_worksheet->change_area_style(
          ip_row           = is_tab-row_fld_tech_name - 2
          ip_row_to        = is_tab-row_fld_tech_name - 1
          ip_column_start  = <ls_field_group>-column_from_int
          ip_column_end    = <ls_field_group>-column_to_int
          ip_style_changer = lif_field_description_sc ).
      ENDIF.

      IF <ls_field_group>-field_color IS NOT INITIAL.

        DATA(lif_field_tech_name_sc) = zcl_excel_style_changer=>create( mo_excel ).
        lif_field_tech_name_sc->set_fill_filltype( zcl_excel_style_fill=>c_fill_solid ).
        lif_field_tech_name_sc->set_fill_fgcolor_rgb( <ls_field_group>-field_color ).

        lo_worksheet->change_area_style(
          ip_row           = is_tab-row_fld_tech_name
          ip_row_to        = is_tab-start_row_data - 1
          ip_column_start  = <ls_field_group>-column_from_int
          ip_column_end    = <ls_field_group>-column_to_int
          ip_style_changer = lif_field_tech_name_sc ).
      ENDIF.

    ENDLOOP.

  ENDMETHOD.


  METHOD get_field_mapping.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 15.02.2023 | 17268 : Mass Upl for Free supply Orders  *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*

    DATA(lv_namespace) = substring_before(
      val = iv_application_ID
      sub = '-' ).

    SELECT
        excel_sheet,
        fld_name,
        excel_col
      FROM /aif/lfa_excel_f
      WHERE fileconfns = @lv_namespace
        AND fileconf   = @iv_file_config
      INTO TABLE @mts_field_mapping.

    IF sy-subrc > 0.
      get_default_field_mapping( it_tab ).
    ENDIF.

    TRY.
        zcl_pe_hm_complex=>get_table(
          EXPORTING
            iv_id           = '0239FC3B8D041EDD948941D2B070DF59'
            is_filter       = iv_application_ID
            iv_mapping_type = zcl_pe_hm_complex=>gc_s_mapping_type-data_elements
          IMPORTING
            et_table  = mt_field_group ).

        TRY.
            LOOP AT mt_field_group ASSIGNING FIELD-SYMBOL(<ls_field_group>).
              <ls_field_group>-column_from_int = zcl_excel_common=>convert_column2int(
                VALUE /aif/lfa_excel_col_e(
                  mts_field_mapping[ KEY key_field
                                     fld_name = <ls_field_group>-field_from  ]-excel_col OPTIONAL ) ).
              <ls_field_group>-column_to_int = zcl_excel_common=>convert_column2int(
                VALUE /aif/lfa_excel_col_e(
                  mts_field_mapping[ KEY key_field
                                     fld_name = <ls_field_group>-field_to ]-excel_col OPTIONAL ) ).
            ENDLOOP.

          CATCH  zcx_excel ##NO_HANDLER.
        ENDTRY.

      CATCH  zcx_pe_hm ##NO_HANDLER.
    ENDTRY.

  ENDMETHOD.


  METHOD get_default_field_mapping.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 15.02.2023 | 17268 : Mass Upl for Free supply Orders  *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*

    mts_field_mapping = REDUCE #(
      INIT lts_field_mapping TYPE gtyp_ts_field_mapping
           lv_row            TYPE i
      FOR <ls_tab>   IN it_tab
      FOR <ls_field> IN CAST cl_abap_structdescr(
                          <ls_tab>-rtts_class->get_table_line_type( )
                          )->get_components( )
      NEXT
        lv_row += 1
        lts_field_mapping = VALUE #( BASE lts_field_mapping
          ( excel_sheet = <ls_tab>-sheet_name
            fld_name    = <ls_field>-name
            excel_col   = zcl_excel_common=>convert_column2alpha( lv_row )
           ) )
    ).

  ENDMETHOD.


  METHOD get_sheet_config.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 15.02.2023 | 17268 : Mass Upl for Free supply Orders  *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*

    DATA(lv_namespace) = substring_before(
      val = iv_application_ID
      sub = '-' ).

    SELECT
        fld_name_raw,
        excel_sheet,
        row_fld_names,
        row_from      AS start_row_data,
        row_to        AS last_row_data
      FROM /aif/lfa_excel_h
      WHERE fileconfns = @lv_namespace
        AND fileconf   = @iv_file_config
      INTO TABLE @mt_sheet_config.

    IF sy-subrc > 0.
      RETURN.
    ENDIF.

    TRY.
        gv_max_row = zcl_pe_hm_basic=>get_value( iv_id = '852' iv_sequence = '1' ).
      CATCH zcx_pe_hm ##NO_HANDLER.
    ENDTRY.

    get_mandatory_field( iv_application_id ).

  ENDMETHOD.


  METHOD generate_excel_from_aif.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 11.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*
* BOBRULA      | 15.02.2023 | 17268 : Mass Upl for Free supply Orders  *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*

    mo_excel = NEW zcl_excel( ).

    get_style_changers( ).

    get_sheet_config(
      iv_application_id = iv_application_id
      iv_file_config    = iv_file_config ).

    DATA(lt_tab) = get_tabs( iv_application_id ).

    get_field_mapping(
      iv_application_id = iv_application_id
      iv_file_config    = iv_file_config
      it_tab            = lt_tab ).

    LOOP AT lt_tab ASSIGNING FIELD-SYMBOL(<ls_tab>).

      IF sy-tabix = 1.
        DATA(lo_worksheet) = mo_excel->get_active_worksheet( ).
      ELSE.
        lo_worksheet = mo_excel->add_new_worksheet( ).
      ENDIF.

      set_header( <ls_tab> ).

      set_field_groups( <ls_tab> ).

      DATA(lts_tab_field) = get_tab_fields( <ls_tab> ).

      LOOP AT lts_tab_field ASSIGNING FIELD-SYMBOL(<ls_tab_field>). "#EC CI_NESTED

        DATA(lv_column) = zcl_excel_common=>convert_column2alpha( sy-tabix ).

        lo_worksheet->get_column( lv_column )->set_width(
          ip_width = mc_default_column_width ).

        set_field_description(
          is_tab    = <ls_tab>
          iv_column = lv_column
          iv_value  = <ls_tab_field>-description ).

        set_field_tech_name(
           is_tab    = <ls_tab>
           iv_column = lv_column
           iv_value  = <ls_tab_field>-ddic_fld_name ).

        set_column_data_type(
           is_tab       = <ls_tab>
           iv_column    = lv_column
           iv_abap_type = <ls_tab_field>-data_type ).

      ENDLOOP.

      change_field_color( <ls_tab> ).

    ENDLOOP.

    rv_content = iif_excel_writer->write_file( mo_excel ).

  ENDMETHOD.


  METHOD get_content.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 13.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*

    TYPES:
      BEGIN OF ltyp_s_template_source,
        source_type TYPE zpe_template_source_type,
        source      TYPE zpe_template_source,
        file_name   TYPE file_name,
      END OF ltyp_s_template_source.

    DATA:
      lo_ref             TYPE REF TO object,
      lt_template_source TYPE STANDARD TABLE OF ltyp_s_template_source.

    TRY.
        zcl_pe_hm_complex=>get_table(
          EXPORTING
            iv_id           = '024F85009E261EED928F055A4B10A0D2'
            is_filter       = iv_application_ID
            iv_mapping_type = zcl_pe_hm_complex=>gc_s_mapping_type-data_elements
          IMPORTING
            et_table  = lt_template_source ).

      CATCH  zcx_pe_hm.
        RETURN.
    ENDTRY.

    DATA(ls_template_source) = VALUE ltyp_s_template_source( lt_template_source[ 1 ] OPTIONAL ).

    CASE ls_template_source-source_type.

      WHEN 'MIME'.

        DATA(lif_mime_api) = cl_mime_repository_api=>get_api( ).
        lif_mime_api->get(
          EXPORTING
            i_url              = ls_template_source-source && ls_template_source-file_name
          IMPORTING
            e_content          = ev_content
          EXCEPTIONS
            parameter_missing  = 1
            error_occured      = 2
            not_found          = 3
            permission_failure = 4
            OTHERS             = 5 ).

        IF sy-subrc > 0.
          RETURN.
        ENDIF.

      WHEN 'CLASS'.

        TRY.
            ls_template_source-source = to_upper( val = ls_template_source-source ).
            CREATE OBJECT lo_ref TYPE (ls_template_source-source).

            IF lo_ref IS INSTANCE OF zif_pe_excel_template.
              DATA(lif_excel_template) = CAST zif_pe_excel_template( lo_ref ).
              ev_content = lif_excel_template->get_excel( ).
            ENDIF.

          CATCH cx_root.
            RETURN.
        ENDTRY.

      WHEN 'AIF'.

        TRY.
            ev_content = generate_excel_from_AIF(
              iv_application_ID = iv_application_ID
              iv_file_config    = CONV #( ls_template_source-source )
              iif_excel_writer  = NEW zcl_excel_writer_2007( ) ).
          CATCH zcx_excel.
            RETURN.
        ENDTRY.

    ENDCASE.

    ev_file_name = ls_template_source-file_name.

  ENDMETHOD.


  METHOD get_mandatory_field.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.02.2023 | 17268 : Mass Upl for Free supply Orders  *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*

    TRY.
        zcl_pe_hm_complex=>get_table(
          EXPORTING
            iv_id           = '0239FC3B8D041EDDABD27D9E2844DF59'
            is_filter       = iv_application_ID
            iv_mapping_type = zcl_pe_hm_complex=>gc_s_mapping_type-data_elements
          IMPORTING
            et_table  = mt_mandatory_field ).

      CATCH  zcx_pe_hm.
        CLEAR mt_mandatory_field.
    ENDTRY.

  ENDMETHOD.


  METHOD get_tabs.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*
* BOBRULA      | 15.02.2023 | 17268 : Mass Upl for Free supply Orders  *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*

    TYPES:
      BEGIN OF ltyp_s_linetype,
        tabname  TYPE tabname,
        linetype TYPE refname,
      END OF ltyp_s_linetype,

      BEGIN OF ltyp_s_description,
        tabname     TYPE tabname,
        description TYPE as4text,
      END OF ltyp_s_description.

    DATA:
      lt_line_type    TYPE STANDARD TABLE OF ltyp_s_linetype
                           WITH KEY primary_key COMPONENTS tabname,
      lts_description TYPE SORTED TABLE OF ltyp_s_description
                           WITH NON-UNIQUE KEY tabname.

    SPLIT iv_application_ID AT '-' INTO
      DATA(lv_namespace)
      DATA(lv_interface_name)
      DATA(lv_version).

    SELECT SINGLE
        ddicstructure
      FROM /aif/t_finf
      WHERE ns        = @( CONV /aif/ns( lv_namespace ) )
        AND ifname    = @( CONV /aif/ifname( lv_interface_name ) )
        AND ifversion = @( CONV /aif/ifversion( lv_version ) )
      INTO @DATA(lv_structure_name).

    IF sy-subrc > 0.
      RETURN.
    ENDIF.

    DATA(lo_type_descr) = cl_abap_typedescr=>describe_by_name(
      lv_structure_name ).

    IF lo_type_descr IS NOT INSTANCE OF cl_abap_structdescr.
      RETURN.
    ENDIF.

    DATA(lo_str_descr) = CAST cl_abap_structdescr( lo_type_descr ).
    DATA(lt_table_type) = lo_str_descr->get_components( ).

    LOOP AT lt_table_type ASSIGNING FIELD-SYMBOL(<ls_table_type>).

      CHECK <ls_table_type>-type IS INSTANCE OF cl_abap_tabledescr.
      DATA(lo_tbl_descr)  = CAST cl_abap_tabledescr( <ls_table_type>-type ).
      DATA(ls_struct_header) = lo_tbl_descr->get_ddic_header( ).

      INSERT VALUE ltyp_s_linetype(
        tabname  = <ls_table_type>-name
        linetype = ls_struct_header-refname
      ) INTO TABLE lt_line_type.

      DATA(ls_sheet_config) = VALUE #(
         mt_sheet_config[ fld_name_raw = CONV #( <ls_table_type>-name ) ] OPTIONAL ).

      INSERT VALUE #(
        ddic_name         = <ls_table_type>-name
        rtts_class        = lo_tbl_descr
        sheet_name        = ls_sheet_config-excel_sheet
        row_fld_tech_name = ls_sheet_config-row_fld_tech_name
        start_row_data    = ls_sheet_config-start_row_data
        last_row_data     = ls_sheet_config-last_row_data
        ) INTO TABLE rt_tab.

    ENDLOOP.

    SELECT
        line~tabname,
        dd02t~ddtext AS description
      FROM dd02t
      INNER JOIN @lt_line_type AS line
              ON dd02t~tabname = line~linetype
      WHERE ddlanguage = @sy-langu
        AND as4local   = 'A'
      INTO TABLE @lts_description.

    IF sy-subrc > 0.
      RETURN.
    ENDIF.

    LOOP AT rt_tab ASSIGNING FIELD-SYMBOL(<ls_tab>).
      <ls_tab>-description = VALUE #( lts_description[ tabname = <ls_tab>-ddic_name ]-description OPTIONAL ).
    ENDLOOP.

  ENDMETHOD.


  METHOD get_tab_fields.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*
* BOBRULA      | 15.02.2023 | 17268 : Mass Upl for Free supply Orders  *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*

     DATA(lt_key) = COND abap_table_keydescr-components(
                      WHEN mt_mandatory_field IS INITIAL THEN
                           LET lt_tbl_keys = is_tab-rtts_class->get_keys( ) IN
                           VALUE #( lt_tbl_keys[ 1 ]-components OPTIONAL )
                      ELSE
                           VALUE #( FOR <ls_mandatory_field> IN FILTER #( mt_mandatory_field
                                                                            USING KEY tab_fld
                                                                            WHERE tab_name = is_tab-ddic_name )
                                      ( name = <ls_mandatory_field>-fld_name ) ) ).

    DATA(lo_line_type)  = CAST cl_abap_structdescr(
      is_tab-rtts_class->get_table_line_type( ) ).
    DATA(lt_field) = lo_line_type->get_components( ).

    LOOP AT lt_field ASSIGNING FIELD-SYMBOL(<ls_field>).

      CHECK <ls_field>-type IS INSTANCE OF cl_abap_elemdescr.
      DATA(lo_elem_descr) = CAST cl_abap_elemdescr( <ls_field>-type ).
      DATA(ls_elem_ddic_field) = lo_elem_descr->get_ddic_field( ).

      DATA(lv_is_key) = COND char1(
        WHEN line_exists( lt_key[ name = CONV #( <ls_field>-name ) ] ) THEN '*' ).

      DATA(lv_column) = VALUE /aif/lfa_excel_col_e(
           mts_field_mapping[ excel_sheet = is_tab-sheet_name
                              fld_name    = CONV #( <ls_field>-name ) ]-excel_col OPTIONAL ).

      DATA(lv_column_int) = COND zexcel_cell_column(
        WHEN lv_column IS NOT INITIAL THEN
             zcl_excel_common=>convert_column2int( lv_column ) ).

      DATA(ls_tab_field) = VALUE gtyp_s_tab_field(
        ddic_tab_name = is_tab-ddic_name
        column_int    = lv_column_int
        ddic_fld_name = <ls_field>-name
        description   = | { lv_Is_key } { COND #( WHEN ls_elem_ddic_field-scrtext_m IS NOT INITIAL THEN
                                                        ls_elem_ddic_field-scrtext_m
                                                  ELSE  ls_elem_ddic_field-fieldtext ) } { ls_elem_ddic_field-inttype }[{ CONV i( ls_elem_ddic_field-outputlen ) }] |
        column        = lv_column
        data_type     = ls_elem_ddic_field-inttype ).

      IF lv_column_int IS INITIAL.
        APPEND ls_tab_field TO rts_tab_field.         "#EC CI_APPEND_OK
      ELSE.
        INSERT ls_tab_field INTO TABLE rts_tab_field.
      ENDIF.

    ENDLOOP.

  ENDMETHOD.


  METHOD set_column_data_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 02.12.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*
* BOBRULA      | 21.04.2023 | 19722  : Add dates validation            *
*              |            | DS4K949213                               *
*----------------------------------------------------------------------*

    CHECK iv_abap_type = 'D'.

    DATA(lv_row_to) = nmin( val1 = gv_max_row
                            val2 = is_tab-last_row_data ).

    DATA(lo_worksheet) = mo_excel->get_active_worksheet( ).

   "set cell format as date
    DATA(lv_row) = is_tab-start_row_data.
    WHILE lv_row < lv_row_to.

      lo_worksheet->set_cell(
        ip_row       = lv_row
        ip_column    = iv_column
        ip_value     = space
        ip_abap_type = iv_abap_type ).

      lv_row += 1.

    ENDWHILE.

    "create new validation for date type
    DATA(lo_data_validation) = lo_worksheet->add_new_data_validation( ).

    lo_data_validation->type           = zcl_excel_data_validation=>c_type_date.
    lo_data_validation->cell_row       = is_tab-start_row_data.
    lo_data_validation->cell_row_to    = lv_row_to.
    lo_data_validation->cell_column    = iv_column.
    lo_data_validation->cell_column_to = iv_column.
    lo_data_validation->allowblank     = abap_true.
    lo_data_validation->errorstyle     = zcl_excel_data_validation=>c_style_warning.
    lo_data_validation->errortitle     = text-001.
    lo_data_validation->error          = text-002.

  ENDMETHOD.


  METHOD set_field_description.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*

    CHECK is_tab-row_fld_tech_name > 1.

    DATA(lo_worksheet) = mo_excel->get_active_worksheet( ).

    lo_worksheet->set_cell(
      ip_row    = is_tab-row_fld_tech_name - 1
      ip_column = iv_column
      ip_value  = iv_value ).

    lo_worksheet->change_area_style(
      ip_row           = is_tab-row_fld_tech_name - 1
      ip_column_start  = iv_column
      ip_column_end    = iv_column
      ip_style_changer = ms_style_changer-field_description ).

  ENDMETHOD.


  METHOD set_field_groups.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*

    CHECK mt_field_group IS NOT INITIAL
      AND is_tab-row_fld_tech_name > 2.

    DATA(lo_worksheet) = mo_excel->get_active_worksheet( ).

    LOOP AT mt_field_group ASSIGNING FIELD-SYMBOL(<ls_field_group>).

      lo_worksheet->set_area(
        ip_row          = is_tab-row_fld_tech_name - 2
        ip_column_start = <ls_field_group>-column_from_int
        ip_column_end   = <ls_field_group>-column_to_int
        ip_value        = <ls_field_group>-category_name
        ip_merge        = abap_true ).

      lo_worksheet->change_area_style(
        ip_row           = is_tab-row_fld_tech_name - 2
        ip_column_start  = <ls_field_group>-column_from_int
        ip_column_end    = <ls_field_group>-column_to_int
        ip_style_changer = ms_style_changer-field_group ).

    ENDLOOP.

  ENDMETHOD.


  METHOD set_header.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 17.10.2022 | 14122  : Template download ODATA service *
*              |            | DS4K931876                               *
*----------------------------------------------------------------------*

    DATA(lo_worksheet) = mo_excel->get_active_worksheet( ).

    lo_worksheet->set_title( is_tab-sheet_name ).

    IF   ( mt_field_group IS NOT INITIAL AND  is_tab-row_fld_tech_name > 3 )
      OR ( mt_field_group IS INITIAL     AND  is_tab-row_fld_tech_name > 2 ) .

      lo_worksheet->set_area(
        ip_row          = 1
        ip_column_start = 'A'
        ip_column_end   = 'D'
        ip_value        = is_tab-description
        ip_merge        = abap_true ).

      lo_worksheet->change_area_style(
        ip_row                  = 1
        ip_column_start  = 'A'
        ip_column_end    = 'D'
        ip_style_changer = ms_style_changer-header ).

    ENDIF.

  ENDMETHOD.
ENDCLASS.