class ZCL_SC_PO_SEND_OUT definition
  public
  final
  create public .

public section.

  types:
    BEGIN OF mtyp_s_ebeln,
        ebeln TYPE ebeln,
      END OF mtyp_s_ebeln .
  types:
    mtyp_t_ebeln TYPE STANDARD TABLE OF mtyp_s_ebeln WITH EMPTY KEY .
  types:
    BEGIN OF mtyp_s_po_send_check,
        bsart                TYPE bsart,
        zz1_mm_tradecomodel  TYPE zz1_mm_tradecomodel,
        zpd_check_date_act   TYPE zpd_check_date_act,
        zpd_work_week_length TYPE zpd_work_week_length,
      END OF mtyp_s_po_send_check .
  types:
    mtyp_t_po_send_check TYPE STANDARD TABLE OF mtyp_s_po_send_check WITH EMPTY KEY .
  types:
    BEGIN OF mtyp_s_so,
        ebeln TYPE ebeln,
        bsart TYPE bsart,
        aedat TYPE mmpur_erdat,
        ekorg TYPE ekorg,
        ekgrp TYPE bkgrp,
        werks TYPE werks_d,
      END OF mtyp_s_so .
  types:
    BEGIN OF mtyp_s_selections,
        s_ebeln TYPE RANGE OF ebeln,
        s_bsart TYPE RANGE OF bsart,
        s_aedat TYPE RANGE OF mmpur_erdat,
        s_ekorg TYPE RANGE OF ekorg,
        s_ekgrp TYPE RANGE OF bkgrp,
        s_werks TYPE RANGE OF werks_d,
      END OF mtyp_s_selections .

  constants:
    BEGIN OF mc_work_week_length,
        days5 TYPE zpd_work_week_length VALUE '5DAYS',
        days7 TYPE zpd_work_week_length VALUE '7DAYS',
      END OF mc_work_week_length .
  constants:
    BEGIN OF mc_event,
        created TYPE swo_event VALUE 'CREATED',
        changed TYPE swo_event VALUE 'CHANGED',
      END OF mc_event .
  constants MC_OBJTYPE type SWO_OBJTYP value 'FREBUS2012' ##NO_TEXT.
  constants MC_OBJTYPE_CONTRACT type SWO_OBJTYP value 'BUS2014' ##NO_TEXT.

  class-methods GET_WEEK_FOR_DATE
    importing
      !IV_DATE type DATUM default SY-DATUM
      !IV_WORK_WEEK_LENGTH type ZPD_WORK_WEEK_LENGTH default MC_WORK_WEEK_LENGTH-DAYS5
    exporting
      !EV_FIRST_DAY type DATUM
      !EV_LAST_DAY type DATUM .
  methods CONSTRUCTOR
    importing
      !IS_SELECTIONS type MTYP_S_SELECTIONS .
  methods RUN .
  class-methods GET_HM_PO_SENDING_CHECK
    returning
      value(RT_RESULT) type MTYP_T_PO_SEND_CHECK .
  class-methods PO_SENDING_CHECK
    importing
      !IV_EBELN type EBELN
      !IV_EVENT type SWO_EVENT
      !IV_OBJECT type SWO_OBJTYP optional
    exporting
      !EV_NOT_TRIGGERED type ABAP_BOOL .
  class-methods CLASS_CONSTRUCTOR .
protected section.
private section.

  data MS_SELECTIONS type MTYP_S_SELECTIONS .
  class-data GT_HM_PO_SEND_CHECK type MTYP_T_PO_SEND_CHECK .

  methods SEND_DOC
    importing
      !IT_EBELN type MTYP_T_EBELN .
  methods FETCH_PO
    exporting
      !ET_EBELN type MTYP_T_EBELN .
ENDCLASS.



CLASS ZCL_SC_PO_SEND_OUT IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 08.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*

    ms_selections = is_selections.

  ENDMETHOD.


  METHOD fetch_po.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 15.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 18.09.2023 | FB_24858_CR - ZSC_R_PO_SEND_OUT  for Con *
*              |            | DS4K962828                               *
*----------------------------------------------------------------------*

    CLEAR et_ebeln.

    IF gt_hm_po_send_check IS INITIAL.
      RETURN.
    ENDIF.

    SELECT po_item~purchaseorder AS ebeln
      FROM i_purchaseorderitemapi01 AS po_item
      JOIN i_purchaseorderapi01 AS po_header ON po_header~purchaseorder = po_item~purchaseorder
      FOR ALL ENTRIES IN @gt_hm_po_send_check
     WHERE po_item~purchaseorder            IN @ms_selections-s_ebeln[]
       AND po_item~plant                    IN @ms_selections-s_werks[]
       AND po_header~purchaseordertype      IN @ms_selections-s_bsart[]
       AND po_header~purchasingorganization IN @ms_selections-s_ekorg[]
       AND po_header~purchasinggroup        IN @ms_selections-s_ekgrp[]
       AND po_header~creationdate           IN @ms_selections-s_aedat[]
       AND po_header~purchaseordertype       = @gt_hm_po_send_check-bsart
       AND po_header~zz1_mm_tradecomodel_pdh = @gt_hm_po_send_check-zz1_mm_tradecomodel
      INTO TABLE @et_ebeln.
    IF sy-subrc <> 0.
      CLEAR et_ebeln.
    ENDIF.

    SELECT po_item~purchasecontract AS ebeln
     FROM i_purchasecontractitemapi01 AS po_item
     JOIN i_purchasecontractapi01 AS po_header ON po_header~purchasecontract = po_item~purchasecontract
     FOR ALL ENTRIES IN @gt_hm_po_send_check
    WHERE po_item~purchasecontract         IN @ms_selections-s_ebeln[]
      AND po_item~plant                    IN @ms_selections-s_werks[]
      AND po_header~purchasecontracttype   IN @ms_selections-s_bsart[]
      AND po_header~purchasingorganization IN @ms_selections-s_ekorg[]
      AND po_header~purchasinggroup        IN @ms_selections-s_ekgrp[]
      AND po_header~creationdate           IN @ms_selections-s_aedat[]
      AND po_header~purchasecontracttype       = @gt_hm_po_send_check-bsart
     INTO TABLE @DATA(lt_contract).
    IF sy-subrc = 0.
      APPEND LINES OF lt_contract TO et_ebeln.
    ENDIF.

  ENDMETHOD.


  METHOD get_hm_po_sending_check.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 15.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*

    TRY.
        zcl_pe_hm_complex=>get_table(
          EXPORTING
            iv_id           = '024F85009E261EEE87DC53198EB580D2'
          IMPORTING
            et_table        = rt_result
        ).
      CATCH zcx_pe_hm.
        CLEAR rt_result.
    ENDTRY.

  ENDMETHOD.


  METHOD run.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 18.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*

    fetch_po( IMPORTING et_ebeln = DATA(lt_ebeln) ).
    send_doc( it_ebeln = lt_ebeln ).

  ENDMETHOD.


  METHOD po_sending_check.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 15.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 18.09.2023 | FB_24858_CR - ZSC_R_PO_SEND_OUT  for Con *
*              |            | DS4K962828                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 18.09.2023 | 24101     :  - STO STATUS Update         *
*              |            | DS4K964057                               *
*----------------------------------------------------------------------*

    CLEAR: ev_not_triggered.
    SELECT SINGLE purchasingdocumentcategory FROM i_purchasingdocument INTO @DATA(lv_doc_cat) WHERE purchasingdocument = @iv_ebeln.
    IF sy-subrc IS  NOT INITIAL.
      CLEAR lv_doc_cat.
    ENDIF.
    IF lv_doc_cat EQ 'F'.
      "Get Purchasing document info
      SELECT SINGLE po~purchaseorder AS docno,
                    po~purchaseordertype AS doctype,
                    po~zz1_mm_tradecomodel_pdh,
                    po~creationdate,
                    po~zz1_zorderreason_pdh
        FROM i_purchaseorderapi01 AS po
        WHERE po~purchaseorder = @iv_ebeln
        INTO @DATA(ls_po).
      IF sy-subrc <> 0.
        CLEAR ls_po.
      ENDIF.
      IF ls_po-zz1_zorderreason_pdh IN zcl_sd_sto_status_update=>gs_hm_data-order_reason_range.
        RETURN.
      ENDIF.
    ELSEIF lv_doc_cat = 'K'.
      SELECT SINGLE contract~purchasecontract AS docno,
                  contract~purchasecontracttype AS doctype,
                  contract~zz1_mm_tradecomodel_pdh,
                  contract~creationdate
      FROM i_purchasecontractapi01 AS contract
      WHERE contract~purchasecontract = @iv_ebeln
      INTO @ls_po.
      IF sy-subrc <> 0.
        CLEAR ls_po.
      ENDIF.
    ENDIF.
    TRY.
        DATA(ls_send_cust) = gt_hm_po_send_check[ bsart               = ls_po-doctype
                                                  zz1_mm_tradecomodel = ls_po-zz1_mm_tradecomodel_pdh ]. "#EC CI_STDSEQ
      CATCH cx_sy_itab_line_not_found.
        CLEAR ls_send_cust.
    ENDTRY.

    "Don't send message if doesn't have customizing
    IF ls_send_cust IS INITIAL.
      ev_not_triggered = abap_true.
      RETURN.
    ENDIF.

    "Without immediate sending flag we can't send messages online
    IF ls_send_cust-zpd_check_date_act = abap_true.
      "Get current week dates
      zcl_sc_po_send_out=>get_week_for_date(
        EXPORTING
          iv_date             = sy-datum
          iv_work_week_length = ls_send_cust-zpd_work_week_length
        IMPORTING
          ev_first_day        = DATA(lv_first_day)
          ev_last_day         = DATA(lv_last_day)
      ).

      "The day in current work week
      IF ls_po-creationdate >= lv_first_day AND
         ls_po-creationdate <= lv_last_day.
        ev_not_triggered = abap_true.
        RETURN.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD class_constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 17.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*

    gt_hm_po_send_check = get_hm_po_sending_check( ).

  ENDMETHOD.


  METHOD get_week_for_date.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 18.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*
    CLEAR: ev_first_day, ev_last_day.

    TRY.
        cl_scal_utils=>date_get_week(
          EXPORTING
            iv_date      = iv_date
          IMPORTING
            ev_year_week = DATA(lv_year_week)
            ev_year      = DATA(lv_year)
            ev_week      = DATA(lv_week)
        ).

        cl_scal_utils=>week_get_first_day(
          EXPORTING
            iv_year_week = lv_year_week
            iv_year      = lv_year
            iv_week      = lv_week
          IMPORTING
            ev_date      = ev_first_day
        ).

        CASE iv_work_week_length.
          WHEN mc_work_week_length-days5.
            ev_last_day = ev_first_day + 4.
          WHEN mc_work_week_length-days7.
            ev_last_day = ev_first_day + 6.
        ENDCASE.

      CATCH cx_scal.
        CLEAR: ev_first_day, ev_last_day.
    ENDTRY.

  ENDMETHOD.


  METHOD send_doc.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* RAPANKON     | 18.05.2023 | 21020     : PO Integration from S/4 to D *
*              |            | DS4K951608                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 18.09.2023 | FB_24858_CR - ZSC_R_PO_SEND_OUT  for Con *
*              |            | DS4K962828                               *
*----------------------------------------------------------------------*

    LOOP AT it_ebeln ASSIGNING FIELD-SYMBOL(<ls_ebeln>).
      TRY.
          zcl_sp_pur_pochgedinfo=>get_instance( )->process_out( iv_po_no  = <ls_ebeln>-ebeln ).
        CATCH cx_ai_system_fault.
          CONTINUE.
        CATCH cx_ops_se.
          CONTINUE.
        CATCH cx_appl_proxy_badi_processing.
          CONTINUE.
      ENDTRY.
    ENDLOOP.

  ENDMETHOD.
ENDCLASS.