CLASS zcl_sc_reatp_change_record DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.

    TYPES:
      BEGIN OF gtyp_s_change_record,
        artnr           TYPE  matnr,
        werks           TYPE  werks_d,
        change_category TYPE  zsc_change_categ,
        doc_number      TYPE  zsc_ntchg_doc_number,
        doc_item        TYPE  zsc_ntchg_doc_item,
      END OF gtyp_s_change_record.

    DATA gs_change_record TYPE gtyp_s_change_record.

    METHODS constructor
      IMPORTING
        !is_change_record TYPE gtyp_s_change_record.
    METHODS insert_records.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.


CLASS zcl_sc_reatp_change_record IMPLEMENTATION.
  METHOD constructor.
    " -----------------------------------------------------------------------
    "  R E V I S I O N   H I S T O R Y                                     -
    " -----------------------------------------------------------------------
    " AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
    "              |            | TRANSPORT REQUESTS                       -
    " -----------------------------------------------------------------------
    " KALSHZHA     | 24.04.2023 | 20252     : Feature - [Build] Event-base -
    "              |            | DS4K949975                               -
    " -----------------------------------------------------------------------
    gs_change_record = is_change_record.
  ENDMETHOD.

  METHOD insert_records.
    " -----------------------------------------------------------------------
    "  R E V I S I O N   H I S T O R Y                                     -
    " -----------------------------------------------------------------------
    " AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
    "              |            | TRANSPORT REQUESTS                       -
    " -----------------------------------------------------------------------
    " KALSHZHA     | 24.04.2023 | 20252     : Feature - [Build] Event-base -
    "              |            | DS4K949975                               -
    " -----------------------------------------------------------------------

    DATA ls_ntch TYPE zsc_t_reatp_ntch.
    DATA ls_tims TYPE zsc_t_reatp_tims.
    DATA ls_trc TYPE zsc_t_reatp_trc.

    TRY.
        SELECT netchange_uuid
          INTO @DATA(lv_uuid)
          FROM zsc_t_reatp_ntch
          UP TO 1 ROWS
          WHERE artnr = @gs_change_record-artnr
                AND werks = @gs_change_record-werks
                AND change_category = @gs_change_record-change_category
          ORDER BY artnr, werks, change_category.
        ENDSELECT.                                        "#EC CI_SUBRC

        ls_tims-tmstmp_uuid    = NEW cl_system_uuid( )->if_system_uuid~create_uuid_x16( ).
        ls_tims-netchange_uuid = COND #( WHEN lv_uuid IS INITIAL THEN NEW cl_system_uuid( )->if_system_uuid~create_uuid_x16( ) ELSE lv_uuid ).
        GET TIME STAMP FIELD ls_tims-change_tmstmp.

        ls_trc-tmstmp_uuid = ls_tims-tmstmp_uuid.
        ls_trc-doc_number  = gs_change_record-doc_number.
        ls_trc-doc_item    = gs_change_record-doc_item.

        IF lv_uuid IS INITIAL.
          ls_ntch-netchange_uuid  = ls_tims-netchange_uuid.
          ls_ntch-artnr           = gs_change_record-artnr.
          ls_ntch-werks           = gs_change_record-werks.
          ls_ntch-change_category = gs_change_record-change_category.
        ENDIF.

        CALL FUNCTION 'ZSC_FM_REATP_NETCHANGE_INSERT' IN UPDATE TASK
          EXPORTING  is_tims               = ls_tims
                     is_ntch               = ls_ntch
                     is_trc                = ls_trc
          EXCEPTIONS communication_failure = 1
                     system_failure        = 2
                     resource_failure      = 3.
        IF sy-subrc NE 0.
          CLEAR: ls_tims, ls_ntch, ls_trc.
        ENDIF.

      CATCH cx_uuid_error.
        CLEAR ls_tims.
    ENDTRY.
  ENDMETHOD.
ENDCLASS.