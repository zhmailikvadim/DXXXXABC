CLASS zcl_sc_s4_deliver_out_ext2 DEFINITION
  PUBLIC
  FINAL
  CREATE PRIVATE .

  PUBLIC SECTION.

    METHODS get_data
      CHANGING
        !ct_raw_struct TYPE any .
    CLASS-METHODS get_instance
      RETURNING
        VALUE(ro_inst) TYPE REF TO zcl_sc_s4_deliver_out_ext2 .
  PROTECTED SECTION.

PRIVATE SECTION.

  TYPES:
    BEGIN OF gtyp_vas,
      vbeln           TYPE vbeln,
      posnr           TYPE posnr,
      vgbel           TYPE vgbel,
      vgpos           TYPE vgpos,
      zzvastype       TYPE fsh_vas_ser,
      zzvastype_des   TYPE fsh_ser_desc,
      zzvascode       TYPE fsh_vas_sub_ser,
      zzvascode_des   TYPE fsh_sub_ser_desc,
      zzvasfield1     TYPE fsh_vas_custf1,
      zzvasfield2     TYPE fsh_vas_custf2,
      zzvasfield3     TYPE fsh_vas_custf3,
      zzproc_rel_flag TYPE fsh_vas_im_rel,
    END OF gtyp_vas .
  TYPES:
    BEGIN OF gtyp_header,
      zzcustomdistributionchannel TYPE zz1_zsales_channel,
      zzcustomorderreasonheader   TYPE zz1_zorderreason,
      zzcustomdeliverypriority    TYPE zz1_delivery_prio,
    END OF gtyp_header .

  DATA:
    mt_caselots_headers TYPE TABLE OF zsd_s_caselot_cust_fields_head .
  DATA:
    mt_caselots_items TYPE SORTED TABLE OF zsd_s_caselot_cust_fields_item WITH NON-UNIQUE KEY caseloth_uuid .
  DATA:
    mt_vas TYPE SORTED TABLE OF gtyp_vas WITH NON-UNIQUE KEY posnr .
  DATA ms_header TYPE gtyp_header .
  CONSTANTS gc_edi_outbound_delivery_item TYPE name_feld VALUE 'DELIVERY_ITEM' ##NO_TEXT.
  CONSTANTS gc_delivery_document TYPE name_feld VALUE 'DELIVERY_DOCUMENT' ##NO_TEXT.
  CONSTANTS gc_delivery TYPE name_feld VALUE 'DELIVERY' ##NO_TEXT.
  CLASS-DATA go_inst TYPE REF TO zcl_sc_s4_deliver_out_ext2 .
  CONSTANTS gc_vas TYPE name_feld VALUE 'ZZSDSLS_ESRVAS' ##NO_TEXT.
  CONSTANTS gc_delivery_document_item TYPE name_feld VALUE 'DELIVERY_DOCUMENT_ITEM' ##NO_TEXT.
  CONSTANTS gc_zzcustomdistributionchannel TYPE name_feld VALUE 'ZZCUSTOMDISTRIBUTIONCHANNEL' ##NO_TEXT.
  CONSTANTS gc_zzcustomorderreasonheader TYPE name_feld VALUE 'ZZCUSTOMORDERREASONHEADER' ##NO_TEXT.
  CONSTANTS gc_zzcustomdeliverypriority TYPE name_feld VALUE 'ZZCUSTOMDELIVERYPRIORITY' ##NO_TEXT.

  METHODS fill_data
    CHANGING
      !ct_raw_struct TYPE any .
  METHODS get_data_from_db
    IMPORTING
      !iv_fs_delivery_document TYPE any OPTIONAL .
  METHODS fill_header
    CHANGING
      !ct_delivery TYPE any .
  METHODS fill_items_vas
    CHANGING
      !ct_delivery TYPE any .
  METHODS fill_caselots
    CHANGING
      !ct_delivery TYPE any .
ENDCLASS.



CLASS ZCL_SC_S4_DELIVER_OUT_EXT2 IMPLEMENTATION.


  METHOD fill_header.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.08.2022 | 13118 : FB_13118_SD_[Platform] - S4 to WMS - Outbound Delive*
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 17.01.2023 | 16988     : [Build] - STO - Remove  obsolet*
*              |            | DS4K940678                               *
*----------------------------------------------------------------------*

    ASSIGN COMPONENT gc_zzcustomdistributionchannel OF STRUCTURE ct_delivery TO FIELD-SYMBOL(<lv_zzcustomdistributionchan>).
    IF  sy-subrc IS INITIAL.
      <lv_zzcustomdistributionchan> = ms_header-zzcustomdistributionchannel.
    ENDIF.

    ASSIGN COMPONENT gc_zzcustomorderreasonheader OF STRUCTURE ct_delivery TO FIELD-SYMBOL(<lv_zzcustomorderreasonheader>).
    IF  sy-subrc IS INITIAL.
      <lv_zzcustomorderreasonheader> = ms_header-zzcustomorderreasonheader.
    ENDIF.

    ASSIGN COMPONENT gc_zzcustomdeliverypriority OF STRUCTURE ct_delivery TO FIELD-SYMBOL(<lv_zzcustomdeliverypriority>).
    IF  sy-subrc IS INITIAL.
      <lv_zzcustomdeliverypriority> = ms_header-zzcustomdeliverypriority.
    ENDIF.

  ENDMETHOD.


  METHOD fill_items_vas.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.08.2022 | 13118 : FB_13118_SD_[Platform] - S4 to WMS - Outbound Delive*
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 17.01.2023 | 16988     : [Build] - STO - Remove  obsolet*
*              |            | DS4K940678                               *
************************************************************************
* CHISLRIM     | 21.11.2023 | 27479    : FB_27479_SD_[FEATURE] -       *
*              |            |             -Delivery API ( Routing statu*
*              |            | DS4K973219                               *
*----------------------------------------------------------------------*
    DATA:
          lv_posnr TYPE posnr_vl.
    FIELD-SYMBOLS: <lt_delivery_item> TYPE STANDARD TABLE,
                   <lt_vas>           TYPE STANDARD TABLE.

    IF mt_vas IS NOT INITIAL.

      ASSIGN COMPONENT gc_edi_outbound_delivery_item OF STRUCTURE ct_delivery TO <lt_delivery_item>.

      IF sy-subrc IS INITIAL.

        LOOP AT <lt_delivery_item> ASSIGNING FIELD-SYMBOL(<ls_delivery_item>) .
          ASSIGN COMPONENT gc_delivery_document_item OF STRUCTURE <ls_delivery_item> TO FIELD-SYMBOL(<ls_posnr>).
          IF sy-subrc = 0.
            lv_posnr = <ls_posnr>.

            ASSIGN COMPONENT gc_vas OF STRUCTURE <ls_delivery_item> TO <lt_vas>.
            IF sy-subrc = 0.

              LOOP AT mt_vas ASSIGNING FIELD-SYMBOL(<ls_vas_db>) WHERE posnr = lv_posnr. "#EC CI_NESTED

                TRY.
                    DATA(lv_is_sto) = zcl_sc_s4_deliver_out_ext=>gv_is_sto.
                  CATCH zcx_pe_hm.
                    RETURN.
                ENDTRY.
                IF lv_is_sto = abap_true.
                  CLEAR <ls_vas_db>-zzproc_rel_flag.
                ENDIF.

                APPEND INITIAL LINE TO <lt_vas> ASSIGNING FIELD-SYMBOL(<ls_vas>).
                <ls_vas> = CORRESPONDING #( <ls_vas_db> ).

              ENDLOOP.

            ENDIF.
          ENDIF.
        ENDLOOP.

        CLEAR: mt_vas.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD fill_caselots.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.08.2022 | 13118 : FB_13118_SD_[Platform] - S4 to WMS - Outbound Delive*
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 17.01.2023 | 16988     : [Build] - STO - Remove  obsolet*
*              |            | DS4K940678                               *
*----------------------------------------------------------------------*
    FIELD-SYMBOLS: <lt_caselot_header> TYPE STANDARD TABLE,
                   <lt_caselot_item>   TYPE STANDARD TABLE.

    IF mt_caselots_headers IS NOT INITIAL.

      ASSIGN COMPONENT 'ZZSD_S_CASE_LOTS' OF STRUCTURE ct_delivery TO <lt_caselot_header>.

      IF sy-subrc = 0.
        LOOP AT mt_caselots_headers ASSIGNING FIELD-SYMBOL(<ls_caselots_headers_db>).
          APPEND INITIAL LINE TO <lt_caselot_header> ASSIGNING FIELD-SYMBOL(<ls_caselot_header>).
          <ls_caselot_header> = CORRESPONDING #( BASE ( <ls_caselot_header> ) <ls_caselots_headers_db> ).

          ASSIGN COMPONENT 'ITEM' OF STRUCTURE <ls_caselot_header> TO <lt_caselot_item>.
          IF sy-subrc = 0.
            LOOP AT mt_caselots_items ASSIGNING FIELD-SYMBOL(<ls_caselots_items_db>) WHERE caseloth_uuid = <ls_caselots_headers_db>-caseloth_uuid. "#EC CI_NESTED
              APPEND INITIAL LINE TO <lt_caselot_item> ASSIGNING FIELD-SYMBOL(<ls_caselot_item>).
              <ls_caselot_item> = CORRESPONDING #( BASE ( <ls_caselot_item> ) <ls_caselots_items_db> ).
            ENDLOOP.
          ENDIF.
        ENDLOOP.

        CLEAR: mt_caselots_headers, mt_caselots_items.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD fill_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.08.2022 | 13118 : FB_13118_SD_[Platform] - S4 to WMS - Outbound Delive*
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 17.01.2023 | 16988     : [Build] - STO - Remove  obsolet*
*              |            | DS4K940678                               *
*----------------------------------------------------------------------*

*)-Assign Header data 'DELIVERY' to a dynamic field-symbol and then populate the data
    ASSIGN COMPONENT gc_delivery  OF STRUCTURE ct_raw_struct TO FIELD-SYMBOL(<ls_delivery>).
    IF sy-subrc = 0.
      get_data_from_db(  iv_fs_delivery_document = <ls_delivery> ).

*)-Fill header
      fill_header( CHANGING ct_delivery = <ls_delivery> ).

*)-Populate Value Added Services
      fill_items_vas( CHANGING ct_delivery = <ls_delivery> ).

*)-Populate Caselots
      fill_caselots( CHANGING ct_delivery = <ls_delivery> ).
    ENDIF.
  ENDMETHOD.


  METHOD get_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.08.2022 | 13118 : FB_13118_SD_[Platform] - S4 to WMS - Outbound Delive*
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 17.01.2023 | 16988     : [Build] - STO - Remove  obsolet*
*              |            | DS4K940678                               *
*----------------------------------------------------------------------*

    fill_data( CHANGING ct_raw_struct = ct_raw_struct ).
  ENDMETHOD.


  METHOD get_data_from_db.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.08.2022 | 13118 : FB_13118_SD_[Platform] - S4 to WMS - Outbound Delive*
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 14.10.2022 | 13118     : [Platform] - S4 to WMS - Outbound Delive *
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
*----------------------------------------------------------------------*
* SRIKAVEN     | 17.01.2023 | 16988     : [Build] - STO - Remove  obsolet*
*              |            | DS4K940678                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 17.02.2023 | 18221     : [Platform] - S4 to WMS - Out *
*              |            | DS4K944432                               *
*----------------------------------------------------------------------*
* SHNIPALI     | 18.07.2022 | 23462 : FB_23462_SD_Feature - [Build][PFE]-  S4 to TMS - Del*
*              |            | DS4K958556                               *
*----------------------------------------------------------------------*
* KASHTULA     | 23.10.2023 | 23809_SD_[CR] Development - Consider     *
*              |            | warehouse, DS4K959580                    *
************************************************************************
* CHISLRIM     | 21.11.2023 | 27479    : FB_27479_SD_[FEATURE] -       *
*              |            |             -Delivery API ( Routing statu*
*              |            | DS4K959580                               *
*----------------------------------------------------------------------*

    DATA:
          lv_vbeln TYPE vbeln_vl.

    ASSIGN COMPONENT gc_delivery_document OF STRUCTURE iv_fs_delivery_document TO FIELD-SYMBOL(<ls_delivery_document>).

    IF sy-subrc IS INITIAL.
      lv_vbeln = |{ <ls_delivery_document> ALPHA = OUT }|.

***********************************************************************
**)-Fetch Additional header data
***********************************************************************
      SELECT SINGLE  zz1_zsales_channel_pdh, zz1_zorderreason_pdh, zz1_delivery_prio_pdh ##WARN_OK "#EC CI_SUBRC
        FROM I_PurchaseOrderAPI01 AS PurOrd
          JOIN I_DeliveryDocumentItem AS DelDocItm
            ON PurOrd~PurchaseOrder = DelDocItm~ReferenceSDDocument
        INTO (@ms_header-zzcustomdistributionchannel, @ms_header-zzcustomorderreasonheader, @ms_header-zzcustomdeliverypriority)
        WHERE DeliveryDocument = @lv_vbeln.

***********************************************************************
**)-Fetch Case Lots Headers and Items
***********************************************************************
      SELECT DISTINCT
             CaseLotHUUID         AS caseloth_uuid,
             CaselotNo            AS zzcaselotnbr,
             _caselots~SalesOrder AS zzsales_document,
             NumberOfMPB          AS zztotalpolybagpercaselot,
             NumberOfMPBPerCnt    AS zzpolybagpermastercarton,
             PackingMode          AS zzpackingmode
        FROM zsd_i_case_lot_h AS _caselots
          JOIN I_DeliveryDocumentItem AS _DelDocItm
            ON  _caselots~SalesOrder = _DelDocItm~ReferenceSDDocument
        INTO CORRESPONDING FIELDS OF TABLE @mt_caselots_headers
        WHERE _DelDocItm~DeliveryDocument = @lv_vbeln.

      IF sy-subrc = 0.
        SELECT item~CaselotHUUID    AS caseloth_uuid,     "#EC CI_SUBRC
               item~CaselotIUUID    AS caseloti_uuid,
               item~CaselotItem     AS zzcaselotitemnbr,
               item~Material        AS zzvariantarticle,
               item~SalesOrderItem  AS zzsales_order_item,
               item~CaseLotQuantity AS zzcaselotitemqty
          FROM zsd_i_case_lot_i AS item
            JOIN zsd_i_case_lot_h AS _header
              ON item~CaselotHUUID = _header~CaselotHUUID
            JOIN I_DeliveryDocumentItem AS _DelDocItm
              ON _header~SalesOrder = _DelDocItm~ReferenceSDDocument
              AND item~SalesOrderItem = _DelDocItm~ReferenceSDDocumentItem
          INTO CORRESPONDING FIELDS OF TABLE @mt_caselots_items
          WHERE _DelDocItm~DeliveryDocument = @lv_vbeln.

        IF sy-subrc <> 0.
          CLEAR mt_caselots_headers.
        ENDIF.
      ENDIF.

      LOOP AT mt_caselots_headers ASSIGNING FIELD-SYMBOL(<ls_caselots_headers>).
        <ls_caselots_headers>-zzsales_document =  |{ <ls_caselots_headers>-zzsales_document ALPHA = OUT }|.
      ENDLOOP.

***********************************************************************
**)-Fetch VAS
***********************************************************************

      SELECT _DelDocItm~DeliveryDocument            AS vbeln, "#EC CI_SUBRC
             _DelDocItm~DeliveryDocumentItem        AS posnr,
             _vas~fsh_vas_ser                       AS zzvastype,
             fsh_vas_serv_t~fsh_ser_desc            AS zzvastype_des,
             _vas~fsh_vas_sub_ser                   AS zzvascode,
             _vas~fsh_im_rel                        AS zzproc_rel_flag,
             fsh_vas_subser_t~fsh_sub_ser_desc      AS zzvascode_des,
             fsh_cust_field1                        AS zzvasfield1,
             fsh_cust_field2                        AS zzvasfield2,
             fsh_cust_field3                        AS zzvasfield3
        FROM fsh_v_vass_tl AS _vas
        JOIN I_DeliveryDocumentItem AS _DelDocItm
          ON  _vas~fsh_vgbel  = _DelDocItm~ReferenceSDDocument
          AND _vas~fsh_vgpos  = _DelDocItm~ReferenceSDDocumentItem
        LEFT JOIN fsh_vas_serv_t ON fsh_vas_serv_t~fsh_vas_ser = _vas~fsh_vas_ser AND
                                    fsh_vas_serv_t~spras       = @sy-langu
        LEFT JOIN fsh_vas_subser_t ON fsh_vas_subser_t~fsh_vas_ser     = _vas~fsh_vas_ser     AND
                                      fsh_vas_subser_t~fsh_vas_sub_ser = _vas~fsh_vas_sub_ser AND
                                      fsh_vas_subser_t~spras = @sy-langu
        WHERE  _DelDocItm~DeliveryDocument = @lv_vbeln
        ##TOO_MANY_ITAB_FIELDS INTO CORRESPONDING FIELDS OF TABLE @mt_vas.

    ENDIF.
  ENDMETHOD.


  METHOD get_instance.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.08.2022 | 13118 : FB_13118_SD_[Platform] - S4 to WMS - Outbound Delive*
*              |            | DS4K927689                               *
*----------------------------------------------------------------------*
* SRIKAVEN     | 17.01.2023 | 16988     : [Build] - STO - Remove  obsolet*
*              |            | DS4K940678                               *
*----------------------------------------------------------------------*
    IF go_inst IS NOT BOUND.
      ro_inst = NEW zcl_sc_s4_deliver_out_ext2( ).
      go_inst = ro_inst.
    ELSE.
      ro_inst = go_inst.
    ENDIF.
  ENDMETHOD.
ENDCLASS.