CLASS zcl_sc_tlt_change DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    TYPES gtyp_v_exec_mode TYPE char5 .
    TYPES:
      BEGIN OF gtyp_s_context,
        exec_mode TYPE gtyp_v_exec_mode,
      END OF gtyp_s_context,

      BEGIN OF gtyp_s_lfa1,
        lifnr        TYPE lifnr,
        dept_country TYPE land1,
        dept_city    TYPE regio,
      END OF gtyp_s_lfa1,


      BEGIN OF gtyp_s_plant,
        plant       TYPE werks_d,
        arv_country TYPE land1,
        arv_city    TYPE regio,
      END OF gtyp_s_plant,


      BEGIN OF gtyp_s_temp_po,
        ebeln             TYPE ebeln,
        ebelp             TYPE ebelp,
        lifn2             TYPE lifnr,
        ebtyp             TYPE ebtyp,
        kunnr             TYPE ekunnr,
        zz1_lexfct_da_pdi TYPE zz1_lexfct_da,
        knttp             TYPE knttp,
        evers             TYPE evers,
        arv_country       TYPE land1,
        arv_city          TYPE regio,
        lifnr             TYPE lifnr,
        vktra             TYPE vktra,
      END OF gtyp_s_temp_po.

    CONSTANTS:
      BEGIN OF gc_s_exec_modes,
        full  TYPE gtyp_v_exec_mode VALUE 'FULL' ##NO_TEXT,
        delta TYPE gtyp_v_exec_mode VALUE 'DELTA' ##NO_TEXT,
      END OF gc_s_exec_modes .
*    DATA:
*      gt_coo TYPE TABLE OF rsdsselopt .
*    DATA:
*      gt_cod TYPE TABLE OF rsdsselopt .
    DATA : gt_lfa1    TYPE TABLE OF gtyp_s_lfa1,
           gt_plant   TYPE TABLE OF gtyp_s_plant,
           gt_temp_po TYPE TABLE OF gtyp_s_temp_po.

    METHODS constructor
      IMPORTING
        !is_context TYPE gtyp_s_context
      .
    METHODS recalculate_purch_orders
      RETURNING
        VALUE(rt_messages) TYPE bapiret2_t .
  PROTECTED SECTION.
  PRIVATE SECTION.


    TYPES:
      BEGIN OF gtyp_s_zhm_evers,
        evers TYPE evers,
        vktra TYPE vktra,
      END OF gtyp_s_zhm_evers .
    TYPES:
      BEGIN OF gtyp_s_purch_order,
        ebeln                  TYPE ebeln,
        ebelp                  TYPE ebelp,
        etens                  TYPE etens,
        meins                  TYPE bstme,
        evers                  TYPE evers,
        zz1_mm_exfactorydt_pdi TYPE zz1_mm_exfactorydt,
        werks                  TYPE ewerk,
        lifnr                  TYPE elifn,
        vktra                  TYPE vktra,
        ebtyp                  TYPE ebtyp,
        zz1_lexfct_da_pdi      TYPE zz1_lexfct_da,
        kunnr                  TYPE ekunnr,
        knttp                  TYPE knttp,
      END OF gtyp_s_purch_order.

    TYPES:
      gtyp_t_evers_range   TYPE RANGE OF evers .
    TYPES:
      gtyp_t_vktra_range   TYPE RANGE OF vktra .
    TYPES:
      gtyp_t_bsart_range   TYPE RANGE OF bsart .
    TYPES:
      gtyp_t_pstyp_range   TYPE RANGE OF pstyp .
    TYPES:
      gtyp_t_knttp_range   TYPE RANGE OF knttp .
    TYPES:
      gtyp_t_purch_orders  TYPE STANDARD TABLE OF gtyp_s_purch_order WITH EMPTY KEY .
    TYPES:
      gtyp_th_zhm_evers    TYPE HASHED TABLE OF gtyp_s_zhm_evers WITH UNIQUE KEY vktra evers .
    TYPES:
      gtyp_th_pscd_route   TYPE HASHED TABLE OF wrf_pscd_route WITH UNIQUE KEY dept_country dept_city arv_country arv_city transp_mode .

    CONSTANTS:
      BEGIN OF mc_s_appl_log,
        object    TYPE balobj_d VALUE 'ZSC_TLT_ROUTE' ##NO_TEXT,
        subobject TYPE  balsubobj VALUE 'ZSC_TLT_TAB_UPDATE' ##NO_TEXT,
      END OF mc_s_appl_log .
    CONSTANTS:
      BEGIN OF mc_s_change_types,
        update TYPE cdchngind VALUE 'U' ##NO_TEXT,
      END OF mc_s_change_types .
    CONSTANTS:
      BEGIN OF mc_s_hm_ids,
        ship_instr_by_mot TYPE if_fdt_types=>id VALUE '0239FC3B8D041EDD85FF66BFA3A27F59' ##NO_TEXT,
        cross_dock_direct TYPE zpe_hm_id VALUE '1061' ##NO_TEXT,
      END OF mc_s_hm_ids .
    DATA mv_msg_dummy TYPE string  ##NEEDED.
    DATA ms_context TYPE gtyp_s_context .
    DATA mt_last_exec_timestamp TYPE zsc_tt_timestamp_range .
    DATA mth_pscd_route TYPE gtyp_th_pscd_route .
    DATA mt_purch_orders TYPE gtyp_t_purch_orders .
    DATA mt_cross_dock_direct TYPE zpe_tt_hm_table .
    DATA mif_message_list TYPE REF TO if_reca_message_list .

    METHODS create_appl_log .
    METHODS save_appl_log .
    METHODS get_appl_log_messages
      RETURNING
        VALUE(rt_messages) TYPE bapiret2_t .
    METHODS get_last_exec_timestamp .
    METHODS has_error
      IMPORTING
        !it_messages        TYPE bapiret2_t
      RETURNING
        VALUE(rv_has_error) TYPE abap_bool .
    METHODS get_trans_lead_time .
    METHODS save_last_exec_info .
    METHODS get_ship_instructions
      RETURNING
        VALUE(rt_evers) TYPE gtyp_t_evers_range .
    METHODS get_purch_orders .
    METHODS process_purch_orders .
    METHODS get_cross_dock_direct .
    METHODS get_order_types
      RETURNING
        VALUE(rt_bsart) TYPE gtyp_t_bsart_range .
    METHODS get_item_categories
      RETURNING
        VALUE(rt_pstyp) TYPE gtyp_t_pstyp_range .
    METHODS get_acc_assign_categories
      RETURNING
        VALUE(rt_knttp) TYPE gtyp_t_knttp_range .
    METHODS get_confirmation
      IMPORTING
        !iv_ebeln      TYPE ebeln
        !iv_ebelp      TYPE ebelp
        !iv_etens      TYPE etens
      RETURNING
        VALUE(rs_ekes) TYPE ekes .
    METHODS get_schedule_lines
      IMPORTING
        !iv_ebeln      TYPE ebeln
        !iv_ebelp      TYPE ebelp
      RETURNING
        VALUE(rt_eket) TYPE eket_tt .
ENDCLASS.



CLASS ZCL_SC_TLT_CHANGE IMPLEMENTATION.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*
* KAWATHIM     | 25.09.2023 | 25376     : P&FE - [BUILD] TLT Re-Design *
*              |            | DS4K964893                               *
*----------------------------------------------------------------------*

    ms_context = is_context.
  ENDMETHOD.


  METHOD create_appl_log.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    mif_message_list = cf_reca_message_list=>create( id_object    = mc_s_appl_log-object
                                                     id_subobject = mc_s_appl_log-subobject ).

  ENDMETHOD.


  METHOD get_acc_assign_categories.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    LOOP AT mt_cross_dock_direct ASSIGNING FIELD-SYMBOL(<ls_cross_dock_direct>) TO 2.
      APPEND VALUE #( sign   = if_fsbp_const_range=>sign_include
                      option = if_fsbp_const_range=>option_equal
                      low    = <ls_cross_dock_direct>-field3 ) TO rt_knttp.
    ENDLOOP.

    SORT rt_knttp BY sign option low.
    DELETE ADJACENT DUPLICATES FROM rt_knttp COMPARING ALL FIELDS.

  ENDMETHOD.


  METHOD get_appl_log_messages.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 30.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*
    mif_message_list->get_list_as_bapiret(
      IMPORTING
        et_list = rt_messages ).

  ENDMETHOD.


  METHOD get_confirmation.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    SELECT SINGLE purchaseorder                 AS ebeln, "#EC CI_SEL_NESTED
                  purchaseorderitem             AS ebelp,
                  sequentialnmbrofsuplrconf     AS etens,
                  supplierconfirmationcategory  AS ebtyp,
                  deliverydate                  AS eindt,
                  delivdatecategory             AS lpein,
                  deliverytime                  AS uzeit,
                  creationdate                  AS erdat,
                  creationtime                  AS ezeit,
                  confirmedquantity             AS menge,
                  mrprelevantquantity           AS dabmg,
                  suplrconfcreationcategory     AS estkz,
                  isdeleted                     AS loekz,
                  confisrelevanttomrp           AS kzdis,
                  supplierconfirmationextnumber AS xblnr,
                  deliverydocument              AS vbeln,
                  deliverydocumentitem          AS vbelp,
                  manufacturerpartprofile       AS mprof,
                  manufacturermaterial          AS ematn,
                  numberofreminders             AS mahnz,
                  batch                         AS charg,
                  deliveryisinplant             AS imwrk,
                  handoverdate                  AS handoverdate,
                  handovertime                  AS handovertime,
                  performanceperiodstartdate    AS startdate,
                  performanceperiodenddate      AS enddate,
                  serviceperformer              AS serviceperformer,
                  supplierconfirmation          AS supplier_conf_no,
                  supplierconfirmationitem      AS supplier_conf_item
      FROM i_posupplierconfirmationapi01
        INTO CORRESPONDING FIELDS OF @rs_ekes
          WHERE purchaseorder             = @iv_ebeln
            AND purchaseorderitem         = @iv_ebelp
            AND sequentialnmbrofsuplrconf = @iv_etens.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

  ENDMETHOD.


  METHOD get_cross_dock_direct.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    IF has_error( get_appl_log_messages( ) ).
      RETURN.
    ENDIF.

    TRY.
        mt_cross_dock_direct = zcl_pe_hm_basic=>get_table( '1061' ).
      CATCH zcx_pe_hm.
        MESSAGE w007(zsc_tlt_change) WITH mc_s_hm_ids-cross_dock_direct INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).
        RETURN.
    ENDTRY.

  ENDMETHOD.


  METHOD get_item_categories.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    LOOP AT mt_cross_dock_direct ASSIGNING FIELD-SYMBOL(<ls_cross_dock_direct>) TO 2.
      APPEND VALUE #( sign   = if_fsbp_const_range=>sign_include
                      option = if_fsbp_const_range=>option_equal
                      low    = <ls_cross_dock_direct>-field2 ) TO rt_pstyp.
    ENDLOOP.

    SORT rt_pstyp BY sign option low.
    DELETE ADJACENT DUPLICATES FROM rt_pstyp COMPARING ALL FIELDS.

  ENDMETHOD.


  METHOD get_last_exec_timestamp.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    MESSAGE s000(zsc_tlt_change) WITH ms_context-exec_mode INTO mv_msg_dummy.
    mif_message_list->add_symsg( ).

    IF ms_context-exec_mode = gc_s_exec_modes-full.
      RETURN.
    ENDIF.

    DATA(ls_last_exec_info) = zcl_pe_abap_runtime=>get_last_execution( ).

    IF ls_last_exec_info-timestamp IS NOT INITIAL.
      DATA(lv_timestamp_short) = cl_abap_tstmp=>move_to_short( ls_last_exec_info-timestamp ).

      MESSAGE s001(zsc_tlt_change) WITH |{ lv_timestamp_short TIMESTAMP = SPACE }| INTO mv_msg_dummy.
      mif_message_list->add_symsg( ).

      TRY.
          mt_last_exec_timestamp = VALUE #( ( sign   = if_fsbp_const_range=>sign_include
                                                  option = if_fsbp_const_range=>option_greater
                                                  low    = lv_timestamp_short ) ).

        CATCH cx_parameter_invalid_type cx_parameter_invalid_range.
          CLEAR mt_last_exec_timestamp.
      ENDTRY.
    ENDIF.

  ENDMETHOD.


  METHOD get_order_types.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    LOOP AT mt_cross_dock_direct ASSIGNING FIELD-SYMBOL(<ls_cross_dock_direct>) TO 2.
      APPEND VALUE #( sign   = if_fsbp_const_range=>sign_include
                      option = if_fsbp_const_range=>option_equal
                      low    = <ls_cross_dock_direct>-field1 ) TO rt_bsart.
    ENDLOOP.

    SORT rt_bsart BY sign option low.
    DELETE ADJACENT DUPLICATES FROM rt_bsart COMPARING ALL FIELDS.

  ENDMETHOD.


  METHOD get_purch_orders.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*
* KAWATHIM     | 25.09.2023 | 25376     : P&FE - [BUILD] TLT Re-Design *
*              |            | DS4K964893                               *
*----------------------------------------------------------------------*
* RAPANKON     | 27.11.2023 | 25376     : P&FE - [BUILD] TLT Re-Design *
*              |            | DS4K964893                               *
*----------------------------------------------------------------------*
    IF has_error( get_appl_log_messages( ) ).
      RETURN.
    ENDIF.

    DATA(lt_ship_instructions) = get_ship_instructions( ).
    DATA(lt_order_types) = get_order_types( ).
    DATA(lt_item_categories) = get_item_categories( ).
    DATA(lt_acc_assign_categories) = get_acc_assign_categories( ).


    SELECT _ekko~purchasingdocument AS ebeln,
           _ekpo~purchaseorderitem AS ebelp,
           _ekes~sequentialnmbrofsuplrconf AS etens,
           _ekpo~purchaseorderquantityunit AS meins,
           _ekpo~shippinginstruction AS evers,
           _ekpo~zz1_mm_exfactorydt_pdi,
           _ekpo~plant AS werks,
           _ekko~supplier AS lifnr,
           t173~vktra,
           _ekes~supplierconfirmationcategory AS ebtyp,
           _ekpo~zz1_lexfct_da_pdi,
           _ekpo~customer AS kunnr,
           _ekpo~accountassignmentcategory AS knttp
     FROM i_purchasingdocument AS _ekko
     JOIN i_purchaseorderitemapi01 AS _ekpo ON _ekpo~purchaseorder = _ekko~purchasingdocument
     JOIN i_purordschedulelineapi01 AS _eket ON _eket~purchaseorder     = _ekpo~purchaseorder
                                            AND _eket~purchaseorderitem = _ekpo~purchaseorderitem
     JOIN i_posupplierconfirmationapi01 AS _ekes ON _ekes~purchaseorder     = _ekpo~purchaseorder
                                                AND _ekes~purchaseorderitem = _ekpo~purchaseorderitem
     JOIN t173 ON t173~vsart = _ekpo~shippinginstruction "#EC CI_BUFFJOIN
    WHERE _ekko~purchasingdocumenttype IN @lt_order_types
      AND _ekpo~purchaseorderitemcategory IN @lt_item_categories
      AND _ekpo~accountassignmentcategory IN @lt_acc_assign_categories
      AND _ekpo~purchasingdocumentdeletioncode = @space
      AND _ekpo~iscompletelydelivered = @space
      AND _ekpo~shippinginstruction IN @lt_ship_instructions
      INTO CORRESPONDING FIELDS OF TABLE @mt_purch_orders.

    IF sy-subrc <> 0.
      MESSAGE e004(zsc_tlt_change) INTO mv_msg_dummy.
      mif_message_list->add_symsg( ).
      RETURN.
    ENDIF.

    TRY.
        DATA(lo_changeamdp) = NEW zcl_sc_tlt_change_amdp( ).
        lo_changeamdp->get_purchase_order(
          EXPORTING
            it_lfa1  = gt_lfa1
            it_werks = gt_plant
            it_po    = mt_purch_orders
          IMPORTING
            et_po    = mt_purch_orders
        ).
        ##CATCH_ALL
      CATCH cx_root.
        MESSAGE e004(zsc_tlt_change) INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).
        RETURN.
    ENDTRY.


    DATA(lt_purch_order_count) = mt_purch_orders.
    SORT lt_purch_order_count BY ebeln.
    DELETE ADJACENT DUPLICATES FROM lt_purch_order_count COMPARING ebeln.

    MESSAGE s005(zsc_tlt_change) WITH |{ lines( lt_purch_order_count ) }| INTO mv_msg_dummy.
    mif_message_list->add_symsg( ).

  ENDMETHOD.


  METHOD get_schedule_lines.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    SELECT ebeln,                                    "#EC CI_SEL_NESTED
           ebelp,
           etenr,
           eindt
      FROM eket
        INTO CORRESPONDING FIELDS OF TABLE @rt_eket ##TOO_MANY_ITAB_FIELDS
          WHERE ebeln = @iv_ebeln
            AND ebelp = @iv_ebelp
              ORDER BY ebeln, ebelp, etenr.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

  ENDMETHOD.


  METHOD get_ship_instructions.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    DATA(lth_zhm_evers) = VALUE gtyp_th_zhm_evers( ).

    TRY .
        zcl_pe_hm_complex=>get_table(
          EXPORTING
            iv_id    = '0239FC3B8D041EDD85FF66BFA3A27F59'
          IMPORTING
            et_table = lth_zhm_evers ).
      CATCH zcx_pe_hm.
        MESSAGE w007(zsc_tlt_change) WITH mc_s_hm_ids-ship_instr_by_mot INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).
        RETURN.
    ENDTRY.

    IF lth_zhm_evers IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lt_transp_modes) = VALUE gtyp_t_vktra_range( FOR <ls_transp_mode> IN mth_pscd_route
                                                      ( sign   = if_fsbp_const_range=>sign_include
                                                        option = if_fsbp_const_range=>option_equal
                                                        low    = <ls_transp_mode>-transp_mode ) ).

    SORT lt_transp_modes BY sign option low.
    DELETE ADJACENT DUPLICATES FROM lt_transp_modes COMPARING ALL FIELDS.

    LOOP AT lth_zhm_evers ASSIGNING FIELD-SYMBOL(<ls_zhm_evers>) WHERE vktra IN lt_transp_modes. "#EC CI_HASHSEQ
      APPEND VALUE #( sign   = if_fsbp_const_range=>sign_include
                      option = if_fsbp_const_range=>option_equal
                      low    = <ls_zhm_evers>-evers ) TO rt_evers.
    ENDLOOP.

    SORT rt_evers BY sign option low.
    DELETE ADJACENT DUPLICATES FROM rt_evers COMPARING ALL FIELDS.

  ENDMETHOD.


  METHOD get_trans_lead_time.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*
* KAWATHIM     | 25.09.2023 | 25376     : P&FE - [BUILD] TLT Re-Design *
*              |            | DS4K964893                               *
* RAPANKON     | 27.11.2023 | 25376     : P&FE - [BUILD] TLT Re-Design *
*              |            | DS4K964893                               *
*----------------------------------------------------------------------*

    CONSTANTS : lc_03(2) TYPE c VALUE '03'.
    IF has_error( get_appl_log_messages( ) ).
      RETURN.
    ENDIF.

    SELECT *
*      dept_country,
*           dept_city,
*           arv_country,
*           arv_city,
*           transp_mode,
*           move_time,
*            move_time_h
      FROM wrf_pscd_route
        INTO TABLE @mth_pscd_route
          WHERE timestamp IN @mt_last_exec_timestamp
            AND transp_mode NE @lc_03.
    IF sy-subrc <> 0.
      MESSAGE e003(zsc_tlt_change) WITH 'WRF_PSCD_ROUTE' INTO mv_msg_dummy.
      mif_message_list->add_symsg( ).
      RETURN.
    ENDIF.

    DATA(lv_pscd_route_count) = lines( mth_pscd_route ).
                                                   "#EC CI_NO_TRANSFORM
    MESSAGE s002(zsc_tlt_change) WITH 'WRF_PSCD_ROUTE' lv_pscd_route_count INTO mv_msg_dummy.
    mif_message_list->add_symsg( ).
    IF mth_pscd_route IS NOT INITIAL.

      SELECT _supplier~supplier AS lifnr,          "#EC CI_NO_TRANSFORM
             _supplier~country AS dept_country,
             _supplier~region AS dept_city
  FROM i_supplier AS _supplier
  FOR ALL ENTRIES IN @mth_pscd_route
  WHERE _supplier~country = @mth_pscd_route-dept_country
  AND _supplier~region = @mth_pscd_route-dept_city
  INTO TABLE @gt_lfa1.
      IF sy-subrc <> 0.
        MESSAGE e003(zsc_tlt_change) WITH 'LFA1' INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).
        RETURN.
      ELSE.
        SORT gt_lfa1 BY lifnr.
      ENDIF.
      lv_pscd_route_count = lines( gt_lfa1 ).

      MESSAGE s002(zsc_tlt_change) WITH 'LFA1' lv_pscd_route_count INTO mv_msg_dummy.
      mif_message_list->add_symsg( ).


      SELECT werks,                                "#EC CI_NO_TRANSFORM
              land1 AS arv_country,
              regio AS arv_city
   FROM t001w AS _t001w
   FOR ALL ENTRIES IN @mth_pscd_route
   WHERE land1 = @mth_pscd_route-arv_country
   AND regio = @mth_pscd_route-arv_city
   INTO TABLE @gt_plant.
      IF sy-subrc <> 0.
        MESSAGE e003(zsc_tlt_change) WITH 'T001W' INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).
        RETURN.
      ENDIF.
      lv_pscd_route_count = lines( gt_plant ).

      MESSAGE s002(zsc_tlt_change) WITH 'T001W' lv_pscd_route_count INTO mv_msg_dummy.
      mif_message_list->add_symsg( ).

    ENDIF.

  ENDMETHOD.


  METHOD has_error.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    LOOP AT it_messages TRANSPORTING NO FIELDS WHERE type CA 'EAX'. "#EC CI_STDSEQ
      EXIT.
    ENDLOOP.

    IF sy-subrc = 0.
      rv_has_error = abap_true.
    ENDIF.

  ENDMETHOD.


  METHOD process_purch_orders.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*
* KAWATHIM     | 25.09.2023 | 25376     : P&FE - [BUILD] TLT Re-Design *
*              |            | DS4K964893                               *
*----------------------------------------------------------------------*

    DATA : lv_date   TYPE datum,
           lt_wroute TYPE TABLE OF wrf_pscd_route.
    CONSTANTS : lc_tlt TYPE c VALUE ''.
    IF has_error( get_appl_log_messages( ) ).
      RETURN.
    ENDIF.

    DATA(lt_return) = VALUE bapiret2_t( ).
    DATA(ls_return) = VALUE bapiret2( ).
    DATA(lt_poschedule) = VALUE bapimeposchedule_tp( ).
    DATA(lt_poschedulex) = VALUE bapimeposchedulx_tp( ).
    DATA(lt_xekes) = VALUE mmpr_uekes( ).
    DATA(lt_confirmations) = VALUE ekes_t( ).
    lt_wroute = mth_pscd_route[].
    SORT lt_wroute BY dept_country dept_city arv_country arv_city transp_mode.
    LOOP AT mt_purch_orders
      ASSIGNING FIELD-SYMBOL(<ls_purch_order>)
        GROUP BY ( ebeln = <ls_purch_order>-ebeln
                   size  = GROUP SIZE
                   lifnr = <ls_purch_order>-lifnr )
          REFERENCE INTO DATA(lo_ebeln_group).

      CLEAR: lt_poschedule,
             lt_poschedulex,
             lt_return,
             ls_return,
             lv_date,
             lt_xekes.

      MESSAGE s008(zsc_tlt_change) WITH lo_ebeln_group->ebeln INTO mv_msg_dummy.
      mif_message_list->add_symsg( ).

      LOOP AT GROUP lo_ebeln_group
        ASSIGNING FIELD-SYMBOL(<ls_ebeln_group>)
          GROUP BY ( ebelp = <ls_ebeln_group>-ebelp
                     size  = GROUP SIZE )
            REFERENCE INTO DATA(lo_ebelp_group).

        IF lo_ebelp_group->size = 0.
          CONTINUE.
        ENDIF.

        CLEAR: lt_confirmations.

        DATA(lt_schedule_lines) = get_schedule_lines( iv_ebeln = lo_ebeln_group->ebeln
                                                      iv_ebelp = lo_ebelp_group->ebelp ).

        LOOP AT GROUP lo_ebelp_group ASSIGNING FIELD-SYMBOL(<ls_ebelp_group>).
          DATA(ls_confirmation) = get_confirmation( iv_ebeln = <ls_ebelp_group>-ebeln
                                                    iv_ebelp = <ls_ebelp_group>-ebelp
                                                    iv_etens = <ls_ebelp_group>-etens ).
          IF ls_confirmation IS NOT INITIAL.
            APPEND ls_confirmation TO lt_confirmations.

          ENDIF.

        ENDLOOP.

        CHECK <ls_ebelp_group> IS ASSIGNED.

        IF <ls_ebelp_group>-ebtyp <> 'CD'.
          lv_date = <ls_ebelp_group>-zz1_mm_exfactorydt_pdi.
        ELSE.
          lv_date = <ls_ebelp_group>-zz1_lexfct_da_pdi.
        ENDIF.
        IF <ls_ebelp_group>-knttp = 'X'.
          DATA(lv_kunnr) = <ls_ebelp_group>-kunnr.
        ENDIF.
        zcl_sc_tlt_calculation=>calculate_dd_for_cd_z2_z3_z4(
          EXPORTING
            iv_ebtyp   = <ls_ebelp_group>-ebtyp                  " Confirmation Category
            iv_evers   = <ls_ebelp_group>-evers                 " Shipping Instructions
            iv_tltf    = lv_date                " Date that is used to calculate delivery date From
            iv_refdoc  = CONV zsc_refdoc( <ls_ebelp_group>-ebeln )                 " Reference Number
            iv_refitem = CONV zsc_refitem( <ls_ebelp_group>-ebelp )                " Reference Item
            iv_werks   = <ls_ebelp_group>-werks                 " Plant
            iv_gparn   = <ls_ebelp_group>-lifnr                  " Number of a Business Partner in Vendor Master Record
            iv_kunnr   = lv_kunnr                 " Customer Number
          IMPORTING
            ev_tltd    = DATA(lv_tlt)
            ev_eeind   = DATA(lv_eindt)                 " Item Delivery Date
        ).

        IF ( lv_eindt IS INITIAL OR lv_eindt = 00000000 )
            AND <ls_ebelp_group>-ebtyp <> 'CD'  ##NUMBER_OK .
          lv_eindt = lv_date.
        ELSEIF lv_tlt = lc_tlt OR lv_tlt = 0.
          lv_eindt = lv_date.
        ENDIF.
        CLEAR : lv_kunnr, lv_tlt.
        MESSAGE s009(zsc_tlt_change) WITH |{ lv_eindt DATE = ISO }| INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).

        LOOP AT lt_schedule_lines ASSIGNING FIELD-SYMBOL(<ls_schedule_line>). "#EC CI_NESTED

          IF <ls_schedule_line>-eindt >= lv_eindt.
            MESSAGE w010(zsc_tlt_change) WITH <ls_schedule_line>-ebelp <ls_schedule_line>-etenr INTO mv_msg_dummy.
            mif_message_list->add_symsg( ).
            MESSAGE w011(zsc_tlt_change) INTO mv_msg_dummy.
            mif_message_list->add_symsg( ).
            CONTINUE.
          ENDIF.

          APPEND VALUE #( po_item       = <ls_schedule_line>-ebelp
                          sched_line    = <ls_schedule_line>-etenr
                          delivery_date = lv_eindt ) TO lt_poschedule.

          APPEND VALUE #( po_item       = <ls_schedule_line>-ebelp
                          sched_line    = <ls_schedule_line>-etenr
                          po_itemx      = abap_true
                          sched_linex   = abap_true
                          delivery_date = abap_true ) TO lt_poschedulex.

        ENDLOOP.

        LOOP AT lt_confirmations ASSIGNING FIELD-SYMBOL(<ls_confirmation>). "#EC CI_NESTED

          IF <ls_confirmation>-eindt >= lv_eindt.
            MESSAGE w012(zsc_tlt_change) WITH <ls_confirmation>-ebelp <ls_confirmation>-etens INTO mv_msg_dummy.
            mif_message_list->add_symsg( ).
            MESSAGE w013(zsc_tlt_change) INTO mv_msg_dummy.
            mif_message_list->add_symsg( ).
            CONTINUE.
          ENDIF.

          DATA(ls_xekes) = CORRESPONDING uekes( <ls_confirmation> ).
          ls_xekes-eindt = lv_eindt.
          ls_xekes-kz    = mc_s_change_types-update.
          APPEND ls_xekes TO lt_xekes.
        ENDLOOP.

      ENDLOOP.

      DATA(lv_is_commit_required) = VALUE abap_bool( ).

      IF lt_poschedule IS NOT INITIAL.
        CALL FUNCTION 'BAPI_PO_CHANGE'
          EXPORTING
            purchaseorder = lo_ebeln_group->ebeln
          TABLES
            return        = lt_return
            poschedule    = lt_poschedule
            poschedulex   = lt_poschedulex.

        IF has_error( lt_return ).
          CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'
            IMPORTING
              return = ls_return.

          IF ls_return IS NOT INITIAL.
            APPEND ls_return TO lt_return.
          ENDIF.
        ELSE.
          lv_is_commit_required = abap_true.
        ENDIF.

      ELSE.
        MESSAGE e017(zsc_tlt_change) INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).
      ENDIF.

      IF lt_xekes IS NOT INITIAL.
        MESSAGE s014(zsc_tlt_change) INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).

        CALL FUNCTION 'ME_CONFIRMATION_UPDATE'
          EXPORTING
            i_ebeln = lo_ebeln_group->ebeln
          TABLES
            xekes   = lt_xekes
          EXCEPTIONS
            OTHERS  = 1.

        IF sy-subrc = 0.
          lv_is_commit_required = abap_true.
          MESSAGE s016(zsc_tlt_change) INTO mv_msg_dummy.
        ELSE.
          MESSAGE e015(zsc_tlt_change) INTO mv_msg_dummy.
        ENDIF.
        mif_message_list->add_symsg( ).
      ENDIF.

      IF lv_is_commit_required = abap_true.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait   = abap_true
          IMPORTING
            return = ls_return.

        IF ls_return IS NOT INITIAL.
          APPEND ls_return TO lt_return.
        ENDIF.
      ENDIF.

      IF lt_return IS NOT INITIAL.
        mif_message_list->add_from_bapi( it_bapiret = lt_return ).
      ENDIF.
      CLEAR : lv_date, lv_kunnr, lv_tlt, lv_eindt.
    ENDLOOP.

  ENDMETHOD.


  METHOD recalculate_purch_orders.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*
* KAWATHIM     | 25.09.2023 | 25376     : P&FE - [BUILD] TLT Re-Design *
*              |            | DS4K964893                               *
*----------------------------------------------------------------------*

    create_appl_log( ).
    get_last_exec_timestamp( ).
    get_trans_lead_time( ).
    get_cross_dock_direct( ).
    get_purch_orders( ).
    process_purch_orders( ).
    save_last_exec_info( ).
    save_appl_log( ).

    rt_messages = get_appl_log_messages( ).

  ENDMETHOD.


  METHOD save_appl_log.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    mif_message_list->store(
      EXPORTING
        if_in_update_task = abap_false
      EXCEPTIONS
        error             = 1
        OTHERS            = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.

  ENDMETHOD.


  METHOD save_last_exec_info.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 27.01.2023 | 17169     : P&FE - [BUILD] TLT duration  *
*              |            | DS4K941255                               *
*----------------------------------------------------------------------*

    TRY.
        zcl_pe_abap_runtime=>save( ).
      CATCH zcx_pe_runtime_info.
        MESSAGE e006(zsc_tlt_change) INTO mv_msg_dummy.
        mif_message_list->add_symsg( ).
        RETURN.
    ENDTRY.

  ENDMETHOD.
ENDCLASS.