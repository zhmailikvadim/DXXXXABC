CLASS zcl_sd_aif_post_message_p001 DEFINITION
  PUBLIC FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.
    INTERFACES /aif/if_post_message_process.
    INTERFACES if_badi_interface.
ENDCLASS.


CLASS zcl_sd_aif_post_message_p001 IMPLEMENTATION.
  METHOD /aif/if_post_message_process~post_message_processing.
    " -----------------------------------------------------------------------
    "  R E V I S I O N   H I S T O R Y                                     -
    " -----------------------------------------------------------------------
    " AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
    "              |            | TRANSPORT REQUESTS                       -
    " -----------------------------------------------------------------------
    " ZHMAIVAD     | 29.09.2023 | 22859     : [Build] - Stock reconciliati -
    "              |            | DS4K957133                               -
    " -----------------------------------------------------------------------

    CONSTANTS lc_matdoc_item_persist_table TYPE /aif/pers_db_map-to_dbtab_name VALUE 'ZSD_T_MDREQ_MATE'.

    DATA ls_return TYPE bapiret2.

    FIELD-SYMBOLS <lv_msgguid> TYPE any.

    ASSIGN COMPONENT /aif/if_globals=>gc_eh_fix_key_flds-msgguid OF STRUCTURE is_message_data TO <lv_msgguid>.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
    FINAL(lv_is_error) = NEW zcl_sd_stock_recon_savepayload( )->store_to_persistence(
                                 iv_uuid          = <lv_msgguid>
                                 io_payload       = is_post_proc-ref_to_raw
                                 iv_db_table_name = lc_matdoc_item_persist_table ).
    IF lv_is_error = abap_true.
      MESSAGE w005(zcm_sd_stcock_recon) INTO FINAL(lv_message) ##NEEDED ##MG_MISSING.
      CALL FUNCTION 'BALW_BAPIRETURN_GET2'
        EXPORTING type   = 'W'
                  cl     = 'ZCM_SD_STCOCK_RECON'
                  number = '005'
        IMPORTING return = ls_return.

      APPEND ls_return TO ct_return.
    ENDIF.
  ENDMETHOD.
ENDCLASS.