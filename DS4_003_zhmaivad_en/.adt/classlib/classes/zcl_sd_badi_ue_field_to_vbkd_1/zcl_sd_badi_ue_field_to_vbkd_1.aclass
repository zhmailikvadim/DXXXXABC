CLASS zcl_sd_badi_ue_field_to_vbkd_1 DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_badi_interface .
    INTERFACES zif_sd_badi_ue_field_to_vbkd .
  PROTECTED SECTION.
private section.

  methods CHANGE_PRICING_DATE
    importing
      !IV_PROCESSING_MODE type CHAR1
      !IS_VBAK type VBAK
      !IV_POSNR type POSNR
      !IS_YVBKD type VBKD
      !IS_KVBKD type VBKDVB
      !IS_YKVBKD type VBKDVB
      !IT_XVBEP type VA_VBEPVB_T
      !IT_XVBAP type TT_VBAPVB
    changing
      !CS_VBKD type VBKD .
  methods MAP_TO_SALES_ORDER_TABLES
    importing
      !IV_VKORG type VKORG
      !IV_VTWEG type VTWEG
      !IV_AUART type AUART
      !IV_DESTINATION_TABLE type DBOBJ_NAME   ##NEEDED
      !IV_CONTROL_TYPE type ZSD_SO_FIELD_COPY_TYPE   ##NEEDED
      !IS_VBKD type VBKD
    changing
      !CS_VBAK type VBAK
      !CS_VBKD type VBKD
      !CT_VBPA type TT_VBPAVB .
  methods COPY_HEADER_FIELDS_TO_ITEM
    importing
      !IV_PROCESSING_MODE type CHAR1
      !IV_POSNR type POSNR
      !IS_VBAK type VBAK
      !IT_XVBKD type VA_VBKDVB_T
    changing
      !CS_VBKD type VBKD
      !CT_XVBAP type TT_VBAPVB optional
      !CT_XVBUV type VA_VBUVVB_T optional .
  methods SET_SCHEDULE_LINE_CATEGORY
    importing
      !IV_POSNR type POSNR
      !IS_YVBKD type VBKD
      !IT_XVBAP type TT_VBAPVB
    changing
      !CS_VBKD type VBKD
      !CT_XVBEP type VA_VBEPVB_T .
  methods COPY_RETURN_TIMESTAMP
    importing
      !IS_YVBKD type VBKD
      !IS_VBAK type VBAK
      !IV_POSNR type POSNR
    changing
      !CS_VBKD type VBKD .
  methods RESTORE_GPS_PRICING_DATE
    importing
      !IV_PROCESSING_MODE type CHAR1
      !IV_POSNR type POSNR
      !IS_VBAK type VBAK
      !IS_ORIGINAL_VBKD type VBKD
    changing
      !CS_VBKD type VBKD .
  methods INTAKE_INTERFACE_SUBSTITUTIONS
    changing
      !CS_VBKD type VBKD optional .
  methods SET_PURCHACE_ORDER_ITEM
    importing
      !IV_PROCESSING_MODE type CHAR1
      !IV_POSNR type POSNR
      !IS_VBAK type VBAK
      !IT_XVBKD type VA_VBKDVB_T
    changing
      !CS_VBKD type VBKD
      !CT_XVBAP type TT_VBAPVB optional
      !CT_XVBUV type VA_VBUVVB_T optional .
  methods METH_VBAK_CHANGE
    importing
      !IT_XVBPA type VA_VBPAVB_T
    changing
      !CS_VBAK type VBAK .
ENDCLASS.



CLASS ZCL_SD_BADI_UE_FIELD_TO_VBKD_1 IMPLEMENTATION.


  METHOD zif_sd_badi_ue_field_to_vbkd~run.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHIZHAND     | 17.05.2022 | 11272 : Copy Header/Item RDD             *
*              |            | to Pricing Date,                         *
*              |            | DS4K919120                               *
************************************************************************
* BERNAANT     |14.07.2022  | 12219 - Customer Order
*                                     Copy Control Enhancement
*                                     (BP to Sales Doc, Sales Doc to Sales Doc)
*              |            | DS4K924288
************************************************************************
* PISHCALI     | 25.07.2022 | 12508 : Customer Returns                 *
*              |            |         Avoid DN and Invoice Split       *
*              |            |         for Returns                      *
*              |            | DS4K924288                               *
************************************************************************
* BERNAANT     |26.10.2022  | 14805 - Customer Order
*                                     Promo Enhancement Customer Order
*              |            | DS4K934115
************************************************************************
* BERNAANT     |02.11.2022  | 14959 - Customer Order
*                                     update FIELD
*              |            | DS4K934539
************************************************************************
* IAPRYALE     | 14.11.2022 | 28083 : Copy Header/Item RSD             *
*              |            | to Pricing Date                          *
*              |            | DS4K935308                               *
************************************************************************
* KASHTULA     | 29.11.2022 | 15573     : O2C - Mass Upload tool for   *
*              |            | Sales Contracts, DS4K936558              *
************************************************************************
* MUKHAMAK     | 24.01.2023 | 17291     : Customer Order - Change      *
*              |            | schedule  line DS4K941579                *
*----------------------------------------------------------------------*
* NIABOALI     | 27.01.2023 | 17153: Customer Order Mass Upload Tool   *
*              |            | DS4K940281 (WI 16863 )                   *
*----------------------------------------------------------------------*
* LIKHAALI     | 02.02.2023 | FB_17268_SD_Cust Order - Mass Upl for    *
*                             Free supply Orders                       *
*              |            | DS4K941523                               *
*----------------------------------------------------------------------*
* MARTIRIA     | 06.03.2023 | 18294 : Return reason copy from header   *
*              |            | DS4K944565                               *
*              |            |                                          *
*----------------------------------------------------------------------*
* MARTIRIA     | 07.06.2023 | 21545 : Return reason copy from header   *
*              |            | DS4K953184                               *
*              |            |                                          *
*----------------------------------------------------------------------*
* DOLYADMI     | 26.06.2023 | 20730     : Feature - Development - O2C- *
*              |            | DS4K951058                               *
*----------------------------------------------------------------------*
* DOLYADMI     | 07.08.2023 | 24094     : [Investigation] - ShipTo par *
*              |            | DS4K960110                               *
*----------------------------------------------------------------------*
* PISHCALI     | 17.11.2023 | 23900: SD_PO field change to SO          *
*              |            | DS4K959743                               *
*----------------------------------------------------------------------*
* KURAPAND     | 29.11.2023 | 27185   :[Build] - CR - Update VBKD-BSTKD*
*              |            | DS4K972154                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 27.11.2023 | 25135     : Feature- Defect- Test- Retur *
*              |            | DS4K963930                               *
*----------------------------------------------------------------------*
* NAGARSOW     | 29.01.2024 | 28337    : Feature- Development - RDD heade *
*              |            | DS4K977099                               *
*----------------------------------------------------------------------*

    DATA(ls_original_vbkd) = cs_vbkd.

    intake_interface_substitutions( CHANGING cs_vbkd = cs_vbkd ).

    change_pricing_date(
      EXPORTING
        iv_processing_mode = iv_processing_mode
        is_vbak            = cs_vbak
        iv_posnr           = iv_posnr
        is_yvbkd           = is_yvbkd
        is_kvbkd           = is_kvbkd
        is_ykvbkd          = is_ykvbkd
        it_xvbap           = ct_xvbap
        it_xvbep           = it_xvbep
      CHANGING
        cs_vbkd            = cs_vbkd ).

    restore_gps_pricing_date(
      EXPORTING
        iv_processing_mode = iv_processing_mode
        iv_posnr           = iv_posnr
        is_vbak            = cs_vbak
        is_original_vbkd   = ls_original_vbkd
      CHANGING
        cs_vbkd            = cs_vbkd ).

    map_to_sales_order_tables(
      EXPORTING
        iv_vkorg             = cs_vbak-vkorg
        iv_vtweg             = cs_vbak-vtweg
        iv_auart             = cs_vbak-auart
        iv_destination_table = 'VBKD'
        iv_control_type      = '1'
        is_vbkd              = is_yvbkd
      CHANGING
        cs_vbak              = cs_vbak
        cs_vbkd              = cs_vbkd
        ct_vbpa              = ct_vbpa ).

    " Avoid DN and Invoice Split for Returns. Copy Header fields to Item fields
    copy_header_fields_to_item(
      EXPORTING
        iv_processing_mode = iv_processing_mode
        iv_posnr           = iv_posnr
        is_vbak            = cs_vbak
        it_xvbkd           = ct_xvbkd
      CHANGING

        cs_vbkd            = cs_vbkd
        ct_xvbuv           = ct_xvbuv
        ct_xvbap           = ct_xvbap ).

    set_schedule_line_category(
      EXPORTING
        it_xvbap = ct_xvbap
        iv_posnr = iv_posnr
        is_yvbkd = is_yvbkd
      CHANGING
        cs_vbkd  = cs_vbkd
        ct_xvbep = ct_xvbep ).

    zcl_sd_sdoc_ue_data_transfer=>get_instance( )->change_field_to_customer( CHANGING cs_vbkd = cs_vbkd
                                                                                      ct_vbpa = ct_vbpa ).

    copy_return_timestamp(
       EXPORTING
         is_yvbkd = is_yvbkd
         is_vbak  = cs_vbak
         iv_posnr = iv_posnr
       CHANGING
         cs_vbkd = cs_vbkd ).

    set_purchace_order_item(
      EXPORTING
        iv_processing_mode = iv_processing_mode
        iv_posnr           = iv_posnr
        is_vbak            = cs_vbak
        it_xvbkd           = ct_xvbkd
      CHANGING

        cs_vbkd            = cs_vbkd
        ct_xvbuv           = ct_xvbuv
        ct_xvbap           = ct_xvbap ).

    meth_vbak_change(
       exporting
         it_xvbpa = ct_vbpa
       changing
         cs_vbak  = cs_vbak ).

  ENDMETHOD.


  METHOD change_pricing_date.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHIZHAND     | 17.05.2022 | 11272 : Copy Header/Item RDD             *
*              |            | to Pricing Date,                         *
*              |            | DS4K919120                               *
*----------------------------------------------------------------------*
* IAPRYALE     | 16.11.2023 | 17001 : Copy Header/Item RSD             *
*              |            | to Pricing Date                          *
*              |            | DS4K940703                               *
*----------------------------------------------------------------------*
    IF VALUE #( it_xvbap[ posnr = iv_posnr ]-vgbel OPTIONAL ) IS NOT INITIAL.
      RETURN.
    ENDIF.

    IF iv_posnr IS INITIAL
      AND ( iv_processing_mode = cl_lord_co=>sc_create_mode
         OR iv_processing_mode = cl_lord_co=>sc_change_mode ).
      IF is_vbak-zz1_rsd_head IS NOT INITIAL.
        cs_vbkd-prsdt = is_vbak-zz1_rsd_head.
      ELSE.
        cs_vbkd-prsdt = is_vbak-vdatu.
      ENDIF.
    ELSEIF iv_posnr IS NOT INITIAL
       AND iv_processing_mode = cl_lord_co=>sc_create_mode.
      READ TABLE it_xvbap ASSIGNING FIELD-SYMBOL(<ls_xvbap>) WITH KEY posnr = iv_posnr .
      IF sy-subrc = 0 AND
         <ls_xvbap>-zz1_rsd_item IS NOT INITIAL.
        cs_vbkd-prsdt = <ls_xvbap>-zz1_rsd_item.
      ELSE.
*      if it's item we need check date item and header
        READ TABLE it_xvbep ASSIGNING FIELD-SYMBOL(<ls_xvbep>) WITH KEY posnr = iv_posnr .
        IF  sy-subrc         = 0
        AND <ls_xvbep>-edatu <> is_vbak-vdatu
        AND cs_vbkd-prsdt    <> is_yvbkd-prsdt.
          cs_vbkd-prsdt = <ls_xvbep>-edatu.
        ENDIF.
      ENDIF.
    ELSEIF iv_posnr IS NOT INITIAL
       AND iv_processing_mode = cl_lord_co=>sc_change_mode.
      IF  cs_vbkd-prsdt  <> is_yvbkd-prsdt
      AND is_kvbkd-prsdt <> is_ykvbkd-prsdt
      AND cs_vbkd-prsdt  = is_kvbkd-prsdt.
        cs_vbkd-prsdt = is_yvbkd-prsdt.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD copy_header_fields_to_item.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* PISHCALI     | 25.07.2022 | 12508 : Customer Returns                 *
*              |            |         Avoid DN and Invoice Split       *
*              |            |         for Returns                      *
*              |            | DS4K924288 (WI 12219)                    *
*----------------------------------------------------------------------*
* MARTIRIA     | 06.03.2023 | 18294 : Return reason copy from header  *
*              |            | DS4K944565                               *
*              |            |                                          *
*----------------------------------------------------------------------*
* MARTIRIA     | 23.05.2023 | 21545 & Copy the Order reason from       *
*              |            | Return reason in case of returns scenario*
*              |            | DS4K953184                               *
*----------------------------------------------------------------------*

    CONSTANTS: lc_target_table TYPE dbobj_name VALUE 'VBKD'.
    DATA: lt_hm_table TYPE zpe_tt_hm_table,
          ls_hm_table TYPE zpe_s_hm_table.

    DATA(lo_avoid_dn_and_inv_split) = zcl_sd_avoid_dn_and_inv_split=>get_instance( iv_vkorg = is_vbak-vkorg
                                                                                   iv_vtweg = is_vbak-vtweg
                                                                                   iv_auart = is_vbak-auart ).

    lo_avoid_dn_and_inv_split->copy_header_fields_to_item(
      EXPORTING
        iv_processing_mode = iv_processing_mode
        iv_posnr           = iv_posnr
        iv_target_table    = lc_target_table
        it_xvbkd           = it_xvbkd
      CHANGING
        cs_vbkd            = cs_vbkd ).


    "../To garantee that the return reason is equal to Header.
    IF ( iv_processing_mode EQ 'H' OR iv_processing_mode EQ 'V' ) AND  is_vbak-augru IS NOT INITIAL.

      TRY.
          lt_hm_table = zcl_pe_hm_basic=>get_table( iv_id = '1791' ).
          ls_hm_table = lt_hm_table[ 1 ].
        CATCH zcx_pe_hm. " Harcode Manager(
          RETURN.
      ENDTRY.

      LOOP AT ct_xvbap ASSIGNING FIELD-SYMBOL(<ls_xvbap>) WHERE msr_ret_reason IS  INITIAL. "#EC CI_STDSEQ
        <ls_xvbap>-msr_ret_reason = is_vbak-augru.
      ENDLOOP.

      "../To not remove any message where the MSR_RET_REASON is empty.
      LOOP AT ct_xvbap ASSIGNING <ls_xvbap> WHERE msr_ret_reason IS NOT INITIAL. "#EC CI_STDSEQ
        DATA(lv_index) = line_index( ct_xvbuv[ posnr = <ls_xvbap>-posnr "#EC CI_STDSEQ
                                               tbnam = ls_hm_table-field1
                                               fdnam = ls_hm_table-field2 ] ).
        IF lv_index > 0.
          DELETE ct_xvbuv INDEX lv_index.
        ENDIF.
        CLEAR lv_index.
      ENDLOOP.


    ENDIF.

  ENDMETHOD.


  METHOD map_to_sales_order_tables.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BERNAANT     |14.07.2022  | 12219 - Customer Order
*                                     Copy Control Enhancement
*                                     (BP to Sales Doc, Sales Doc to Sales Doc)
*              |            | DS4K924288
************************************************************************
* BERNAANT     |26.10.2022  | 14805 - Customer Order
*                                     Promo Enhancement Customer Order
*              |            | DS4K934115
************************************************************************
* BERNAANT     |02.11.2022  | 14959 - Customer Order
*                                     update FIELD
*              |            | DS4K934539
************************************************************************
    DATA(lo_cust_order) = NEW zcl_sd_customer_order( ).

    lo_cust_order->map_to_sales_order_tables(
      EXPORTING
        iv_vkorg             = iv_vkorg
        iv_vtweg             = iv_vtweg
        iv_auart             = iv_auart
        iv_destination_table = 'VBKD'
        iv_control_type      = '1'
        is_vbkd              = is_vbkd
      CHANGING
        cs_vbak              = cs_vbak
        cs_vbkd              = cs_vbkd
        ct_vbpa              = ct_vbpa ).
  ENDMETHOD.


  METHOD set_schedule_line_category.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* MUKHAMAK     | 24.01.2023 | 16863     : Customer Order - Change      *
*              |            | schedule  line  DS4K940281               *
*----------------------------------------------------------------------*

    IF is_yvbkd-podkz = cs_vbkd-podkz.
      RETURN.
    ENDIF.

    READ TABLE it_xvbap
        ASSIGNING FIELD-SYMBOL(<ls_xvbap>)
        WITH KEY posnr = iv_posnr.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    DATA(lo_set_schedule_line_category) = zcl_sd_set_sched_line_cat=>get_instance( ).
    lo_set_schedule_line_category->set_schedule_line_category(
      EXPORTING
        iv_podkz = cs_vbkd-podkz
        iv_werks = <ls_xvbap>-werks
        iv_matnr = <ls_xvbap>-matnr
        iv_pstyv = <ls_xvbap>-pstyv
        iv_posnr = iv_posnr
      CHANGING
        ct_vbep  = ct_xvbep
    ).

  ENDMETHOD.


  METHOD copy_return_timestamp.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DOLYADMI     | 26.06.2023 | 20730     : Feature - Development - O2C- *
*              |            | DS4K951058                               *
*----------------------------------------------------------------------*
* DOLYADMI     | 07.08.2023 | 24094     : [Investigation] - ShipTo par *
*              |            | DS4K960110                               *
*----------------------------------------------------------------------*
* DOLYADMI     | 10.08.2023 | 24273     : Feature - Development - O2C- *
*              |            | DS4K960694                               *
*----------------------------------------------------------------------*
    CONSTANTS: lc_varname TYPE /aif/variant_name VALUE 'CUST_RET_CONF_ITEM',
               lc_ifname  TYPE /aif/ifname VALUE 'CR_BULK_IN'.

    IF iv_posnr IS NOT INITIAL.
      SELECT SINGLE `X`
        FROM zsd_t_aif_var_so
        INTO @DATA(lv_bool)
        WHERE variant_name = @lc_varname
          AND ifname = @lc_ifname
          AND auart = @is_vbak-auart.
      CHECK lv_bool IS NOT INITIAL AND sy-subrc IS INITIAL.
      IF cs_vbkd-vbeln = is_vbak-vbeln AND
         cs_vbkd-posnr = iv_posnr.
        cs_vbkd-bstkd_e = shift_left( val = |{ is_vbak-ext_rev_tmstmp }| sub = space ).
      ENDIF.
    ELSE.
      cs_vbkd-bstkd_e = COND #( WHEN is_yvbkd-bstkd_e IS NOT INITIAL
                                 THEN is_yvbkd-bstkd_e
                                 ELSE cs_vbkd-bstkd_e ).
    ENDIF.
  ENDMETHOD.


  METHOD restore_gps_pricing_date.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* PISHCALI     | 17.11.2023 | 23900: SD_PO field change to SO          *
*              |            | DS4K959743                               *
*----------------------------------------------------------------------*

    " Restore the original Pricing Date value for SO documents of the GPS's type

    DATA: lr_so_doc_type TYPE zpe_tt_hm_range.

    TRY.
        lr_so_doc_type = zcl_pe_hm_basic=>get_range( iv_id = '2018' ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.

    IF is_vbak-auart NOT IN lr_so_doc_type.
      RETURN.
    ENDIF.

    IF iv_posnr IS INITIAL AND
      ( iv_processing_mode = cl_lord_co=>sc_create_mode OR
        iv_processing_mode = cl_lord_co=>sc_change_mode ).
      cs_vbkd-prsdt = is_original_vbkd-prsdt.
    ENDIF.

  ENDMETHOD.


  METHOD intake_interface_substitutions.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KURAPAND     | 29.11.2023 | 27185   :[Build] - CR - Update VBKD-BSTKD*
*              |            | DS4K972154                               *
*----------------------------------------------------------------------*
    DATA: ls_idoc_data  TYPE LINE OF idoc_data,
          ls_e1edk01_02 TYPE zsd_s_e1edk01_02.
    IF zcl_sd_order_intake_interface=>gv_intake_interface_processing = abap_true AND
       zcl_sd_order_intake_interface=>gv_mestyp IS INITIAL AND
       line_exists( zcl_sd_order_intake_interface=>gt_idoc_data[ segnam = 'ZSD_S_E1EDK01_02' ] ).
      CLEAR: ls_idoc_data.
      ls_idoc_data = zcl_sd_order_intake_interface=>gt_idoc_data[ segnam = 'ZSD_S_E1EDK01_02' ].
      ls_e1edk01_02 = ls_idoc_data-sdata.
      cs_vbkd-bstkd               = ls_e1edk01_02-zz1_bstkd_sdh.
    ENDIF.
  ENDMETHOD.


  METHOD set_purchace_order_item.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 27.11.2023 | 27185    : Feature- Defect- Test- Retur *
*              |            | DS4K972154                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 04.01.2023 | 25587    : SD_Feature [CR 1376] - Return Order  update  *
*              |            | DS4K965593                               *
*----------------------------------------------------------------------*
" ZHMAIVAD     | 29.01.2024 | 28123     : Feature- Defect- Test- Retur -
"              |            | DS4K977699                               -
"-----------------------------------------------------------------------
    CHECK zcl_sd_order_intake_interface=>gv_intake_interface_processing IS INITIAL
          AND is_vbak-vbtyp = 'H' and zcl_sd_return_data_provider=>gv_wms_call = abap_true.

    DATA(lt_buffer_soa_data) = zcl_sd_aif_returns_buffer=>get_instance(  )->get_buffer_data( ).

    LOOP AT ct_xvbap ASSIGNING FIELD-SYMBOL(<ls_vbap>).
      TRY.
          <ls_vbap>-posex = VALUE #( lt_buffer_soa_data[ salesdocument = <ls_vbap>-vbeln
                                                         material = <ls_vbap>-matnr
                                                         externalitemidnew = <ls_vbap>-ext_ref_item_id ]-externalitemidold ).
        CATCH cx_sy_itab_line_not_found.
          CONTINUE.
      ENDTRY.
    ENDLOOP.
  ENDMETHOD.


  METHOD meth_vbak_change.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* NAGARSOW     | 29.01.2024 | 28337    : Feature- Development - RDD heade *
*              |            | DS4K977099                               *
*----------------------------------------------------------------------*
    CONSTANTS : lc_we                TYPE char2 VALUE 'WE',
                lc_ag                TYPE char2 VALUE 'AG',
                lc_bookingin         TYPE char13 VALUE 'BOOKINGINDATE',
                lc_customer_dispatch TYPE char20 VALUE 'CUSTOMER_DISPATCH_LT'.
    DATA : lv_booking_dt            TYPE atinn,
           lv_customer_dt           TYPE atinn,
           lv_customer_dipatch_time TYPE atflv,
           lv_customer_dipatch_conv TYPE cha_class_view-sollwert.

    CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
      EXPORTING
        input  = lc_bookingin
      IMPORTING
        output = lv_booking_dt.

    CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
      EXPORTING
        input  = lc_customer_dispatch
      IMPORTING
        output = lv_customer_dt.

*...// Get Ship to party number
    DATA(lv_shipto) = VALUE #( it_xvbpa[  parvw = lc_we ]-kunnr OPTIONAL ).
    DATA(lv_soldto) = VALUE #( it_xvbpa[  parvw = lc_ag ]-kunnr OPTIONAL ).
*...// Ship to party
    IF lv_shipto IS NOT INITIAL.
      SELECT atinn,
             atwrt,
             atflv
             FROM ausp
             INTO TABLE @DATA(lt_atwrt)
             WHERE objek = @lv_shipto
             AND ( atinn = @lv_booking_dt
             OR    atinn = @lv_customer_dt ).
      IF sy-subrc IS INITIAL.
        DATA(lt_bookingindate) = lt_atwrt.
        DELETE lt_bookingindate WHERE atinn = lv_customer_dt.
      ENDIF.
    ENDIF.
*...// If Ship to party not there find Sold to Party
    IF lt_bookingindate IS INITIAL AND lv_soldto IS NOT INITIAL.
      SELECT atinn,
             atwrt,
             atflv
             FROM ausp INTO TABLE @lt_atwrt
             WHERE objek = @lv_soldto
             AND ( atinn = @lv_booking_dt
             OR    atinn = @lv_customer_dt ) .
      IF sy-subrc IS INITIAL.
        lt_bookingindate = lt_atwrt.
        DELETE lt_bookingindate WHERE atinn = lv_customer_dt.
      ENDIF.
    ENDIF.

*...// Update Requested Date header
*...// Get the Customer Dispatch Lead Time

    IF  ( cs_vbak-vbeln IS NOT INITIAL  AND zcl_sd_move_vbak_cusflds=>gv_mass_change IS NOT INITIAL )
     OR ( cs_vbak-vbeln IS INITIAL ).
      DATA(lt_cdlt) = lt_atwrt.
      DELETE lt_cdlt WHERE   atinn = lv_booking_dt.

      IF lt_cdlt IS NOT INITIAL AND cs_vbak-zz1_rsd_head IS NOT INITIAL.
        lv_customer_dipatch_time =  lt_cdlt[ 1 ]-atflv .
        CALL FUNCTION 'QSS0_FLTP_TO_CHAR_CONVERSION'
          EXPORTING
            i_number_of_digits = 0
            i_fltp_value       = lv_customer_dipatch_time
          IMPORTING
            e_char_field       = lv_customer_dipatch_conv.
        cs_vbak-vdatu = cs_vbak-zz1_rsd_head - lv_customer_dipatch_conv.
      ELSEIF lt_cdlt IS INITIAL AND cs_vbak-zz1_rsd_head IS NOT INITIAL.
        cs_vbak-vdatu = cs_vbak-zz1_rsd_head.
      ENDIF.
    ENDIF.

  ENDMETHOD.
ENDCLASS.