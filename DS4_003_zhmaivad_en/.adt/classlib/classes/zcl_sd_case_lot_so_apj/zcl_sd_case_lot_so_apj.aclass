CLASS zcl_sd_case_lot_so_apj DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_apj_dt_exec_object .
    INTERFACES if_apj_rt_exec_object .
  PROTECTED SECTION.
  PRIVATE SECTION.
    DATA:
      mt_message         TYPE    bapiret2_t,
      mt_case_lot_excel  TYPE zif_sd_case_lot_so_apj=>gtyp_t_case_lot_excel,
      mt_case_lot_output TYPE zif_sd_case_lot_so_apj=>gtyp_t_case_lot_output.

    DATA:
      mr_email               TYPE RANGE OF ad_smtpadr,
      mr_vbeln_va            TYPE RANGE OF vbeln_va,
      mr_werks_d             TYPE RANGE OF werks_d,
      mr_edatu_vbak          TYPE RANGE OF edatu_vbak,
      mr_matnr_g             TYPE RANGE OF matnr,
      mr_matnr_a             TYPE RANGE OF matnr,
      mr_kunag               TYPE RANGE OF kunag,
      mr_kunwe               TYPE RANGE OF kunwe,
      mr_auart               TYPE RANGE OF auart,
      mr_vkorg               TYPE RANGE OF vkorg,
      mr_vtweg               TYPE RANGE OF vtweg,
      mr_pstyv               TYPE RANGE OF pstyv,
      mr_zsd_caselot_pkmode  TYPE RANGE OF zsd_caselot_pkmode,
      mr_zsd_cl_status       TYPE RANGE OF zsd_cl_status,
      mr_abp_creation_tstmpl TYPE RANGE OF abp_lastchange_tstmpl,
      mr_abp_creation_user   TYPE RANGE OF abp_creation_user.

    METHODS get_parameters_values
      IMPORTING
        !it_parameters TYPE if_apj_dt_exec_object=>tt_templ_val .
    METHODS convert_date_to_tstmpl
      IMPORTING
        !iv_date         TYPE edatu
      RETURNING
        VALUE(rv_tstmpl) TYPE abp_lastchange_tstmpl.
    METHODS execute.
    METHODS save_application_log.
    METHODS read_hardcode_values.
    METHODS fetch_data.
    METHODS build_xslx
      EXPORTING
        !et_xls_attachm           TYPE solix_tab
        VALUE(ev_attachment_size) TYPE i.
    METHODS send_email
      IMPORTING
        !it_xls_attachm     TYPE solix_tab
        !iv_attachment_size TYPE i.
ENDCLASS.



CLASS ZCL_SD_CASE_LOT_SO_APJ IMPLEMENTATION.


  METHOD if_apj_rt_exec_object~execute.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    get_parameters_values( it_parameters ).

    execute( ).

    save_application_log( ).
  ENDMETHOD.


  METHOD if_apj_dt_exec_object~get_parameters.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*
    et_parameter_def = VALUE #( ##TYPE
          ( selname = 'S_EMAIL' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'AD_SMTPADR' length = 241 param_text = 'Email IDs'(001)  mandatory_ind = abap_true  changeable_ind = abap_true )
          ( selname = 'S_VBELN' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'VBELN_VA' length = 10 param_text = 'Sales Order'(002) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_WERKS' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'WERKS_D' length = 4 param_text = 'Plant'(003) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_EDATU' kind = if_apj_dt_exec_object=>select_option  datatype = 'D' component_type = 'EDATU_VBAK' length = 8 param_text = 'Requested Delivery Date'(004) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_MAT_G' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'MATNR' length = 40 param_text = 'Generic Article'(005) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_MAT_A' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'MATNR' length = 40 param_text = 'Variant Article'(006) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_KUNNR' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'KUNAG' length = 10 param_text = 'Sold To Party'(007) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_KUNWE' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'KUNWE' length = 10 param_text = 'Ship To Party'(008) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_AUART' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'AUART' length = 4 param_text = 'Sales Order Type'(009) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_VKORG' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'VKORG' length = 4 param_text = 'Sales Organization'(010) mandatory_ind = abap_true  changeable_ind = abap_true )
          ( selname = 'S_VTWEG' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'VTWEG' length = 2 param_text = 'Distribution Channel'(011) mandatory_ind = abap_true  changeable_ind = abap_true )
          ( selname = 'S_PSTYV' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'PSTYV' length = 4 param_text = 'Item Category'(012) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_PKMOD' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'ZSD_CASELOT_PKMODE' length = 4 param_text = 'Packing Type'(013) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_STATU' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'ZSD_CL_STATUS' length = 1 param_text = 'Case Lot Status'(014) mandatory_ind = abap_false  changeable_ind = abap_true )
          ( selname = 'S_CREAT' kind = if_apj_dt_exec_object=>select_option  datatype = 'D' component_type = 'ABP_LASTCHANGE_TSTMPL' length = 8 param_text = 'Creation Date'(015) mandatory_ind = abap_true  changeable_ind = abap_true )
          ( selname = 'S_CREBY' kind = if_apj_dt_exec_object=>select_option  datatype = 'C' component_type = 'ABP_CREATION_USER' length = 12 param_text = 'Created By'(016) mandatory_ind = abap_false  changeable_ind = abap_true )
        ).
  ENDMETHOD.


  METHOD get_parameters_values.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    CLEAR:
      mr_email,
      mr_vbeln_va,
      mr_werks_d,
      mr_edatu_vbak,
      mr_matnr_g,
      mr_matnr_a,
      mr_kunag,
      mr_kunwe,
      mr_auart,
      mr_vkorg,
      mr_vtweg,
      mr_pstyv,
      mr_zsd_caselot_pkmode,
      mr_zsd_cl_status,
      mr_abp_creation_tstmpl,
      mr_abp_creation_user.

    LOOP AT it_parameters INTO DATA(ls_parameter).
      CASE ls_parameter-selname.
        WHEN 'S_EMAIL'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_email.
        WHEN 'S_VBELN'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_vbeln_va.
        WHEN 'S_WERKS'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_werks_d.
        WHEN 'S_EDATU'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_edatu_vbak.
        WHEN 'S_MAT_G'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_matnr_g.
        WHEN 'S_MAT_A'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_matnr_a.
        WHEN 'S_KUNNR'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_kunag.
        WHEN 'S_KUNWE'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_kunwe.
        WHEN 'S_AUART'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_auart.
        WHEN 'S_VKORG'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_vkorg.
        WHEN 'S_VTWEG'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_vtweg.
        WHEN 'S_PSTYV'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_pstyv.
        WHEN 'S_PKMOD'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_zsd_caselot_pkmode.
        WHEN 'S_STATU'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_zsd_cl_status.
        WHEN 'S_CREAT'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = convert_date_to_tstmpl( CONV #( ls_parameter-low ) )
                          high   = convert_date_to_tstmpl( CONV #( ls_parameter-high ) ) ) TO mr_abp_creation_tstmpl.
        WHEN 'S_CREBY'.
          APPEND VALUE #( sign   = ls_parameter-sign
                          option = ls_parameter-option
                          low    = ls_parameter-low
                          high   = ls_parameter-high ) TO mr_abp_creation_user.
      ENDCASE.
    ENDLOOP.
  ENDMETHOD.


  METHOD build_xslx.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    CONSTANTS:
      lc_title_font_size TYPE zexcel_style_font_size VALUE 13,
      lc_font_size       TYPE zexcel_style_font_size VALUE 11.

    DATA: lo_excel              TYPE REF TO zcl_excel,
          lo_worksheet          TYPE REF TO zcl_excel_worksheet,
          lo_column             TYPE REF TO zcl_excel_column,
          lo_style_hdr          TYPE REF TO zcl_excel_style,
          lo_style_tab_hdr      TYPE REF TO zcl_excel_style,
          lo_style_tab_itm      TYPE REF TO zcl_excel_style,
          lo_style_title        TYPE REF TO zcl_excel_style,
          lv_style_hdr_guid     TYPE zexcel_cell_style,
          lv_style_tab_itm_guid TYPE zexcel_cell_style,
          lv_style_tab_hdr_guid TYPE zexcel_cell_style,
          lv_style_title_guid   TYPE zexcel_cell_style.
    DATA: lv_itm_row TYPE i,
          lv_content TYPE xstring.

    CLEAR et_xls_attachm.

    TRY.
        DATA(lo_border_dark_thin) = NEW zcl_excel_style_border( ).
        lo_border_dark_thin->border_color-rgb = zcl_excel_style_color=>c_black.
        lo_border_dark_thin->border_style   = zcl_excel_style_border=>c_border_thin.

        DATA(lo_alignment) = NEW zcl_excel_style_alignment( ).
        lo_alignment->horizontal   = zcl_excel_style_alignment=>c_horizontal_center.
        lo_alignment->vertical     = zcl_excel_style_alignment=>c_vertical_center.
        lo_alignment->wraptext     = abap_true.
        lo_alignment->shrinktofit  = abap_true.

        lo_excel = NEW #( ).

        "Create a title style
        lo_style_title                      = lo_excel->add_new_style( ).
        lo_style_title->font->name          = zcl_excel_style_font=>c_name_calibri.
        lo_style_title->font->scheme        = zcl_excel_style_font=>c_scheme_none.
        lo_style_title->font->underline     = abap_false.
        lo_style_title->font->size          = lc_title_font_size.
        lo_style_title->font->bold          = abap_true.
        lo_style_title->borders->right      = lo_border_dark_thin.
        lo_style_title->fill->bgcolor-rgb   = zcl_excel_style_color=>c_blue.
        lo_style_title->alignment           = lo_alignment.
        lv_style_title_guid                 = lo_style_title->get_guid( ).

        "Create a header style
        lo_style_hdr                      = lo_excel->add_new_style( ).
        lo_style_hdr->font->name          = zcl_excel_style_font=>c_name_calibri.
        lo_style_hdr->font->scheme        = zcl_excel_style_font=>c_scheme_none.
        lo_style_hdr->font->underline     = abap_false.
        lo_style_hdr->font->size          = lc_font_size.
        lv_style_hdr_guid                 = lo_style_hdr->get_guid( ).

        "Create a Table header style
        lo_style_tab_hdr                      = lo_excel->add_new_style( ).
        lo_style_tab_hdr->font->name          = zcl_excel_style_font=>c_name_calibri.
        lo_style_tab_hdr->font->scheme        = zcl_excel_style_font=>c_scheme_none.
        lo_style_tab_hdr->font->bold          = abap_true.
        lo_style_tab_hdr->font->size          = lc_font_size.
        lv_style_tab_hdr_guid                 = lo_style_tab_hdr->get_guid( ).

        "Create a Table item style
        lo_style_tab_itm                      = lo_excel->add_new_style( ).
        lo_style_tab_itm->font->name          = zcl_excel_style_font=>c_name_calibri.
        lo_style_tab_itm->font->scheme        = zcl_excel_style_font=>c_scheme_none.
        lo_style_tab_itm->font->size          = lc_font_size.
        lv_style_tab_itm_guid                 = lo_style_tab_itm->get_guid( ).

        lo_excel->set_default_style( lv_style_hdr_guid ).  " Default for all new worksheets

        lo_worksheet = lo_excel->get_active_worksheet( ).
        lo_worksheet->set_area( ip_row = 1 ip_row_to = 1 ip_column_start = 'A' ip_column_end = 'O' ip_style = lv_style_title_guid ip_value = 'Header Data'(032) ip_merge = abap_true ).
        lo_worksheet->set_area( ip_row = 1 ip_row_to = 1 ip_column_start = 'P' ip_column_end = 'T' ip_style = lv_style_title_guid ip_value = 'Item Data'(033) ip_merge = abap_true ).
        lo_worksheet->set_title( 'Sheet1' ) ##NO_TEXT.

        "Build Case Lot table Header
        LOOP AT mt_case_lot_excel ASSIGNING FIELD-SYMBOL(<ls_excel>).
          lo_worksheet->set_cell( ip_column = <ls_excel>-output_column
                                  ip_row    = <ls_excel>-output_row
                                  ip_value  = <ls_excel>-text
                                  ip_style  = lv_style_tab_hdr_guid ).
          lo_column = lo_worksheet->get_column( <ls_excel>-output_column ).
          lo_column->set_auto_size( ip_auto_size = abap_true ).
          IF lv_itm_row IS INITIAL.
            lv_itm_row = <ls_excel>-output_row.
          ENDIF.
        ENDLOOP.

        "Build Case Lot table Body
        LOOP AT mt_case_lot_output ASSIGNING FIELD-SYMBOL(<ls_output>).
          lv_itm_row = lv_itm_row + 1.

          LOOP AT mt_case_lot_excel ASSIGNING <ls_excel>. "#EC CI_NESTED
            ASSIGN COMPONENT <ls_excel>-fieldname OF STRUCTURE <ls_output> TO FIELD-SYMBOL(<lv_fvalue>).
            IF sy-subrc IS INITIAL.
              lo_worksheet->set_cell( ip_column = <ls_excel>-output_column
                                      ip_row    = lv_itm_row
                                      ip_value  = <lv_fvalue>
                                      ip_style  = lv_style_tab_itm_guid ).
            ENDIF.
          ENDLOOP.
        ENDLOOP.

        lo_worksheet->calculate_column_widths( ).
        "Get content of xls
        DATA(lo_excel_writer)  = NEW zcl_excel_writer_2007( ).

        lv_content = lo_excel_writer->zif_excel_writer~write_file( lo_excel ).
        et_xls_attachm = cl_bcs_convert=>xstring_to_solix( lv_content ).
        ev_attachment_size    = xstrlen( lv_content ).

      CATCH zcx_excel.
        MESSAGE e012(zsd_caselot) INTO DATA(lv_message).
        APPEND VALUE #( message = lv_message
                        id = sy-msgid
                        type = sy-msgty
                        number = sy-msgno ) TO mt_message.
    ENDTRY.
  ENDMETHOD.


  METHOD execute.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    read_hardcode_values( ).

    fetch_data( ).

    build_xslx( IMPORTING et_xls_attachm     = DATA(lt_xls_attachm)
                          ev_attachment_size = DATA(lv_attachment_size) ).

    send_email( it_xls_attachm     = lt_xls_attachm
                iv_attachment_size = lv_attachment_size ).

    save_application_log( ).
  ENDMETHOD.


  METHOD fetch_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*
    SELECT
     cl_header~vbeln AS so_header,
     cl_header~cl_status,
     cl_header~caselot_no,
     cl_header~caselot_text,
     cl_header~matnr AS matnr_g,
     cl_header~werks AS plant,
     cl_header~edatu AS requesteddeliverydate,
     cl_header~caselot_total_kwmeng AS totalorderqty,
     cl_header~caselot_total_kbmeng AS   totalconfirmedqty,
     cl_header~ebeln,
     cl_header~caselot_pkmode,
     cl_header~no_maspb_ctn,
     cl_header~no_caselot_units,
     cl_header~no_caselot_cnt,
     cl_header~total_qty_ctn,
     cl_item~caselot_item,
     cl_item~matnr AS matnr_a,
     cl_item~uepos,
     cl_item~posnr AS so_item,
     cl_item~etenr,
     cl_item~edatu,
     cl_item~kwmeng,
     cl_item~caselot_cl_pb
   FROM  zsd_t_case_lot_h AS cl_header
   LEFT OUTER JOIN zsd_t_case_lot_i AS cl_item ON cl_header~caseloth_uuid = cl_item~caseloth_uuid
     WHERE cl_header~vbeln IN @mr_vbeln_va
     AND cl_header~werks IN @mr_werks_d
     AND cl_header~edatu IN @mr_edatu_vbak
     AND cl_header~matnr IN @mr_matnr_g
     AND cl_item~matnr IN @mr_matnr_a
     AND cl_header~caselot_pkmode IN @mr_zsd_caselot_pkmode
     AND cl_header~cl_status IN @mr_zsd_cl_status
     AND cl_header~created_at IN @mr_abp_creation_tstmpl
     AND cl_header~created_by IN @mr_abp_creation_user
     ORDER BY cl_header~vbeln, cl_item~posnr ASCENDING
     INTO TABLE @DATA(lt_case_lot_db).
    IF sy-subrc <> 0.
      MESSAGE e009(zsd_caselot) INTO DATA(lv_message).
      APPEND VALUE #( message = lv_message
                      id = sy-msgid
                      type = sy-msgty
                      number = sy-msgno ) TO mt_message.
      RETURN.
    ENDIF.

    IF lt_case_lot_db IS NOT INITIAL.
      SELECT
        so_header~salesdocument AS so_header,
        so_item~salesdocumentitem AS so_item
      FROM i_salesdocument AS so_header
      LEFT OUTER JOIN i_salesdocumentitem AS so_item ON so_header~salesdocument = so_item~salesdocument
      FOR ALL ENTRIES IN @lt_case_lot_db
      WHERE so_header~salesdocument = @lt_case_lot_db-so_header
        AND so_item~salesdocumentitem = @lt_case_lot_db-so_item
        AND so_header~soldtoparty IN @mr_kunag
        AND so_header~salesdocumenttype IN @mr_auart
        AND so_header~salesorganization IN @mr_vkorg
        AND so_header~distributionchannel IN @mr_vtweg
        AND so_item~shiptoparty IN @mr_kunwe
        AND so_item~salesdocumentitemcategory IN @mr_pstyv
        INTO TABLE @DATA(lt_sales_order_db). "#EC CI_NO_TRANSFORM "#EC CI_SUBRC
    ENDIF.

    IF lines( lt_sales_order_db ) <> lines( lt_case_lot_db ).
      LOOP AT lt_case_lot_db ASSIGNING FIELD-SYMBOL(<ls_case_lot_db>).
        IF line_exists( lt_sales_order_db[ so_header = <ls_case_lot_db>-so_header so_item = <ls_case_lot_db>-so_item ] ).
          APPEND CORRESPONDING #( <ls_case_lot_db> ) TO mt_case_lot_output.
        ENDIF.
      ENDLOOP.
    ELSE.
      mt_case_lot_output = CORRESPONDING #( lt_case_lot_db ).
    ENDIF.
  ENDMETHOD.


  METHOD save_application_log.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    TRY.
        DATA(lif_log) = cl_bali_log=>create_with_header( cl_bali_header_setter=>create( object = zif_sd_case_lot_so_apj=>gc_appl_log-object
                                                                                        subobject = zif_sd_case_lot_so_apj=>gc_appl_log-subobject ) ).
        LOOP AT mt_message ASSIGNING FIELD-SYMBOL(<ls_message>).
          DATA(lif_text) = cl_bali_message_setter=>create( severity   = if_bali_constants=>c_severity_error
                                                           id         = <ls_message>-id
                                                           number     = <ls_message>-number
                                                           variable_1 = <ls_message>-message_v1
                                                           variable_2 = <ls_message>-message_v2
                                                           variable_3 = <ls_message>-message_v3
                                                           variable_4 = <ls_message>-message_v4 ).
          lif_log->add_item( item = lif_text ).
        ENDLOOP.
        IF sy-subrc <> 0.
          MESSAGE s010(zsd_caselot) INTO DATA(lv_message) ##NEEDED.
          lif_text = cl_bali_message_setter=>create( severity = if_bali_constants=>c_severity_status
                                                     id       = sy-msgid
                                                     number   = sy-msgno ).
          lif_log->add_item( item = lif_text ).
        ENDIF.
        cl_bali_log_db=>get_instance( )->save_log( log = lif_log
                                                   assign_to_current_appl_job = abap_true ).
        COMMIT WORK.
      CATCH cx_bali_runtime.
        MESSAGE e011(zsd_caselot).
    ENDTRY.
  ENDMETHOD.


  METHOD send_email.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    DATA: lo_send_request    TYPE REF TO cl_bcs,
          lo_document        TYPE REF TO cl_document_bcs,
          lif_recipient      TYPE REF TO if_recipient_bcs,
          lo_sender          TYPE REF TO cl_sapuser_bcs,
          lv_attachment_name TYPE sood-objdes,
          lv_subject         TYPE so_obj_des,
          lt_body            TYPE soli_tab,
          lv_file_name       TYPE string,
          lv_email_subject   TYPE string,
          lv_datetime        TYPE timestamp.

    TRY.
        "Create send request
        lo_send_request = cl_bcs=>create_persistent( ).

        "Sender
        lo_sender = cl_sapuser_bcs=>create( sy-uname ).
        lo_send_request->set_sender( lo_sender ).

        "Recipients
        LOOP AT mr_email ASSIGNING FIELD-SYMBOL(<ls_email>).
          lif_recipient = cl_cam_address_bcs=>create_internet_address( <ls_email>-low ).
          lo_send_request->add_recipient( i_recipient = lif_recipient i_express = abap_true ).
        ENDLOOP.

        GET TIME STAMP FIELD lv_datetime.
        "Email details
        lv_file_name = 'Excel download of Case Lot - Sales Order List'(034).
        lv_email_subject = 'Case Lot Report - '(035).
        lv_subject = |{ lv_email_subject }| && | { lv_datetime TIMEZONE = sy-zonlo }|.
        lt_body = VALUE #( ( line = lv_subject ) ).
        lv_attachment_name = |{ lv_file_name }| & |.xlsx|.

        lo_document = cl_document_bcs=>create_document(
          i_type    = 'RAW'
          i_text    = lt_body
          i_subject = lv_subject ).

        lo_document->add_attachment(
          i_attachment_type    = 'EXT'
          i_attachment_subject = lv_attachment_name
          i_attachment_size    = CONV #( iv_attachment_size )
          i_att_content_hex    = it_xls_attachm
        ).

        "Add document to send request
        lo_send_request->set_document( lo_document ).
        "Set send immediately
        lo_send_request->set_send_immediately( abap_true ).

        IF lo_send_request->send( abap_true ) = abap_false.
          MESSAGE e012(zsd_caselot) INTO DATA(lv_message).
          APPEND VALUE #( message = lv_message
                          id = sy-msgid
                          type = sy-msgty
                          number = sy-msgno ) TO mt_message.
        ENDIF.

        COMMIT WORK.

      CATCH cx_bcs.
        MESSAGE e012(zsd_caselot) INTO lv_message.
        APPEND VALUE #( message = lv_message
                        id = sy-msgid
                        type = sy-msgty
                        number = sy-msgno ) TO mt_message.
    ENDTRY.
  ENDMETHOD.


  METHOD read_hardcode_values.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    DATA: lt_hm_table TYPE zpe_tt_hm_table.

    TRY.
        lt_hm_table = zcl_pe_hm_basic=>get_table( '2953' ).
      CATCH zcx_pe_hm INTO DATA(lo_exc).
        MESSAGE lo_exc.
    ENDTRY.

    LOOP AT lt_hm_table ASSIGNING FIELD-SYMBOL(<ls_hm_table>).
      CASE <ls_hm_table>-field1.
        WHEN 'SO_HEADER'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Sales Order'(002)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'CL_STATUS'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Case Lot Status'(014)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'CASELOT_NO'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Case Lot No'(017)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'CASELOT_TEXT'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Case Lot Text'(018)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'MATNR_G'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Generic Article'(005)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'PLANT'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Plant'(003)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'REQUESTEDDELIVERYDATE'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Req.Dlv.Date'(019)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'TOTALORDERQTY'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Total Qty'(020)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'TOTALCONFIRMEDQTY'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Total Confirmed Qty'(021)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'EBELN'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Purchasing Document'(022)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'CASELOT_PKMODE'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Packing Mode'(023)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'NO_MASPB_CTN'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Number of Polybags per Case Lot'(024)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'NO_CASELOT_UNITS'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Number of Units per Case lot/Polybag'(025)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'NO_CASELOT_CNT'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Number of Case Lots'(026)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'TOTAL_QTY_CTN'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Total Quantity per Case Lot'(027)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'CASELOT_ITEM'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Case Lot Item'(028)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'MATNR_A'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Variant Article'(006)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'UEPOS'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Higher Level Item'(029)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'SO_ITEM'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Sales order Item'(030)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'ETENR'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Schedule line'(031)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'EDATU'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Schedule Line Date'(036)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'KWMENG'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Schedule Line Qty'(037)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN 'CASELOT_CL_PB'.
          APPEND VALUE #(
           fieldname = <ls_hm_table>-field1
           text = 'Size Split per Case Lot/ Polybag'(038)
           output_column = <ls_hm_table>-field2
           output_row = <ls_hm_table>-field3 ) TO mt_case_lot_excel.
        WHEN OTHERS. "Nothing to do
      ENDCASE.
    ENDLOOP.
  ENDMETHOD.


  METHOD convert_date_to_tstmpl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 31.01.2024 | 28074 : [Feature] Case Lot- Schedule Job for SO *
*              |            | DS4K976334                               *
*----------------------------------------------------------------------*

    CONVERT DATE iv_date INTO TIME STAMP rv_tstmpl TIME ZONE sy-zonlo .
  ENDMETHOD.
ENDCLASS.