class ZCL_SD_DELIVERY_EVENTS_UPDATE definition
  public
  final
  create public .

public section.

  types:
    BEGIN OF gtyp_data,
        delivery_note     TYPE vbeln_vl,
        event_type        TYPE tsegevttyp,
        begin_plan_date   TYPE d,
        begin_plan_time   TYPE t,
        end_plan_date     TYPE d,
        end_plan_time     TYPE t,
        begin_actual_date TYPE d,
        begin_actual_time TYPE t,
        end_actual_date   TYPE d,
        end_actual_time   TYPE t,
        actual_time_zone  TYPE systzonlo,
        plan_time_zone    TYPE systzonlo,
        reason            TYPE tsegws01-vstga,
        event_text        TYPE char25,
      END OF gtyp_data .
  types:
    gtyp_t_data TYPE STANDARD TABLE OF gtyp_data WITH EMPTY KEY .

  class-methods PROCESS
    importing
      !IV_TEST type ABAP_BOOL optional
      !IV_COMMIT type ABAP_BOOL default ABAP_TRUE
      !IT_DATA type GTYP_T_DATA
    changing
      !CT_RETURN type BAPIRET2_T .
  PROTECTED SECTION.
private section.

  types:
    BEGIN OF gtyp_s_zhm_evnt_cfg,
      zsd_dlv_event     TYPE zsd_dlv_event,
      zsd_dlv_event_des TYPE zsd_dlv_event_des,
      zsd_evkorg        TYPE zsd_evkorg,
      zsd_trigger_dn    TYPE zsd_trigger_dn,
      zsd_trigger_edf   TYPE zsd_trigger_edf,
    END OF gtyp_s_zhm_evnt_cfg .
  types:
    BEGIN OF gtyp_s_update_control,
      actual_begin_date TYPE abap_bool,
      actual_end_date   TYPE abap_bool,
      actual_time_zone  TYPE abap_bool,
      actual_reason     TYPE abap_bool,
      actual_event_text TYPE abap_bool,
      plan_begin_date   TYPE abap_bool,
      plan_end_date     TYPE abap_bool,
      plan_time_zone    TYPE abap_bool,
      plan_reason       TYPE abap_bool,
      plan_event_text   TYPE abap_bool,
    END OF gtyp_s_update_control .
  types:
    BEGIN OF gtyp_event,
      even       TYPE ttsegevty-even,
      even_timfr TYPE ttsegevty-even_timfr,
      even_timto TYPE ttsegevty-even_timto,
    END OF gtyp_event .
  types:
    gtyp_ts_zhm_evnt_cfg TYPE SORTED TABLE OF gtyp_s_zhm_evnt_cfg WITH NON-UNIQUE KEY zsd_dlv_event .

  constants GC_OBJECT type TSEGOBJHDR value 'WSHDRLIKP' ##NO_TEXT.
  constants:
    BEGIN OF gc_s_vertyp,
      plan   TYPE tsegvertyp VALUE '0' ##NO_TEXT,
      actual TYPE tsegvertyp VALUE '1' ##NO_TEXT,
    END OF gc_s_vertyp .
  class-data:
    gth_delivery_headers   TYPE HASHED TABLE OF zsd_i_delivery_header_01 WITH UNIQUE KEY vbeln .
  class-data:
    gt_events        TYPE TABLE OF tsege .
  class-data:
    gth_evtype       TYPE HASHED TABLE OF gtyp_event WITH UNIQUE KEY even .
  class-data GTS_ZHM_EVNT_CFG type GTYP_TS_ZHM_EVNT_CFG .
  class-data GV_REFU_EVENT type TSEGEVTTYP .

  class-methods INIT_ADDITIONAL_DATA
    importing
      !IT_DATA type GTYP_T_DATA .
  class-methods GET_ZHM_EVENTS_CONFIG .
  class-methods GET_EVENT_UPDATE_CONTROL
    importing
      !IV_HANDLE type TSEGGUID_LIKP
      !IV_EVENT_TYPE type TSEGEVTTYP
      !IS_TIMELINE type GTYP_DATA
    returning
      value(RS_UPDATE_CONTROL) type GTYP_S_UPDATE_CONTROL .
  class-methods CHECK_DELIVERY
    importing
      !IV_VBELN type VBELN_VL
    exporting
      !ES_DELIVERY_HEADER type ZSD_I_DELIVERY_HEADER_01
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods TSEG_INIT
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods TSEG_READ
    importing
      !IS_DELIVERY_HEADER type ZSD_I_DELIVERY_HEADER_01
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods TSEG_UPDATE
    importing
      !IS_DELIVERY_HEADER type ZSD_I_DELIVERY_HEADER_01
      !IS_TIMELINE type GTYP_DATA
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods CONVERT_TO_TIMESTAMP
    importing
      !IV_DATE like SY-DATLO
      !IV_TIME like SY-TIMLO
      !IV_ZONE type SY-ZONLO
    returning
      value(RV_RESULT) type TSEGTIMTST .
  class-methods TIMESTAMP_UPDATE
    importing
      !IV_EVENT type TSEGEVTTYP
      !IV_HANDLE type TSEGGUID_LIKP
      !IV_TIME_TYPE type TSEGTIMTYP
      !IV_EVENTYP type TSEGVERTYP
      !IV_TIME type TSEGTIMTST
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods ZONE_UPDATE
    importing
      !IV_EVENT type TSEGEVTTYP
      !IS_DELIVERY_HEADER type ZSD_I_DELIVERY_HEADER_01
      !IV_TIME_TYPE type TSEGTIMTYP
      !IV_EVENTYP type TSEGVERTYP
      !IV_TZONE type AD_TZONE
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods TSEG_COMMIT
    importing
      !IV_TEST type ABAP_BOOL
      !IV_COMMIT type ABAP_BOOL
      !IS_DELIVERY_HEADER type ZSD_I_DELIVERY_HEADER_01
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods CHECK_SEGMENT
    importing
      !IV_EVENT type TSEGEVTTYP
    exporting
      !EV_TIME_TYPE_FROM type TSEGTIMEFR
      !EV_TIME_TYPE_TO type TSEGTIMETO
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods GET_EVENT
    importing
      !IV_EVENT type TSEGEVTTYP
      !IV_HANDLE type TSEGGUID_LIKP
      !IV_EVENTYP type TSEGVERTYP
    exporting
      !ES_EVENT type TSEGE
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods REASON_UPDATE
    importing
      !IV_EVENT type TSEGEVTTYP
      !IV_HANDLE type TSEGGUID_LIKP
      !IV_EVENTYP type TSEGVERTYP
      !IV_REASON type TSEGWS01-VSTGA
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods GET_DEFAULT_TZONE
    importing
      !IV_VSTEL type VSTEL
    returning
      value(RV_RESULT) type SYSTZONLO .
  class-methods EVENT_TEXT_UPDATE
    importing
      !IV_EVENT type TSEGEVTTYP
      !IV_HANDLE type TSEGGUID_LIKP
      !IV_EVENTYP type TSEGVERTYP
      !IV_EVENT_TEXT type CHAR25
    changing
      !CT_RETURN type BAPIRET2_T .
  class-methods GET_LOCAL_TZONE
    returning
      value(RV_RESULT) type SYSTZONLO .
ENDCLASS.



CLASS ZCL_SD_DELIVERY_EVENTS_UPDATE IMPLEMENTATION.


  METHOD process.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* KARPOSER     | 04.08.2022 | 12191 : DN Events Update                 *
*              |            | DS4K924254                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*
    DATA: lt_return_local TYPE bapiret2_t.

    IF it_data IS INITIAL.
      MESSAGE e001 INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
      RETURN.
    ENDIF.

    init_additional_data( it_data ).

    LOOP AT it_data ASSIGNING FIELD-SYMBOL(<ls_grp>)
     GROUP BY ( delivery_note = <ls_grp>-delivery_note ) ASSIGNING FIELD-SYMBOL(<ls_data_grp>).

      CLEAR lt_return_local.

      DO 1 TIMES.  "#EC CI_NESTED
        check_delivery(
          EXPORTING
            iv_vbeln           = <ls_data_grp>-delivery_note
          IMPORTING
            es_delivery_header = DATA(ls_delivery_header)
          CHANGING
            ct_return          = lt_return_local ).
        CHECK cl_bapi_msg=>contains_error(  lt_return_local ) = abap_false.

        tseg_init(
          CHANGING
            ct_return = ct_return ).
        CHECK cl_bapi_msg=>contains_error(  lt_return_local ) = abap_false.

        tseg_read(
          EXPORTING
            is_delivery_header = ls_delivery_header
          CHANGING
            ct_return          = lt_return_local ).
        CHECK cl_bapi_msg=>contains_error( lt_return_local ) = abap_false.

        LOOP AT GROUP <ls_data_grp> ASSIGNING FIELD-SYMBOL(<ls_data>).  "#EC CI_NESTED

          tseg_update(
            EXPORTING
              is_delivery_header = ls_delivery_header
              is_timeline        = <ls_data>
            CHANGING
              ct_return          = lt_return_local ).
        ENDLOOP.
      ENDDO.

      tseg_commit(
        EXPORTING
          iv_test            = iv_test
          iv_commit          = iv_commit
          is_delivery_header = ls_delivery_header
        CHANGING
          ct_return          = lt_return_local ).

      APPEND LINES OF lt_return_local TO ct_return.
    ENDLOOP.
  ENDMETHOD.


  METHOD init_additional_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 28.02.2023 | 18274     : [Build] - TMS Milestone      *
*              |            | DS4K944534                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*

    DATA: lts_vbeln TYPE SORTED TABLE OF vbeln WITH UNIQUE KEY table_line,
          lts_event TYPE SORTED TABLE OF tsegevttyp WITH UNIQUE KEY table_line.

    LOOP AT it_data ASSIGNING FIELD-SYMBOL(<ls_data>)
     WHERE delivery_note IS NOT INITIAL.                 "#EC CI_STDSEQ

      INSERT <ls_data>-delivery_note INTO TABLE lts_vbeln.
      INSERT <ls_data>-event_type INTO TABLE lts_event.
    ENDLOOP.

    IF lts_vbeln IS INITIAL OR lts_event IS INITIAL.
      RETURN.
    ENDIF.

    SELECT *
      FROM zsd_i_delivery_header_01
        INTO TABLE @gth_delivery_headers
          FOR ALL ENTRIES IN @lts_vbeln
            WHERE vbeln = @lts_vbeln-table_line.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    SELECT * FROM tsege INTO TABLE @gt_events      "#EC CI_NO_TRANSFORM
      FOR ALL ENTRIES IN @gth_delivery_headers
      WHERE head_hdl = @gth_delivery_headers-handle.
    IF sy-subrc = 0.
* Please don't change the sort order
      SORT gt_events BY head_hdl
                        even
                        even_cnt   DESCENDING
                        even_verty
                        even_vernu DESCENDING.
    ENDIF.

    SELECT                                                "#EC CI_SUBRC
       even,
       even_timfr,
       even_timto
    FROM ttsegevty
    INTO TABLE @gth_evtype
    FOR ALL ENTRIES IN @lts_event
    WHERE even = @lts_event-table_line.

    get_zhm_events_config( ).

  ENDMETHOD.


  METHOD check_delivery.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*

    READ TABLE gth_delivery_headers INTO es_delivery_header
        WITH TABLE KEY vbeln = iv_vbeln.

    IF sy-subrc <> 0.

      MESSAGE e002 WITH iv_vbeln INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
      RETURN.
    ENDIF.

    es_delivery_header-tsegfl = abap_true.
  ENDMETHOD.


  METHOD tseg_init.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
    CALL FUNCTION 'TSEG_INIT'
      EXCEPTIONS
        fatal_error = 1
        OTHERS      = 2.
    IF sy-subrc <> 0.
      sy-msgty = if_msg_output=>msgtype_error.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ENDIF.

    CALL FUNCTION 'WS_DEADLINE_APPLDATA_INIT'.
  ENDMETHOD.


  METHOD tseg_read.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*

    CALL FUNCTION 'TSEG_READ'
      EXPORTING
        if_object                = gc_object
        is_object_wa             = is_delivery_header
      EXCEPTIONS
        object_not_found         = 1
        handle_not_found         = 2
        customizing_inconsistent = 3
        fatal_error              = 4
        OTHERS                   = 5.
    IF sy-subrc <> 0.
      sy-msgty = if_msg_output=>msgtype_error.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ENDIF.

    CALL FUNCTION 'WS_DEADLINE_APPLDATA_READ'
      EXPORTING
        if_handle         = is_delivery_header-handle
        if_append_allowed = abap_true
      EXCEPTIONS
        no_app_data       = 1
        tab_not_empty     = 2
        OTHERS            = 3.
    IF sy-subrc <> 0.
      MESSAGE e010 INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ENDIF.
  ENDMETHOD.


  METHOD tseg_update.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 28.02.2023 | 18274     : [Build] - TMS Milestone      *
*              |            | DS4K944534                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*
************************************************************************
* HURTSALE     | 15.06.2023 | 22378 : Book in reference in Event text  *
*              |            | DS4K955639                               *
* ZUJSKAS      | 08.12.2023 | 27458 : Application flag check           *
*              |            | DS4K973118                               *
*----------------------------------------------------------------------*

    DATA: lt_return_local TYPE bapiret2_t.

    check_segment(
      EXPORTING
        iv_event          = is_timeline-event_type
      IMPORTING
        ev_time_type_from = DATA(lv_time_type_from)
        ev_time_type_to   = DATA(lv_time_type_to)
      CHANGING
        ct_return         = lt_return_local ).

    IF cl_bapi_msg=>contains_error( lt_return_local ).
      RETURN.
    ENDIF.

    DATA(ls_event_update_control) = get_event_update_control( iv_handle          = is_delivery_header-handle
                                                              iv_event_type      = is_timeline-event_type
                                                              is_timeline        = is_timeline ).

* Update timezones
    IF ls_event_update_control-plan_time_zone = abap_true.
      IF is_timeline-plan_time_zone IS NOT INITIAL.
        DATA(lv_plan_time_zone) = is_timeline-plan_time_zone.
      ELSE.
        lv_plan_time_zone =  get_local_tzone( ).
      ENDIF.
    ENDIF.

    IF ls_event_update_control-actual_time_zone = abap_true.
      IF is_timeline-actual_time_zone IS NOT INITIAL.
        DATA(lv_actual_time_zone) = is_timeline-actual_time_zone.
      ELSE.
        lv_actual_time_zone =  get_local_tzone( ).
      ENDIF.
    ENDIF.

    IF ls_event_update_control-plan_begin_date = abap_true.
      timestamp_update(
        EXPORTING
          iv_event     = is_timeline-event_type
          iv_handle    = is_delivery_header-handle
          iv_time_type = lv_time_type_from
          iv_eventyp   = gc_s_vertyp-plan
          iv_time      = convert_to_timestamp( iv_date = is_timeline-begin_plan_date
                                               iv_time = is_timeline-begin_plan_time
                                               iv_zone = lv_plan_time_zone )
        CHANGING
         ct_return     = lt_return_local ).
    ENDIF.

    IF ls_event_update_control-plan_end_date = abap_true.
      timestamp_update(
        EXPORTING
          iv_event     = is_timeline-event_type
          iv_handle    = is_delivery_header-handle
          iv_time_type = lv_time_type_to
          iv_eventyp   = gc_s_vertyp-plan
          iv_time      = convert_to_timestamp( iv_date = is_timeline-end_plan_date
                                               iv_time = is_timeline-end_plan_time
                                               iv_zone = lv_plan_time_zone )
        CHANGING
         ct_return     = lt_return_local ).
    ENDIF.

    IF ls_event_update_control-actual_begin_date = abap_true.
      timestamp_update(
        EXPORTING
          iv_event     = is_timeline-event_type
          iv_handle    = is_delivery_header-handle
          iv_time_type = lv_time_type_from
          iv_eventyp   = gc_s_vertyp-actual
          iv_time      = convert_to_timestamp( iv_date = is_timeline-begin_actual_date
                                               iv_time = is_timeline-begin_actual_time
                                               iv_zone = lv_actual_time_zone )
        CHANGING
         ct_return     = lt_return_local ).
    ENDIF.

    IF ls_event_update_control-actual_end_date = abap_true.
      timestamp_update(
        EXPORTING
          iv_event     = is_timeline-event_type
          iv_handle    = is_delivery_header-handle
          iv_time_type = lv_time_type_to
          iv_eventyp   = gc_s_vertyp-actual
          iv_time      = convert_to_timestamp( iv_date = is_timeline-end_actual_date
                                               iv_time = is_timeline-end_actual_time
                                               iv_zone = lv_actual_time_zone )
        CHANGING
         ct_return     = lt_return_local ).
    ENDIF.

* Update timezones
    IF ls_event_update_control-plan_time_zone = abap_true.
      DATA(lv_sp_plan_tz) = get_default_tzone( is_delivery_header-vstel ).
      IF lv_sp_plan_tz IS NOT INITIAL.
        zone_update(
          EXPORTING
            iv_event           = is_timeline-event_type
            is_delivery_header = is_delivery_header
            iv_time_type       = lv_time_type_from
            iv_eventyp         = gc_s_vertyp-plan
            iv_tzone           = lv_sp_plan_tz
          CHANGING
            ct_return          = lt_return_local ).
      ENDIF.
    ENDIF.

    IF ls_event_update_control-actual_time_zone = abap_true.
      DATA(lv_sp_actual_tz) = get_default_tzone( is_delivery_header-vstel ).
      IF lv_sp_actual_tz IS NOT INITIAL.
        zone_update(
          EXPORTING
            iv_event           = is_timeline-event_type
            is_delivery_header = is_delivery_header
            iv_time_type       = lv_time_type_from
            iv_eventyp         = gc_s_vertyp-actual
            iv_tzone           = lv_sp_actual_tz
          CHANGING
            ct_return          = lt_return_local ).
      ENDIF.
    ENDIF.

* Update reason code
    IF ls_event_update_control-actual_reason = abap_true.
      reason_update(
        EXPORTING
          iv_event   = is_timeline-event_type
          iv_eventyp = gc_s_vertyp-actual
          iv_handle  = is_delivery_header-handle
          iv_reason  = is_timeline-reason
        CHANGING
          ct_return  = lt_return_local ).
    ENDIF.

    IF ls_event_update_control-plan_reason = abap_true.
      reason_update(
        EXPORTING
          iv_event   = is_timeline-event_type
          iv_eventyp = gc_s_vertyp-plan
          iv_handle  = is_delivery_header-handle
          iv_reason  = is_timeline-reason
        CHANGING
          ct_return  = lt_return_local ).
    ENDIF.

* Update event text
    IF is_timeline-event_text IS NOT INITIAL.

      IF ls_event_update_control-actual_event_text = abap_true.
        event_text_update(
          EXPORTING
            iv_event   = is_timeline-event_type
            iv_eventyp = gc_s_vertyp-actual
            iv_handle  = is_delivery_header-handle
            iv_event_text = is_timeline-event_text
          CHANGING
            ct_return  = lt_return_local ).
      ENDIF.

      IF ls_event_update_control-plan_event_text = abap_true.
        event_text_update(
          EXPORTING
            iv_event   = is_timeline-event_type
            iv_eventyp = gc_s_vertyp-plan
            iv_handle  = is_delivery_header-handle
            iv_event_text = is_timeline-event_text
          CHANGING
            ct_return  = lt_return_local ).
      ENDIF.

    ENDIF.
    APPEND LINES OF lt_return_local TO ct_return.
  ENDMETHOD.


  METHOD convert_to_timestamp.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
    CHECK iv_date IS NOT INITIAL AND iv_time IS NOT INITIAL.

    CONVERT DATE iv_date TIME iv_time INTO TIME STAMP rv_result TIME ZONE iv_zone.
  ENDMETHOD.


  METHOD timestamp_update.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*

    CHECK iv_time IS NOT INITIAL.

    get_event(
      EXPORTING
        iv_event   = iv_event
        iv_handle  = iv_handle
        iv_eventyp = iv_eventyp
      IMPORTING
        es_event   = DATA(ls_event)
      CHANGING
        ct_return  = ct_return ).

    IF cl_bapi_msg=>contains_error( ct_return ).
      RETURN.
    ENDIF.

    CALL FUNCTION 'TSEG_MODIFY_TST'
      EXPORTING
        if_handle     = iv_handle
        if_time       = iv_time_type
        if_even_cnt   = ls_event-even_cnt
        if_even_verty = ls_event-even_verty
        if_even_vernu = ls_event-even_vernu
        if_time_tst   = iv_time
      EXCEPTIONS
        fatal_error   = 1
        OTHERS        = 2.
    IF sy-subrc <> 0.
      MESSAGE e003 INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ENDIF.

  ENDMETHOD.


  METHOD zone_update.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*

    IF iv_tzone IS INITIAL.
      RETURN.
    ENDIF.

*-- To fetch the Local time from table TTSEGTIME
    SELECT SINGLE time_loc
           FROM ttsegtime
           INTO @DATA(lv_time_loc)
           WHERE time = @iv_time_type.

    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    get_event(
      EXPORTING
        iv_event   = iv_event
        iv_handle  = is_delivery_header-handle
        iv_eventyp = iv_eventyp
      IMPORTING
        es_event   = DATA(ls_event)
      CHANGING
        ct_return  = ct_return ).

    IF ls_event IS INITIAL OR cl_bapi_msg=>contains_error( ct_return ).
      RETURN.
    ENDIF.

*FM to update timezone in table TSEGE
    CALL FUNCTION 'TSEG_MODIFY_ZON'
      EXPORTING
        if_handle   = is_delivery_header-handle
        if_loca     = lv_time_loc
        if_even_cnt = ls_event-even_cnt
        if_loca_zon = iv_tzone
      EXCEPTIONS
        fatal_error = 1
        OTHERS      = 2.

    IF sy-subrc <> 0.
      MESSAGE e004 INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ENDIF.
  ENDMETHOD.


  METHOD tseg_commit.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054     : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* KARPOSER     | 04.08.2022 | 12191 : DN Events Update                 *
*              |            | DS4K924254                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*

    DATA:  lv_update TYPE tsegflupdt.

    CHECK NOT is_delivery_header-vbeln IS INITIAL.

    IF cl_bapi_msg=>contains_error( ct_return ).
      MESSAGE i005 WITH is_delivery_header-vbeln INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ELSEIF iv_test = abap_true.
      MESSAGE i007 WITH is_delivery_header-vbeln INTO lv_msg.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ELSE.

      DATA(lt_handle) = VALUE vltm_handletab( ( head_hdl = is_delivery_header-handle ) ).
      CALL FUNCTION 'WS_DEADLINE_APPLDATA_WRITE'
        EXPORTING
          if_update_task = space
        TABLES
          t_handle       = lt_handle
        EXCEPTIONS
          OTHERS         = 0.

      CALL FUNCTION 'TSEG_WRITE'
        EXPORTING
          if_object      = gc_object
          is_object_wa   = is_delivery_header
          if_update_task = space
        IMPORTING
          ef_update      = lv_update
        EXCEPTIONS
          fatal_error    = 4
          OTHERS         = 8.

      IF sy-subrc <> 0.

        IF iv_commit = abap_true.
          ROLLBACK WORK.                               "#EC CI_ROLLBACK
        ENDIF.

        MESSAGE e006 WITH is_delivery_header-vbeln INTO lv_msg.
        cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
      ELSE.

        IF lv_update <> space.
*-- update without problems
          MESSAGE s008 WITH is_delivery_header-vbeln INTO lv_msg.
        ELSE.
*-- update w/o tech. problem - nevertheless no records have been updated
          MESSAGE w009 WITH is_delivery_header-vbeln INTO lv_msg.
        ENDIF.

        cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).

        IF iv_commit = abap_true.
          COMMIT WORK AND WAIT.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD check_segment.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD_Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 28.02.2023 | 18274     : [Build] - TMS Milestone      *
*              |            | DS4K944534                               *
*----------------------------------------------------------------------*

    CLEAR: ev_time_type_from, ev_time_type_to.

    READ TABLE gth_evtype ASSIGNING FIELD-SYMBOL(<ls_evtype>)
        WITH TABLE KEY even = iv_event.
    IF sy-subrc <> 0.
      MESSAGE e011 WITH iv_event INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
      RETURN.
    ENDIF.

    ev_time_type_from = <ls_evtype>-even_timfr.
    ev_time_type_to = <ls_evtype>-even_timto.

  ENDMETHOD.


  METHOD get_event.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* GERTSVIK     | 25.07.2022 | 22855 : CR-20297 Time zone fix           *
*              |            | DS4K959190                               *
*----------------------------------------------------------------------*
    DATA: lv_tabix_tseg TYPE sytabix,
          lv_subrc_tseg TYPE sysubrc.

    CLEAR es_event.

    READ TABLE gt_events INTO      es_event               "#EC CI_SUBRC
               WITH KEY head_hdl    = iv_handle
                        even        = iv_event
                        BINARY SEARCH.

    lv_subrc_tseg = sy-subrc.
    lv_tabix_tseg = sy-tabix.

    IF lv_subrc_tseg <> 0.
*-- event doesn't exist ...
      CALL FUNCTION 'TSEG_INSERT_EVEN'
        EXPORTING
          if_handle = iv_handle
          if_even   = iv_event
        EXCEPTIONS
          OTHERS    = 1.

      IF sy-subrc <> 0.
        sy-msgty = if_msg_output=>msgtype_error.
        cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
        RETURN.
      ELSE.
        es_event-head_hdl    = iv_handle.
        es_event-even        = iv_event.
        es_event-even_verty  = iv_eventyp.
        INSERT es_event INTO gt_events INDEX lv_tabix_tseg.
      ENDIF.
    ELSE.
*-- find the latest count and version
      LOOP AT gt_events ASSIGNING FIELD-SYMBOL(<ls_event>) FROM lv_tabix_tseg.
        IF <ls_event>-even <> es_event-even.
          EXIT.
        ENDIF.
        IF <ls_event>-even_verty = iv_eventyp.
          es_event = <ls_event>.
          EXIT.
        ENDIF.
      ENDLOOP.

      IF es_event-even_verty <> iv_eventyp.
*-- no event found for the given event type
        CLEAR es_event.
        es_event-head_hdl    = iv_handle.
        es_event-even        = iv_event.
        es_event-even_verty  = iv_eventyp.

        INSERT es_event INTO gt_events INDEX lv_tabix_tseg.

      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD get_zhm_events_config.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 28.02.2023 | 18274     : [Build] - TMS Milestone      *
*              |            | DS4K944534                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*
* HURTSALE     | 18.07.2023 | 22864 : [Build] - Milestone interface Ref*
*              |            | DS4K958705                               *
*----------------------------------------------------------------------*



    TRY.
        zcl_pe_hm_complex=>get_table(
          EXPORTING
            iv_id           = '024F85009E261EEDB3AF4D9AA640C0D2'
            iv_mapping_type = zcl_pe_hm_complex=>gc_s_mapping_type-data_elements
          IMPORTING
            et_table        = gts_zhm_evnt_cfg ).

        gv_refu_event = zcl_pe_hm_basic=>get_value( iv_id = '1987' ).
      CATCH zcx_pe_hm.
        CLEAR gts_zhm_evnt_cfg.
        CLEAR gv_refu_event.
    ENDTRY.

  ENDMETHOD.


  METHOD get_event_update_control.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* YURCHALA     | 28.02.2023 | 18274     : [Build] - TMS Milestone      *
*              |            | DS4K944534                               *
*----------------------------------------------------------------------*
* HURTSALE     | 15.06.2023 | 22378 : Book in reference in Event text  *
*              |            | DS4K955639                               *
*----------------------------------------------------------------------*
* GERTSVIK     | 27.07.2023 | 22855 : Time zone fix                    *
*              |            | DS4K955639                               *
*----------------------------------------------------------------------*
* ZUJSKAS      | 29.09.2023 | 25443 : Enhancement for no revision of   *
*              |            |         WMS route date in DN dates       *
*              |            | DS4K965021                               *
*----------------------------------------------------------------------*

    rs_update_control = VALUE #( actual_begin_date = abap_true
                                 actual_end_date   = abap_true
                                 actual_time_zone  = abap_true
                                 actual_reason     = abap_true
                                 actual_event_text = abap_true
                                 plan_begin_date   = abap_true
                                 plan_end_date     = abap_true
                                 plan_time_zone    = abap_true
                                 plan_reason       = abap_true
                                 plan_event_text   = abap_true ).

    TRY.
        DATA(lr_event_type) = zcl_pe_hm_basic=>get_range( '2347' ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.

    IF iv_event_type IN lr_event_type AND line_exists( gt_events[ even = iv_event_type ] ).
      CLEAR rs_update_control.
      RETURN.
    ENDIF.

    IF is_timeline-plan_time_zone IS NOT INITIAL.
      DATA(lv_plan_time_zone) = is_timeline-plan_time_zone.
    ELSE.
      lv_plan_time_zone =  get_local_tzone( ).
    ENDIF.

    IF is_timeline-actual_time_zone IS NOT INITIAL.
      DATA(lv_actual_time_zone) = is_timeline-actual_time_zone ##NEEDED.
    ELSE.
      lv_actual_time_zone =  get_local_tzone( ).
    ENDIF.

    TRY.
        DATA(lv_refu_event) = zcl_pe_hm_basic=>get_value( iv_id = '1987' ).
      CATCH zcx_pe_hm.
        CLEAR lv_refu_event.
    ENDTRY.
    TRY.
        DATA(lv_pode_event) = zcl_pe_hm_basic=>get_value( iv_id = '795' ).
      CATCH zcx_pe_hm.
        CLEAR lv_pode_event.
    ENDTRY.

    IF ( iv_event_type = lv_refu_event OR iv_event_type = lv_pode_event ) AND
       NOT line_exists( gt_events[ even = iv_event_type ] ).
      CLEAR: rs_update_control-plan_begin_date,
             rs_update_control-plan_end_date,
             rs_update_control-plan_time_zone,
             rs_update_control-plan_reason,
             rs_update_control-plan_event_text.
    ENDIF.

    LOOP AT gt_events ASSIGNING FIELD-SYMBOL(<ls_event>)
      WHERE head_hdl = iv_handle
        AND even     = iv_event_type.                    "#EC CI_STDSEQ

      CASE <ls_event>-even_verty.
        WHEN gc_s_vertyp-actual.

          IF rs_update_control-actual_begin_date = abap_false AND rs_update_control-actual_end_date = abap_false.
            CLEAR: rs_update_control-actual_time_zone,
                   rs_update_control-actual_reason,
                   rs_update_control-actual_event_text.
          ENDIF.

        WHEN gc_s_vertyp-plan.

          IF <ls_event>-even_tstfr >= convert_to_timestamp( iv_date = is_timeline-begin_plan_date
                                                            iv_time = is_timeline-begin_plan_time
                                                            iv_zone = lv_plan_time_zone ).
            CLEAR rs_update_control-plan_begin_date.
          ENDIF.

          IF <ls_event>-even_tstto >= convert_to_timestamp( iv_date = is_timeline-end_plan_date
                                                            iv_time = is_timeline-end_plan_time
                                                            iv_zone = lv_plan_time_zone ).
            CLEAR rs_update_control-plan_end_date.
          ENDIF.

          IF rs_update_control-plan_begin_date = abap_false AND rs_update_control-plan_end_date = abap_false.
            CLEAR: rs_update_control-plan_time_zone,
                   rs_update_control-plan_reason,
                   rs_update_control-plan_event_text.
          ENDIF.

        WHEN OTHERS.
          CONTINUE.
      ENDCASE.

    ENDLOOP.

    READ TABLE gt_events ASSIGNING <ls_event>
      WITH KEY head_hdl   = iv_handle
               even       = iv_event_type
               even_cnt   = 0
               even_verty = gc_s_vertyp-actual BINARY SEARCH.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    READ TABLE gts_zhm_evnt_cfg ASSIGNING FIELD-SYMBOL(<ls_zhm_evnt_cfg>)
      WITH TABLE KEY primary_key COMPONENTS zsd_dlv_event = iv_event_type.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    IF <ls_zhm_evnt_cfg>-zsd_trigger_edf = abap_true AND rs_update_control-actual_end_date = abap_true.
      CLEAR rs_update_control.
      rs_update_control-actual_end_date   = abap_true.
      rs_update_control-actual_time_zone  = abap_true.
      rs_update_control-actual_reason     = abap_true.
      rs_update_control-actual_event_text = abap_true.
    ENDIF.

  ENDMETHOD.


  METHOD get_default_tzone.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 01.08.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*
* KALSHZHA     | 15.06.2023 | 22378 : Book in reference in Event text  *
*              |            | DS4K955639                               *
*----------------------------------------------------------------------*
    DATA: lt_adrc TYPE STANDARD TABLE OF adrc.

    SELECT SINGLE addressid                          "#EC CI_SEL_NESTED
      FROM i_shippingpoint
        INTO @DATA(lv_adrnr)
          WHERE shippingpoint = @iv_vstel.
    IF sy-subrc = 0.
      CALL FUNCTION 'ADDR_SELECT_ADRC_SINGLE'
        EXPORTING
          addrnumber        = lv_adrnr
        TABLES
          et_adrc           = lt_adrc
        EXCEPTIONS
          address_not_exist = 1
          parameter_error   = 2
          internal_error    = 3
          address_blocked   = 4
          OTHERS            = 5.
      IF sy-subrc = 0 AND lt_adrc IS NOT INITIAL.
        rv_result = lt_adrc[ 1 ]-time_zone.
      ENDIF.
    ENDIF.

    IF rv_result IS NOT INITIAL.
      RETURN.
    ENDIF.

    rv_result = sy-zonlo.

    IF rv_result IS NOT INITIAL.
      RETURN.
    ENDIF.

    rv_result = get_local_tzone( ).

  ENDMETHOD.


  METHOD reason_update.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 18.07.2022 | 12054 : FB_12054_SD Delivery note events *
*              |            | DS4K923702                               *
*----------------------------------------------------------------------*
* YURCHALA     | 21.04.2023 | 19940 : [Build] - CR for TMS Milestone   *
*              |            | DS4K949551                               *
*----------------------------------------------------------------------*
* ZUJSKAS      | 24.10.2023 | 26096     : [POC]- Application flag check*
*              |            | DS4K967594                               *
*----------------------------------------------------------------------*

    get_event(
      EXPORTING
        iv_event   = iv_event
        iv_handle  = iv_handle
        iv_eventyp = iv_eventyp
      IMPORTING
        es_event   = DATA(ls_event)
      CHANGING
        ct_return  = ct_return ).

    IF ls_event IS INITIAL OR cl_bapi_msg=>contains_error( ct_return ).
      RETURN.
    ENDIF.

    CALL FUNCTION 'TSEG_MODIFY_APD'
      EXPORTING
        if_handle     = iv_handle
        if_even       = iv_event
        if_even_cnt   = ls_event-even_cnt
        if_even_flgap = SWITCH tsegevtapp( iv_reason WHEN space THEN abap_false
                                                     ELSE abap_true )
      EXCEPTIONS
        fatal_error   = 1
        OTHERS        = 2.
    IF sy-subrc <> 0.
      MESSAGE e010 INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
      RETURN.
    ENDIF.
    IF iv_reason IS NOT INITIAL.

      CALL FUNCTION 'WS_DEADLINE_APPLDATA_MODIFY'
        EXPORTING
          i_tsegws01     = VALUE tsegws01( head_hdl = iv_handle
                                           even = iv_event
                                           even_cnt = ls_event-even_cnt
                                           vstga = iv_reason )
        EXCEPTIONS
          incomplete_key = 1
          OTHERS         = 2.
      IF sy-subrc <> 0.
        MESSAGE e010 INTO lv_msg ##NEEDED.
        cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD event_text_update.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* HURTSALE     | 15.06.2023 | 22378 : Book in reference in Event text  *
*              |            | DS4K955639                               *
*----------------------------------------------------------------------*

    CONSTANTS:
      lc_header_tdobject TYPE tdobject VALUE 'TSEG',
      lc_header_tdid     TYPE tdid VALUE '0001'.
    DATA: lv_subrc       LIKE sy-subrc,
          ls_tdname_tseg TYPE tseg_tdname,
          lv_tdname      TYPE tdobname.
    get_event(
      EXPORTING
        iv_event   = iv_event
        iv_handle  = iv_handle
        iv_eventyp = iv_eventyp
      IMPORTING
        es_event   = DATA(ls_event)
      CHANGING
        ct_return  = ct_return ).

    IF ls_event IS INITIAL OR cl_bapi_msg=>contains_error( ct_return ).
      RETURN.
    ENDIF.

    ls_tdname_tseg-even = iv_event.
    ls_tdname_tseg-even_cnt = ls_event-even_cnt.

    "to get header handle
    PERFORM conv_handle_to_uppercase IN PROGRAM sapltseg
     USING    iv_handle
     CHANGING ls_tdname_tseg-head_hdl_upper.

    lv_tdname = ls_tdname_tseg.
    CALL FUNCTION 'TSEG_MODIFY_TXT'
      EXPORTING
        if_handle    = iv_handle
        if_even      = iv_event
        if_even_cnt  = ls_event-even_cnt
        is_diat_even = VALUE tseg_textdatawa( head_hdl = iv_handle
                                              even = iv_event
                                              even_cnt = ls_event-even_cnt
                                              diag_modif = 'I'
                                              header = VALUE #( tdobject = lc_header_tdobject tdid = lc_header_tdid tdspras = sy-langu tdname = lv_tdname )
                                              lines = VALUE #( ( tdformat = '*' tdline = iv_event_text ) ) )
      IMPORTING
        ef_subrc     = lv_subrc
      EXCEPTIONS
        fatal_error  = 4
        OTHERS       = 8.
    IF sy-subrc <> 0 or lv_subrc <> 0.
      MESSAGE e014 INTO DATA(lv_msg) ##NEEDED.
      cl_bapi_msg=>add_message( CHANGING ct_message = ct_return ).
    ENDIF.
  ENDMETHOD.


  method GET_LOCAL_TZONE.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 15.06.2023 | 22378 : Book in reference in Event text  *
*              |            | DS4K955639                               *
*----------------------------------------------------------------------*
    CALL FUNCTION 'GET_SYSTEM_TIMEZONE'
      IMPORTING
        timezone            = rv_result
      EXCEPTIONS
        customizing_missing = 1
        OTHERS              = 2.
    IF sy-subrc <> 0.
      CLEAR rv_result.
    ENDIF.
  endmethod.
ENDCLASS.