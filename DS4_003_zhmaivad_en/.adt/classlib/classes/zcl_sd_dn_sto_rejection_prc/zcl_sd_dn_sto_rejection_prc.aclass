CLASS zcl_sd_dn_sto_rejection_prc DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_badi_interface .
    INTERFACES if_ex_le_shp_delivery_proc .
  PROTECTED SECTION.
  PRIVATE SECTION.

    TYPES:
      BEGIN OF mtyp_s_sto_item_to_update,
        ebeln TYPE ebeln,
        ebelp TYPE ebelp,
        bsart TYPE bsart,
        lfimg TYPE lfimg,
        menge TYPE etmen,
        meins TYPE bstme,
      END OF mtyp_s_sto_item_to_update .
    TYPES: mtyp_tt_sto_items_to_update TYPE STANDARD TABLE OF mtyp_s_sto_item_to_update.
    TYPES:
      BEGIN OF mtyp_s_sto_part_rej,
        bsart          TYPE bsart,
        default_reason TYPE zz1_abgru,
      END OF mtyp_s_sto_part_rej .
    TYPES:
      mtyp_tt_sto_part_rej TYPE STANDARD TABLE OF mtyp_s_sto_part_rej  WITH NON-UNIQUE KEY bsart .
    TYPES:
      BEGIN OF mtyp_s_hm_data,
        sto_part_rej_dn_lfart TYPE RANGE OF lfart,
        sto_part_rej_bsart    TYPE RANGE OF bsart,
        sto_part_rej          TYPE mtyp_tt_sto_part_rej,
      END OF mtyp_s_hm_data .

    DATA mth_messages TYPE finchdel_t .
    DATA ms_hm_data TYPE mtyp_s_hm_data .

    METHODS add_message
      IMPORTING
        !iv_vbeln TYPE vbeln_vl .
    METHODS update_sto
      IMPORTING
        !iv_vbeln              TYPE vbeln_vl
        !it_sto_item_to_update TYPE mtyp_tt_sto_items_to_update .
    METHODS get_hm_data .
ENDCLASS.



CLASS ZCL_SD_DN_STO_REJECTION_PRC IMPLEMENTATION.


  METHOD get_hm_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection                *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

    CONSTANTS : lc_dn_hardcode_id  TYPE zpe_hm_id VALUE '1776',
                lc_sto_hardcode_id TYPE zpe_hm_id VALUE '1775',
                lc_i               TYPE ddsign    VALUE 'I',
                lc_eq              TYPE ddoption  VALUE 'EQ'.

    TRY.
        ms_hm_data-sto_part_rej = CORRESPONDING #( zcl_pe_hm_basic=>get_table( iv_id = lc_sto_hardcode_id )
                                                   MAPPING bsart          = field1
                                                           default_reason = field3 ).

        ms_hm_data-sto_part_rej_dn_lfart  = VALUE #( FOR ls_sto_part_rej_dn IN zcl_pe_hm_basic=>get_table( iv_id = lc_dn_hardcode_id )
                                                      ( sign   = lc_i
                                                        option = lc_eq
                                                        low    = ls_sto_part_rej_dn-field1 ) ) .

        ms_hm_data-sto_part_rej_bsart  = VALUE #( FOR ls_sto_part_rej IN zcl_pe_hm_basic=>get_range( iv_id = lc_sto_hardcode_id )
                                                    WHERE ( sign = lc_i )
                                                    ( sign   = lc_i
                                                      option = lc_eq
                                                      low    = ls_sto_part_rej-low ) ) .

        SORT ms_hm_data-sto_part_rej BY bsart default_reason.
        DELETE ADJACENT DUPLICATES FROM ms_hm_data-sto_part_rej COMPARING bsart default_reason.

        SORT ms_hm_data-sto_part_rej_dn_lfart BY low.
        DELETE ADJACENT DUPLICATES FROM ms_hm_data-sto_part_rej_dn_lfart COMPARING low.

        SORT ms_hm_data-sto_part_rej_bsart BY low.
        DELETE ADJACENT DUPLICATES FROM ms_hm_data-sto_part_rej_bsart COMPARING low.

      CATCH zcx_pe_hm.
        CLEAR ms_hm_data.
    ENDTRY.

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~change_delivery_header ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 26.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~change_delivery_item ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*
  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~change_fcode_attributes ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~change_field_attributes ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~check_item_deletion ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~delivery_deletion ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~delivery_final_check ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection                *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*
* SHNIPALI     | 09.11.2023 | 26616     : Defect STO Rejection         *
*              |            | DS4K969742                               *
*----------------------------------------------------------------------*
* SHNIPALI     | 12.15.2023 | 27650     : Defect STO Rejection         *
*              |            | DS4K974059                               *
*----------------------------------------------------------------------*
* KONRANAR     | 20.12.2023 | 27570     : Defect No status object HU   *
*              |            | DS4K974424                               *
*----------------------------------------------------------------------*

    CONSTANTS:
      lc_outbound_delivery TYPE vbtyp VALUE 'J',
      lc_purchase_order    TYPE vgtyp VALUE 'V',
      lc_create            TYPE trtyp VALUE 'H',
      lc_update            TYPE trtyp VALUE 'V'.

    DATA:
      lt_sto_items_to_update     TYPE mtyp_tt_sto_items_to_update.

    CHECK if_trtyp = lc_create OR if_trtyp = lc_update.

    CLEAR: mth_messages.

    DATA(lt_lips) = it_xlips.

    get_hm_data( ).

    SELECT  DISTINCT
            deliverydocumentitem~deliverydocument       AS vbeln,
            deliverydocumentitem~deliverydocumentitem   AS posnr,
            deliverydocumentitem~actualdeliveryquantity AS lfimg,
            purchaseorderitem~purchaseorder             AS ebeln,
            purchaseorderitem~purchaseorderitem         AS ebelp
          FROM @lt_lips AS t_lips
          INNER JOIN i_deliverydocumentitem   AS deliverydocumentitem ON deliverydocumentitem~referencesddocument     =  t_lips~vgbel AND
                                                                         deliverydocumentitem~referencesddocumentitem =  t_lips~vgpos
          INNER JOIN i_purchaseorderitemapi01 AS purchaseorderitem ON purchaseorderitem~purchaseorder     = deliverydocumentitem~referencesddocument AND
                                                                      purchaseorderitem~purchaseorderitem = right( deliverydocumentitem~referencesddocumentitem, 5 )
          WHERE deliverydocumentitem~referencesddocumentcategory = @lc_purchase_order      AND
                deliverydocumentitem~referencesddocument         IS NOT INITIAL            AND
                deliverydocumentitem~referencesddocumentitem     IS NOT INITIAL            AND
                deliverydocumentitem~sddocumentcategory          = @lc_outbound_delivery   AND
                deliverydocumentitem~\_deliverydocument-deliverydocumenttype IN @ms_hm_data-sto_part_rej_dn_lfart AND
                purchaseorderitem~\_purchaseorder-purchaseordertype IN @ms_hm_data-sto_part_rej_bsart
          INTO TABLE @DATA(lt_delivery_items_data_db).

    IF sy-subrc = 0.
      LOOP AT lt_lips ASSIGNING FIELD-SYMBOL(<ls_lips>).
        IF line_exists( lt_delivery_items_data_db[ vbeln = <ls_lips>-vbeln
                                                   posnr = <ls_lips>-posnr ] ).
          DELETE lt_delivery_items_data_db WHERE vbeln = <ls_lips>-vbeln AND
                                                 posnr = <ls_lips>-posnr.
        ENDIF.
      ENDLOOP.

      SELECT ebeln,
             ebelp,
             SUM( lfimg ) AS lfimg
      FROM @lt_delivery_items_data_db AS deliverydocumentitem
      GROUP BY deliverydocumentitem~ebeln, deliverydocumentitem~ebelp
      INTO TABLE @DATA(lt_delivery_items_data_db_sum).

      IF sy-subrc <> 0.
        CLEAR lt_delivery_items_data_db_sum.
      ENDIF.
    ENDIF.

    LOOP AT it_xlikp ASSIGNING FIELD-SYMBOL(<ls_xlikp>)
      WHERE vbtyp = lc_outbound_delivery AND
            lfart IN ms_hm_data-sto_part_rej_dn_lfart.

      SELECT
            purchaseorderitem~purchaseorder       AS ebeln,
            purchaseorderitem~purchaseorderitem   AS ebelp,
            SUM( deliverydocumentitem~lfimg )     AS lfimg
          FROM @it_xlips AS deliverydocumentitem
          INNER JOIN i_purchaseorderitemapi01 AS purchaseorderitem ON purchaseorderitem~purchaseorder     = deliverydocumentitem~vgbel AND
                                                                      purchaseorderitem~purchaseorderitem = right( deliverydocumentitem~vgpos, 5 )
          WHERE deliverydocumentitem~vbeln = @<ls_xlikp>-vbeln      AND
                deliverydocumentitem~vgtyp = @lc_purchase_order     AND
                deliverydocumentitem~vgbel IS NOT INITIAL           AND
                deliverydocumentitem~vgpos IS NOT INITIAL           AND
                purchaseorderitem~\_purchaseorder-purchaseordertype IN @ms_hm_data-sto_part_rej_bsart
          GROUP BY purchaseorderitem~purchaseorder, purchaseorderitem~purchaseorderitem
          INTO TABLE @DATA(lt_delivery_items_data_upd_sum).

      IF sy-subrc <> 0.
        CLEAR lt_delivery_items_data_upd_sum.
      ENDIF.

      LOOP AT lt_delivery_items_data_upd_sum ASSIGNING FIELD-SYMBOL(<ls_deliv_items_data_upd_sum>). "#EC CI_NESTED
        READ TABLE lt_delivery_items_data_db_sum WITH KEY ebeln = <ls_deliv_items_data_upd_sum>-ebeln
                                                          ebelp = <ls_deliv_items_data_upd_sum>-ebelp
                                                          ASSIGNING FIELD-SYMBOL(<ls_deliv_items_data_db_sum>).
        IF sy-subrc = 0.
          <ls_deliv_items_data_upd_sum>-lfimg = <ls_deliv_items_data_upd_sum>-lfimg + <ls_deliv_items_data_db_sum>-lfimg.
        ENDIF.
      ENDLOOP.


      SELECT purchaseorderschedule~purchaseorder                                 AS ebeln,
             purchaseorderschedule~purchaseorderitem                             AS ebelp,
             purchaseorderschedule~\_purchaseorder-purchaseordertype             AS bsart,
             deliverydocumentitem~lfimg,
             SUM( purchaseorderschedule~schedulelineorderquantity )              AS menge,
             purchaseorderschedule~\_purchaseorderitem-purchaseorderquantityunit AS meins,
             purchaseorderschedule~\_purchaseorderitem-zz1_abgru_pdi
        FROM @lt_delivery_items_data_upd_sum AS deliverydocumentitem
        INNER JOIN i_purordschedulelineapi01 AS purchaseorderschedule ON purchaseorderschedule~purchaseorder     = deliverydocumentitem~ebeln AND
                                                                         purchaseorderschedule~purchaseorderitem = deliverydocumentitem~ebelp
        WHERE purchaseorderschedule~schedulelineorderquantity > 0
        GROUP BY purchaseorderschedule~purchaseorder, purchaseorderschedule~purchaseorderitem, purchaseorderschedule~\_purchaseorder-purchaseordertype,
                 deliverydocumentitem~lfimg, purchaseorderschedule~\_purchaseorderitem-purchaseorderquantityunit,
                 purchaseorderschedule~\_purchaseorderitem-zz1_abgru_pdi
        ORDER BY purchaseorderschedule~purchaseorder, purchaseorderschedule~purchaseorderitem
        INTO TABLE @DATA(lt_sto_items_data_sum).

      IF sy-subrc = 0.

        LOOP AT lt_sto_items_data_sum ASSIGNING FIELD-SYMBOL(<ls_sto_items_data_sum>) "#EC CI_NESTED
          GROUP BY ( ebeln = <ls_sto_items_data_sum>-ebeln )
          ASSIGNING FIELD-SYMBOL(<ls_sto_group>).

          CLEAR lt_sto_items_to_update.

          LOOP AT GROUP <ls_sto_group> ASSIGNING FIELD-SYMBOL(<ls_sto_item>).
            IF ( <ls_sto_item>-menge > <ls_sto_item>-lfimg AND ( if_trtyp = lc_create OR if_trtyp = lc_update ) )
                OR
               ( <ls_sto_item>-menge = <ls_sto_item>-lfimg AND if_trtyp = lc_update AND
                 <ls_sto_item>-zz1_abgru_pdi = VALUE #( ms_hm_data-sto_part_rej[ bsart = <ls_sto_item>-bsart ]-default_reason OPTIONAL ) ).
              INSERT INITIAL LINE INTO TABLE lt_sto_items_to_update ASSIGNING FIELD-SYMBOL(<ls_sto_item_to_update>).
              <ls_sto_item_to_update> = CORRESPONDING #( <ls_sto_item> ).
            ENDIF.
          ENDLOOP.

          CHECK lt_sto_items_to_update IS NOT INITIAL. "27570: Prevent not needed update which calls re-ATP which calls commit

          update_sto( iv_vbeln              = <ls_xlikp>-vbeln
                      it_sto_item_to_update = lt_sto_items_to_update ).

        ENDLOOP.

      ENDIF.

    ENDLOOP.

    IF mth_messages IS NOT INITIAL.
      INSERT LINES OF mth_messages INTO TABLE ct_finchdel.
    ENDIF.

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~document_number_publish ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~fill_delivery_header ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~fill_delivery_item ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~initialize_delivery ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~item_deletion ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~publish_delivery_item ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~read_delivery ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~save_and_publish_before_output ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection                *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~save_and_publish_document ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD if_ex_le_shp_delivery_proc~save_document_prepare ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*


  ENDMETHOD.


  METHOD add_message.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection                *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*

    INSERT VALUE #(   vbeln = iv_vbeln
                      msgty = sy-msgty
                      msgno = sy-msgno
                      msgid = sy-msgid
                      msgv1 = sy-msgv1
                      msgv2 = sy-msgv2
                      msgv3 = sy-msgv3
                      msgv4 = sy-msgv4 ) INTO TABLE mth_messages.


  ENDMETHOD.


  METHOD update_sto.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 22.06.2023 | 21909     : STO Rejection                *
*              |            | DS4K954453                               *
*----------------------------------------------------------------------*
" ZHMAIVAD     | 09.10.2023 | 24101     : Feature - [PFE build] CNC ST -
"              |            | DS4K964057                               -
" ----------------------------------------------------------------------*
    CONSTANTS:
      lc_update TYPE trtyp VALUE 'V',
      lc_tcode  TYPE sy-tcode VALUE 'ME22N'.

    DATA:
      lo_po_api      TYPE REF TO cl_po_header_handle_mm,
      lo_po_item_api TYPE REF TO cl_po_item_handle_mm,
      ls_document    TYPE mepo_document,
      lv_result      TYPE mmpur_bool,
      ls_mepo_item   TYPE mepoitem,
      ls_mepo_itemx  TYPE mepoitemx,
      lt_models      TYPE mmpur_models,
      ls_model       LIKE LINE OF lt_models.

    CHECK it_sto_item_to_update IS NOT INITIAL.

    DATA(lv_ebeln) = it_sto_item_to_update[ 1 ]-ebeln.

    lo_po_api = NEW #( lv_ebeln ).
    IF sy-subrc <> 0.
      MESSAGE e003(zsd_sto_rejection) WITH lv_ebeln
         INTO DATA(lv_dummy_msg) ##NEEDED.
      add_message( iv_vbeln ).
      RETURN.
    ENDIF.

    IF lo_po_api IS BOUND.
      ls_document = VALUE #( process             = mmpur_po_process
                             initiator-initiator = mmpur_initiator_call
                             trtyp               = lc_update ).

      ls_document-doc_key(10) = lv_ebeln.

      lo_po_api->set_state( im_state = cl_po_header_handle_mm=>c_available ).
      lo_po_api->for_bapi = mmpur_yes.
      lo_po_api->po_initialize( im_document = ls_document ).
      lo_po_api->po_read(
        EXPORTING
          im_tcode     = lc_tcode
          im_trtyp     = ls_document-trtyp
          im_aktyp     = ls_document-trtyp
          im_po_number = lv_ebeln
          im_document  = ls_document
        IMPORTING
          ex_result    = lv_result ).

      IF lv_result = abap_true.
        LOOP AT lo_po_api->if_purchase_order_mm~get_items( ) ASSIGNING FIELD-SYMBOL(<ls_po_item>). "#EC CI_NESTED
          lo_po_item_api ?= <ls_po_item>-item.
          lo_po_item_api->get_data(  IMPORTING ex_data = ls_mepo_item  ).
          lo_po_item_api->get_datax( IMPORTING ex_data = ls_mepo_itemx ).

          READ TABLE it_sto_item_to_update WITH KEY ebelp = ls_mepo_item-ebelp ASSIGNING FIELD-SYMBOL(<ls_sto_item_to_update>).
          IF sy-subrc = 0.
            ls_mepo_item-zz1_abgru_pdi  = COND #( WHEN <ls_sto_item_to_update>-menge > <ls_sto_item_to_update>-lfimg
                                                    THEN VALUE #( ms_hm_data-sto_part_rej[ bsart = <ls_sto_item_to_update>-bsart ]-default_reason OPTIONAL )
                                                  WHEN <ls_sto_item_to_update>-menge - <ls_sto_item_to_update>-lfimg = 0
                                                    THEN '' ).
            ls_mepo_itemx-zz1_abgru_pdi = abap_true.

            ls_mepo_item-zz1_menge_pdi  = <ls_sto_item_to_update>-menge - <ls_sto_item_to_update>-lfimg.
            ls_mepo_itemx-zz1_menge_pdi = abap_true.

            ls_mepo_item-zz1_menge_pdiu  = COND #( WHEN ls_mepo_item-zz1_menge_pdi IS NOT INITIAL
                                                     THEN <ls_sto_item_to_update>-meins
                                                   ELSE '' ).
            ls_mepo_itemx-zz1_menge_pdiu = abap_true.

            lo_po_item_api->set_data(  ls_mepo_item  ).
            lo_po_item_api->set_datax( ls_mepo_itemx ).

            "->CNC STO status update S4 to OMNI
            zcl_sd_sto_status_update=>update_from_sto_rejetion( EXPORTING is_mepo_item = ls_mepo_item CHANGING co_po_item_api = lo_po_item_api ).
            "<-CNC STO status update S4 to OMNI
          ENDIF.

          lo_po_item_api->my_parent ?= lo_po_api.
          ls_model-model ?= lo_po_item_api.
          APPEND ls_model TO lt_models.

        ENDLOOP.

        IF lt_models[] IS NOT INITIAL.
          lo_po_api->if_flush_transport_mm~start( EXPORTING im_models = lt_models[] EXCEPTIONS illegal_call = 1 error = 2 OTHERS = 3 ).
          IF sy-subrc <> 0.
            MESSAGE e003(zsd_sto_rejection) WITH lv_ebeln
              INTO lv_dummy_msg ##NEEDED.
            add_message( iv_vbeln ).
            RETURN.
          ENDIF.
        ENDIF.

        lo_po_api->po_post( EXPORTING im_uncomplete = mmpur_no im_no_commit = mmpur_yes im_commit_wait = mmpur_no
                            EXCEPTIONS failure = 1 OTHERS = 2 ).

        IF sy-subrc <> 0.
          MESSAGE e003(zsd_sto_rejection) WITH lv_ebeln
            INTO lv_dummy_msg ##NEEDED.
          add_message( iv_vbeln ).
          RETURN.
        ENDIF.

        lo_po_api->po_close( ).

      ENDIF.

    ENDIF.

  ENDMETHOD.
ENDCLASS.