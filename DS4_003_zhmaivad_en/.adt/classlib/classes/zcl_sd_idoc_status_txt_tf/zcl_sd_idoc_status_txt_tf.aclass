CLASS zcl_sd_idoc_status_txt_tf DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_amdp_marker_hdb .
    CLASS-METHODS: get_latest_status_txt FOR TABLE FUNCTION zsd_i_status_txt_tf.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_sd_idoc_status_txt_tf IMPLEMENTATION.
  METHOD get_latest_status_txt BY DATABASE FUNCTION FOR HDB LANGUAGE SQLSCRIPT OPTIONS READ-ONLY USING edids.
*----------------------------------------------------------------------*
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 26.09.2022 | 13444     : [Build] - Stock reconciliation with WMS *
*              |            | DS4K929106                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 14.05.2023 | 20960     : [Feature] [Build] - Stock re *
*              |            | DS4K951475                               *
*----------------------------------------------------------------------*
    rank_table =
      select mandt    AS client,
             docnum,
             countr,
             status,
             statxt,
             credat,
             cretim,
             stapa1,
             stapa2,
             stapa3,
             stapa4,
             RANK ( ) OVER ( PARTITION BY docnum, status
                                 ORDER BY credat desc, cretim desc, countr desc) AS rank
        FROM edids;

    RETURN
      SELECT client,
             docnum,
             status,
             statxt,
             credat,
             cretim,
             stapa1,
             stapa2,
             stapa3,
             stapa4
        FROM :rank_table
       WHERE rank = 1;
  ENDMETHOD.

ENDCLASS.