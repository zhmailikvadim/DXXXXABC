CLASS zcl_sd_inb_omni_shipmnt_conf DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.

    CONSTANTS gc_idoctp TYPE edidc-idoctp VALUE 'FSHDLV04' ##NO_TEXT.
    CONSTANTS:
      BEGIN OF gc_segnam,
        header   TYPE edilsegtyp VALUE 'E1EDL20',
        control  TYPE edilsegtyp VALUE 'E1EDL18',
        deadline TYPE edilsegtyp VALUE 'E1EDT13',
        hu_hdr   TYPE edilsegtyp VALUE 'E1EDL37',
        hu_cust  TYPE edilsegtyp VALUE 'ZE1EDL37',
        hu_item  TYPE edilsegtyp VALUE 'E1EDL44',
        e1edl20  TYPE edilsegtyp VALUE 'E1EDL20',
        e1edl24  TYPE edilsegtyp VALUE 'E1EDL24',
        ze1edl24 TYPE edilsegtyp VALUE 'ZE1EDL24',
      END OF gc_segnam.
    CONSTANTS:
      BEGIN OF gc_qualf,
        pgi   TYPE e1edl18-qualf VALUE 'PGI',
        tstmp TYPE e1edl18-qualf VALUE '006',
      END OF gc_qualf.
    CONSTANTS:
      BEGIN OF gc_clissifier,
        shpmnt_confrm TYPE zsd_t_omni_shpcf-clasfr VALUE '0001',
        return_order  TYPE zsd_t_omni_shpcf-clasfr VALUE '0003',
      END OF gc_clissifier.
    DATA gv_success TYPE abap_bool READ-ONLY.
    DATA gt_result TYPE bapiret2_t READ-ONLY.

    METHODS constructor.
    METHODS is_omni_shipment_idoc
      IMPORTING
        !is_idoc_control TYPE edidc
      RETURNING
        VALUE(rv_omni)   TYPE abap_bool.
    METHODS update_omni_shipment_confirm
      IMPORTING
        !is_idoc_control TYPE edidc
        !it_idoc_data    TYPE edidd_tt
      RAISING
        cx_uuid_error.
  PROTECTED SECTION.
  PRIVATE SECTION.

    TYPES: mtyp_t_path TYPE STANDARD TABLE OF edilsegtyp.

    TYPES:
      BEGIN OF mtyp_s_customizing,
        sndprn TYPE edi_sndprn,
        sndprt TYPE edi_sndprt,
        mestyp TYPE edi_mestyp,
        mescod TYPE edi_mescod,
        lgort  TYPE lgort_d,
      END OF mtyp_s_customizing.
    TYPES mtyp_ts_edidd              TYPE SORTED TABLE OF edidd WITH NON-UNIQUE KEY docnum segnam psgnum segnum.
    TYPES mtyp_ts_edidd_read_segment TYPE SORTED TABLE OF edidd WITH NON-UNIQUE KEY segnam psgnum.

    DATA:
      mt_cust TYPE STANDARD TABLE OF mtyp_s_customizing.

    METHODS get_segment_sdata IMPORTING iv_index           TYPE sy-tabix DEFAULT 1
                                        iv_previous_segnum TYPE edi_psgnum DEFAULT 0
                                        it_path            TYPE mtyp_t_path
                                        its_idoc           TYPE mtyp_ts_edidd_read_segment
                              RETURNING VALUE(rv_sdata)    TYPE edi_sdata.
ENDCLASS.



CLASS ZCL_SD_INB_OMNI_SHIPMNT_CONF IMPLEMENTATION.


  METHOD constructor.
    " -----------------------------------------------------------------------
    "  R E V I S I O N   H I S T O R Y                                     -
    " -----------------------------------------------------------------------
    " AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
    "              |            | TRANSPORT REQUESTS                       -
    " -----------------------------------------------------------------------
    " TARUSVIT     | 16.02.2023 | 18054  WMS-OMNI shipment confirmation    -
    "              |            | DS4K943844                               -
    " -----------------------------------------------------------------------
    " Kalshzha     | 05.06.2023 | 22023 WMS-OMNI shipment confirmation     -
    "              |            | DS4K954656                               -
    " -----------------------------------------------------------------------
    " KARPOSER     | 11.07.2023 | 23184  OMNI GI enhancement               -
    "              |            | DS4K957984                               -
    " -----------------------------------------------------------------------

    TRY.
        DATA(lt_cust) = zcl_pe_hm_basic=>get_table( '1209' ).
      CATCH zcx_pe_hm.
        CLEAR lt_cust.
    ENDTRY.
    mt_cust = CORRESPONDING #( lt_cust MAPPING sndprn = field1
                                               sndprt = field2
                                               mestyp = field3
                                               mescod = field4
                                               lgort  = field5 ).
  ENDMETHOD.


  METHOD is_omni_shipment_idoc.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* TARUSVIT     | 16.02.2023 | 18054  WMS-OMNI shipment confirmation    *
*              |            | DS4K943844                               *
*----------------------------------------------------------------------*
* Kalshzha     | 05.06.2023 | 22023 WMS-OMNI shipment confirmation    *
*              |            | DS4K954656                               *
*----------------------------------------------------------------------*
    DELETE mt_cust
      WHERE sndprn NE is_idoc_control-sndprn
         OR sndprt NE is_idoc_control-sndprt
         OR mestyp NE is_idoc_control-mestyp
         OR mescod NE is_idoc_control-mescod.

    READ TABLE mt_cust TRANSPORTING NO FIELDS
    WITH KEY sndprn = is_idoc_control-sndprn
             sndprt = is_idoc_control-sndprt
             mestyp = is_idoc_control-mestyp
             mescod = is_idoc_control-mescod.
    IF sy-subrc EQ 0.
      rv_omni = abap_true.
    ENDIF.
  ENDMETHOD.


  METHOD update_omni_shipment_confirm.
    " -----------------------------------------------------------------------
    "  R E V I S I O N   H I S T O R Y                                     -
    " -----------------------------------------------------------------------
    " AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
    "              |            | TRANSPORT REQUESTS                       -
    " -----------------------------------------------------------------------
    " TARUSVIT     | 16.02.2023 | 18054  WMS-OMNI shipment confirmation    -
    "              |            | DS4K943844                               -
    " -----------------------------------------------------------------------
    " Kalshzha     | 05.06.2023 | 22023 WMS-OMNI shipment confirmation    -
    "              |            | DS4K954656                               -
    " -----------------------------------------------------------------------
    " KARPOSER     | 11.07.2023 | 23184  OMNI GI enhancement               -
    "              |            | DS4K957984                               -
    " -----------------------------------------------------------------------
    DATA ls_hdr      TYPE e1edl20.
    DATA ls_ctrl     TYPE e1edl18.
    DATA ls_deadln   TYPE e1edt13.
    DATA ls_hu_cust  TYPE ze1edl37.
    DATA ls_hu_item  TYPE e1edl44.
    DATA ls_ze1edl24 TYPE ze1edl24.
    DATA lt_omni     TYPE zsd_tt_omni_shpcf.

    DATA(lts_idoc_data) = CORRESPONDING mtyp_ts_edidd( it_idoc_data ).
    READ TABLE lts_idoc_data ASSIGNING FIELD-SYMBOL(<ls_hdr>)
          WITH KEY docnum = is_idoc_control-docnum
                   segnam = gc_segnam-header.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    " check whether idoc contains segment L18 with QUALF=PGI. Return if not.
    LOOP AT lts_idoc_data ASSIGNING FIELD-SYMBOL(<ls_ctrl>)
          WHERE docnum = <ls_hdr>-docnum   AND
                segnam = gc_segnam-control AND
                psgnum = <ls_hdr>-segnum.
      ls_ctrl = <ls_ctrl>-sdata.
      IF ls_ctrl-qualf = gc_qualf-pgi.
        DATA(lv_pgi) = abap_true.
        EXIT.
      ENDIF.
    ENDLOOP.
    IF lv_pgi IS INITIAL.
      RETURN.
    ENDIF.

    " get T13 segment with qualf=006 for deadlines
    CLEAR: ls_deadln.
    LOOP AT lts_idoc_data ASSIGNING FIELD-SYMBOL(<ls_deadln>)
          WHERE docnum = <ls_hdr>-docnum   AND
                segnam = gc_segnam-deadline AND
                psgnum = <ls_hdr>-segnum.
      ls_deadln = <ls_deadln>-sdata.
      IF ls_deadln-qualf = gc_qualf-tstmp.
        EXIT.
      ENDIF.
      CLEAR: ls_deadln. "to have empty deadline struc if there is no segment with QUALF=006
    ENDLOOP.

    " fill constant part of OMNI shipment confirmation
    ls_hdr = <ls_hdr>-sdata.

    ls_ze1edl24 = get_segment_sdata( it_path  = VALUE #( ( gc_segnam-e1edl20 )
                                                         ( gc_segnam-e1edl24 )
                                                         ( gc_segnam-ze1edl24 ) )
                                     its_idoc = CORRESPONDING #( lts_idoc_data ) ).

    LOOP AT mt_cust ASSIGNING FIELD-SYMBOL(<ls_cust>).
      DATA(ls_omni) = VALUE zsd_t_omni_shpcf( clasfr = gc_clissifier-shpmnt_confrm
                                              ord_id = ls_hdr-vbeln
                                              wms_dt = ls_ze1edl24-ziedd
                                              wms_tm = ls_ze1edl24-ziedz
                                              werks  = substring_after( val = is_idoc_control-sndpor sub = `_` )
                                              lgort  = <ls_cust>-lgort ).

      LOOP AT lts_idoc_data ASSIGNING FIELD-SYMBOL(<ls_hu_hdr>) "#EC CI_NESTED
            WHERE docnum = <ls_hdr>-docnum   AND
                  segnam = gc_segnam-hu_hdr AND
                  psgnum = <ls_hdr>-segnum.
        ASSIGN lts_idoc_data[ docnum = <ls_hdr>-docnum
                              segnam = gc_segnam-hu_cust
                              psgnum = <ls_hu_hdr>-segnum ] TO FIELD-SYMBOL(<ls_hu_cust>).
        IF sy-subrc = 0.
          ls_hu_cust = <ls_hu_cust>-sdata.
        ENDIF.
        LOOP AT lts_idoc_data ASSIGNING FIELD-SYMBOL(<ls_hu_item>) "#EC CI_NESTED
              WHERE docnum = <ls_hdr>-docnum   AND
                    segnam = gc_segnam-hu_item AND
                    psgnum = <ls_hu_hdr>-segnum.
          ls_hu_item = <ls_hu_item>-sdata.
          APPEND VALUE #( BASE ls_omni
                          rec_id    = cl_system_uuid=>create_uuid_c32_static( )
                          track_num = ls_hu_cust-spe_ident_01
                          artc_nr   = ls_hu_item-matnr
                          qty       = ls_hu_item-vemng ) TO lt_omni.
        ENDLOOP.
      ENDLOOP.
    ENDLOOP.

    " schedule insert into custom table
    IF lt_omni IS NOT INITIAL.
      gt_result = zcl_sd_omni_shpcf=>save( lt_omni ).

      IF cl_bapi_msg=>contains_error( gt_result ) = abap_false.
        gv_success = abap_true.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD get_segment_sdata.
    " -----------------------------------------------------------------------
    "  R E V I S I O N   H I S T O R Y                                     -
    " -----------------------------------------------------------------------
    " AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
    "              |            | TRANSPORT REQUESTS                       -
    " -----------------------------------------------------------------------
    " ZHMAIVAD     | 02.08.2023 | 23184 WMS-OMNI shipment confirmation     -
    "              |            | DS4K957984                               -
    " -----------------------------------------------------------------------
    TRY.
        DATA(lv_segnum) = its_idoc[ segnam = it_path[ iv_index ]  psgnum = iv_previous_segnum ]-segnum.
        IF lines( it_path ) > iv_index.
          rv_sdata = get_segment_sdata( iv_index           = iv_index + 1
                                        iv_previous_segnum = lv_segnum
                                        its_idoc           = its_idoc
                                        it_path            = it_path ).
        ELSE.
          rv_sdata = its_idoc[ segnam = it_path[ iv_index ]  segnum = lv_segnum ]-sdata.
        ENDIF.
      CATCH cx_sy_itab_line_not_found.
        CLEAR rv_sdata.
    ENDTRY.
  ENDMETHOD.
ENDCLASS.