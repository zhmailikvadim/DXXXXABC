class ZCL_SD_OMNI_SHPCF definition
  public
  final
  create public .

public section.

  class-methods SAVE
    importing
      !IV_IN_UPDATE_TASK type ABAP_BOOL default ABAP_TRUE
      !IT_OMNI type ZSD_TT_OMNI_SHPCF
    returning
      value(RT_RESULT) type BAPIRET2_T .
  class-methods CLASS_CONSTRUCTOR .
protected section.
private section.
  class-data GT_CHECK_FIELDS type FIELDNAME_TAB .
  class-methods CHECK
    importing
      !IT_OMNI type ZSD_TT_OMNI_SHPCF
    returning
      value(RT_RESULT) type BAPIRET2_T .
ENDCLASS.



CLASS ZCL_SD_OMNI_SHPCF IMPLEMENTATION.


  METHOD check.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 11.07.2023 | 23184  OMNI GI enhancement               *
*              |            | DS4K957984                               *
*----------------------------------------------------------------------*
    FIELD-SYMBOLS: <lv_field> TYPE any.

    LOOP AT it_omni ASSIGNING FIELD-SYMBOL(<ls_omni>).
      LOOP AT gt_check_fields ASSIGNING FIELD-SYMBOL(<lv_fieldname>). "#EC CI_NESTED
        ASSIGN COMPONENT <lv_fieldname> OF STRUCTURE <ls_omni> TO <lv_field>.
        CHECK sy-subrc = 0 AND <lv_field> IS INITIAL.

        cl_reca_ddic_dtel=>get_text_by_field(
          EXPORTING
            id_field     = <lv_field>
          IMPORTING
            ed_scrtext_m = DATA(lv_text)
          EXCEPTIONS
            not_found    = 1
            OTHERS       = 2 ).
        IF sy-subrc = 0.
          lv_text = |{ lv_text } ({ <lv_fieldname> })|.
        ELSE.
          lv_text = <lv_fieldname>.
        ENDIF.

        MESSAGE e001(zsd_omni) WITH lv_text INTO sy-msgli.
        cl_bapi_msg=>add_message( CHANGING ct_message = rt_result ).
      ENDLOOP.
    ENDLOOP.
  ENDMETHOD.


  METHOD class_constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 11.07.2023 | 23184  OMNI GI enhancement               *
*              |            | DS4K957984                               *
*----------------------------------------------------------------------*
    TRY.
        DATA(lt_fields) = zcl_pe_hm_basic=>get_table( '1939' ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.
    gt_check_fields = VALUE #( FOR <ls_wa> IN lt_fields ( <ls_wa>-field1 ) ).
  ENDMETHOD.


  METHOD save.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KARPOSER     | 11.07.2023 | 23184  OMNI GI enhancement               *
*              |            | DS4K957984                               *
*----------------------------------------------------------------------*
    CHECK it_omni IS NOT INITIAL.

    rt_result = check( it_omni ).

    CHECK cl_bapi_msg=>contains_error( rt_result ) = abap_false.

    IF iv_in_update_task = abap_true.
      CALL FUNCTION 'ZSD_FM_OMNI_SHPMT_CONF_INS' IN UPDATE TASK
        EXPORTING
          it_omni_shpcf = it_omni.
    ELSE.
      CALL FUNCTION 'ZSD_FM_OMNI_SHPMT_CONF_INS'
        EXPORTING
          it_omni_shpcf = it_omni
        EXCEPTIONS
          error_message = 1.
      IF sy-subrc <> 0.
        cl_bapi_msg=>add_message( CHANGING ct_message = rt_result ).
      ENDIF.
    ENDIF.
  ENDMETHOD.
ENDCLASS.