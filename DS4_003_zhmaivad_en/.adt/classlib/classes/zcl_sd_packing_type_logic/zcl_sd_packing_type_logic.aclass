class ZCL_SD_PACKING_TYPE_LOGIC definition
  public
  final
  create public .

public section.

  types:
    BEGIN OF gtyp_s_filter_excl,
        zsd_doccat  TYPE zsd_doccat,
        zsd_doctype TYPE zsd_doctype,
      END OF gtyp_s_filter_excl .
  types:
    BEGIN OF gtyp_s_filter,
        vkorg           TYPE vkorg,
        zsd_pal_cc_dch  TYPE zsd_pal_cc_dch,
        zz1_zfsh_mg_at1 TYPE fsh_mg_attribute1,
        werks           TYPE reswk,
        pstyv           TYPE pstyv,
      END OF gtyp_s_filter .
  types:
    BEGIN OF gtyp_s_output,
        zz1_zpacking_type     TYPE zz1_zpacking_type,
        zsd_cartonquant       TYPE zsd_cartonquant,
        zz1_zpacking_type_sdi TYPE zz1_zpacking_type,
        horizon_months        TYPE zsd_horizon_months,
      END OF gtyp_s_output .

  data MV_UOM type MEINH .

  methods CONSTRUCTOR .
  methods GET_MAT_QUANT
    importing
      !IV_MATNR type MARA-MATNR
    returning
      value(RV_MAT_QUANT) type MARM-UMREZ .
  methods READ_PACKING_TYPE_BRF
    importing
      !IS_FILTER type GTYP_S_FILTER
    returning
      value(RS_OUTPUT) type GTYP_S_OUTPUT .
  methods IS_EXCLUDED
    importing
      !IV_DOCCAT type ZSD_DOCCAT
      !IV_DOCTYPE type ZSD_DOCTYPE
    returning
      value(RV_BOOL) type ABAP_BOOL .
  methods READ_BRF_EXCL
    importing
      !IS_FILTER type GTYP_S_FILTER_EXCL
    returning
      value(RV_BOOL) type ABAP_BOOL .
  methods GET_PACKING_TYPE
    importing
      !IV_VKORG type VKORG
      !IV_VTWEG type ZSD_PAL_CC_DCH
      !IV_MATDIV type ZZ1_ZFSH_MG_AT_1
      !IV_DOCCAT type ZSD_DOCCAT
      !IV_MATNR type MATNR
      !IV_DOCTYPE type ZSD_DOCTYPE
      !IV_QUANT type MENG15
      !IV_WERKS type WERKS_D
      !IV_PSTYV type PSTYV
      !IV_DELIVERY_DATE type EDATU
    returning
      value(RV_PACKTYPE) type ZZ1_ZPACKING_TYPE .
  PROTECTED SECTION.
private section.

  methods GET_PACKING_TYPE_BY_MANTR
    importing
      !IV_MATNR type MATNR
      !IV_QUANT type MENG15
      !IV_CARTONQUANT type ZSD_CARTONQUANT
    returning
      value(RV_PACKTYPE) type ZZ1_ZPACKING_TYPE .
ENDCLASS.



CLASS ZCL_SD_PACKING_TYPE_LOGIC IMPLEMENTATION.


  METHOD READ_PACKING_TYPE_BRF.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 08.12.2022 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*
    DATA: lt_out   TYPE STANDARD TABLE OF gtyp_s_output.
    TRY.
        zcl_pe_hm_complex=>get_table( EXPORTING iv_id           = '0239FC3B8D041EDD9DD7BA050759BF59'
                                        is_filter = is_filter
                                        iv_mapping_type = zcl_pe_hm_complex=>gc_s_mapping_type-names
                              IMPORTING et_table        = lt_out ).
      CATCH zcx_pe_hm.
        CLEAR lt_out.
    ENDTRY.

    IF lt_out IS NOT INITIAL.
      rs_output = lt_out[ 1 ].
    ENDIF.
  ENDMETHOD.


  METHOD GET_MAT_QUANT.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 08.12.2022 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*
    SELECT SINGLE quantitynumerator
      FROM i_materialunitofmeasure WITH PRIVILEGED ACCESS
      WHERE material EQ @iv_matnr
      AND alternativeunit EQ @mv_uom
      INTO @rv_mat_quant.
    IF sy-subrc NE 0.
      CLEAR rv_mat_quant.
    ENDIF.
  ENDMETHOD.


  METHOD get_packing_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 08.12.2022 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*
* KALSHZHA     | 07.03.2023 | 18609     : [DEFECT] PAL Enhancemen      *
*              |            | DS4K945601                               *
*----------------------------------------------------------------------*
* BALUNAYN     | 11.06.2023 | 21261     : PAL Enhancement Piece Pick   *
*              |            | DS4K952194                               *
*----------------------------------------------------------------------*
    DATA: lv_new_date TYPE d.

    IF read_brf_excl( VALUE #( zsd_doccat =  iv_doccat zsd_doctype = iv_doctype ) )  EQ abap_true.
      RETURN.
    ENDIF.

    DATA(ls_data) = read_packing_type_brf( VALUE #(
     vkorg = iv_vkorg
     zsd_pal_cc_dch = iv_vtweg
     zz1_zfsh_mg_at1 = iv_matdiv
     pstyv = iv_pstyv
     werks = iv_werks
    ) ).

    IF ls_data IS INITIAL.
      RETURN.
    ENDIF.

    IF ls_data-zz1_zpacking_type IS NOT INITIAL.
      rv_packtype = ls_data-zz1_zpacking_type.
    ELSEIF ls_data-horizon_months IS NOT INITIAL.

      CALL FUNCTION 'RE_ADD_MONTH_TO_DATE'
        EXPORTING
          months  = ls_data-horizon_months
          olddate = sy-datum
        IMPORTING
          newdate = lv_new_date.

      IF iv_delivery_date <=  lv_new_date.
        rv_packtype = get_packing_type_by_mantr( iv_matnr       = iv_matnr
                                                 iv_cartonquant = ls_data-zsd_cartonquant
                                                 iv_quant       = iv_quant ).

      ELSE.
        rv_packtype = ls_data-zz1_zpacking_type_sdi.
      ENDIF.
    ELSE.
      rv_packtype = get_packing_type_by_mantr( iv_matnr       = iv_matnr
                                               iv_cartonquant = ls_data-zsd_cartonquant
                                               iv_quant       = iv_quant ).

    ENDIF.
  ENDMETHOD.


  METHOD IS_EXCLUDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 08.12.2022 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*
    DATA ls_filter TYPE gtyp_s_filter_excl.

    ls_filter-zsd_doctype  = iv_doctype.
    ls_filter-zsd_doccat = iv_doccat.

    IF ls_filter-zsd_doccat  IS INITIAL.
      ls_filter-zsd_doccat  = '*'.
    ENDIF.

    IF ls_filter-zsd_doctype IS INITIAL.
      ls_filter-zsd_doctype = '*'.
    ENDIF.

    rv_bool = read_brf_excl( ls_filter ).

    IF rv_bool IS INITIAL AND ls_filter-zsd_doctype NE '*'.
      ls_filter-zsd_doctype = '*'.
      rv_bool = read_brf_excl( ls_filter ).
    ENDIF.

    IF rv_bool IS INITIAL AND ls_filter-zsd_doccat NE '*'.
      ls_filter-zsd_doccat = '*'.
      rv_bool = read_brf_excl( ls_filter ).
    ENDIF.
  ENDMETHOD.


  METHOD READ_BRF_EXCL.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 08.12.2022 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*
    TYPES: BEGIN OF ltyp_s_data,
             zsd_exclude TYPE zsd_exclude,
           END OF ltyp_s_data.
    DATA: lt_out   TYPE STANDARD TABLE OF ltyp_s_data.
    TRY.
        zcl_pe_hm_complex=>get_table( EXPORTING iv_id           = '0239FC3B8D041EDD9DD726B61C711F59'
                                        is_filter = is_filter
                                        iv_mapping_type = zcl_pe_hm_complex=>gc_s_mapping_type-names
                              IMPORTING et_table        = lt_out ).
      CATCH zcx_pe_hm.
        CLEAR lt_out.
    ENDTRY.

    IF lt_out IS NOT INITIAL.
      rv_bool = lt_out[ 1 ].
    ENDIF.

  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 06.01.2023 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*

    TRY.
        mv_uom = zcl_pe_hm_basic=>get_value( '1024' ).
      CATCH zcx_pe_hm.
        CLEAR mv_uom.
    ENDTRY.
  ENDMETHOD.


  METHOD get_packing_type_by_mantr.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BALUNAYN     | 11.06.2023 | 21261     : PAL Enhancement Piece Pick   *
*              |            | DS4K952194                               *
*----------------------------------------------------------------------*
    DATA(lv_mat_quant) = get_mat_quant( iv_matnr ).
    DATA(lv_carton_quant) = CONV meng15( iv_cartonquant ).
    IF iv_quant IS INITIAL
    OR lv_mat_quant IS INITIAL
    OR lv_carton_quant IS INITIAL.
      RETURN.
    ENDIF.
    IF iv_quant GE lv_mat_quant
    AND ( iv_quant MOD lv_mat_quant ) EQ 0.
      rv_packtype = 'FC'.
    ELSEIF iv_quant GE ( lv_carton_quant * lv_mat_quant ).
      rv_packtype = 'FC'.
    ELSEIF iv_quant LT lv_mat_quant.
      rv_packtype = 'PP'.
    ELSEIF iv_quant LT ( lv_carton_quant * lv_mat_quant ).
      rv_packtype = 'PP'.
    ENDIF.
  ENDMETHOD.
ENDCLASS.