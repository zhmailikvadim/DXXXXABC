class ZCL_SD_PAL_STO_PACKING_TYPE definition
  public
  final
  create public .

public section.

  interfaces IF_BADI_INTERFACE .
  interfaces IF_MM_PUR_S4_PO_FLDCNTRL .
  interfaces IF_MM_PUR_S4_PO_MODIFY_ITEM .
  interfaces IF_EX_ME_TRIGGER_ATP .

  class-data GV_TRIGGER_ATP type ABAP_BOOL .
protected section.
private section.
ENDCLASS.



CLASS ZCL_SD_PAL_STO_PACKING_TYPE IMPLEMENTATION.


  METHOD if_ex_me_trigger_atp~trigger_atp.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 14.09.2023 | 21261     : FB_21261_SD_CR PAL Enhancement Piece Pick & Full Cas *
*              |            | DS4K952194                               *
*----------------------------------------------------------------------*

    IF IM_OFEKPO-zz1_zpacking_type_po_pdi IS NOT INITIAL AND IM_OFEKPO-zz1_zpacking_type_po_pdi NE im_ekpo-zz1_zpacking_type_po_pdi.
      ch_charac_changed = abap_true.
    ENDIF.
  ENDMETHOD.


  METHOD if_mm_pur_s4_po_fldcntrl~modify_fieldcontrols.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 08.12.2022 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*
* KALSHZHA     | 07.03.2023 | 18609     : [DEFECT] PAL Enhancemen      *
*              |            | DS4K945601                               *
*----------------------------------------------------------------------*
    DATA:
      lv_pt_filed TYPE mmpur_bd_fs_field.

    DATA(lo_helper) = NEW zcl_sd_packing_type_logic( ).

    TRY.
        lv_pt_filed = zcl_pe_hm_basic=>get_value( '1154' ).
      CATCH zcx_pe_hm INTO DATA(lo_exc).
        MESSAGE lo_exc.
        RETURN.
    ENDTRY.

    IF lo_helper->is_excluded( iv_doccat =  purchaseorderitem-purchaseordercategory  iv_doctype = purchaseorder-purchaseordertype ) EQ abap_true.
     data(lv_status) = cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_suppress.
    else.
      lv_status = cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~FS_DISPLAY.
    ENDIF.

    READ TABLE fieldselection_table ASSIGNING FIELD-SYMBOL(<ls_fields>) WITH KEY field = lv_pt_filed.
    IF sy-subrc = 0.
      <ls_fields>-fieldstatus = lv_status.
    ENDIF.
  ENDMETHOD.


  METHOD if_mm_pur_s4_po_modify_item~modify_item.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KALSHZHA     | 08.12.2022 | 15624     : [Development] PAL Enhancemen *
*              |            | DS4K937655                               *
*----------------------------------------------------------------------*
* KALSHZHA     | 12.05.2023 | 21261     :CR PAL Enhancement Piece Pick & Full Cas *
*              |            | DS4K952194                               *
*----------------------------------------------------------------------*
* BALUNYAN     | 13.07.2023 | 21261     : CR PAL Enhancement Piece Pic *
*              |            | DS4K952194                               *
*----------------------------------------------------------------------*

    DATA: ls_ekpv          TYPE ekpv,
          lt_schedule_line TYPE if_ex_mmpur_final_check_po=>b_mmpur_t_purchorderschedline.

    PERFORM get_ekpv IN PROGRAM saplmepo USING    purchaseorderitem-purchaseorderitem
                                      CHANGING ls_ekpv.
    IF  ls_ekpv IS NOT INITIAL.

      lt_schedule_line = purorderscheduleline_table.

      IF lt_schedule_line IS NOT INITIAL.

        SORT lt_schedule_line BY schedulelinedeliverydate DESCENDING.
        READ TABLE lt_schedule_line ASSIGNING FIELD-SYMBOL(<ls_schedule_lines>) INDEX 1.

        IF sy-subrc = 0.
          DATA(lo_helper) = NEW zcl_sd_packing_type_logic( ).
          purchaseorderitemchange-zz1_zpacking_type_po_pdi = lo_helper->get_packing_type(
            iv_vkorg = ls_ekpv-vkorg
            iv_vtweg = CONV #( purchaseorder-ext-zz1_zsales_channel_pdh )
            iv_matdiv = purchaseorderitem-ext-zz1_zfsh_mg_at_1_pdi
            iv_matnr = purchaseorderitem-material
            iv_doccat = purchaseorderitem-purchaseordercategory
            iv_doctype = purchaseorder-purchaseordertype
            iv_quant = CONV #( purchaseorderitem-orderquantity )
            iv_werks = purchaseorder-supplyingplant
            iv_pstyv = CONV #( purchaseorderitem-purchaseorderitemcategory )
            iv_delivery_date = <ls_schedule_lines>-schedulelinedeliverydate
          ).
        ENDIF.
      ENDIF.
    ENDIF.
  ENDMETHOD.
ENDCLASS.