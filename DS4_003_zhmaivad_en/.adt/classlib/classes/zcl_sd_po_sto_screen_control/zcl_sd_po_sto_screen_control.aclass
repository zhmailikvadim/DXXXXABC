class ZCL_SD_PO_STO_SCREEN_CONTROL definition
  public
  final
  create private .

public section.

  methods GET_CONFIGS
    exporting
      !ET_SCR_FLD_CONFIGS type ZSD_TT_SCR_FLD_CONFIGS .
  class-methods GET_INSTANCE
    importing
      !IV_BUSINESS_CONTEXT type CFD_BUSINESS_CONTEXT
    returning
      value(RO_ME) type ref to ZCL_SD_PO_STO_SCREEN_CONTROL .
  methods PROCESS
    importing
      !IS_PURCHASEORDER type IF_EX_MMPUR_FINAL_CHECK_PO=>B_MMPUR_S_PURCHASEORDER
      !IS_PURCHASEORDERITEM type IF_EX_MMPUR_FINAL_CHECK_PO=>GTY_S_PURORDERITEM optional
    changing
      !CT_FIELDSELECTION_TABLE type MMPUR_T_PO_FLDSEL .
  PROTECTED SECTION.
PRIVATE SECTION.

  DATA mv_is_po_tcode TYPE abap_bool .
  CLASS-DATA gv_business_context TYPE cfd_business_context .
  DATA mt_scr_fld_configs TYPE zsd_tt_scr_fld_configs .
  DATA mr_fields_to_del TYPE RANGE OF mmpur_bd_fs_field .
  CONSTANTS: mc_doc_category TYPE bstyp VALUE 'F'.
  CONSTANTS:
    BEGIN OF mc_field_status,
      optinal   TYPE mmpur_bd_fs_fieldstatus VALUE 'O',
      mandatory TYPE mmpur_bd_fs_fieldstatus VALUE 'M',
      displayed TYPE mmpur_bd_fs_fieldstatus VALUE 'D',
    END OF mc_field_status .
  CONSTANTS:
    BEGIN OF mc_business_context_hm,
      header TYPE cfd_business_context VALUE 'EKKO',
      item   TYPE cfd_business_context VALUE 'EKPO',
    END OF mc_business_context_hm .
  CONSTANTS:
    BEGIN OF mc_tcode,
      me21n TYPE syst_tcode VALUE 'ME21N',
      me22n TYPE syst_tcode VALUE 'ME22N',
      me23n TYPE syst_tcode VALUE 'ME23N',
    END OF mc_tcode .
  CLASS-DATA go_me TYPE REF TO zcl_sd_po_sto_screen_control .

  METHODS filter_configs .
  METHODS constructor
    IMPORTING
      !iv_business_context TYPE cfd_business_context .
  METHODS prepare_configs .
  METHODS map_configs .
  METHODS clean .
  METHODS initialize
    IMPORTING
      !iv_business_context TYPE cfd_business_context .
  METHODS get_hm_info .
ENDCLASS.



CLASS ZCL_SD_PO_STO_SCREEN_CONTROL IMPLEMENTATION.


  METHOD clean.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 28.10.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*

    CLEAR:
      gv_business_context,
      mr_fields_to_del,
      mt_scr_fld_configs,
      mv_is_po_tcode.

  ENDMETHOD.


  METHOD get_hm_info.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 27.10.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*
* ISAICSVI     | 23.02.2023 | 17892     : Feature - [Build] - PO custo *
*              |            | DS4K943439                               *
*----------------------------------------------------------------------*


    TYPES: BEGIN OF ltyp_s_scr_fld_configs,
             bsart              TYPE bsart,
             zsd_document_level TYPE zsd_document_level,
             zsd_field_name     TYPE zsd_field_name,
             zsd_field_status   TYPE zsd_field_status,
             bstyp              TYPE bstyp,
           END OF ltyp_s_scr_fld_configs.

    DATA: lt_scr_fld_configs TYPE STANDARD TABLE OF ltyp_s_scr_fld_configs.

    TRY.
        IF mv_is_po_tcode = abap_true.

          zcl_pe_hm_complex=>get_table( EXPORTING iv_id = '0239FC3B8D041EDDAA910624E7879F59'
                                        IMPORTING et_table = lt_scr_fld_configs ).

          mt_scr_fld_configs = CORRESPONDING #( lt_scr_fld_configs
                                 MAPPING business_context = zsd_document_level
                                         fieldname        = zsd_field_name
                                         esart            = bsart
                                         fieldstatus      = zsd_field_status
                                         doc_category     = bstyp    ).


          IF mt_scr_fld_configs IS NOT INITIAL.
            "Only the first field configuration will be applied
            SORT mt_scr_fld_configs BY business_context esart fieldname sequence doc_category.
            DELETE ADJACENT DUPLICATES FROM mt_scr_fld_configs  COMPARING business_context esart fieldname.
          ENDIF.

        ENDIF.

      CATCH zcx_pe_hm.
        CLEAR mt_scr_fld_configs.

    ENDTRY.

  ENDMETHOD.


  METHOD get_instance.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 01.11.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*

    IF go_me IS NOT BOUND OR
       gv_business_context <> iv_business_context.
      go_me = NEW #( iv_business_context ).
    ENDIF.

    ro_me = go_me.

  ENDMETHOD.


  METHOD initialize.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 28.10.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*

    CONSTANTS:
      lc_i  TYPE char1 VALUE 'I',
      lc_eq TYPE char2 VALUE 'EQ'.

    DATA:
          lt_fields_to_del TYPE STANDARD TABLE OF mmpur_bd_fs_field.

    gv_business_context = iv_business_context.

    IF ( sy-tcode = mc_tcode-me21n OR
         sy-tcode = mc_tcode-me22n OR
         sy-tcode = mc_tcode-me23n ).
      mv_is_po_tcode = abap_true.
    ENDIF.

    lt_fields_to_del = VALUE #( ( 'ZZ1_ZVTWEG_PDH' ) ( 'ZZ1_LPRIO_PDH' ) ( 'ZZ1_ORDER_REASON' ) ).

    mr_fields_to_del = VALUE #( FOR lv_fields_to_del IN lt_fields_to_del ( sign   = lc_i
                                                                           option = lc_eq
                                                                           low    = lv_fields_to_del ) ).

  endmethod.


  METHOD process.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 02.11.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*
* ISAICSVI     | 22.02.2023 | 16859     : Populating Realistic         *
*              |            | DS4K940307                              *
*----------------------------------------------------------------------*

*   Value for FIELDSTATUS can be set to anyone of the following
*   '-' Field is Hidden
*   '*' Field is only Displayed, which means no entry is possible
*   '+' Field is Mandatory, which means an entry must be made
*   '.' Field is Optional, which means an entry may be made

    IF mv_is_po_tcode = abap_true.

      SORT mt_scr_fld_configs BY esart fieldname business_context.

      LOOP AT ct_fieldselection_table ASSIGNING FIELD-SYMBOL(<ls_fieldselection>).

        "Hide fields to delete
        IF <ls_fieldselection> IN mr_fields_to_del.
          <ls_fieldselection>-fieldstatus = cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_suppress.
          CONTINUE.
        ENDIF.

        READ TABLE mt_scr_fld_configs WITH KEY esart            = is_purchaseorder-purchaseordertype
                                               fieldname        = <ls_fieldselection>-field
                                               ASSIGNING FIELD-SYMBOL(<ls_scr_fld_config>) BINARY SEARCH.
        "Skip mandatory due to
        "invalid screen control behavior for some mandatory fields.
        "It wil be cheked by validation rules
        IF sy-subrc <> 0 AND
          <ls_fieldselection>-field CP 'ZZ1*'.

          <ls_fieldselection>-fieldstatus = cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_suppress.

        ELSEIF  sy-subrc = 0 AND
          <ls_scr_fld_config>-fieldstatus  <> cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_mandatory.

          <ls_fieldselection>-fieldstatus = <ls_scr_fld_config>-fieldstatus.

        ENDIF.

      ENDLOOP.
    ENDIF.

    clean( ).

  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 01.11.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*

    initialize( iv_business_context ).

    prepare_configs( ).

    filter_configs( ).

  ENDMETHOD.


  METHOD filter_configs.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 01.11.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*
* ISAICSVI     | 23.02.2023 | 17892     : Feature - [Build] - PO custo *
*              |            | DS4K943439                               *
*----------------------------------------------------------------------*

    IF mv_is_po_tcode = abap_true AND
       mt_scr_fld_configs IS NOT INITIAL.

      SORT mt_scr_fld_configs BY business_context.

      DATA(lt_scr_fld_configs_filtered) = VALUE zsd_tt_scr_fld_configs( FOR ls_scr_fld_config IN mt_scr_fld_configs
                                                                        WHERE ( business_context = gv_business_context AND
                                                                                doc_category     = mc_doc_category )
                                                                        ( ls_scr_fld_config ) ) . "#EC CI_STDSEQ

      mt_scr_fld_configs = lt_scr_fld_configs_filtered.

    ENDIF.
  ENDMETHOD.


  METHOD get_configs.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 01.11.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*

    et_scr_fld_configs = mt_scr_fld_configs.

  ENDMETHOD.


  METHOD map_configs.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 02.11.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*
* ISAICSVI     | 22.02.2023 | 16859     : Populating Realistic         *
*              |            | DS4K940307                              *
*----------------------------------------------------------------------*

    IF mv_is_po_tcode = abap_true AND
       mt_scr_fld_configs IS NOT INITIAL.

      DATA(lt_scr_fld_configs_mapped) = VALUE zsd_tt_scr_fld_configs( FOR ls_scr_fld_config IN mt_scr_fld_configs
                                                                      LET ls_mapping = VALUE zsd_s_scr_fld_config(
                                                                                  business_context = SWITCH #( ls_scr_fld_config-business_context
                                                                                                               WHEN mc_business_context_hm-header
                                                                                                               THEN if_mmpur_po_extens_type_consts=>cv_bus_context_header
                                                                                                               WHEN mc_business_context_hm-item
                                                                                                               THEN if_mmpur_po_extens_type_consts=>cv_bus_context_item  )
                                                                                  "Hidden by default
                                                                                  fieldstatus = SWITCH #( ls_scr_fld_config-fieldstatus
                                                                                                          WHEN mc_field_status-mandatory
                                                                                                          THEN cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_mandatory
                                                                                                          WHEN mc_field_status-optinal
                                                                                                          THEN cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_optional
                                                                                                          WHEN mc_field_status-displayed
                                                                                                          THEN cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_display
                                                                                                          ELSE cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_suppress
                                                                                                         ) )
                                                                       IN
                                                                       ( business_context = ls_mapping-business_context
                                                                         esart            = ls_scr_fld_config-esart
                                                                         fieldname        = ls_scr_fld_config-fieldname
                                                                         fieldstatus      = COND #( WHEN ls_scr_fld_config-fieldname IN mr_fields_to_del
                                                                                                    THEN cl_mm_pur_po_fldctr_selection=>if_mmpur_constants_status~fs_suppress
                                                                                                    ELSE ls_mapping-fieldstatus )
                                                                         doc_category    = ls_scr_fld_config-doc_category ) ).

      mt_scr_fld_configs = lt_scr_fld_configs_mapped.

    ENDIF.
  ENDMETHOD.


  METHOD PREPARE_CONFIGS.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 28.10.2022 | 14795     : STO screen control, default valu  *
*              |            | DS4K934079                               *
*----------------------------------------------------------------------*

    get_hm_info( ).

    map_configs( ).

  ENDMETHOD.
ENDCLASS.