CLASS zcl_sd_po_type_io_copy DEFINITION
  PUBLIC
  CREATE PRIVATE .

  PUBLIC SECTION.

    TYPES:
      BEGIN OF gtyp_s_filter,
        vkorg   TYPE vkorg,
        vtweg   TYPE vtweg,
        auart   TYPE auart,
        tabname TYPE tabname,
      END OF gtyp_s_filter .

    CLASS-METHODS get_instance
      RETURNING
        VALUE(ro_instance) TYPE REF TO zcl_sd_po_type_io_copy .
    METHODS copy_fields
      IMPORTING
        !is_filter TYPE gtyp_s_filter
        !is_source TYPE any
      CHANGING
        !cs_target TYPE any .
PROTECTED SECTION.

  TYPES:
    BEGIN OF mtyp_s_copy_config.
      INCLUDE TYPE gtyp_s_filter.
  TYPES:
      fieldname_source TYPE fieldname,
      fieldname_target TYPE fieldname,
    END OF mtyp_s_copy_config .
  TYPES:
    mtyp_ts_copy_config TYPE SORTED TABLE OF mtyp_s_copy_config
                             WITH NON-UNIQUE KEY primary_key
                                      COMPONENTS vkorg vtweg auart tabname.

  DATA mts_copy_config TYPE mtyp_ts_copy_config .

  METHODS get_copy_config
    IMPORTING
      !is_filter       TYPE gtyp_s_filter
    EXPORTING
      !ets_copy_config TYPE mtyp_ts_copy_config
    RAISING
      zcx_sd_po_type_io_copy .
private section.

  class-data GO_INSTANCE type ref to ZCL_SD_PO_TYPE_IO_COPY .
ENDCLASS.



CLASS ZCL_SD_PO_TYPE_IO_COPY IMPLEMENTATION.


  METHOD copy_fields.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 04.12.2023 | 27280     : O2C - Returns Process - ARM  *
*              |            | copy control enhanc. PO Type and IO Copy *
*              |            | DS4K972522                               *
*----------------------------------------------------------------------*

    TRY.
        get_copy_config(
          EXPORTING
            is_filter = is_filter
          IMPORTING
            ets_copy_config = DATA(lts_copy_config) ).

      CATCH zcx_sd_po_type_io_copy.
        RETURN.
    ENDTRY.

    LOOP AT lts_copy_config ASSIGNING FIELD-SYMBOL(<ls_copy_config>).

      ASSIGN COMPONENT <ls_copy_config>-fieldname_source
         OF STRUCTURE is_source
                   TO FIELD-SYMBOL(<lv_source>).
      IF sy-subrc <> 0.
        RETURN.
      ENDIF.

      ASSIGN COMPONENT <ls_copy_config>-fieldname_target
          OF STRUCTURE cs_target
                    TO FIELD-SYMBOL(<lv_target>).
      IF sy-subrc <> 0.
        RETURN.
      ENDIF.

      <lv_target> = <lv_source>.

    ENDLOOP.

  ENDMETHOD.


  METHOD get_copy_config.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 04.12.2023 | 27280     : O2C - Returns Process - ARM  *
*              |            | copy control enhanc. PO Type and IO Copy *
*              |            | DS4K972522                               *
*----------------------------------------------------------------------*

    ets_copy_config = FILTER #( mts_copy_config
                          WHERE vkorg   = is_filter-vkorg
                            AND vtweg   = is_filter-vtweg
                            AND auart   = is_filter-auart
                            AND tabname = is_filter-tabname ).

    IF ets_copy_config IS NOT INITIAL.
      RETURN.
    ENDIF.

    TRY.
        zcl_pe_hm_complex=>get_table(
          EXPORTING
            iv_id     = 'EB209CAFFE131EDEA4CE4C43733A1F5D'
            is_filter = is_filter
          IMPORTING
            et_table  = ets_copy_config ).

      CATCH zcx_pe_hm.
        RAISE EXCEPTION TYPE zcx_sd_po_type_io_copy
          EXPORTING
            textid = zcx_sd_po_type_io_copy=>not_allowed.
    ENDTRY.

    mts_copy_config = VALUE #( BASE mts_copy_config
                               ( LINES OF ets_copy_config ) ).

  ENDMETHOD.


  METHOD get_instance.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 04.12.2023 | 27280     : O2C - Returns Process - ARM  *
*              |            | copy control enhanc. PO Type and IO Copy *
*              |            | DS4K972522                               *
*----------------------------------------------------------------------*

    IF go_instance IS NOT BOUND.
      go_instance = NEW #( ).
    ENDIF.

    ro_instance = go_instance.

  ENDMETHOD.
ENDCLASS.