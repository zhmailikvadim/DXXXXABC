class ZCL_SD_PR_PO_VALID definition
  public
  final
  create private .

public section.

  types:
    BEGIN OF gtyp_check_mode,
             sanity  TYPE abap_bool,
             general TYPE abap_bool,
             header  TYPE abap_bool,
           END OF gtyp_check_mode .

  methods SET_BUSINESS_OBJ_ID
    importing
      !IV_ID type INT4 .
  methods SET_DATA_TO_CHCK
    importing
      !ITS_PR_ITEMS_DATA type ZSD_TT_PR_PO_FIELDS_CHK optional
      !IV_BANFN type BANFN optional
      !IV_BNFPO type BNFPO optional
      !IV_SEL_ITEMS_COUNT type INT4 optional
      !ITS_PROCESSED_PR type ZSD_TT_PROCESSED_PRS optional .
  class-methods GET_INSTANCE
    returning
      value(RO_ME) type ref to ZCL_SD_PR_PO_VALID .
  methods RUN_CHECKS
    importing
      !IS_CHECK_MODE type GTYP_CHECK_MODE
    returning
      value(RV_FAILED) type ABAP_BOOL .
  PROTECTED SECTION.
private section.

  data MTS_PROCESSED_PR type ZSD_TT_PROCESSED_PRS .
  data MV_SEL_ITEMS_COUNT type INT4 .
  constants MC_DAYS_LIMIT type P value 365 ##NO_TEXT.
  data MTS_MESSAGES type ZSD_TT_PR_PO_VAL_MESS .
  constants MC_HEADER_METHOD_MASK type ZSD_PR_PO_METHOD_MASK value 'CHECK_HEADER_*' ##NO_TEXT.
  constants MC_GENERAL_METHOD_MASK type ZSD_PR_PO_METHOD_MASK value 'CHECK_GENERAL_*' ##NO_TEXT.
  constants MC_SANITY_METHOD_MASK type ZSD_PR_PO_METHOD_MASK value 'CHECK_SANITY_*' ##NO_TEXT.
  constants MC_CLASS_MASK type ZSD_PR_PO_CLASS_MASK value '\CLASS=' ##NO_TEXT.
  constants MC_MSG_ID type SYST_MSGID value 'ZSD_PR_PO_CONV' ##NO_TEXT.
  data MS_PR_ITEM_DATA type ZSD_S_PR_PO_FIELDS_CHK .
  data MTS_PR_ITEMS_DATA type ZSD_TT_PR_PO_FIELDS_CHK .
  class-data GO_ME type ref to ZCL_SD_PR_PO_VALID .

  methods CLEAN .
  methods GENERATE_MESSAGES .
  methods CHECK_GENERAL_BLOCKED_ITEMS  ##RELAX.
  methods CHECK_GENERAL_MULT_PR_DOC  ##RELAX.
  methods CHECK_GENERAL_SEL_ITEMS  ##RELAX.
  methods APPEND_MESSAGE
    importing
      !IS_MESSAGE type ZSD_S_PR_PO_VAL_MESS .
  methods CHECK_SANITY_EX_FACT_DATE  ##RELAX.
  methods CHECK_SANITY_MAN_LEAD_TIME  ##RELAX.
  methods CHECK_SANITY_PLANN_DATE  ##RELAX.
  methods CHECK_SANITY_PROMO_PRICE_FACT  ##RELAX.
  methods CHECK_SANITY_SC_SEGM  ##RELAX.
  methods CHECK_SANITY_SHIPP_INSTR  ##RELAX.
  methods CHECK_SANITY_STORAGE_LOCATION  ##RELAX.
  methods CHECK_SANITY_TRADE_CO_MODEL  ##RELAX.
ENDCLASS.



CLASS ZCL_SD_PR_PO_VALID IMPLEMENTATION.


  METHOD check_sanity_sc_segm.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF ms_pr_item_data-zz1_mm_scmsegment_pri IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '007'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ENDIF.

  ENDMETHOD.


  METHOD check_general_blocked_items.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    DATA(lv_blckd_item) = REDUCE i( INIT lv_i = 0
                                    FOR ls_pr_item_data IN mts_pr_items_data
                                    WHERE ( blckd <> '' )
                                    NEXT lv_i = lv_i + 1 ). "#EC CI_SORTSEQ

    IF lv_blckd_item IS NOT INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '017'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error )  ).
    ENDIF.

  ENDMETHOD.


  METHOD check_sanity_trade_co_model.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF ms_pr_item_data-zz1_mm_tradecomodel_pri IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess(  msgno = '008'
                                                                msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ELSE.
      DATA(lv_diff_count) = REDUCE i( INIT lv_i = 0
                                      FOR ls_pr_item_data IN mts_pr_items_data
                                      WHERE ( zz1_mm_tradecomodel_pri <> ms_pr_item_data-zz1_mm_tradecomodel_pri )
                                      NEXT lv_i = lv_i + 1 ). "#EC CI_SORTSEQ
      IF lv_diff_count IS NOT INITIAL.
        append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '009'
                                                                 msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
      ENDIF.
    ENDIF.

  ENDMETHOD.


  METHOD check_general_mult_pr_doc.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*
    DATA lv_pr_count TYPE int4.

    "Only applied for Fiori app(1048)

    IF sy-tcode IS INITIAL.

      LOOP AT mts_pr_items_data ASSIGNING FIELD-SYMBOL(<ls_pr_item_data>).
        AT NEW banfn.
          lv_pr_count += 1.
        ENDAT.
      ENDLOOP.

      IF lv_pr_count > 1.
        append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '019'
                                                                 msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
      ENDIF.

    ENDIF.

  ENDMETHOD.


  METHOD check_general_sel_items.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*
    "Only applied for Fiori app(1048)
    IF sy-tcode IS INITIAL.
      DATA(lv_item_count) = REDUCE i( INIT lv_i = 0
                                      FOR ls_pr_item_data IN mts_pr_items_data
                                      WHERE ( blckd = '' )
                                      NEXT lv_i = lv_i + 1 ). "#EC CI_SORTSEQ

      IF lv_item_count <> mv_sel_items_count.
        append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '020'
                                                                 msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error )  ).
      ENDIF.
    ENDIF.

    IF sy-tcode = 'ME59N'.
      LOOP AT mts_pr_items_data ASSIGNING FIELD-SYMBOL(<ls_pr_item_data>)
                                WHERE loekz <> 'X' AND
                                      statu <> 'B' AND
                                      ebakz <> 'X' AND
                                      blckd = ''. "#EC CI_SORTSEQ

        READ TABLE mts_processed_pr WITH KEY banfn = <ls_pr_item_data>-banfn
                                             bnfpo = <ls_pr_item_data>-bnfpo
                                    TRANSPORTING NO FIELDS.
        IF sy-subrc <> 0.
          append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '021'
                                                                   msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error )  ).
        ENDIF.

      ENDLOOP.
    ENDIF.

  ENDMETHOD.


  METHOD generate_messages.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF mts_messages IS NOT INITIAL.
      LOOP AT mts_messages ASSIGNING FIELD-SYMBOL(<ls_message>).
        CALL METHOD cl_message_mm=>create(
          EXPORTING
            im_msgid         = <ls_message>-msgid
            im_msgty         = <ls_message>-msgty
            im_msgno         = <ls_message>-msgno
            im_msgv1         = <ls_message>-msgv1
            im_msgv2         = <ls_message>-msgv2
            im_msgv3         = <ls_message>-msgv3
            im_msgv4         = <ls_message>-msgv4
            im_force_collect = cl_mmpur_constants=>yes
          EXCEPTIONS
            failure          = 01
            dialog           = 02 ).
        IF sy-subrc = 1 OR sy-subrc = 2.
          MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                  WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
        ENDIF.
      ENDLOOP.
    ENDIF.

  ENDMETHOD.


  METHOD clean.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 09.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*
    CLEAR:
      ms_pr_item_data,
      mts_messages,
      mts_processed_pr,
      mts_pr_items_data,
      mv_sel_items_count.

  ENDMETHOD.


  METHOD get_instance.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF go_me IS NOT BOUND.
      go_me = NEW #( ).
    ENDIF.

    ro_me = go_me.

  ENDMETHOD.


  METHOD append_message.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    DATA:
          ls_message LIKE is_message.

    ls_message = is_message.

    "Default message id
    ls_message-msgid = COND #( WHEN ls_message-msgid IS INITIAL
                                  THEN mc_msg_id
                                  ELSE ls_message-msgid ).

    READ TABLE mts_messages WITH KEY msgid = ls_message-msgid
                                     msgno = ls_message-msgno
                                     msgty = ls_message-msgty
                                     TRANSPORTING NO FIELDS.
    IF sy-subrc <> 0.
      INSERT ls_message INTO TABLE mts_messages.
    ENDIF.

  ENDMETHOD.


  METHOD check_sanity_promo_price_fact.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF ms_pr_item_data-zz1_adi_m_promoflag_pri IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '010'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ENDIF.

  ENDMETHOD.


  METHOD check_sanity_plann_date.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF ms_pr_item_data-zz1_adi_m_planned_pri IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '005'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ENDIF.

  ENDMETHOD.


  METHOD CHECK_SANITY_EX_FACT_DATE.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    DATA:
        lv_date_diff TYPE p.

    IF ms_pr_item_data-zz1_mm_exfactorydt_pri IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '001'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ELSE.
      IF ms_pr_item_data-zz1_mm_exfactorydt_pri < sy-datum.
        append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '002'
                                                                 msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
      ENDIF.
      IF ms_pr_item_data-zz1_mm_exfactorydt_pri > ms_pr_item_data-lfdat.
        append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '003'
                                                                 msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
      ENDIF.

      CALL FUNCTION 'SD_DATETIME_DIFFERENCE'
        EXPORTING
          date1            = sy-datum
          time1            = sy-uzeit
          date2            = ms_pr_item_data-zz1_mm_exfactorydt_pri
          time2            = sy-uzeit
        IMPORTING
          datediff         = lv_date_diff
        EXCEPTIONS
          invalid_datetime = 1
          OTHERS           = 2.

      IF sy-subrc = 0 AND lv_date_diff > mc_days_limit.
        append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '004'
                                                                 msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
      ENDIF.

    ENDIF.

  ENDMETHOD.


  METHOD check_sanity_man_lead_time.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF ms_pr_item_data-zz1_adi_m_leadtimeplan_pri IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '006'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ENDIF.

  ENDMETHOD.


  METHOD check_sanity_shipp_instr.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF ms_pr_item_data-zz1_everss_pri IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '000'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ENDIF.

  ENDMETHOD.


  METHOD check_sanity_storage_location.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF ms_pr_item_data-lgort IS INITIAL.
      append_message( is_message = VALUE zsd_s_pr_po_val_mess( msgno = '013'
                                                               msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ) ).
    ENDIF.

  ENDMETHOD.


  METHOD run_checks.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    DATA:
      lt_methods    TYPE seo_methods,
      lv_curr_class TYPE seoclsname.

    CLEAR: mts_messages.

    TRY.
       "Check if data for validation were provided
        IF ( is_check_mode-general = abap_true AND
             mts_pr_items_data  IS INITIAL )
           OR
           ( is_check_mode-sanity = abap_true AND
             ms_pr_item_data IS INITIAL ).
          RETURN.
        ENDIF.

        lv_curr_class = cl_abap_classdescr=>get_class_name( me ).

        REPLACE mc_class_mask IN lv_curr_class WITH ''.

        lt_methods = cl_oo_class=>get_instance( lv_curr_class )->get_methods( ).

        LOOP AT lt_methods ASSIGNING FIELD-SYMBOL(<ls_method>).
          "General checks
          IF ( is_check_mode-general = abap_true AND
               <ls_method>-cmpname CP mc_general_method_mask )
              OR
             "Sanity checks
             ( is_check_mode-sanity = abap_true AND
               <ls_method>-cmpname CP mc_sanity_method_mask )
             OR
             "Header checks - for future adjustments
             ( is_check_mode-header = abap_true AND
               <ls_method>-cmpname CP mc_header_method_mask ).

            CALL METHOD (<ls_method>-cmpname).

          ENDIF.

        ENDLOOP.

        generate_messages( ).

        rv_failed = boolc( line_exists( mts_messages[ msgty = cl_mmpur_constants=>if_mmpur_constants_general~msg_error ] ) ) . "#EC CI_SORTSEQ

        clean( ).

      CATCH cx_class_not_existent cx_static_check.
        MESSAGE e015.
    ENDTRY.

  ENDMETHOD.


  METHOD set_business_obj_id.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    DATA: lo_handle_mm TYPE REF TO cl_handle_mm.

    cl_handle_manager_mm=>get(
      EXPORTING
        im_id     = iv_id
      IMPORTING
        ex_handle = lo_handle_mm
      EXCEPTIONS failure  = 01 ).

    IF sy-subrc = 0.
      cl_message_mm=>set_current_business_obj( im_business_obj = lo_handle_mm ).
      cl_message_mm=>set_current_context( im_context = 5 ).
    ENDIF.

  ENDMETHOD.


  METHOD set_data_to_chck.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SHNIPALI     | 05.09.2022 | 12942     : [BUILD] - Enhancement PR to PO conversio *
*              |            | DS4K927085                               *
*----------------------------------------------------------------------*

    IF its_pr_items_data IS SUPPLIED.
      mts_pr_items_data = its_pr_items_data.

      IF iv_banfn IS SUPPLIED AND iv_bnfpo IS SUPPLIED.
        ms_pr_item_data  = its_pr_items_data[ banfn = iv_banfn bnfpo = iv_bnfpo ] .
      ENDIF.

    ENDIF.

    IF iv_sel_items_count IS SUPPLIED.
      mv_sel_items_count = iv_sel_items_count.
    ENDIF.

    IF its_processed_pr IS SUPPLIED.
      mts_processed_pr = its_processed_pr.
    ENDIF.

  ENDMETHOD.
ENDCLASS.