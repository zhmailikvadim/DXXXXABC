CLASS zcl_sd_route_schedul_for_kafka DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_oo_adt_classrun .

    TYPES:
      gtyp_range_departure          TYPE RANGE OF i_plant-plant.
    TYPES:
      gtyp_range_destination        TYPE RANGE OF i_plant-plant.
    TYPES:
      gtyp_range_shipping_point     TYPE  RANGE OF i_shippingpoint-shippingpoint.
    TYPES:
      gtyp_range_shipping_condition TYPE  RANGE OF trolz-vsbed .
    TYPES:
      gtyp_range_distribution_chann TYPE  RANGE OF zsd_t_os_lt-zvtweg .
    TYPES:
      gtyp_range_last_changed_date TYPE  RANGE OF aedat .
    TYPES:
      gtyp_range_last_changed_time TYPE  RANGE OF aezeit .
    TYPES gtyp_include_transportation_lt TYPE abap_bool .

    METHODS get_data
      IMPORTING
        !it_departures           TYPE gtyp_range_departure OPTIONAL
        !it_destinations         TYPE gtyp_range_destination OPTIONAL
        !it_shipping_points      TYPE gtyp_range_shipping_point OPTIONAL
        !it_shipping_conditions  TYPE gtyp_range_shipping_condition OPTIONAL
        !it_distribution_channel TYPE gtyp_range_distribution_chann OPTIONAL
        !it_last_changed_date    TYPE gtyp_range_last_changed_date OPTIONAL
        !it_last_changed_time    TYPE gtyp_range_last_changed_time OPTIONAL
        !iv_inc_transp_lt        TYPE gtyp_include_transportation_lt OPTIONAL .
    METHODS send_to_kafka .
  PROTECTED SECTION.
private section.

  data:
    mt_dncreationleadtimes   TYPE TABLE OF zsd_i_dncreationleadtimes .
  data:
    mt_dnprocessingleadtimes TYPE TABLE OF zsd_i_dnprocessingleadtimes .
  data:
    mt_dnloadingleadtimes    TYPE TABLE OF zsd_i_dnloadingleadtimes .
  data:
    mt_vasleadtimes          TYPE TABLE OF zsd_i_vasleadtimes .
  data:
    mts_outboundschedulinglt TYPE SORTED TABLE OF zsd_i_outboundschedulinglt WITH NON-UNIQUE KEY departuresite shippingpoint .
  data MTS_VALUE type ZSD_TT_OUTBOUNDSCHEDULINGLT .
  constants MC_EVENT_NAME type STRING value 'OutboundScheduling_Update' ##NO_TEXT.
  constants MC_KAFKA_TOPIC type STRING value 'csd_fulfilment.s4hana.event.outboundscheduling' ##NO_TEXT.
  constants MC_EVENT_PREFIX type STRING value 'OutboundScheduling' ##NO_TEXT.

  methods GET_DB_DATA
    importing
      !IT_DEPARTURES type GTYP_RANGE_DEPARTURE optional
      !IT_DESTINATIONS type GTYP_RANGE_DESTINATION optional
      !IT_SHIPPING_POINTS type GTYP_RANGE_SHIPPING_POINT optional
      !IT_SHIPPING_CONDITIONS type GTYP_RANGE_SHIPPING_CONDITION optional
      !IT_DISTRIBUTION_CHANNEL type GTYP_RANGE_DISTRIBUTION_CHANN optional
      !IT_LAST_CHANGED_DATE type GTYP_RANGE_LAST_CHANGED_DATE
      !IT_LAST_CHANGED_TIME type GTYP_RANGE_LAST_CHANGED_TIME
      !IV_INC_TRANSP_LT type GTYP_INCLUDE_TRANSPORTATION_LT .
  methods GET_PAYLOAD .
ENDCLASS.



CLASS ZCL_SD_ROUTE_SCHEDUL_FOR_KAFKA IMPLEMENTATION.


  METHOD get_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 03.01.2023 | 16554     : [Feature] [Platform] [Custom *
*              |            | DS4K939267                               *
*----------------------------------------------------------------------*
* DOCHIIGO     | 10.08.2023 | 24047     : Development - Activate delta *
*              |            | DS4K960050                               *
*----------------------------------------------------------------------*
    get_db_data( it_departures          = it_departures
                 it_destinations        = it_destinations
                 it_shipping_points     = it_shipping_points
                 it_shipping_conditions = it_shipping_conditions
                 it_distribution_channel = it_distribution_channel
                 it_last_changed_date    = it_last_changed_date
                 it_last_changed_time    = it_last_changed_time
                 iv_inc_transp_lt        = iv_inc_transp_lt ).

    get_payload(  ).
  ENDMETHOD.


  METHOD if_oo_adt_classrun~main.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 08.01.2023 | 16554     : [Feature] [Platform] [Custom *
*              |            | DS4K939267                               *
*----------------------------------------------------------------------*

    send_to_kafka( ).
  ENDMETHOD.


  METHOD send_to_kafka.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 09.01.2023 | 16554     : [Feature] [Platform] [Custom *
*              |            | DS4K939267                               *
*----------------------------------------------------------------------*
    IF lines( mts_value ) = 0.
      MESSAGE w002(zcm_sd_schedul_rout).
      RETURN.
    ENDIF.

    LOOP AT mts_value ASSIGNING FIELD-SYMBOL(<ls_value>).
      DATA(ls_kafka_key) = VALUE zpe_s_kafka_key( event_name = mc_event_name
                                              event_key = |{ condense( val = |{ mc_event_prefix ALPHA = OUT }| ) }_{ condense( val = |{ <ls_value>-departuresite ALPHA = OUT }| ) }_{ <ls_value>-shippingpoint }| ).

      DATA(ls_kafka_data) = VALUE zsd_s_aif_out_scheduling_kafka( topic = mc_kafka_topic
                                                                  key = ls_kafka_key
                                                                  value-metadata = CORRESPONDING #( ls_kafka_key )
                                                                  value-payload  = <ls_value> ).
      TRY.
          /aif/cl_enabler_xml=>transfer_to_aif( ls_kafka_data ).
        ##CATCH_ALL CATCH cx_root INTO DATA(lo_cx).
          MESSAGE lo_cx->get_longtext( ) TYPE 'I'.
      ENDTRY.
    ENDLOOP.

    MESSAGE s001(zcm_sd_schedul_rout).
  ENDMETHOD.


  METHOD get_db_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 03.01.2023 | 16554     : [Feature] [Platform] [Custom *
*              |            | DS4K939267                               *
*----------------------------------------------------------------------*
* DOCHIIGO     | 10.08.2023 | 24047     : Development - Activate delta *
*              |            | DS4K960050                               *
*----------------------------------------------------------------------*
    SELECT DISTINCT departuresite, shippingpoint, destinationsite, shippingcondition, destinationsitelt
      FROM zsd_i_outboundschedulinglt
      INTO CORRESPONDING FIELDS OF TABLE @mts_outboundschedulinglt ##TOO_MANY_ITAB_FIELDS
      WHERE departuresite IN @it_departures AND
            destinationsite IN @it_destinations AND
            shippingpoint IN @it_shipping_points AND
            shippingcondition IN @it_shipping_conditions
      ORDER BY departuresite, shippingpoint, destinationsite, shippingcondition, destinationsitelt. "#EC CI_SUBRC "#EC CI_NOWHERE

    SELECT *
      FROM zsd_i_dncreationleadtimes AS ltimes
      WHERE departuresite IN @it_departures AND
            destinationsite IN @it_destinations AND
            shippingpoint IN @it_shipping_points AND
            distributionchannel IN @it_distribution_channel AND
            lastchangeddate     IN @it_last_changed_date     AND
            lastchangedtime     IN @it_last_changed_time
      INTO CORRESPONDING FIELDS OF TABLE @mt_dncreationleadtimes. "#EC CI_SUBRC "#EC CI_NOWHERE

    SELECT *
      FROM zsd_i_dnprocessingleadtimes AS ltimes
      WHERE departuresite IN @it_departures AND
            destinationsite IN @it_destinations AND
            shippingpoint IN @it_shipping_points AND
            distributionchannel IN @it_distribution_channel
      INTO CORRESPONDING FIELDS OF TABLE @mt_dnprocessingleadtimes. "#EC CI_SUBRC "#EC CI_NOWHERE

    SELECT *
      FROM zsd_i_dnloadingleadtimes
      WHERE departuresite IN @it_departures AND
            destinationsite IN @it_destinations AND
            shippingpoint IN @it_shipping_points AND
            shippingcondition IN @it_shipping_conditions AND
            distributionchannel IN @it_distribution_channel
      INTO CORRESPONDING FIELDS OF TABLE @mt_dnloadingleadtimes. "#EC CI_SUBRC "#EC CI_NOWHERE

    SELECT *
      FROM zsd_i_vasleadtimes
      WHERE departuresite IN @it_departures AND
            destinationsite IN @it_destinations AND
            shippingpoint IN @it_shipping_points
      INTO CORRESPONDING FIELDS OF TABLE @mt_vasleadtimes. "#EC CI_SUBRC "#EC CI_NOWHERE
  ENDMETHOD.


  METHOD get_payload.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 06.02.2023 | 16554     : [Feature] [Platform] [Custom *
*              |            | DS4K939267                               *
*----------------------------------------------------------------------*
* DOCHIIGO     | 10.08.2023 | 24047     : Development - Activate delta *
*              |            | DS4K960050                               *
*----------------------------------------------------------------------*
    CHECK lines( mts_outboundschedulinglt ) > 0.

    mts_value = CORRESPONDING #( mts_outboundschedulinglt ).
    DELETE ADJACENT DUPLICATES FROM mts_value COMPARING departuresite shippingpoint.

    LOOP AT mts_value ASSIGNING FIELD-SYMBOL(<ls_outboundschedulinglt>).
      LOOP AT mts_outboundschedulinglt ASSIGNING FIELD-SYMBOL(<ls_outboundschedulinglt_all>) "#EC CI_NESTED
                                                                              WHERE departuresite = <ls_outboundschedulinglt>-departuresite AND
                                                                              shippingpoint = <ls_outboundschedulinglt>-shippingpoint.

        READ TABLE <ls_outboundschedulinglt>-leadtimes-transportationlt ASSIGNING FIELD-SYMBOL(<ls_transportationlt>) "#EC CI_STDSEQ
                                                                        WITH KEY destinationsite = <ls_outboundschedulinglt_all>-destinationsite.
        IF sy-subrc <> 0.
          APPEND INITIAL LINE TO <ls_outboundschedulinglt>-leadtimes-transportationlt ASSIGNING <ls_transportationlt>.
        ENDIF.
        <ls_transportationlt>-destinationsite = <ls_outboundschedulinglt_all>-destinationsite.

        APPEND INITIAL LINE TO <ls_transportationlt>-destinationlt ASSIGNING FIELD-SYMBOL(<ls_destinationlt>).
        <ls_destinationlt>-shippingcondition = <ls_outboundschedulinglt_all>-shippingcondition.
        <ls_destinationlt>-destinationsitelt = <ls_outboundschedulinglt_all>-destinationsitelt.
      ENDLOOP.

      SELECT DISTINCT distributionchannel, ordertype, orderreason, dncreationlt, LastChangedDate, LastChangedTime, DeletionIndicator
        FROM @mt_dncreationleadtimes AS _leadtimes ##ITAB_KEY_IN_SELECT ##ITAB_DB_SELECT
        WHERE departuresite = @<ls_outboundschedulinglt>-departuresite AND shippingpoint = @<ls_outboundschedulinglt>-shippingpoint
        INTO CORRESPONDING FIELDS OF TABLE @<ls_outboundschedulinglt>-leadtimes-dncreationleadtimes. "#EC CI_SUBRC

      SELECT DISTINCT distributionchannel, ordertype, orderreason, dnprocessinglt, LastChangedDate, LastChangedTime, DeletionIndicator
        FROM @mt_dnprocessingleadtimes AS _leadtimes ##ITAB_KEY_IN_SELECT ##ITAB_DB_SELECT
        WHERE departuresite = @<ls_outboundschedulinglt>-departuresite AND shippingpoint = @<ls_outboundschedulinglt>-shippingpoint
        INTO CORRESPONDING FIELDS OF TABLE @<ls_outboundschedulinglt>-leadtimes-dnprocessingleadtimes. "#EC CI_SUBRC

      SELECT DISTINCT distributionchannel, shippingcondition, ordertype, orderreason, dnloadinglt, LastChangedDate, LastChangedTime, DeletionIndicator
        FROM @mt_dnloadingleadtimes AS _leadtimes ##ITAB_KEY_IN_SELECT ##ITAB_DB_SELECT
        WHERE departuresite = @<ls_outboundschedulinglt>-departuresite AND shippingpoint = @<ls_outboundschedulinglt>-shippingpoint
        INTO CORRESPONDING FIELDS OF TABLE @<ls_outboundschedulinglt>-leadtimes-dnloadingleadtimes. "#EC CI_SUBRC

      SELECT DISTINCT vascode,vaslt, LastChangedDate, LastChangedTime, DeletionIndicator
        FROM @mt_vasleadtimes AS _leadtimes ##ITAB_KEY_IN_SELECT ##ITAB_DB_SELECT
        WHERE departuresite = @<ls_outboundschedulinglt>-departuresite AND shippingpoint = @<ls_outboundschedulinglt>-shippingpoint
        INTO CORRESPONDING FIELDS OF TABLE @<ls_outboundschedulinglt>-leadtimes-vasleadtimes. "#EC CI_SUBRC
    ENDLOOP.
  ENDMETHOD.
ENDCLASS.