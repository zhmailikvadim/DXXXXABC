class ZCL_SD_SO_REJECTION definition
  public
  final
  create public .

public section.

  interfaces BI_EVENT_HANDLER_STATIC .
protected section.
private section.

  class-methods GET_AIF_STRUCTURE
    importing
      !IV_OBJKEY type SWEINSTCOU-OBJKEY
    returning
      value(RS_SO_REJECTION) type ZSD_S_SO_REJECTION .
ENDCLASS.



CLASS ZCL_SD_SO_REJECTION IMPLEMENTATION.


  METHOD bi_event_handler_static~on_event.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHADDFAD     | 24.07.2023 | 23261: Enhancement SO cancel field rej.  *
*              |            |  DS4K958130                              *
*----------------------------------------------------------------------*


    DATA: ls_so_rejection TYPE zsd_s_so_rejection.

    ls_so_rejection = get_aif_structure( iv_objkey = sender-instid ).

    IF ls_so_rejection IS INITIAL.
      RETURN.
    ENDIF.

    TRY.
        /aif/cl_enabler_xml=>transfer_to_aif( is_any_structure = ls_so_rejection ).

      CATCH: /aif/cx_inf_det_base
             /aif/cx_enabler_base
             /aif/cx_aif_engine_not_found
             /aif/cx_error_handling_general
             /aif/cx_aif_engine_base.
        RETURN.
    ENDTRY.
  ENDMETHOD.


  METHOD get_aif_structure.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHADDFAD     | 24.07.2023 | 23261: Enhancement SO cancel field rej.  *
*              |            |  DS4K958130                              *
*----------------------------------------------------------------------*


    DATA: lt_order_items    TYPE zsd_tt_order_items.


    TRY.
        DATA(lt_hm_const) = zcl_pe_hm_basic=>get_table( iv_id = '1985' ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.

    DATA(ls_hm_const)        = lt_hm_const[ 1 ].
    DATA(lv_rej_reason)      = ls_hm_const-field1.
    DATA(lv_doc_cat)         = ls_hm_const-field2.
    DATA(lv_billing_type)    = ls_hm_const-field3.

    SELECT
       precedingdocument, precedingdocumentitem
      FROM i_sddocumentmultilevelprocflow WITH PRIVILEGED ACCESS AS vbfa
      INNER JOIN i_billingdocumentbasic AS vbrk
      ON vbfa~subsequentdocument = vbrk~billingdocument
      WHERE vbfa~subsequentdocument = @iv_objkey
      AND   vbfa~precedingdocumentcategory = @lv_doc_cat
      AND   vbrk~billingdocumenttype = @lv_billing_type
      INTO TABLE @DATA(lt_vbfa).

    IF sy-subrc IS INITIAL.
      LOOP AT lt_vbfa ASSIGNING FIELD-SYMBOL(<ls_vbfa>).
        APPEND INITIAL LINE TO lt_order_items ASSIGNING FIELD-SYMBOL(<ls_order_items>).
        <ls_order_items>-salesorder = <ls_vbfa>-precedingdocument.
        <ls_order_items>-salesorderitem = <ls_vbfa>-precedingdocumentitem.
        <ls_order_items>-salesdocumentrjcnreason = lv_rej_reason.

      ENDLOOP.
    ENDIF.

    rs_so_rejection = VALUE #( order_items = lt_order_items ).

  ENDMETHOD.
ENDCLASS.