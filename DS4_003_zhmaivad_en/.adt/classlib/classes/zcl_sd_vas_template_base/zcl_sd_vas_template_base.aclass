class ZCL_SD_VAS_TEMPLATE_BASE definition
  public
  abstract
  create public .

public section.

  interfaces ZIF_PE_EXCEL_TEMPLATE .

  types:
    mtyp_t_cond_tables_range TYPE RANGE OF tabname .
  types:
    BEGIN OF mtyp_s_column_info,
        field_name   TYPE fieldname,
        data_element TYPE rollname,
        field_descr  TYPE string,
      END OF mtyp_s_column_info .
  types:
    mtyp_t_columns_info TYPE STANDARD TABLE OF mtyp_s_column_info WITH DEFAULT KEY .
  types:
    BEGIN OF mtyp_s_vas_sub_service,
        vas_sub_ser      TYPE fsh_vas_subser-fsh_vas_sub_ser,
        vas_sub_ser_desc TYPE fsh_vas_subser_t-fsh_sub_ser_desc,
      END OF mtyp_s_vas_sub_service .
  types:
    mtyp_t_vas_sub_services TYPE STANDARD TABLE OF mtyp_s_vas_sub_service WITH DEFAULT KEY .
  types:
    BEGIN OF mtyp_s_vas_service_columns,
        vas_service_columns     TYPE mtyp_t_columns_info,
        vas_sub_service_columns TYPE mtyp_t_columns_info,
      END OF mtyp_s_vas_service_columns .
  types:
    BEGIN OF mtyp_s_vas_service,
        vas_ser          TYPE fsh_vas_serv-fsh_vas_ser,
        vas_ser_desc     TYPE fsh_vas_serv_t-fsh_ser_desc,
        vas_sub_services TYPE mtyp_t_vas_sub_services,
      END OF mtyp_s_vas_service .
  types:
    mtyp_t_vas_services TYPE STANDARD TABLE OF mtyp_s_vas_service WITH DEFAULT KEY .
  types:
    BEGIN OF mtyp_s_cond_table_fields,
        table_name           TYPE tabname,
        vas_cond_columns     TYPE mtyp_t_columns_info,
        base_columns         TYPE mtyp_t_columns_info,
        vas_sub_serv_columns TYPE mtyp_t_columns_info,
      END OF mtyp_s_cond_table_fields .
  types:
    mtyp_t_cond_tables_fields TYPE STANDARD TABLE OF mtyp_s_cond_table_fields WITH DEFAULT KEY .
  types:
    BEGIN OF mtyp_s_cond_table_records,
        table_name    TYPE tabname,
        table_records TYPE REF TO data,
      END OF mtyp_s_cond_table_records .
  types:
    mtyp_t_cond_tables_records TYPE STANDARD TABLE OF mtyp_s_cond_table_records WITH DEFAULT KEY .
  types:
    BEGIN OF mtyp_s_vas_cond_template,
        vas_cond_tab_columns TYPE mtyp_t_cond_tables_fields,
        vas_cond_rows_data   TYPE mtyp_t_cond_tables_records,
        vas_ser_columns      TYPE mtyp_s_vas_service_columns,
        vas_ser_rows_data    TYPE mtyp_t_vas_services,
        all_columns          TYPE mtyp_t_columns_info,
        all_columns_data     TYPE REF TO data,
      END OF mtyp_s_vas_cond_template .
  types:
    mtyp_t_vas_cond_template TYPE STANDARD TABLE OF mtyp_s_vas_cond_template WITH DEFAULT KEY .

  methods CONSTRUCTOR
    importing
      !IT_VAS_COND_TABLES_RANGE type MTYP_T_COND_TABLES_RANGE
      !IV_VAS_COND_TYPE type KSCHL optional .
  PROTECTED SECTION.

    DATA ms_vas_template_content TYPE mtyp_s_vas_cond_template .
    DATA mo_excel TYPE REF TO zcl_excel .
    DATA mv_vas_cond_type TYPE kschl .
    DATA mt_vas_cond_tables_range TYPE mtyp_t_cond_tables_range .

    METHODS download_excel_content
      RETURNING
        VALUE(rv_xlsx_document) TYPE xstring
      RAISING
        cx_static_check .

    METHODS get_vas_cond_ser_data.
    METHODS prepare_columns_info_list
      ABSTRACT
      IMPORTING
        !it_cond_tables_range TYPE mtyp_t_cond_tables_range
      RAISING
        cx_static_check .
    METHODS prepare_rows_data
      ABSTRACT
      IMPORTING
        !it_columns_info TYPE mtyp_t_columns_info
      RAISING
        cx_static_check .
    METHODS create_excel_template_headers
      ABSTRACT
      RAISING
        zcx_excel .
    METHODS fill_template_rows
        ABSTRACT .
  PRIVATE SECTION.

    METHODS get_xstring_content
      RETURNING
        VALUE(rv_content) TYPE xstring
      RAISING
        zcx_excel .
ENDCLASS.



CLASS ZCL_SD_VAS_TEMPLATE_BASE IMPLEMENTATION.


  METHOD zif_pe_excel_template~get_excel.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 05.01.2023 | 16257  : VAS condition record mass       *
*              |            | DS4K938452                               *
*----------------------------------------------------------------------*
    TRY.
        rv_content = download_excel_content( ).

      CATCH cx_static_check INTO DATA(lo_exception).
        DATA(lv_msg) = lo_exception->get_text( ).
        MESSAGE lv_msg TYPE 'E'.
    ENDTRY.

  ENDMETHOD.


  METHOD download_excel_content.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 05.01.2023 | 16257  : VAS condition record mass       *
*              |            | DS4K938452                               *
*----------------------------------------------------------------------*-*

    get_vas_cond_ser_data( ).

    prepare_columns_info_list( mt_vas_cond_tables_range ).
    prepare_rows_data( ms_vas_template_content-all_columns ).

    create_excel_template_headers( ).

    fill_template_rows( ).

    rv_xlsx_document =  get_xstring_content(  ).

  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 05.01.2023 | 16257  : VAS condition record mass       *
*              |            | DS4K938452                               *
*----------------------------------------------------------------------*
    mt_vas_cond_tables_range = it_vas_cond_tables_range.
    mv_vas_cond_type = iv_vas_cond_type.
  ENDMETHOD.


  METHOD get_xstring_content.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 05.01.2023 | 16257  : VAS condition record mass       *
*              |            | DS4K938452                               *
*----------------------------------------------------------------------*
    DATA: lif_writer TYPE REF TO zif_excel_writer.

    lif_writer = NEW zcl_excel_writer_2007( ).
    DATA(lv_xdata) = lif_writer->write_file( mo_excel ).

    rv_content = lv_xdata.

  ENDMETHOD.


  METHOD get_vas_cond_ser_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 05.01.2023 | 16257  : VAS condition record mass       *
*              |            | DS4K938452                               *
*----------------------------------------------------------------------*
    SELECT
      service~fsh_vas_ser                 AS vas_ser,
      service_text~fsh_ser_desc           AS vas_ser_desc
      FROM fsh_vas_serv AS service
      INNER JOIN fsh_vas_serv_t AS service_text ON service_text~fsh_vas_ser  = service~fsh_vas_ser
                                                AND service_text~spras       = @sy-langu
      INTO CORRESPONDING FIELDS OF TABLE @ms_vas_template_content-vas_ser_rows_data.  ##TOO_MANY_ITAB_FIELDS
    IF sy-subrc = 0.
      LOOP AT ms_vas_template_content-vas_ser_rows_data ASSIGNING FIELD-SYMBOL(<ls_vas_service>).
        SELECT                                       "#EC CI_SEL_NESTED
           sub_service~fsh_vas_sub_ser         AS vas_sub_ser ,
           sub_service_text~fsh_sub_ser_desc   AS vas_sub_ser_desc
           FROM fsh_vas_subser AS sub_service
           INNER JOIN fsh_vas_subser_t AS sub_service_text ON  sub_service_text~fsh_vas_ser     = sub_service~fsh_vas_ser
                                                           AND sub_service_text~fsh_vas_sub_ser = sub_service~fsh_vas_sub_ser
                                                           AND sub_service_text~spras           = @sy-langu
           WHERE sub_service~fsh_vas_ser = @<ls_vas_service>-vas_ser
           INTO  TABLE @<ls_vas_service>-vas_sub_services.  "#EC CI_SUBRC
      ENDLOOP.
    ENDIF.
  ENDMETHOD.
ENDCLASS.