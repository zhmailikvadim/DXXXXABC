class ZCL_SD_VAS_UPL_VALIDATIONS definition
  public
  abstract
  create public .

public section.

  interfaces ZIF_SD_VALIDATION_METHODS .

  methods CONSTRUCTOR
    importing
      !IS_VAS_COND type ZSD_S_VAS_COND .
  class-methods VALIDATE_LINE
    changing
      !CS_VAS_COND type ZSD_S_VAS_COND
    returning
      value(RT_RETURN_TAB) type BAPIRET2_TAB .
  class-methods GET_OBJ_FOR_APPLICATION_TYPE
    changing
      !CS_VAS_COND type ZSD_S_VAS_COND
    returning
      value(RO_INSTANCE) type ref to ZCL_SD_VAS_UPL_VALIDATIONS .
  methods CHECK_SHAS_CONFIG
    returning
      value(RV_RESULT) type ABAP_BOOLEAN .
protected section.

  types:
    BEGIN OF mtyp_product,
      fsh_mg_at1 TYPE fsh_mg_attribute1,
    END OF  mtyp_product .
  types:
    BEGIN OF mtyp_date_brf,
      date        TYPE dats,
      time        TYPE tims,
      timestamp   TYPE fdt_timestamp,
      offset_time TYPE fdt_utc_offset_time,
      offset_sign TYPE tznutcsign,
      type        TYPE fdt_timepoint_type,
    END OF mtyp_date_brf .
  types:
    BEGIN OF mtyp_formats,
      value_position TYPE ddcds_customer_domain_value-value_position,
      value_low      TYPE ddcds_customer_domain_value-value_low,
    END OF mtyp_formats .
  types:
    BEGIN OF       mtyp_shas_config,
      vkorg             TYPE vkorg,
      ekgrp             TYPE ekgrp,
      kunnr             TYPE kunnr,
      zsd_serviden      TYPE zsd_serviden,
      fsh_mg_attribute1 TYPE fsh_mg_attribute1,
      fsh_vas_sub_ser   TYPE fsh_vas_sub_ser,
      datab             TYPE mtyp_date_brf,
      datbi             TYPE mtyp_date_brf,
      fsh_vas_ser       TYPE fsh_vas_ser,
      zsd_siidentifier  TYPE zsd_siidentifier,
      zsd_condition     TYPE zsd_condition,
      zsd_field_type    TYPE zsd_field_type,
      zsd_fld_len_min   TYPE zsd_fld_len_min,
      zsd_fld_len_max   TYPE zsd_fld_len_max,
      zsd_format        TYPE zsd_format,
      zsd_data          TYPE zsd_data,
    END OF mtyp_shas_config .
  types:
    mtyp_tt_shas_config TYPE SORTED TABLE OF  mtyp_shas_config WITH NON-UNIQUE KEY fsh_vas_sub_ser fsh_vas_ser .
  types:
    BEGIN OF mtyp_condition,
      kschl            TYPE kschl,
      procurement_flag TYPE zsd_vas_procurement_flag,
      active           TYPE zsd_vas_is_active,
    END OF mtyp_condition .
  types:
    mtyp_tt_condition TYPE SORTED TABLE OF mtyp_condition WITH UNIQUE KEY kschl .

  data MS_VAS_CONDITION type ZSD_S_VAS_COND .
  data MTS_SHAS_CONFIG type MTYP_TT_SHAS_CONFIG .
  data MT_RETURN_TAB type BAPIRET2_TAB .
  data:
    mts_fsh_vas_subser         TYPE SORTED TABLE OF fsh_vas_subser WITH UNIQUE KEY fsh_vas_sub_ser fsh_vas_ser .
  data MS_PRODUCT type MTYP_PRODUCT .
  data MV_PRICINGREFERENCEPRODUCT type I_PRODUCTSALESDELIVERY-PRICINGREFERENCEPRODUCT .
  data MV_APPLICATION_AREA type KAPPL .
  data:
    mts_formats                TYPE SORTED TABLE OF mtyp_formats WITH NON-UNIQUE KEY value_position .
  constants MC_Z_SIIDENTIFIER_I type ZSD_T_SHAS_CNFG-Z_SIIDENTIFIER value 'I' ##NO_TEXT.
  constants MC_Z_SIIDENTIFIER_S type ZSD_T_SHAS_CNFG-Z_SIIDENTIFIER value 'S' ##NO_TEXT.
  constants MC_Z_CONDITION_M type ZSD_T_SHAS_CNFG-Z_CONDITION value 'M' ##NO_TEXT.
  constants MC_Z_CONDITION_O type ZSD_T_SHAS_CNFG-Z_CONDITION value 'O' ##NO_TEXT.
  class-data GTS_CONDITION type MTYP_TT_CONDITION .
  class-data GV_COND_TYPE type KSCHL .

  methods SET_CONFIGURATION
  abstract .
  methods GET_ADDITIONAL_DATA .
  methods GET_CONFIG_LINE
    importing
      value(IV_VAS_CODE) type FSH_VAS_SUB_SER
    returning
      value(RS_CONFIG) type MTYP_SHAS_CONFIG .
  methods ADD_ERROR_MESSAGE
    importing
      value(IV_MSG_NUMBER) type SYMSGNO .
  methods ADD_WARNING_MESSAGE
    importing
      value(IV_MSG_NUMBER) type SYMSGNO .
  methods CHECK_PLAUSIBILITY
    importing
      value(IV_FORMAT) type ZSD_FORMAT
      value(IV_VALUE) type FSH_VAS_CUSTF1
    returning
      value(RV_RESULT) type ABAP_BOOLEAN .
ENDCLASS.



CLASS ZCL_SD_VAS_UPL_VALIDATIONS IMPLEMENTATION.


  METHOD add_error_message.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 07.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    MESSAGE e001(zsd_aif_vas_cond_upl) INTO DATA(lv_message) ##NEEDED ##MG_MISSING.
    MESSAGE e002(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e003(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e004(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e005(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e006(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e007(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e008(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e009(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e010(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e011(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e012(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e013(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e014(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e015(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e016(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e017(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.

    DATA:
      ls_return TYPE bapiret2,
      lv_par1   TYPE syst_msgv.

    CONSTANTS: lc_error_type TYPE bapireturn-type VALUE 'E'.

    lv_par1 = ms_vas_condition-material.

    CALL FUNCTION 'BALW_BAPIRETURN_GET2'
      EXPORTING
        type   = lc_error_type
        cl     = 'ZSD_AIF_VAS_COND_UPL'
        number = iv_msg_number
        par1   = lv_par1
      IMPORTING
        return = ls_return.

    APPEND ls_return TO mt_return_tab.
  ENDMETHOD.


  METHOD add_warning_message.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    APPEND VALUE #( id = 'ZSD_AIF_VAS_COND_UPL'
                    type = 'W'
                    number = iv_msg_number
                    message_v1 = ms_vas_condition-material ) TO mt_return_tab.
  ENDMETHOD.


  METHOD check_plausibility.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 07.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    DATA:
        lv_date TYPE sy-datum.

    TRY.

        IF iv_format = mts_formats[ value_position = '0001' ]-value_low OR iv_format = mts_formats[ value_position = '0002' ]-value_low.
          lv_date = COND #( WHEN iv_format = mts_formats[ value_position = '0002' ]-value_low THEN |{ iv_value+6(2)  }| &  |{ iv_value+4(2) }| &  |{ iv_value+4 }|
                                ELSE iv_value ).

          CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
            EXPORTING
              date                      = lv_date
            EXCEPTIONS
              plausibility_check_failed = 1
              OTHERS                    = 2.

          IF sy-subrc <> 0.
            rv_result = abap_false.
          ENDIF.

          RETURN.
        ENDIF.

        IF iv_format = mts_formats[ value_position = '0003' ]-value_low AND iv_value CN '0123456789,'.
          rv_result = abap_false.
        ENDIF.

        IF iv_format = mts_formats[ value_position = '0004' ]-value_low AND iv_value CN '0123456789.'.
          rv_result = abap_false.
        ENDIF.

        IF iv_format = mts_formats[ value_position = '0005' ]-value_low AND iv_value CN '0123456789'.
          rv_result = abap_false.
        ENDIF.
      CATCH cx_sy_itab_line_not_found.
        RETURN.
    ENDTRY.
  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 07.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    ms_vas_condition = is_vas_cond.
    get_additional_data(  ).
  ENDMETHOD.


  METHOD get_additional_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 07.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
* GERTSVIK     | 19.10.2023 | 26049  : CR - Skip ZRET condition type   *
*              |            | DS4K967496                               *
*----------------------------------------------------------------------*
    DATA:
      lt_fsh_vas_subser TYPE RANGE OF fsh_vas_sub_ser.

    lt_fsh_vas_subser = VALUE #( FOR ls_vas_sub_services IN ms_vas_condition-vas_sub_services
                                                          ( sign = 'I'
                                                            option = 'EQ'
                                                            low = ls_vas_sub_services-sub_service ) ).
    SORT lt_fsh_vas_subser BY low.
    DELETE ADJACENT DUPLICATES FROM lt_fsh_vas_subser COMPARING low.

    SELECT  fsh_vas_ser, fsh_vas_sub_ser
     FROM fsh_vas_subser
     WHERE fsh_vas_sub_ser IN @lt_fsh_vas_subser
      INTO CORRESPONDING FIELDS OF TABLE @mts_fsh_vas_subser ##TOO_MANY_ITAB_FIELDS.
    IF sy-subrc <> 0.
      CLEAR mts_fsh_vas_subser.
    ENDIF.

    SELECT SINGLE fsh_mg_at1
      FROM zsd_i_product_for_vas_validat
      INTO CORRESPONDING FIELDS OF @ms_product
      WHERE product = @ms_vas_condition-material.
    IF sy-subrc <> 0.
      CLEAR ms_product.
    ENDIF.

    SELECT SINGLE pricingreferenceproduct
      FROM i_productsalesdelivery
      INTO @mv_pricingreferenceproduct
      WHERE product = @ms_vas_condition-material.
    IF sy-subrc <> 0.
      CLEAR mv_pricingreferenceproduct.
    ENDIF.

    SELECT value_position, value_low
      FROM zsd_i_vas_custom_domain_values
       WHERE domain_name = 'ZSD_FORMAT'               "#EC CI_SGLSELECT
       INTO CORRESPONDING FIELDS OF TABLE @mts_formats ##TOO_MANY_ITAB_FIELDS.
    IF sy-subrc <> 0.
      CLEAR mts_formats.
    ENDIF.

    TRY.
        DATA(lt_value) =  zcl_pe_hm_basic=>get_table( iv_id = '2536' ).
      CATCH zcx_pe_hm.
        CLEAR lt_value.
    ENDTRY.

    gts_condition  = VALUE #( FOR ls_val IN lt_value ( kschl            = ls_val-field1
                                                       procurement_flag = ls_val-field2
                                                       active           = ls_val-field3 ) ) .
    TRY.
        gv_cond_type = zcl_pe_hm_basic=>get_value( iv_id = '2560' ).
      CATCH zcx_pe_hm.
        CLEAR gv_cond_type.
    ENDTRY.
  ENDMETHOD.


  METHOD get_config_line.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 07.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    TRY.
        DATA(ls_vas_ser) = mts_fsh_vas_subser[ fsh_vas_sub_ser = iv_vas_code ] ##WARN_OK.

        rs_config = mts_shas_config[ fsh_vas_sub_ser = ls_vas_ser-fsh_vas_sub_ser
                                     fsh_vas_ser = ls_vas_ser-fsh_vas_ser ] ##NUMBER_OK ##WARN_OK .
      CATCH cx_sy_itab_line_not_found.
        add_error_message( 16 ).
        RETURN.
    ENDTRY.
  ENDMETHOD.


  METHOD get_obj_for_application_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 25.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 14.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    DATA:
      lt_vas_sd TYPE RANGE OF kschl,
      lt_vas_mm TYPE RANGE OF kschl.

    TRY.
        lt_vas_sd = VALUE #( FOR ls_serviden IN zcl_pe_hm_basic=>get_table( '1267' )
                                                           ( sign = 'I'
                                                             option = 'EQ'
                                                             low = ls_serviden-field1 ) ).


        lt_vas_mm = VALUE #( FOR ls_serviden IN zcl_pe_hm_basic=>get_table( '1267' )
                                                           ( sign = 'I'
                                                             option = 'EQ'
                                                             low = ls_serviden-field2 ) ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.

    IF cs_vas_cond-kschl IN lt_vas_sd.
      ro_instance = NEW lhc_sd_vas_upl_validations_sd( cs_vas_cond  ).
    ELSEIF cs_vas_cond-kschl IN lt_vas_mm.
      ro_instance = NEW lhc_sd_vas_upl_validations_mm( cs_vas_cond  ).
    ENDIF.
  ENDMETHOD.


  METHOD validate_line.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* GERTSVIK     | 19.10.2023 | 26049  : CR - Skip ZRET condition type   *
*              |            | DS4K967496                               *
*----------------------------------------------------------------------*
    DATA(lo_validations) = get_obj_for_application_type( CHANGING cs_vas_cond = cs_vas_cond ).
    CHECK line_exists( gts_condition[ kschl = cs_vas_cond-kschl ] ).

    IF lo_validations->check_shas_config( ).
    ELSE.
      lo_validations->zif_sd_validation_methods~run_all_validations(  ).
    ENDIF.

    cs_vas_cond = lo_validations->ms_vas_condition.
    rt_return_tab = lo_validations->mt_return_tab.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~item_division.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 20.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    IF ms_vas_condition-material IS NOT INITIAL.
      LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
        IF get_config_line( <ls_vas_condition>-sub_service )-fsh_mg_attribute1 <>  ms_product-fsh_mg_at1.
          IF ms_product-fsh_mg_at1  = '' ##NUMBER_OK.
            add_error_message( 006 ).
          ELSE.
            add_error_message( 017 ).
          ENDIF.
        ENDIF.
      ENDLOOP.
    ENDIF.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~material_availability.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 25.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    IF ms_vas_condition-material IS NOT INITIAL.
      SELECT SINGLE product
       FROM i_product
       WHERE product = @ms_vas_condition-material
        INTO @DATA(lv_matnr) ##NEEDED.

      IF sy-subrc <> 0.
        SELECT SINGLE product
          FROM i_product
            WHERE product = @ms_vas_condition-material
            INTO @lv_matnr ##NEEDED.

        IF sy-subrc <> 0.
          add_error_message( 001 ).
        ENDIF.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~run_all_validations.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    zif_sd_validation_methods~material_availability(  ).

    IF lines( mts_shas_config ) = 0.
      RETURN.
    ENDIF.

    zif_sd_validation_methods~validity_period(  ).
    zif_sd_validation_methods~item_division(  ).
    zif_sd_validation_methods~size_item_identifier(  ).
    zif_sd_validation_methods~vas_codes_values_cfg_tbl(  ).
    zif_sd_validation_methods~vas_codes_types_cfg_tbl(  ).
    zif_sd_validation_methods~vas_codes_formats_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_min_lenghts_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_max_lenghts_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_serviden_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_data_cfg_tbl(  ).
  ENDMETHOD.


  METHOD zif_sd_validation_methods~size_item_identifier.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 20.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    IF ms_vas_condition-material IS NOT INITIAL.
      LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
        IF get_config_line( <ls_vas_condition>-sub_service )-zsd_siidentifier = mc_z_siidentifier_i
          AND mv_pricingreferenceproduct <> ms_vas_condition-material.

          ms_vas_condition-material = mv_pricingreferenceproduct.
        ENDIF.

        IF get_config_line( <ls_vas_condition>-sub_service )-zsd_siidentifier = mc_z_siidentifier_s
          AND  mv_pricingreferenceproduct = ms_vas_condition-material.
          add_error_message( 007 ).
        ENDIF.

        EXIT.
      ENDLOOP.

    ENDIF.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~validity_period.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 07.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      IF ms_vas_condition-date_from <  get_config_line( <ls_vas_condition>-sub_service )-datab-date
                          OR ms_vas_condition-date_to  > get_config_line( <ls_vas_condition>-sub_service )-datbi-date.
        add_error_message( 005 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_codes_formats_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 20.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      IF check_plausibility( iv_format = get_config_line( <ls_vas_condition>-sub_service )-zsd_format iv_value = <ls_vas_condition>-field1 ).
        add_error_message( 009 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_codes_types_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 07.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(ls_config) = get_config_line( <ls_vas_condition>-sub_service ).
      DATA(lv_no_error) = abap_true.


      IF ( ls_config-zsd_format = mts_formats[ value_position = '0001' ]-value_low  OR ls_config-zsd_format = mts_formats[ value_position = '0002' ]-value_low )
                                                                                    AND ls_config-zsd_field_type <> 'DAT'.
        CLEAR lv_no_error.
      ELSEIF ( ls_config-zsd_format = mts_formats[ value_position = '0003' ]-value_low OR ls_config-zsd_format = mts_formats[ value_position = '0004' ]-value_low )
                                                                                       AND ls_config-zsd_field_type <> 'AMOUNT'.
        CLEAR lv_no_error.
      ELSEIF ls_config-zsd_format = mts_formats[ value_position = '0005' ]-value_low AND ls_config-zsd_field_type <> 'NUMC'.
        CLEAR lv_no_error.
      ELSEIF ls_config-zsd_field_type <> 'CHAR'.
        CLEAR lv_no_error.
      ENDIF.

      IF lv_no_error = abap_false.
        add_error_message( 009 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_codes_values_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 25.04.2023 | 19866  : Development - VAS condition re  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_z_condition) = get_config_line( <ls_vas_condition>-sub_service )-zsd_condition.
      DATA(lv_vas_code) = <ls_vas_condition>-sub_service.

      IF lv_vas_code = '' AND lv_z_condition = mc_z_condition_m.
        add_warning_message( 008 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_values_data_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 20.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    DATA:
      lt_zdata_range TYPE RANGE OF zsd_serviden.

    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_zdata) = get_config_line( <ls_vas_condition>-sub_service )-zsd_data.

      IF lv_zdata <> ''.
        SPLIT lv_zdata AT ',' INTO: TABLE DATA(lt_zdata).

        lt_zdata_range = VALUE #( FOR lv_data IN lt_zdata
                                              ( sign = 'I'
                                                option = 'EQ'
                                                low = lv_data ) ).

        IF <ls_vas_condition>-field1 NOT IN lt_zdata_range.
          add_error_message( 013 ) ##NUMBER_OK.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_values_max_lenghts_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 20.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      IF strlen( <ls_vas_condition>-field1 ) > get_config_line( <ls_vas_condition>-sub_service )-zsd_fld_len_max.
        add_error_message( 011 ) ##NUMBER_OK.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_values_min_lenghts_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 20.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      IF strlen( <ls_vas_condition>-field1 ) < get_config_line( <ls_vas_condition>-sub_service )-zsd_fld_len_min.
        add_error_message( 010 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_values_serviden_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 20.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    DATA:
        lt_serviden TYPE RANGE OF zsd_serviden.
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      TRY.
          lt_serviden = VALUE #( FOR ls_serviden IN zcl_pe_hm_basic=>get_table( '1261' )
                                                              ( sign = 'I'
                                                                option = 'EQ'
                                                                low = ls_serviden-field1 ) ).
        CATCH zcx_pe_hm.
          RETURN.
      ENDTRY.

      IF  <ls_vas_condition>-sub_service IN lt_serviden
        AND get_config_line( <ls_vas_condition>-sub_service )-zsd_serviden <> <ls_vas_condition>-field1.
        add_error_message( 012 ) ##NUMBER_OK.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~validate_customer.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 21652     : Feature - Development - VAS  *
*              |            | DS4K953535                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~validate_plant.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 21652     : Feature - Development - VAS  *
*              |            | DS4K953535                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~validate_product.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 21652     : Feature - Development - VAS  *
*              |            | DS4K953535                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~validate_vas_code.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 21652     : Feature - Development - VAS  *
*              |            | DS4K953535                               *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.


  METHOD check_shas_config.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* GERTSVIK     | 19.10.2023 | 26049  : CR - Skip ZRET condition type   *
*              |            | DS4K967496                               *
*----------------------------------------------------------------------*
    DATA(ls_condition) = gts_condition[ kschl = ms_vas_condition-kschl ].
    IF sy-subrc = 0.
      CHECK ls_condition-procurement_flag = abap_true AND ls_condition-active = abap_true.
    ENDIF.

    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(ls_vas_ser) = mts_fsh_vas_subser[ fsh_vas_sub_ser = <ls_vas_condition>-sub_service ].
      CHECK sy-subrc = 0.
      TRY.
          IF ms_vas_condition-kschl = gv_cond_type.
            DATA(ls_config) = mts_shas_config[ fsh_vas_sub_ser = ls_vas_ser-fsh_vas_sub_ser
                                               fsh_vas_ser     = ls_vas_ser-fsh_vas_ser
                                               vkorg           = ms_vas_condition-purchase_organization
                                               ekgrp           = ms_vas_condition-purchase_group ] ##NEEDED.
          ELSE.
            ls_config = mts_shas_config[ fsh_vas_sub_ser = ls_vas_ser-fsh_vas_sub_ser
                                         fsh_vas_ser     = ls_vas_ser-fsh_vas_ser
                                         vkorg           = ms_vas_condition-sales_organization
                                         kunnr           = ms_vas_condition-customer ].
          ENDIF.
        CATCH cx_sy_itab_line_not_found.
          rv_result = abap_true.
          MESSAGE e026(zsd_aif_vas_cond_upl) INTO DATA(lv_message) ##NEEDED ##MG_MISSING.
          add_error_message( 26 ).
          RETURN.
      ENDTRY.

    ENDLOOP.
  ENDMETHOD.
ENDCLASS.