class ZCL_SD_VE_CASELOT_AUTHCHK definition
  public
  final
  create public .

public section.

  interfaces IF_SADL_EXIT .
  interfaces IF_SADL_EXIT_CALC_ELEMENT_READ .
protected section.
private section.
ENDCLASS.



CLASS ZCL_SD_VE_CASELOT_AUTHCHK IMPLEMENTATION.


  METHOD if_sadl_exit_calc_element_read~calculate.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* NAGARSOW     | 12.03.2024 | 29721     : Feature - [CR] - Case Lot Minor Enhancements *
*              |            | DS4K982272                              *
*----------------------------------------------------------------------*
    DATA: lt_data TYPE STANDARD TABLE OF zsd_c_salesorder_rddpro WITH DEFAULT KEY.
    lt_data = CORRESPONDING #( it_original_data ).

    SELECT salesorder,
           material,
           plant,
           salesdoctype,
           salesorganization,
           distributionchanel,
           division
       FROM zsd_c_salesorder_rddpro
       FOR ALL ENTRIES IN @lt_data
       WHERE salesorder = @lt_data-salesorder AND
             material = @lt_data-material AND
             plant           = @lt_data-plant
      INTO TABLE @DATA(lt_data_tmp).
    IF sy-subrc = 0 .
      LOOP AT lt_data ASSIGNING FIELD-SYMBOL(<ls_data>).
        <ls_data>-disablecreatebyauth = 'X'.
        READ TABLE lt_data_tmp ASSIGNING FIELD-SYMBOL(<ls_tmp_data>) WITH KEY salesorder = <ls_data>-salesorder
                                        material = <ls_data>-material
                                        plant  = <ls_data>-plant.
        IF sy-subrc = 0.
          AUTHORITY-CHECK OBJECT
               zif_sd_caselot_const=>gc_authority_object_setting-object
            ID zif_sd_caselot_const=>gc_authority_object_setting-id_salesorg FIELD <ls_tmp_data>-salesorganization
            ID zif_sd_caselot_const=>gc_authority_object_setting-id_distchan FIELD <ls_tmp_data>-distributionchanel
            ID zif_sd_caselot_const=>gc_authority_object_setting-id_division FIELD <ls_tmp_data>-division
            ID zif_sd_caselot_const=>gc_authority_object_setting-id_actvt FIELD '01'.
          IF sy-subrc = 0.
            AUTHORITY-CHECK OBJECT
                            zif_sd_caselot_const=>gc_authority_object_fortype-object
                         ID zif_sd_caselot_const=>gc_authority_object_fortype-id_salesdoctype FIELD <ls_tmp_data>-salesdoctype
                         ID zif_sd_caselot_const=>gc_authority_object_fortype-id_actvt FIELD '01'.
            IF  sy-subrc = 0.
              <ls_data>-disablecreatebyauth = ''.
            ELSE.
              <ls_data>-disablecreatebyauth = 'X'.
            ENDIF.
          ELSE.
            <ls_data>-disablecreatebyauth = 'X'.
          ENDIF.
        ENDIF.
      ENDLOOP.
      ct_calculated_data = CORRESPONDING #( lt_data ).
    ENDIF.
  ENDMETHOD.


  method IF_SADL_EXIT_CALC_ELEMENT_READ~GET_CALCULATION_INFO.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* NAGARSOW     | 12.03.2024 | 29721     : Feature - [CR] - Case Lot Minor Enhancements *
*              |            | DS4K982272                              *
*----------------------------------------------------------------------*
    RETURN.
  endmethod.
ENDCLASS.