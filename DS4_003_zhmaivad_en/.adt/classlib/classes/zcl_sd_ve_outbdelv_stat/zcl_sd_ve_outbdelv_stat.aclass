CLASS zcl_sd_ve_outbdelv_stat DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_sadl_exit .
    INTERFACES if_sadl_exit_calc_element_read .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_SD_VE_OUTBDELV_STAT IMPLEMENTATION.


  METHOD if_sadl_exit_calc_element_read~calculate.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* NAGARSOW     | 09.10.2023 | 25663     : [Build] Outbound Delivery Stat *
*              |            | DS4K965942                              *
*----------------------------------------------------------------------*
    DATA: lt_data TYPE STANDARD TABLE OF zsd_i_outdelv_stat WITH DEFAULT KEY.

    lt_data = CORRESPONDING #( it_original_data ).
    TRY.
        DATA(lt_hm_table) = zcl_pe_hm_basic=>get_table( iv_id = '2486' ).
      CATCH zcx_pe_hm. " Hardcode Manager.
        RETURN.
    ENDTRY.
    SORT lt_hm_table BY sequence DESCENDING.
    IF lt_data IS NOT INITIAL.
      SELECT deliverydocument,
             eventtype
        FROM zsd_i_tevents_out_delivery_int
        INTO TABLE @DATA(lt_eventdet)
        FOR ALL ENTRIES IN @lt_data
        WHERE deliverydocument = @lt_data-deliverydocument.
      IF sy-subrc = 0.
        LOOP AT lt_data ASSIGNING FIELD-SYMBOL(<ls_data>).
          LOOP AT lt_hm_table ASSIGNING FIELD-SYMBOL(<ls_hm_table>). "#EC CI_NESTED
            READ TABLE lt_eventdet WITH KEY deliverydocument = <ls_data>-deliverydocument ##WARN_OK
                                            eventtype = <ls_hm_table>-field1 TRANSPORTING NO FIELDS.
            IF sy-subrc = 0.
              <ls_data>-overalldelvstatus = <ls_hm_table>-field2.
              <ls_data>-overalldelvstatuscriticality = <ls_hm_table>-field3.
              EXIT.
            ENDIF.
          ENDLOOP.
        ENDLOOP.
        ct_calculated_data = CORRESPONDING #( lt_data ).
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD if_sadl_exit_calc_element_read~get_calculation_info.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* NAGARSOW     | 09.10.2023 | 25663     : [Build] Outbound Delivery Stat *
*              |            | DS4K965942                              *
*----------------------------------------------------------------------*
    RETURN.
  ENDMETHOD.
ENDCLASS.