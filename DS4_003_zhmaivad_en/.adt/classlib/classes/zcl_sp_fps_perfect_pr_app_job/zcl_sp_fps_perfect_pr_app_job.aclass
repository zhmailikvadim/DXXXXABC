CLASS zcl_sp_fps_perfect_pr_app_job DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_apj_dt_exec_object .
    INTERFACES if_apj_rt_exec_object .

    CONSTANTS:
      BEGIN OF gc_sel_parameters,
        gen_artic                    TYPE c LENGTH 8   VALUE 'S_MATNR',
        supplier                     TYPE c LENGTH 8   VALUE 'S_LIFNR',
        plant                        TYPE c LENGTH 8   VALUE 'S_WERKS',
        ex_factory_date              TYPE c LENGTH 8   VALUE 'S_XFACT',
        release_date                 TYPE c LENGTH 8   VALUE 'S_RELEAS',
        start_prod_date              TYPE c LENGTH 8   VALUE 'S_START',
        planned_date                 TYPE c LENGTH 8   VALUE 'S_PLAN',
        delivery_date                TYPE c LENGTH 8   VALUE 'S_DELIV',
        mater_avail_date             TYPE c LENGTH 8   VALUE 'S_MAD',
        proc_status                  TYPE c LENGTH 8   VALUE 'S_STAT',
        data_type_char               TYPE c LENGTH 4   VALUE 'CHAR',
        data_type_dats               TYPE c LENGTH 4   VALUE 'DATS',
        component_type_proc_status   TYPE c LENGTH 30  VALUE 'ZSP_PR_PROC_STATUS',
        component_type_plant         TYPE c LENGTH 30  VALUE 'WERKS_D',
        component_type_supplier      TYPE c LENGTH 30  VALUE 'LIFNR',
        component_type_gen_artic     TYPE c LENGTH 30  VALUE 'MATNR',
      END OF gc_sel_parameters .
  PROTECTED SECTION.
private section.

  TYPES: gtyp_r_article         TYPE RANGE OF char40,
         gtyp_r_supplier        TYPE RANGE OF char10,
         gtyp_r_plant           TYPE RANGE OF char4,
         gtyp_r_ex_factory_date TYPE RANGE OF datum,
         gtyp_r_release_date    TYPE RANGE OF datum,
         gtyp_r_start_prod_date TYPE RANGE OF datum,
         gtyp_r_planned_date    TYPE RANGE OF datum,
         gtyp_r_delivery_date   TYPE RANGE OF datum,
         gtyp_r_mad             TYPE RANGE OF datum,
         gtyp_r_proc_status     TYPE RANGE OF char1.
ENDCLASS.



CLASS ZCL_SP_FPS_PERFECT_PR_APP_JOB IMPLEMENTATION.


  METHOD if_apj_dt_exec_object~get_parameters.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 31.01.2023 | 15601     : [BUILD] PR creation from pos *
*              |            | DS4K936641                               *
*----------------------------------------------------------------------*
    " Return the supported selection parameters here
    et_parameter_def = VALUE #(
       ( selname        = gc_sel_parameters-gen_artic
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_char
         length         =  40
         param_text     = TEXT-001
         component_type = gc_sel_parameters-component_type_gen_artic
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-supplier
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_char
         length         =  10
         param_text     = TEXT-002
         component_type = gc_sel_parameters-component_type_supplier
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-plant
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_char
         length         =  4
         param_text     = TEXT-003
         component_type = gc_sel_parameters-component_type_plant
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-ex_factory_date
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_dats
         length         =  8
         param_text     = TEXT-004
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-release_date
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_dats
         length         =  8
         param_text     = TEXT-005
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-start_prod_date
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_dats
         length         =  8
         param_text     = TEXT-006
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-planned_date
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_dats
         length         =  8
         param_text     = TEXT-007
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-delivery_date
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_dats
         length         =  8
         param_text     = TEXT-008
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-mater_avail_date
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_dats
         length         =  8
         param_text     = TEXT-009
         changeable_ind = abap_true )
       ( selname        = gc_sel_parameters-proc_status
         kind           = if_apj_dt_exec_object=>select_option
         datatype       = gc_sel_parameters-data_type_char
         length         =  1
         param_text     = TEXT-010
         component_type = gc_sel_parameters-component_type_proc_status
         changeable_ind = abap_true ) )   ##TYPE ##NUMBER_OK.

    " Return the default parameters values here
    et_parameter_val = VALUE #(
      ( selname = gc_sel_parameters-proc_status
        kind    = if_apj_dt_exec_object=>select_option
        sign    = 'I'
        option  = 'EQ'
        low     = 'E' ) )   ##TYPE.
  ENDMETHOD.


  METHOD if_apj_rt_exec_object~execute.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 31.01.2023 | 15601     : [BUILD] PR creation from pos *
*              |            | DS4K936641                               *
*----------------------------------------------------------------------*

    DATA: lr_sel_article         TYPE gtyp_r_article,
          lr_sel_supplier        TYPE gtyp_r_supplier,
          lr_sel_plant           TYPE gtyp_r_plant,
          lr_sel_ex_fact_date    TYPE gtyp_r_ex_factory_date,
          lr_sel_release_date    TYPE gtyp_r_release_date,
          lr_sel_start_prod_date TYPE gtyp_r_start_prod_date,
          lr_sel_planned_date    TYPE gtyp_r_planned_date,
          lr_sel_delivery_date   TYPE gtyp_r_delivery_date,
          lr_sel_mad             TYPE gtyp_r_mad,
          lr_sel_proc_status     TYPE gtyp_r_proc_status,
          lt_process_data        TYPE TABLE OF zsp_t_pr_group,
          lt_result_messages     TYPE zcl_sp_fps_perfect_pr_postproc=>gtyp_t_pr_result.

    LOOP AT it_parameters ASSIGNING FIELD-SYMBOL(<ls_parameter>).
      CASE <ls_parameter>-selname.
        WHEN gc_sel_parameters-gen_artic.
          lr_sel_article = VALUE #( BASE lr_sel_article ( sign   = <ls_parameter>-sign
                                                          option = <ls_parameter>-option
                                                          low    = <ls_parameter>-low
                                                          high   = <ls_parameter>-high  ) ).

        WHEN gc_sel_parameters-supplier.
          lr_sel_supplier = VALUE #( BASE lr_sel_supplier ( sign   = <ls_parameter>-sign
                                                            option = <ls_parameter>-option
                                                            low    = <ls_parameter>-low
                                                            high   = <ls_parameter>-high  ) ).

        WHEN gc_sel_parameters-plant.
          lr_sel_plant = VALUE #( BASE lr_sel_plant ( sign   = <ls_parameter>-sign
                                                      option = <ls_parameter>-option
                                                      low    = <ls_parameter>-low
                                                      high   = <ls_parameter>-high  ) ).
        WHEN gc_sel_parameters-ex_factory_date.
          lr_sel_ex_fact_date = VALUE #( BASE lr_sel_ex_fact_date ( sign   = <ls_parameter>-sign
                                                                    option = <ls_parameter>-option
                                                                    low    = <ls_parameter>-low
                                                                    high   = <ls_parameter>-high  ) ).
        WHEN gc_sel_parameters-release_date.
          lr_sel_release_date = VALUE #( BASE lr_sel_release_date ( sign   = <ls_parameter>-sign
                                                                    option = <ls_parameter>-option
                                                                    low    = <ls_parameter>-low
                                                                    high   = <ls_parameter>-high  ) ).
        WHEN gc_sel_parameters-start_prod_date.
          lr_sel_start_prod_date = VALUE #( BASE lr_sel_start_prod_date ( sign   = <ls_parameter>-sign
                                                                          option = <ls_parameter>-option
                                                                          low    = <ls_parameter>-low
                                                                          high   = <ls_parameter>-high  ) ).
        WHEN gc_sel_parameters-planned_date.
          lr_sel_planned_date = VALUE #( BASE lr_sel_planned_date ( sign   = <ls_parameter>-sign
                                                                    option = <ls_parameter>-option
                                                                    low    = <ls_parameter>-low
                                                                    high   = <ls_parameter>-high  ) ).
        WHEN gc_sel_parameters-delivery_date.
          lr_sel_delivery_date = VALUE #( BASE lr_sel_delivery_date ( sign   = <ls_parameter>-sign
                                                                      option = <ls_parameter>-option
                                                                      low    = <ls_parameter>-low
                                                                      high   = <ls_parameter>-high  ) ).
        WHEN gc_sel_parameters-mater_avail_date.
          lr_sel_mad = VALUE #( BASE lr_sel_mad ( sign   = <ls_parameter>-sign
                                                  option = <ls_parameter>-option
                                                  low    = <ls_parameter>-low
                                                  high   = <ls_parameter>-high  ) ).
        WHEN gc_sel_parameters-proc_status.
          lr_sel_proc_status = VALUE #( BASE lr_sel_proc_status ( sign   = <ls_parameter>-sign
                                                                  option = <ls_parameter>-option
                                                                  low    = <ls_parameter>-low
                                                                  high   = <ls_parameter>-high  ) ).
      ENDCASE.
    ENDLOOP.

    DATA(lo_pr_create_proc) = NEW zcl_sp_fps_perfect_pr_postproc( ).

    SELECT * FROM zsp_i_pr_group WHERE genericarticle       IN @lr_sel_article         AND
                                       supplier             IN @lr_sel_supplier        AND
                                       plant                IN @lr_sel_plant           AND
                                       xfactorydate         IN @lr_sel_ex_fact_date    AND
                                       releasedate          IN @lr_sel_release_date    AND
                                       startproddate        IN @lr_sel_start_prod_date AND
                                       planneddate          IN @lr_sel_planned_date    AND
                                       deliverydate         IN @lr_sel_delivery_date   AND
                                       materialavailabddate IN @lr_sel_mad             AND
                                       processingstatus     IN @lr_sel_proc_status
    ORDER BY purchaserequisition,
             purchaserequisitionitem,
             processingstatus
    INTO TABLE @DATA(lt_selected_data).

    IF sy-subrc = 0.
      READ TABLE lt_selected_data TRANSPORTING NO FIELDS WITH KEY processingstatus = zcl_sp_fps_perfect_pr_postproc=>gc_process_param-success BINARY SEARCH.
      IF sy-subrc <> 0.
        lt_process_data = CORRESPONDING #( lt_selected_data MAPPING ebeln               = purchaserequisition
                                                                    ebelp               = purchaserequisitionitem
                                                                    zebeln              = purchaserequisitions4
                                                                    matnr               = genericarticle
                                                                    matnr_v             = variantarticle
                                                                    planned_qty         = quantity
                                                                    werks               = plant
                                                                    lifnr               = supplier
                                                                    rel_date            = releasedate
                                                                    startprod_date      = startproddate
                                                                    planned_date        = planneddate
                                                                    xfactory_date       = xfactorydate
                                                                    mad_date            = materialavailabddate
                                                                    del_date            = deliverydate
                                                                    custid              = customerid
                                                                    motid               = modoftransid
                                                                    pleadtime           = manufactleadtime
                                                                    zz1_mm_scmsegment   = scsegment
                                                                    zz1_ordprio         = orderpriority
                                                                    zz1_adi_m_promoflag = promofactory
                                                                    proc_status         = processingstatus
                                                                    proc_message        = processingmessage
                                                                    erdat               = createdon
                                                                    erzet               = createdat ).

        SELECT FROM zsp_i_pr_group
        FIELDS purchaserequisition, purchaserequisitionitem
        FOR ALL ENTRIES IN @lt_selected_data
        WHERE purchaserequisition = @lt_selected_data-purchaserequisition
        ORDER BY PRIMARY KEY
        INTO TABLE @DATA(lt_pr_group).          "#EC CI_FAE_NO_LINES_OK

        IF sy-subrc = 0.
          IF lines( lt_pr_group ) <> lines( lt_process_data ).
            LOOP AT lt_pr_group ASSIGNING FIELD-SYMBOL(<ls_pr_group>).
              READ TABLE lt_process_data TRANSPORTING NO FIELDS WITH KEY ebeln = <ls_pr_group>-purchaserequisition
                                                                         ebelp = <ls_pr_group>-purchaserequisitionitem BINARY SEARCH.
              IF sy-subrc <> 0.
                lt_result_messages = VALUE #( BASE lt_result_messages ( pr_ibp      = <ls_pr_group>-purchaserequisition
                                                                        result_type = lo_pr_create_proc->gc_process_param-sel_not_all ) ).
                DELETE lt_pr_group                      "#EC CI_SEL_DEL
                WHERE purchaserequisition = <ls_pr_group>-purchaserequisition. "#EC CI_STDSEQ
              ENDIF.
            ENDLOOP.
            lo_pr_create_proc->save_log( lt_result_messages ).
          ELSE.
            lo_pr_create_proc->process_pr_creation( EXPORTING it_input_data = lt_process_data
                                                    IMPORTING et_result     = lt_result_messages ).
          ENDIF.
        ENDIF.
      ELSE.
        lt_result_messages = VALUE #( ( result_type = lo_pr_create_proc->gc_process_param-sel_processed ) ).
        lo_pr_create_proc->save_log( lt_result_messages ).
      ENDIF.
    ELSE.
      lt_result_messages = VALUE #( ( result_type = lo_pr_create_proc->gc_process_param-sel_empty ) ).
      lo_pr_create_proc->save_log( lt_result_messages ).
    ENDIF.
  ENDMETHOD.
ENDCLASS.