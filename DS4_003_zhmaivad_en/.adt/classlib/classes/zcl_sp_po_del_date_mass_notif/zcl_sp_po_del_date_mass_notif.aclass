class ZCL_SP_PO_DEL_DATE_MASS_NOTIF definition
  public
  inheriting from /IWNGW/CL_NOTIF_PROVIDER_ABS
  final
  create public .

public section.

  constants:
    BEGIN OF gc_process_param,
        provider_id             TYPE /iwngw/notif_provider_id     VALUE 'ZSP_PO_DEL_DATE_MASS_NOTIF_PROV',
        type_key_success        TYPE /iwngw/notification_type_key VALUE 'PODelDateUpdateSuccess',
        type_key_failed         TYPE /iwngw/notification_type_key VALUE 'PODelDateUpdateFailed',
        type_version            TYPE /iwngw/notification_type_key VALUE '01',
        target_object           TYPE /iwngw/notif_nav_obj         VALUE 'ApplicationLog',
        target_action           TYPE /iwngw/notif_nav_action      VALUE 'showDetails',
        target_param_log_obj    TYPE /iwngw/notif_nav_param_key   VALUE 'LogObjectId',
        target_param_log_subobj TYPE /iwngw/notif_nav_param_key   VALUE 'LogObjectSubId',
        log_object              TYPE balobj_d                     VALUE 'ZSP_PO_MAINT',
        log_subobject           TYPE balsubobj                    VALUE 'ZSP_DEL_DATE_MASSUPD',
      END OF gc_process_param .

  class-methods SEND_NOTIFICATION
    importing
      !IT_RECIPIENT type /IWNGW/IF_NOTIF_PROVIDER=>TY_T_NOTIFICATION_RECIPIENT
      !IV_TYPE_KEY type /IWNGW/IF_NOTIF_PROVIDER=>TY_S_NOTIFICATION_TYPE_ID-TYPE_KEY .

  methods /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_PARAMETERS
    redefinition .
  methods /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_TYPE
    redefinition .
  methods /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_TYPE_TEXT
    redefinition .
  methods /IWNGW/IF_NOTIF_PROVIDER_EXT~HANDLE_ACTION
    redefinition .
protected section.
private section.
ENDCLASS.



CLASS ZCL_SP_PO_DEL_DATE_MASS_NOTIF IMPLEMENTATION.


  METHOD /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_PARAMETERS.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 01.03.2023 | 18064     :  [Build] - PO Delivery Date  *
*              |            | DS4K943975                               *
*----------------------------------------------------------------------*
    CLEAR: et_parameter.
    DATA(lv_lang) = sy-langu.
    SET LANGUAGE lv_lang.
  ENDMETHOD.


  METHOD /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_TYPE.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 01.03.2023 | 18064     :  [Build] - PO Delivery Date  *
*              |            | DS4K943975                               *
*----------------------------------------------------------------------*
    CLEAR:
      es_notification_type,
      et_notification_action.

    es_notification_type = VALUE #( is_groupable   = abap_true
                                    type_key       = iv_type_key
                                    version        = iv_type_version
                                    is_email_enbld = abap_false ).
  ENDMETHOD.


  METHOD /IWNGW/IF_NOTIF_PROVIDER_EXT~GET_NOTIFICATION_TYPE_TEXT.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 01.03.2023 | 18064     :  [Build] - PO Delivery Date  *
*              |            | DS4K943975                               *
*----------------------------------------------------------------------*
    DATA: lv_title    TYPE string,
          lv_subtitle TYPE string.

    CASE iv_type_key.
      WHEN gc_process_param-type_key_success.
        lv_title = TEXT-001.
      WHEN gc_process_param-type_key_failed.
        lv_title = TEXT-002.
    ENDCASE.

    lv_subtitle = TEXT-003.

    et_type_text = VALUE #(
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-description        value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_grouped   value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_public    value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_sensitive value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-subtitle           value = lv_subtitle  ) ).
  ENDMETHOD.


  METHOD /IWNGW/IF_NOTIF_PROVIDER_EXT~HANDLE_ACTION.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 01.03.2023 | 18064     :  [Build] - PO Delivery Date  *
*              |            | DS4K943975                               *
*----------------------------------------------------------------------*
    CLEAR: es_result.
    RETURN.
  ENDMETHOD.


  METHOD SEND_NOTIFICATION.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* KHRYSAND     | 01.03.2023 | 18064     :  [Build] - PO Delivery Date  *
*              |            | DS4K943975                               *
*----------------------------------------------------------------------*
    TRY.

        /iwngw/cl_notification_api=>create_notifications(
          iv_provider_id  = gc_process_param-provider_id
          it_notification = VALUE #(
                              ( id           = cl_uuid_factory=>create_system_uuid( )->create_uuid_x16( )
                                type_key     = iv_type_key
                                type_version = gc_process_param-type_version
                                priority     = SWITCH #( iv_type_key
                                                         WHEN gc_process_param-type_key_success THEN /iwngw/if_notif_provider=>gcs_priorities-low
                                                         WHEN gc_process_param-type_key_failed  THEN /iwngw/if_notif_provider=>gcs_priorities-high )
                                recipients   = it_recipient
                                navigation_target_object = gc_process_param-target_object
                                navigation_target_action = gc_process_param-target_action
                                navigation_parameters    = VALUE /iwngw/if_notif_provider=>ty_t_navigation_parameter(
                                                              ( name  = gc_process_param-target_param_log_obj
                                                                value = gc_process_param-log_object )
                                                              ( name  = gc_process_param-target_param_log_subobj
                                                                value = gc_process_param-log_subobject ) )
                              ) ) ).

      CATCH cx_uuid_error
            /iwngw/cx_notification_api.
        RETURN.
    ENDTRY.
  ENDMETHOD.
ENDCLASS.