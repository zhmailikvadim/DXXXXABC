class ZCL_ZSD_PRICING_CONDIT_MPC_EXT definition
  public
  inheriting from ZCL_ZSD_PRICING_CONDIT_MPC
  create public .

public section.

  methods CONSTRUCTOR .

  methods DEFINE
    redefinition .
protected section.
private section.

  data MO_DEP type ref to IF_CND_MAINT_LTD_DEP .
ENDCLASS.



CLASS ZCL_ZSD_PRICING_CONDIT_MPC_EXT IMPLEMENTATION.


  method CONSTRUCTOR.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DEREVOLE     | 21.10.2022 | T4MTEMP-846 [Build] BE -                 *
*              |            | Pricing Mass Upload Tool                 *
*              |            | DS4K929425, Change number 13489          *
*----------------------------------------------------------------------*
    super->constructor( ).
    mo_dep = cl_cnd_maint_ltd_dep_mgr=>get_instance( ).
  endmethod.


  method DEFINE.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* DEREVOLE     | 21.10.2022 | T4MTEMP-846 [Build] BE -                 *
*              |            | Pricing Mass Upload Tool                 *
*              |            | DS4K929425, Change number 13489          *
*----------------------------------------------------------------------*
 super->define( ).

    DATA:
      lo_annotation   TYPE REF TO /iwbep/if_mgw_odata_annotation,
      lo_entity_type  TYPE REF TO /iwbep/if_mgw_odata_entity_typ,
      lo_complex_type TYPE REF TO /iwbep/if_mgw_odata_cmplx_type,
      lo_property     TYPE REF TO /iwbep/if_mgw_odata_property,
      lo_entity_set   TYPE REF TO /iwbep/if_mgw_odata_entity_set.

    DATA: ls_model_features TYPE /iwbep/if_mgw_appl_types=>ty_s_model_features.

    ls_model_features-use_deferred_batch_resp_crea = abap_true.
    model->set_model_features( is_model_features = ls_model_features ).
    lo_entity_type = model->get_entity_type( iv_entity_name = 'ZC_SlsPricingConditionRecordTPType' ).
    lo_annotation = lo_entity_type->/iwbep/if_mgw_odata_annotatabl~create_annotation( iv_annotation_namespace =  /iwbep/if_mgw_med_odata_types=>gc_sap_namespace ).
    lo_annotation->add(
        iv_key      = 'semantics'
        iv_value    = 'aggregate' ).

    DATA(lo_entity_type_key) =  model->get_entity_type( iv_entity_name = 'I_SlsPrcgKeyCombinationType' ).
    DATA(lo_property_key) = lo_entity_type_key->get_property( iv_property_name = 'PrcgKeyCombinationText' ).
    lo_property_key->set_sortable( abap_false ).


    mo_dep->get_wlf_mp_fields(
*      EXPORTING
*        iv_bor_obj_spec  = 'BUS30110'       " Object Type
*        iv_map_data_cat  = 'B'              " Mapping Data Category
      IMPORTING
        et_wlf_mp_fields = DATA(lt_wlf_mp_fields)  " Mapping external to internal fields
    ).
    DATA(lt_properties) = lo_entity_type->get_properties( ).

    "set key fields as Dimension
    LOOP AT lt_properties INTO DATA(ls_property) WHERE name NE 'CONDITIONRECORD'.
      READ TABLE lt_wlf_mp_fields WITH KEY ext_fieldname = ls_property-name TRANSPORTING NO FIELDS.
      IF sy-subrc = 0.
        TRY.
            lo_property = lo_entity_type->get_property( iv_property_name = CONV #( ls_property-external_name ) ). "'Customer' ).
            lo_annotation = lo_property->/iwbep/if_mgw_odata_annotatabl~create_annotation( iv_annotation_namespace =  /iwbep/if_mgw_med_odata_types=>gc_sap_namespace ).
            lo_annotation->add(
                iv_key      = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_key-aggregation_role
                iv_value    = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_value-dimension-dimension ).
          CATCH /iwbep/cx_mgw_med_exception. " Meta data exception
        ENDTRY.
      ENDIF.
    ENDLOOP.

    "set the ValidOnDate is a DatePicker in filter
    lo_property = lo_entity_type->get_property( iv_property_name = 'ValidOnDate' ).
    lo_annotation = lo_property->/iwbep/if_mgw_odata_annotatabl~create_annotation( iv_annotation_namespace =  /iwbep/if_mgw_med_odata_types=>gc_sap_namespace ).
    lo_annotation->add(
        iv_key      = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_key-filter_restriction
        iv_value    = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_value-filter-single_value ).

    "set the ConditionValidityEndDate is a DatePicker in filter
    lo_property = lo_entity_type->get_property( iv_property_name = 'ConditionValidityEndDate' ).
    lo_annotation = lo_property->/iwbep/if_mgw_odata_annotatabl~create_annotation( iv_annotation_namespace =  /iwbep/if_mgw_med_odata_types=>gc_sap_namespace ).
    lo_annotation->add(
        iv_key      = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_key-filter_restriction
        iv_value    = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_value-filter-single_value ).

    "set the ConditionValidityStartDate is a DatePicker in filter
    lo_property = lo_entity_type->get_property( iv_property_name = 'ConditionValidityStartDate' ).
    lo_annotation = lo_property->/iwbep/if_mgw_odata_annotatabl~create_annotation( iv_annotation_namespace =  /iwbep/if_mgw_med_odata_types=>gc_sap_namespace ).
    lo_annotation->add(
        iv_key      = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_key-filter_restriction
        iv_value    = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_value-filter-single_value ).

*    "set the ConditionRateValue
*    TRY.
*        lo_property = lo_entity_type->get_property( iv_property_name = 'ConditionRateValue' ).
*        lo_annotation = lo_property->/iwbep/if_mgw_odata_annotatabl~create_annotation( iv_annotation_namespace =  /iwbep/if_mgw_med_odata_types=>gc_sap_namespace ).
*        lo_annotation->add(
*          iv_key      = /iwbep/if_ana_odata_types=>gcs_ana_odata_annotation_key-unit
*          iv_value    = 'ConditionRateValueUnit' ).
*      CATCH /iwbep/cx_mgw_med_exception. " Meta data exception
*    ENDTRY.

    lo_property = lo_entity_type->get_property( iv_property_name = 'AdditionalValueDays' ).
    lo_property->set_type_edm_decimal( ).
    lo_property->set_maxlength( iv_max_length = 2 ).
    lo_property->set_precison( iv_precision = 0  ).

    lo_entity_type = model->get_entity_type( iv_entity_name = 'UploadExcel' ).
    lo_property = lo_entity_type->get_property( iv_property_name = 'MimeType' ).
    lo_property->set_as_content_type( ).
  endmethod.
ENDCLASS.