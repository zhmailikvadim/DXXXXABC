@Metadata.layer: #CORE
@UI.headerInfo: { typeNamePlural: 'Articles with IC Mark-Up Value' }
@UI.presentationVariant: [{maxItems: 500 }]
@UI.presentationVariant: [{visualizations: [{type: #AS_LINEITEM}]}]
annotate view ZFI_C_IC_Mark_Up_Costs with
{   
      
      @EndUserText.label               : 'Article'
      @UI.lineItem                     : [
//                                          { type: #FOR_ACTION, label: 'Post Costs', position: 10, dataAction: 'PostCosts' },  
                                          { type: #FOR_INTENT_BASED_NAVIGATION, label: 'Display Setting', position: 20, 
                                                   semanticObject: 'Customer', semanticObjectAction: 'zcostcomponents', 
                                                   requiresContext: false },             
                                          { type: #STANDARD, label: 'Variant Article', position: 20, importance: #HIGH }]                                        
      @UI.selectionField               : [{ position: 60 }]
      @Consumption.filter              : { multipleSelections: true }
      @Consumption.valueHelpDefinition : [{entity: {name: 'I_MaterialStdVH', element: 'Material'}}]
      Article;
  
      @EndUserText.label               : 'Plant'
      @UI.textArrangement              : #TEXT_LAST
      @UI.selectionField               : [{ position: 30 }]
      @Consumption.filter              : { mandatory: true, multipleSelections: true }
      @Consumption.valueHelpDefinition : [{entity: {name: 'ZFI_I_IC_Mark_Up_Plant_VH', element: 'Plant'},
                                          additionalBinding: [{ localElement: 'CompanyCode', element: 'CompanyCode' }] }]
      @UI.lineItem                     : [{ position: 30, importance: #HIGH }]                                          
      Plant;

      @UI.hidden                       : true
      CostingLotSize;
      
      @UI.hidden                       : true      
      CostingVersion;

      @EndUserText.label               : 'Valid from'
      @UI.lineItem                     : [{ position: 90, importance: #HIGH }]
      ValidFrom;

      @UI.hidden                       : true
      ValidTo;
 
      @EndUserText.label               : 'Costing Date'
      @Consumption.filter              : { mandatory: true, selectionType: #INTERVAL }                      
      @UI.selectionField               : [{ position: 40 }]       
      CostingDate;
      
      @UI.hidden                       : true
      ValuationQuantityUnit;

      @EndUserText.label               : 'Standard FOB'
      StandardFOB;

      @EndUserText.label               : 'Buying Commission'
      BuyingCommission;

      @EndUserText.label               : 'Standard Customs'
      StandardCustoms;

      @EndUserText.label               : 'Standard Freight'
      StandardFreight;

      @EndUserText.label               : 'Costing Variant'
      CostingVariant;

      @EndUserText.label               : 'Currency'
      Currency;

      @EndUserText.label               : 'Existing IC Mark-Up %'
      @UI.lineItem                     : [{ position: 110, importance: #HIGH }]
      ExistingMarkUpPercentage;

      @EndUserText.label               : 'Existing IC Mark-Up Value'
      @UI.lineItem                     : [{ position: 120, importance: #HIGH }]
      ExistingMarkUpValue;

      @EndUserText.label               : 'IC Mark-Up %'
      @UI.lineItem                     : [{ position: 130, importance: #HIGH }]
      MarkUpPercentage;

      @EndUserText.label               : 'IC Mark-Up Value'
      @UI.lineItem                     : [{ position: 140, importance: #HIGH }]
      MarkUpValue;  
      
      @UI.selectionField               : [{ position: 10 }]
      @Consumption.filter              : { mandatory: true, multipleSelections: true, defaultValue: '0010' }
      @Consumption.valueHelpDefinition : [{entity: {name: 'ZFI_I_IC_Mark_Up_Cntrl_Area_VH', element: 'ControllingArea'}}]
      ControllingArea;

      @UI.selectionField               : [{ position: 20 }]
      @Consumption.filter              : { multipleSelections: true }
      @Consumption.valueHelpDefinition : [{entity: {name: 'ZFI_I_IC_Mark_UP_Comp_Code_VH', element: 'CompanyCode'}}]
      CompanyCode; 
    
      @EndUserText.label               : 'Status'
      @UI.lineItem                     : [{ position: 10, importance: #HIGH, criticality: 'StatusCriticality' }]
      @UI.selectionField               : [{ position: 50 }]      
      @Consumption.filter              : { multipleSelections: true }
      @Consumption.valueHelpDefinition : [{entity: {name: 'ZFI_I_IC_Mark_Up_Status_VH', element: 'StatusText'}}]           
      Status;           

      @UI.hidden                       : true
      StatusCriticality;  
      
      @UI.hidden                       : true
      PlantName;       
}