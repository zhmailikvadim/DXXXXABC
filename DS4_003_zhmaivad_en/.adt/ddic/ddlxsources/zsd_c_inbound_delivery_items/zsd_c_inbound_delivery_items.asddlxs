@Metadata.layer: #CORE

@UI: { headerInfo: { typeName: 'Item',
                     typeNamePlural: 'Items' } }
@UI.lineItem: [{criticality: 'Criticality'}]
annotate entity ZSD_C_INBOUND_DELIVERY_ITEMS with
{



  @UI: {
  fieldGroup: [{ qualifier: 'DeliveryDocumentData', position: 10 }],
  lineItem: [
    {
      type: #AS_FIELDGROUP,
      label: 'Inbound Delivery',
      valueQualifier: 'DeliveryDocumentData',
      importance: #HIGH,
      position: 10
    },
    { type: #FOR_ACTION,  dataAction: 'ChangeDeliveryDate' , label: 'Change Delivery Date', invocationGrouping: #CHANGE_SET, importance: #HIGH }],
         selectionField: [{ position: 15 }] }
  DeliveryDocument;

  @UI: { lineItem:       [{ position: 14, label: 'Delivery Document Item' }] }
  DeliveryDocumentItem;

  @UI: { lineItem:       [{ position: 17, label: 'Status', criticalityRepresentation: #WITHOUT_ICON, criticality: 'Criticality'
   }],
         selectionField: [{ position: 10 }] }
  Status;

  @UI: { lineItem:       [{ position: 20, label: 'Delivery Date' }],
         selectionField: [{ position: 20 }] }
  DeliveryDate;

  @UI: { lineItem:       [{ position: 40, label: 'Container ID' }],
         selectionField: [{ position: 40 }] }
  MeansOfTransport;

  @UI: { selectionField: [{ position: 70 }] }
  @UI.hidden: true
  Supplier;

  @UI: { lineItem:       [{ position: 50, label: 'Plant' }],
         selectionField: [{ position: 60 }],
         textArrangement: #TEXT_LAST}
  Plant;

  @UI.hidden: true
  PlantName;

  @UI: { lineItem:       [{ position: 60, label: 'Material' }],
         selectionField: [{ position: 50 }],
         textArrangement: #TEXT_LAST }
  Material;

  @UI.hidden: true
  MaterialName;

  @UI: { lineItem:       [{ position: 50, label: 'Purchase Document' }],
         selectionField: [{ position: 80 }],
         textArrangement: #TEXT_LAST }
  ReferenceSDDocument;

  @UI.hidden: true
  PurchasingDocumentTypeName;

  @UI: { lineItem:       [{ position: 30, label: 'Shipment Number' }],
         selectionField: [{ position: 30 }],
         textArrangement: #TEXT_LAST }
  ShipmentNumber;

  @UI.hidden: true
  ShipmentTypeText;

  @UI.fieldGroup: [{label: 'Delivery Document Type', qualifier: 'DeliveryDocumentData', position: 20 }]
  DeliveryDocumentTypeName;

  @UI.hidden: true
  Criticality;

  @UI.hidden: true
  LastChangeDate;
}