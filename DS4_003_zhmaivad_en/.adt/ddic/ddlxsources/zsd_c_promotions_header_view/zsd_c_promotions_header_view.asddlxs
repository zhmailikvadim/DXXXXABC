    @Metadata.layer: #CORE

@UI: { headerInfo: { typeName: 'Promotion',
                   typeNamePlural: 'Promotions',
                   title: { type: #STANDARD, value: 'Promotion' },
                   description: { value: 'PromotionText' }},
     presentationVariant: [{
     visualizations: [{type: #AS_LINEITEM}]  }] }

annotate view ZSD_C_PROMOTIONS_HEADER_VIEW with
{
  @UI.lineItem:       [{ position: 10},
                       { type: #FOR_ACTION,  dataAction: 'delete_promotion' , label: 'Delete', invocationGrouping:#CHANGE_SET},
                       { type: #FOR_ACTION,  dataAction: 'deactivate_promotion' , label: 'Deactivate'},
                       { type: #FOR_ACTION,  dataAction: 'activate_promotion' , label: 'Activate' } ]

  @UI.facet: [ { id:              'Promotion',
                 purpose:         #STANDARD,
                 type:            #IDENTIFICATION_REFERENCE,
                 position:        10,
                 label: 'Promotion Details'
               }]

  @UI: { lineItem: [{ criticality: 'Criticality', criticalityRepresentation: #WITHOUT_ICON }] }
  CRPCheck;

  @Consumption.filter:{ multipleSelections: true }
  @UI: { lineItem: [{ position: 20}]}
  @UI.selectionField: [{ position: 10 }]
  Promotion;

  @UI: { lineItem: [{ position: 20}],
         selectionField: [{ position: 70 }],
         identification: [{ position: 20}]}
  Status;

  @UI: { lineItem: [{ position: 30}],
         selectionField: [{ position: 30 }],
         identification: [{ position: 30, label: 'Valid From' }]}
  ValidFrom;

  @UI: { lineItem: [{ position: 40}],
         selectionField: [{ position: 40 }],
         identification: [{ position: 40, label: 'Valid To' }]}
  ValidTo;

  @UI: { lineItem: [{ position: 70}],
         selectionField: [{ position: 50 }],
         identification: [{ position: 20, label: 'Created On' }]}
  CreatedOn;

  @UI: { lineItem: [{ position: 80}],
        selectionField: [{ position: 60 }],
        identification: [{ position: 10, label: 'Created By' }]}
  CreatedBy;

  @UI: { lineItem: [{ position: 50}]}
  Currency;

  @UI: { lineItem: [{ position: 60}],
         identification: [{ position: 50, label: 'Touchpoint' }]}
  Touchpoint;

  @UI: { lineItem: [{ position: 70}],
        identification: [{ position: 60, label: 'Happy Hours' }]}
  MediaType;

  @UI: { selectionField: [{ position: 80 }]}
  @UI.lineItem: [{ hidden: true }]
  MaterialNumber;

  @UI: { selectionField: [{ position: 90 }]}
  @UI.lineItem: [{ hidden: true }]
  SalesOrg;

  @UI: { selectionField: [{ position: 100 }]}
  @UI.lineItem: [{ hidden: true }]
  DistributionChannel;

  @UI.hidden: true
  PromotionText;

  @UI.hidden: true
  TouchpointText;

  @UI.hidden: true
  MediaTypeText;

  @UI.lineItem: [{ hidden: true }]
  Criticality;

  @EndUserText.label: 'Item'
  _Item;



}