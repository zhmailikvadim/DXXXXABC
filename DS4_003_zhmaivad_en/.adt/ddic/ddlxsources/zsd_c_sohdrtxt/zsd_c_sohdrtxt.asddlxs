@Metadata.layer: #CUSTOMER


@UI: { headerInfo: { typeName: 'HeaderText',
                     typeNamePlural: 'HeaderTexts',
                     title: { type: #STANDARD, value: 'Tdid' } } ,
                     presentationVariant: [{ sortOrder: [{ by: 'Tdid', direction: #DESC }], visualizations: [{type: #AS_LINEITEM}]  }] }

annotate view ZSD_C_SOHDRTXT with
{
  @UI.facet: [{ id:            'SOText',
                purpose:       #STANDARD,
                type:          #IDENTIFICATION_REFERENCE,
                label:         'SOText',
                position:      10 },
              { id:            'SONumbers',
                purpose:       #STANDARD,
                type:          #LINEITEM_REFERENCE,
                label:         'SONumbers',
                position:      20,
                targetElement: '_headertexti'}]

  @UI.hidden: true
  Sohtextuuid;

  @UI: {lineItem:       [ { position: 10 } ,
                          {type: #FOR_ACTION, dataAction: 'massupdate',     label: 'Mass Update'  },
                          {type: #FOR_ACTION, dataAction: 'massupdate_RDD', label: 'RDD re-determination'  }
                         ],
        identification: [{ label: 'Language Key', position: 10 },
                         { type: #FOR_ACTION, dataAction: 'massupdate',      label: 'Mass Update' } ,
                         { type: #FOR_ACTION, dataAction: 'massupdate_RDD' , label: 'RDD re-determination' }
                         ]
        }
  @Consumption.valueHelpDefinition: [{ entity: { name: 'I_LanguageText', element: 'Language'} }]
  Tdspras;
  
  @UI: {lineItem:       [{ position: 20 } ],
  identification: [{label: 'Text ID', position: 20 }] }
  @Consumption.valueHelpDefinition: [{ entity: { name: 'I_TextObjectTypeText', element: 'TextObjectType'} }]
  Tdid;


  @UI: {identification: [{label: 'Header Text', position: 30 }] }
  Text;

  @UI: {identification: [{label: 'Action', position: 40 }] }
  Action;


  @UI.hidden: true
  Tdobject;


}