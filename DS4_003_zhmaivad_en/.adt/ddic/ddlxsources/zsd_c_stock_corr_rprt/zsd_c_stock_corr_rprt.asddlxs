@Metadata.layer: #CORE
@UI: {
    headerInfo:{
            typeName: 'Stock Correction Report',
            typeNamePlural: 'Stock Correction List',
            title: {
                type: #STANDARD, value: 'Article'
            },
            description: {
                label: 'ArticleDescription',
                value: 'ArticleDescription'
            }
    },
     presentationVariant: [
        {
            requestAtLeast: [
              'AdjustmentCriticality',
              'StatusCriticality'
            ]
        }
    ]
}

@Search.searchable: true
@UI.lineItem: [{criticality: 'StatusCriticalityHighLight'}]
annotate view zsd_c_stock_corr_rprt with
{
  @UI.facet: [{
                id:              'GeneralInformation',
                type:            #COLLECTION,
                label:           'General Information',
                position:        90
              },
              {
                id:              'StatusHDR',
                type:            #DATAPOINT_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'StatusHDR',
                position:10
              },
              {
                id:              'QuantityHDR',
                type:            #DATAPOINT_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'QuantityHDR',
                position:13
              },
              {
                id:              'MoveTypeHDR',
                type:            #DATAPOINT_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'MoveTypeHDR',
                position:14
              },
              {
                id:              'StockTypeHDR',
                type:            #DATAPOINT_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'StockTypeHDR',
                position:15
              },
              {
                id:              'PriceHDR',
                type:            #DATAPOINT_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'PriceHDR',
                position:18
              },
               {
                id:              'Header1',
                type:            #FIELDGROUP_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'Header1',
                position:20
              },
              {
                id:              'Header2',
                type:            #FIELDGROUP_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'Header2',
                position:30
              },
              { id:            'CorrectionQty',
                purpose:       #STANDARD,
                type:          #FIELDGROUP_REFERENCE,
                label:         'Correction Qty',
                targetQualifier: 'CorrectionQty',
                parentId: 'GeneralInformation',
                position:      5 },
              { id:            'Correction',
                purpose:       #STANDARD,
                type:          #FIELDGROUP_REFERENCE,
                label:         'Correction',
                targetQualifier: 'Correction',
                parentId: 'GeneralInformation',
                position:      10 },
               { id:            'Changed',
                purpose:       #STANDARD,
                type:          #FIELDGROUP_REFERENCE,
                label:         'Created/Changed',
                targetQualifier: 'Changed',
                parentId: 'GeneralInformation',
                position:      20 },
              { id:             'CorrectionHistory',
                purpose:        #STANDARD,
                type:           #LINEITEM_REFERENCE,
                label:          'Transfer/Adjustment Events',
                position:       70,
                targetElement:  '_StockCorrectionHistory'},
              { id:             'Errors',
                purpose:        #STANDARD,
                type:           #LINEITEM_REFERENCE,
                label:          'Correction Errors',
                position:       80,
                targetElement:  '_StockCorrectionErrors'}
  ]

  @UI.lineItem: [{ type: #FOR_ACTION,position: 20, dataAction: 'ClearUpAdjustment',invocationGrouping: #CHANGE_SET, label: 'Post Stock Correction' }]
  @UI.identification: [
  {
    importance: #HIGH,
    label: 'Post Stock Correction',
    dataAction: 'ClearUpAdjustment',
    type: #FOR_ACTION,
    position: 2
  } ]

  @UI.hidden: true
  Uuid;
  @EndUserText.label: 'Status'
  @UI:{lineItem:       [{  position:10,importance: #HIGH , criticality: 'StatusCriticality',criticalityRepresentation: #WITHOUT_ICON}],
  dataPoint: {qualifier: 'StatusHDR', title: 'Status', criticality: 'StatusCriticality'}}
  @UI.selectionField: [ { position: 150 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_correction_status_vh', element : 'Status'}}]
  Status;
  @UI:{lineItem:       [{ position: 20, importance: #HIGH }], fieldGroup: [{ position: 30,qualifier: 'Header1'}]}
  @UI.selectionField: [ { position: 10 } ]
  @Consumption.filter:{ selectionType:#SINGLE, mandatory: true}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_plant_corr_vh', element : 'Plant'}}]
  @EndUserText.label: 'Plant'
  Plant;
  @UI:{lineItem:       [{ position: 30, importance: #LOW }], fieldGroup: [{ position: 180, qualifier: 'Correction'}]}
  @EndUserText.label: 'Latest Message'
  FreshestErrorMessage;
  @UI:{lineItem:       [{ position: 40, importance: #HIGH }], fieldGroup: [{ position: 40,qualifier: 'Header1'}]}
  @UI.selectionField: [ { position: 20 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_storage_loc_corr_vh', element : 'StorageLocation'},
                        additionalBinding: [{ element: 'Plant', localElement: 'Plant'}] }]
  @Consumption.filter:{mandatory: true}
  @EndUserText.label: 'Storage Location'
  StorageLocation;
  @UI.selectionField: [ { position: 30 } ]
  @Consumption.filter:{ mandatory: true, multipleSelections: false}
  @Consumption.valueHelpDefinition: [{ entity : { name: 'zsd_i_stcorr_date_value_help', element: 'WmsSnapshotDate'}, presentationVariantQualifier: 'SortByDate'}]
  @EndUserText.label: 'WMS Snapshot Date'
  WmsSnapshotDate;
  @UI:{lineItem:       [{ position: 50, importance: #HIGH }]}
  @Search.defaultSearchElement: true
  @UI.selectionField: [ { position: 40 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_article_corr_vh', element : 'Article'} }]
  Article;
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold : 0.8
  @Search.ranking : #HIGH
  ArticleDescription;
  @UI:{fieldGroup: [{ position: 10,qualifier: 'Header2'}]}
  @EndUserText.label: 'Generic Article'
  @Search.fuzzinessThreshold : 0.8
  @Search.ranking : #MEDIUM
  @UI.selectionField: [ { position: 80} ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_generic_article_corr_vh', element : 'GenericArticle'} }]
  GenericArticle;
  @UI:{ fieldGroup: [{ position: 20,qualifier: 'Header2'}]}
  @UI.selectionField: [ { position: 70 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_material_type_corr_vh', element : 'MaterialType'} }]
  @EndUserText.label: 'Article Type'
  MaterialType;
  @UI.selectionField: [ { position: 50 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_material_group_corr_vh', element : 'MaterialGroup'} }]
  @EndUserText.label: 'Article Group'
  MaterialGroup;
  @UI:{ dataPoint: {qualifier: 'PriceHDR', title: 'Price'}}
  @EndUserText.label: 'Price'
  Price;
  @UI:{lineItem:       [{ position: 60, importance: #HIGH }], dataPoint: {qualifier: 'StockTypeHDR', title: 'Stock Type'}}
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_stock_corr_type_vh', element : 'StockType'} }]
  @UI.selectionField: [ { position: 100 } ]
  StockType;
  @UI:{lineItem:       [{ position: 70, importance: #LOW }], fieldGroup: [{ position: 110, qualifier: 'Correction'}]}
  SalesOrderNumber;
  @UI:{lineItem:       [{ position: 80, importance: #LOW }], fieldGroup: [{ position: 120, qualifier: 'Correction'}]}
  SalesOrderItem;
  @UI:{ fieldGroup: [{ position: 130, qualifier: 'Correction'}]}
  @EndUserText.label: 'Execution Date'
  ExecutionDate;
  @UI:{ fieldGroup: [{ position: 140, qualifier: 'Correction'}]}
  @EndUserText.label: 'Execution Time'
  ExecutionTime;
  @UI:{lineItem:       [{ position: 90, importance: #HIGH, criticality: 'AdjustmentCriticality' }],
  dataPoint: {qualifier: 'QuantityHDR', title: 'Quantity'}}
  @UI.identification: [{position: 10,qualifier: 'Correction'}]
  AdjustmentQty;
  @UI:{lineItem:       [{ position: 100, importance: #HIGH }], fieldGroup: [{ position: 160, qualifier: 'CorrectionQty'}]}
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_stock_corr_transf_to_vh', element : 'TransferTo'} }]
  TransferTo;
  @UI:{lineItem:       [{ position: 110, importance: #HIGH }], dataPoint: {qualifier: 'MoveTypeHDR', title: 'Move Type'}}
  @EndUserText.label: 'Move Type'
  @UI.selectionField: [ { position: 180 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_correction_move_types_vh', element : 'MoveType'}}]
  MoveType;
  @UI:{lineItem:       [{ position: 120, importance: #HIGH , criticality: 'AdjustmentCriticality' }], fieldGroup: [{ position: 190, qualifier: 'CorrectionQty'}]}
  @EndUserText.label: 'Quantity posted'
  Adjusted;
  @UI:{ fieldGroup: [{ position: 200, qualifier: 'CorrectionQty'}]}
  @EndUserText.label: 'Difference Calc'
  DifferenceCalc;
  @UI:{lineItem:       [{ position: 130, importance: #HIGH }], fieldGroup: [{ position: 200, qualifier: 'CorrectionQty'}]}
  @EndUserText.label: 'Difference Origin'
  Difference;
  @UI:{fieldGroup: [{ position: 210, qualifier: 'CorrectionQty'}]}
  @EndUserText.label: 'Mat Doc Sum'
  MaterialDocumentsStockSum;
  @UI:{fieldGroup: [{ position: 180, qualifier: 'Correction'}]}
  @EndUserText.label: 'Count Errors'
  CountErrors;
  @UI.hidden: true
  AdjustmentCriticality;
  @UI.hidden: true
  DifferenceCalcCriticality;
  @UI.hidden: true
  StatusCriticality;
  @UI:{fieldGroup: [{ position: 230, qualifier: 'Changed'}]}
  CreatedAt;
  @UI:{ fieldGroup: [{ position: 240, qualifier: 'Changed'}]}
  CreatedBy;
  @UI:{fieldGroup: [{ position: 250, qualifier: 'Changed'}]}
  ChangedAt;
  @UI:{ fieldGroup: [{ position: 260, qualifier: 'Changed'}]}
  ChangedBy;
  @UI:{ fieldGroup: [{ position: 270, qualifier: 'Changed'}]}
  LastChangedAt;
}