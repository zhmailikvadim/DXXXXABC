@Metadata.layer: #CORE
@UI: {
    headerInfo:{
            typeName: 'Stock Difference Report',
            typeNamePlural: 'Stock Difference List',
            title: {
                type: #STANDARD, value: 'Article'
            },
            description: {
                value: 'ArticleDescription'
            }
    },
    presentationVariant: [
        {
            requestAtLeast: [
              'StatusCriticality',
              'DifferenceCriticality',
              'BlockedDifferenceCriticality',
              'UnrectrictedDifferenceCritical',
              'InQualityInspDifferenceCritica'
            ]
        }
    ]
}
@Search.searchable: true
@UI.lineItem: [{criticality: 'StatusCriticality'}]
annotate view zsd_c_stock_difference_report with
{
  @UI.facet:  [              {
                id:              'StatusHDR',
                type:            #DATAPOINT_REFERENCE,
                purpose: #HEADER,
                targetQualifier: 'StatusHDR',
                position:10
              },
                {
                  id:              'ArticleData',
                  type:            #FIELDGROUP_REFERENCE,
                  purpose:         #HEADER,
                  position:        10,
                  targetQualifier: 'ArticleData'
                },
                {
                  id:              'DedicatedOrder',
                  type:            #FIELDGROUP_REFERENCE,
                  purpose:         #HEADER,
                  position:        20,
                  targetQualifier: 'DedicatedOrder'
                },
                {
                  id:              'ValuationData',
                  type:            #DATAPOINT_REFERENCE,
                  purpose:         #HEADER,
                  position:        30,
                  targetQualifier: 'ValuationData'
                },
                {
                  id:              'Stock',
                  type:            #FIELDGROUP_REFERENCE,
                  purpose:         #STANDARD,
                  position:        40,
                  targetQualifier: 'Stock',
                  label: 'Stock'
                },
               {
                  label: 'Interface Goods Receipt Error(AIF)',
                  id  : 'PartialGoodsReceipt',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsPartialGoodsReceipt',
                  position: 220
               },
               {
                  label: 'Interface Goods Issue Error(IDoc)',
                  id  : 'GoodsIssueError',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsGoodsIssueIdoc',
                  position: 230
                },
               {
                  label: 'Interface Inventory Posting Error(AIF)',
                  id  : 'InventoryPostingError',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsInventoryPosting',
                  position: 240
                },
                {
                  label: 'OMNI Shipment Errors',
                  id  : 'OMNIShipmentError',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsOmniShipmentConfirm',
                  position: 250
                },
                {
                  label: 'Timing Adjustment',
                  id  : 'TimingAdjustmentRows',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_TimingAdjustmentRows',
                  position: 260
                 },
                {
                    label: 'Historical Data',
                    id  : 'HistoricalData',
                    purpose: #STANDARD,
                    type : #LINEITEM_REFERENCE,
                    targetElement: '_HistorySapSnapShot',
                    position: 270
               },
                              {
                    label: 'Online Calculated Data',
                    id  : 'OnlineCalculatedData',
                    purpose: #STANDARD,
                    type : #LINEITEM_REFERENCE,
                    targetElement: '_StockDifferenceCalc',
                    position: 200
               }]
  @UI.lineItem: [{ type: #FOR_ACTION,position: 10, dataAction: 'ReadyForStockCorrection',invocationGrouping: #CHANGE_SET,  label: 'Ready for Stock Correction' }]
  @UI.identification: [{importance: #HIGH,label: 'Ready for Stock Correction',dataAction: 'ReadyForStockCorrection',type: #FOR_ACTION,position: 2} ]
  @EndUserText.label: 'Currency Code'
  CurrencyCode;
  @UI:{lineItem:       [{ position: 10, importance: #HIGH,criticality: 'StatusCriticality'}] ,
  dataPoint: {qualifier: 'StatusHDR', title: 'Status', criticality: 'StatusCriticality'}}
  @UI.selectionField: [ { position: 90 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_difference_status_vh',
                                                   element : 'Status'}}]
  @EndUserText.label: 'Status'
  Status;
  @UI:{lineItem:       [{ position: 20, importance: #HIGH }]}
  @UI.selectionField: [ { position: 30  } ]
  @Consumption.valueHelpDefinition: [{ entity : { name: 'zsd_i_date_value_help', element: 'WmsSnapshotDate'}, distinctValues: true, presentationVariantQualifier: 'SortByDate'}]
  @EndUserText.label: 'WMS Snapshot Date'
  @Consumption.filter:{ mandatory: true}
  WmsSnapshotDate;
  @UI:{lineItem:       [{ position: 30, importance: #HIGH }], fieldGroup: [{ position: 20, qualifier: 'ArticleData'}]}
  @UI.selectionField: [ { position: 10 } ]
  @Consumption.filter:{ selectionType:#SINGLE, mandatory: true}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_plant_diff_base_vh', element : 'Plant'}}]
  @EndUserText.label: 'Plant'
  Plant;
  @UI.selectionField: [ { position: 20 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_storage_loc_diff_base_vh', element : 'StorageLocation'},
                            additionalBinding: [{ element: 'Plant', localElement: 'Plant'}] }]
  @UI:{fieldGroup: [{ position: 30, qualifier: 'ArticleData'}]}
  @Consumption.filter:{multipleSelections: false,mandatory: true}
  @EndUserText.label: 'Storage Location'
  StorageLocation;
  @UI:{lineItem:       [{ position: 50, importance: #HIGH }]}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_article_diff_vh', element : 'Article'} }]
  @UI.selectionField: [ { position: 50 } ]
  @EndUserText.label: 'Article'
  Article;
  @Search.defaultSearchElement : true
  @Search.fuzzinessThreshold : 0.8
  @Search.ranking : #HIGH
  @UI.hidden: true
  ArticleForSearch;
  @EndUserText.label: 'Article Description'
  @Search.defaultSearchElement : true
  @Search.fuzzinessThreshold : 0.8
  @Search.ranking : #MEDIUM
  ArticleDescription;
  @UI:{ fieldGroup: [{ position: 10, qualifier: 'ArticleData'}]}
  @EndUserText.label: 'Generic Article'
  @UI.selectionField: [ { position: 60 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_generic_article_diff_vh', element : 'GenericArticle'} }]
  GenericArticle;
  @EndUserText.label: 'Article Type'
  @UI.selectionField: [ { position: 80 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_material_type_diff_vh', element : 'MaterialType'} }]
  MaterialType;
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_material_group_diff_vh', element : 'ArticleGroup'} }]
  @EndUserText.label: 'Article Group'
  @UI.selectionField: [ { position: 70 } ]
  ArticleGroup;
  @UI.dataPoint: {qualifier: 'ValuationData', title: 'Price'}
  Price;
  @UI:{fieldGroup: [{ position: 40, qualifier: 'DedicatedOrder'}]}
  SalesOrderNumber;
  @UI:{fieldGroup: [{ position: 50, qualifier: 'DedicatedOrder'}]}
  SalesOrderItem;
  @UI:{ lineItem: [{ position: 60 , importance: #HIGH}]}
  @EndUserText.label: 'UU SAP'
  @UI.dataPoint: {
  title: 'Unit Price',
  description: 'Name',
  valueFormat: {
    scaleFactor: 1000,
    numberOfFractionalDigits: 1 }
  }
  @UI:{ fieldGroup: [{ qualifier: 'Stock'}]}
  UnrectrictedSapStockQuantity;
  @UI:{ lineItem: [{ position: 70 , importance: #HIGH}]}
  @UI:{ fieldGroup: [{ qualifier: 'Stock'}]}
  @EndUserText.label: 'UU WMS'
  UnrectrictedWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'Stock'}]}
  @EndUserText.label: 'UU Errors Timing'
  UnrectrictedTimingAdjustment;
  @UI:{ lineItem: [{ position: 90 , importance: #HIGH, criticality: 'UnrectrictedDifferenceCritical',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 10, label: 'UU',qualifier: 'Stock'}]}
  @EndUserText.label: 'UU Diff'
  UnrectrictedDifferenceQty;
  @UI:{ lineItem: [{ position: 100 , importance: #HIGH}],  fieldGroup: [{ position: 20, label: 'BL', qualifier: 'Stock'}]}
  @EndUserText.label: 'BL SAP'
  BlockedSapStockQuantity;
  @EndUserText.label: 'BL WMS'
  @UI:{ lineItem: [{ position: 110 , importance: #HIGH}],fieldGroup: [{ position: 20,label: 'BL', qualifier: 'Stock'}]}
  BlockedWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 20,label: 'BL', qualifier: 'Stock'}]}
  @EndUserText.label: 'BL Errors Timing'
  BlockedTimingAdjustment;
  @UI:{ lineItem: [{ position: 130 , importance: #HIGH, criticality: 'BlockedDifferenceCriticality',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 20, label: 'BL',qualifier: 'Stock'}]}
  @EndUserText.label: 'BL Diff'
  BlockedDifferenceQty;
  @UI:{ lineItem: [{ position: 140 , importance: #HIGH}],fieldGroup: [{ position: 30, label: 'QI', qualifier: 'Stock'}]}
  @EndUserText.label: 'QI SAP'
  InQualityInspSapStockQuantity;
  @UI:{ lineItem: [{ position: 150 , importance: #HIGH}],fieldGroup: [{ position: 30, label: 'QI', qualifier: 'Stock'}]}
  @EndUserText.label: 'QI WMS'
  InQualityInspWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI', qualifier: 'Stock'}]}
  @EndUserText.label: 'QI Timing Total'
  InQualityInspTimingAdjustment;
  @UI:{ lineItem: [{ position: 170 , importance: #HIGH, criticality: 'InQualityInspDifferenceCritica',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 30, label: 'QI',qualifier: 'Stock'}]}
  @EndUserText.label: 'QI Diff'
  InQualityInspDifferenceQty;
  @UI:{ lineItem: [{ position: 180 , importance: #HIGH, criticality: 'DifferenceCriticality',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 40,label: 'Total', qualifier: 'Stock'}]}
  @EndUserText.label: 'Total Net Diff'
  DifferenceQty;
  @UI:{fieldGroup: [{ position: 50,label: 'Absolute Total', qualifier: 'Stock'}]}
  @EndUserText.label: 'Total Net Diff Abs'
  AbsoluteDifferenceQty;
  @UI:{fieldGroup: [{ position: 10, label: 'UU',qualifier: 'Stock'}]}
  @EndUserText.label: 'UU Diff Value'
  UnrectrictedDifferenceValue;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'Stock'}]}
  @EndUserText.label: 'BL Diff Value'
  BlockedDifferenceValue;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI', qualifier: 'Stock'}]}
  @EndUserText.label: 'QI Diff Value'
  InQualityInspDifferenceValue;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU',qualifier: 'Stock'}]}
  @EndUserText.label: 'UU Incl Err Diff'
  RealUnrectrictedDifferenceQty;
  @UI:{fieldGroup: [{ position: 20, label: 'QI',qualifier: 'Stock'}]}
  @EndUserText.label: 'QI Incl Err Diff'
  RealInQualityInspDifferenceQty;
  @UI:{ fieldGroup: [{ position: 30, label: 'BL',qualifier: 'Stock'}]}
  @EndUserText.label: 'BL Incl Err Diff'
  RealBlockedDifferenceQty;
  @UI:{ fieldGroup: [{ position: 40,label: 'Total', qualifier: 'Stock'}]}
  @EndUserText.label: 'Total Diff Value'
  DifferenceValue;
  @UI:{fieldGroup: [{ position: 50,label: 'Total Absolute', qualifier: 'Stock'}]}
  @EndUserText.label: 'Total Diff Value Abs'
  AbsoluteDifferenceValue;
  @UI:{fieldGroup: [{ position: 40,label: 'Time', qualifier: 'Stock'}]}
  @EndUserText.label: 'WMS Time'
  WmsTime;
  @UI:{fieldGroup: [{ position: 40,label: 'Time', qualifier: 'Stock'}]}
  @EndUserText.label: 'SAP Time'
  SapTime;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'Stock'}]}
  @EndUserText.label: 'UU Int GI Error'
  ErrorsGoodsIssueQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'Stock'}]}
  @EndUserText.label: 'UU Int GR Error'
  ErrorsGoodsReceiptUUQuantity;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'Stock'}]}
  @EndUserText.label: 'BL Int GR Error'
  ErrorsGoodsReceiptBlQuantity;
  @UI:{ fieldGroup: [{ position: 30, label: 'QI', qualifier: 'Stock'}]}
  @EndUserText.label: 'QI Int GR Error'
  ErrorsGoodsReceiptQiQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU',qualifier: 'Stock'}]}
  @EndUserText.label: 'UU Int Inv Error'
  ErrorsInventoryPostingUnQty;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'Stock'}]}
  @EndUserText.label: 'BL Int Inv Error'
  ErrorsInventoryPostingBlQty;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI',qualifier: 'Stock'}]}
  @EndUserText.label: 'QI Int Inv Error'
  ErrorsInventoryPostingQiQty;
  @UI:{ fieldGroup: [{ position: 10, label: 'GR', qualifier: 'Stock'}]}
  @EndUserText.label: 'UU GR ECOM Error'
  ErrorsGrEcom;
  @UI:{ fieldGroup: [{ position: 20, label: 'GI', qualifier: 'Stock'}]}
  @EndUserText.label: 'UU GI ECOM Error'
  ErrorsGiEcom;
  @UI.selectionField: [{ position: 200 } ]
  @EndUserText.label: 'Display Only differences'
  HasDifference;
  @UI.selectionField: [{ position: 210 } ]
  @EndUserText.label: 'Interface errors exist'
  InterfaceErrorsExist;
  @UI.selectionField: [{ position: 220 } ]
  @EndUserText.label: 'Articles existing in SAP'
  @Consumption.filter.defaultValue: 'X'
  ArticleExistInSap;
  @UI.selectionField: [{ position: 230 } ]
  @EndUserText.label: 'Sales Order exist'
  DisplayOnlySOrdersOrViceVersa;
  @EndUserText.label: 'SAP UOM'
  SapUom;
  @EndUserText.label: 'WMS UOM'
  WmsUom;
  @UI.hidden: true
  StatusCriticality;
  @UI.hidden: true
  DifferenceCriticality;
  @UI.hidden: true
  BlockedDifferenceCriticality;
  @UI.hidden: true
  UnrectrictedDifferenceCritical;
  @UI.hidden: true
  InQualityInspDifferenceCritica;
}