************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 13.01.2023 | 16604     : IC Mark-up costs maintenance *
*              |            | DS4K939282                               *
*----------------------------------------------------------------------*

FUNCTION zfi_fm_mark_up_value_upd
  TABLES
    ct_posted_mark_up_value TYPE zfi_tt_posted_mark_up_value.




  CONSTANTS:
    lc_type_key_success TYPE /iwngw/if_notif_provider=>ty_s_notification_type_id-type_key VALUE 'UpdatingMarkUpValueSuccess',
    lc_type_key_failed  TYPE /iwngw/if_notif_provider=>ty_s_notification_type_id-type_key VALUE 'UpdatingMarkUpValueFaild'.

  TRY.
      SELECT
          elemt,
          kstar
        FROM tckha
        WHERE elehk = @( zcl_pe_hm_basic=>get_value(
                           iv_id       = '1134'
                           iv_sequence = '1' ) )
        INTO TABLE @DATA(lt_cost_element).

      IF sy-subrc > 0.
        RETURN.
      ENDIF.

    CATCH zcx_pe_hm.
      RETURN.
  ENDTRY.


  DATA(lt_transfer_data) = VALUE kkpi_transfer_data(
    FOR <ls_posted_mark_up_value> IN ct_posted_mark_up_value
     ( klvar    = <ls_posted_mark_up_value>-klvar
       matnr    = <ls_posted_mark_up_value>-matnr
       werks    = <ls_posted_mark_up_value>-werks
       losgr    = <ls_posted_mark_up_value>-losgr
       tvers    = <ls_posted_mark_up_value>-tvers
       kadat    = <ls_posted_mark_up_value>-kadat
       bidat    = <ls_posted_mark_up_value>-bidat
       position = VALUE rk70l_tab(
                    FOR <ls_cost_element>  IN lt_cost_element
                        posnr  = 1
                        typps  = 'V'
                        menge  = 1
                        meeht  = <ls_posted_mark_up_value>-meeht
                      ( elemt  = <ls_cost_element>-elemt
                        kstar  = <ls_cost_element>-kstar
                        lpreis = SWITCH #( <ls_cost_element>-elemt
                                   WHEN '001' THEN <ls_posted_mark_up_value>-kst001
                                   WHEN '002' THEN <ls_posted_mark_up_value>-kst003
                                   WHEN '003' THEN <ls_posted_mark_up_value>-kst005
                                   WHEN '004' THEN <ls_posted_mark_up_value>-kst007
                                   WHEN '005' THEN <ls_posted_mark_up_value>-kst009 ) ) )
      ) ).

  CALL FUNCTION 'KKPI_BDC_ADD_ON_COSTS_CREATE'
    EXPORTING
      client                 = sy-mandt
      user                   = sy-uname
      mode                   = 'N'
      update                 = 'S'
      it_transfer_data       = lt_transfer_data
    EXCEPTIONS
      call_transaction_error = 1
      no_transfer_data       = 2
      wrong_mode_parameter   = 3
      OTHERS                 = 4.

  IF sy-subrc = 0.

    zcl_fi_ic_mark_up_value_notif=>send_notification(
      iv_type_key  = lc_type_key_success
      it_recipient = VALUE #( ( id = sy-uname  ) ) ).
  ELSE.

    MESSAGE
        ID sy-msgid
        TYPE sy-msgty
        NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
      INTO DATA(lv_error_message).

    zcl_fi_ic_mark_up_value_notif=>send_notification(
      iv_type_key      = lc_type_key_failed
      it_recipient     = VALUE #( ( id = sy-uname  ) )
      iv_error_message = lv_error_message ).
  ENDIF.

ENDFUNCTION.