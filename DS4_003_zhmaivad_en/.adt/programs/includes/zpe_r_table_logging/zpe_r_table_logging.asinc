************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* CANOCAR      | 23.03.2022 | 10553     : Initial development          *
*              |            | DS4K916132                               *
*----------------------------------------------------------------------*
* KARPOSER     | 13.04.2023 | 20128     : Custom logging - Copy        *
*              |            | DS4K949814                               *
*----------------------------------------------------------------------*
FORM event_19 ##CALLED.

  LOOP AT x_namtab ASSIGNING FIELD-SYMBOL(<ls_field>) WHERE viewfield = 'CREATED_ON' OR
                                                            viewfield = 'CREATED_AT' OR
                                                            viewfield = 'CREATED_BY' OR
                                                            viewfield = 'CHANGED_ON' OR
                                                            viewfield = 'CHANGED_AT' OR
                                                            viewfield = 'CHANGED_BY'.
    <ls_field>-readonly = 'R'.
  ENDLOOP.

ENDFORM.


FORM event_01 ##CALLED.
  CONSTANTS:
    lc_os_lt  TYPE tabname VALUE 'ZSD_V_OS_LT',
    lc_vas_lt TYPE tabname VALUE 'ZSD_V_VAS_LT',
    lc_os_gr TYPE tabname VALUE 'ZSD_V_OS_GR'.

  DATA: lv_index LIKE sy-tabix. "Index to note the lines found

  FIELD-SYMBOLS: <lv_aux> TYPE any.

*-- Automatically populate logging fields
  LOOP AT total.
    IF <action> = neuer_eintrag OR <action> = aendern.
      READ TABLE extract WITH KEY <vim_xtotal_key> ##WARN_OK.
      IF sy-subrc EQ 0.
        lv_index = sy-tabix.
      ELSE.
        CLEAR lv_index.
      ENDIF.

*--   New record
      IF <action> = neuer_eintrag.
        UNASSIGN <lv_aux>.
        ASSIGN COMPONENT 'CREATED_ON' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
        IF <lv_aux> IS ASSIGNED.
          <lv_aux> = sy-datum.
        ENDIF.
        UNASSIGN <lv_aux>.
        ASSIGN COMPONENT 'CREATED_AT' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
        IF <lv_aux> IS ASSIGNED.
          <lv_aux> = sy-uzeit.
        ENDIF.
        UNASSIGN <lv_aux>.
        ASSIGN COMPONENT 'CREATED_BY' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
        IF <lv_aux> IS ASSIGNED.
          <lv_aux> = sy-uname.
        ENDIF.

        IF view_name = lc_os_lt OR view_name = lc_vas_lt OR view_name = lc_os_gr.
          UNASSIGN <lv_aux>.
          ASSIGN COMPONENT 'CHANGED_ON' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
          IF <lv_aux> IS ASSIGNED.
            <lv_aux> = sy-datum.
          ENDIF.
          UNASSIGN <lv_aux>.
          ASSIGN COMPONENT 'CHANGED_AT' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
          IF <lv_aux> IS ASSIGNED.
            <lv_aux> = sy-uzeit.
          ENDIF.
          UNASSIGN <lv_aux>.
          ASSIGN COMPONENT 'CHANGED_BY' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
          IF <lv_aux> IS ASSIGNED.
            <lv_aux> = sy-uname.
          ENDIF.
        ENDIF.

      ELSEIF <action> = aendern.
        UNASSIGN <lv_aux>.
        ASSIGN COMPONENT 'CHANGED_ON' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
        IF <lv_aux> IS ASSIGNED.
          <lv_aux> = sy-datum.
        ENDIF.
        UNASSIGN <lv_aux>.
        ASSIGN COMPONENT 'CHANGED_AT' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
        IF <lv_aux> IS ASSIGNED.
          <lv_aux> = sy-uzeit.
        ENDIF.
        UNASSIGN <lv_aux>.
        ASSIGN COMPONENT 'CHANGED_BY' OF STRUCTURE <vim_total_struc> TO <lv_aux>.
        IF <lv_aux> IS ASSIGNED.
          <lv_aux> = sy-uname.
        ENDIF.
      ENDIF.

      MODIFY total.
      CHECK lv_index GT 0.
      extract = total.
      MODIFY extract INDEX lv_index.
    ENDIF.

  ENDLOOP.

  sy-subrc = 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Module DISABLE_ICONS OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE disable_icons OUTPUT.
  excl_cua_funct-function = 'DELE'.
  APPEND excl_cua_funct.
ENDMODULE.