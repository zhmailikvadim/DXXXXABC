" -----------------------------------------------------------------------
"  R E V I S I O N   H I S T O R Y                                     -
" -----------------------------------------------------------------------
" AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
"              |            | TRANSPORT REQUESTS                       -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 29.04.2023 | 20690     : Feature - ATP push from S/4  -
"              |            | DS4K950897                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 14.08.2023 | 22968     : Feature - ATP push from S/4  -
"              |            | DS4K957400                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 05.01.2024 | 27294     : Feature - GAH - Performance  -
"              |            | DS4K972552                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 20.02.2024 | 28986     : Feature - ATP push from S/4  -
"              |            | DS4K979378                                -
" -----------------------------------------------------------------------
SELECTION-SCREEN BEGIN OF BLOCK bl1 WITH FRAME TITLE TEXT-001.
  SELECT-OPTIONS:
   s_matnr FOR gs_atpnetchgfilter-matnr,
   s_werks FOR gs_atpnetchgfilter-werks,
   s_berid FOR gs_atpnetchgfilter-berid MATCHCODE OBJECT h_mdlv,
   s_vlfkz FOR gv_vlfkz,
   s_netch FOR gs_atpnetchgfilter-netchgcategory.
SELECTION-SCREEN END OF BLOCK bl1.

SELECTION-SCREEN BEGIN OF BLOCK bl2 WITH FRAME TITLE TEXT-003.
  SELECTION-SCREEN: BEGIN OF LINE,
  COMMENT (20) TEXT-004.
  PARAMETERS: p_begd TYPE dats,
              p_begt TYPE tims.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN: BEGIN OF LINE,
  COMMENT (20) TEXT-005.
  PARAMETERS: p_endd TYPE dats,
              p_endt TYPE tims.
  SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK bl2.

SELECTION-SCREEN BEGIN OF BLOCK bl3 WITH FRAME TITLE TEXT-002.
  PARAMETERS:
    p_show  TYPE abap_bool AS CHECKBOX DEFAULT abap_true,
    p_pub2  TYPE abap_bool AS CHECKBOX DEFAULT abap_true,
    p_lastr TYPE abap_bool AS CHECKBOX DEFAULT abap_true.
SELECTION-SCREEN END OF BLOCK bl3.
SELECTION-SCREEN BEGIN OF BLOCK bl5 WITH FRAME TITLE TEXT-008.
  PARAMETERS:
    p_parall AS CHECKBOX DEFAULT 'X' USER-COMMAND prl,
    p_rfcgrp TYPE spta_rfcgr OBLIGATORY VALUE CHECK MEMORY ID spta_rfcgr MODIF ID par,
    p_maxmsg TYPE i OBLIGATORY DEFAULT 10000 MODIF ID par,
    p_msgsiz TYPE i OBLIGATORY DEFAULT 1000 MODIF ID par.
SELECTION-SCREEN END OF BLOCK bl5.
SELECTION-SCREEN BEGIN OF BLOCK bl4 WITH FRAME TITLE TEXT-007.
  PARAMETERS:
    p_cf_ns TYPE /aif/pers_rtcfgr-queue_ns MODIF ID pcf OBLIGATORY VALUE CHECK DEFAULT 'ZSD_O',
    p_cf_nm TYPE /aif/pers_rtcfgr_name MODIF ID pcf OBLIGATORY VALUE CHECK DEFAULT 'A01'.
SELECTION-SCREEN END OF BLOCK bl4.

AT SELECTION-SCREEN.
  IF p_begd IS NOT INITIAL OR p_endd IS NOT INITIAL.
    CONVERT DATE p_begd TIME p_begt INTO TIME STAMP FINAL(lv_begts) TIME ZONE sy-zonlo.
    CONVERT DATE p_endd TIME p_endt INTO TIME STAMP FINAL(lv_endts) TIME ZONE sy-zonlo.
    IF lv_begts > lv_endts.
      MESSAGE e119(zsd_aif) DISPLAY LIKE 'E'.
    ENDIF.
  ENDIF.
  IF     p_pub2 = abap_true
     AND ( p_begd    IS INITIAL AND p_endd    IS INITIAL )
     AND ( s_matnr[] IS INITIAL OR  s_werks[] IS INITIAL ).
    MESSAGE e121(zsd_aif) DISPLAY LIKE 'E'.
  ENDIF.

  IF s_matnr IS NOT INITIAL.
    SELECT SINGLE product INTO @DATA(lv_product) FROM i_product WHERE product IN @s_matnr.
    IF sy-subrc <> 0 OR lv_product IS INITIAL.
      MESSAGE e139(zsd_aif) DISPLAY LIKE 'E'.
    ENDIF.
  ENDIF.
  IF s_werks IS NOT INITIAL.
    SELECT plant INTO @DATA(lv_plant) UP TO 1 ROWS FROM i_productplant WHERE plant IN @s_werks.ENDSELECT.
    IF sy-subrc <> 0 OR lv_plant IS INITIAL.
      MESSAGE e140(zsd_aif) DISPLAY LIKE 'E'.
    ENDIF.
  ENDIF.

  IF s_berid IS NOT INITIAL.
    SELECT mrparea INTO @DATA(lv_mrparea) UP TO 1 ROWS FROM i_productplantmrp WHERE mrparea IN @s_berid.ENDSELECT.
    IF sy-subrc <> 0 OR lv_mrparea IS INITIAL.
      MESSAGE e141(zsd_aif) DISPLAY LIKE 'E'.
    ENDIF.
  ENDIF.
  IF p_rfcgrp IS NOT INITIAL.
    SELECT classname INTO @DATA(lv_classname) UP TO 1 ROWS FROM rzllitab WHERE classname = @p_rfcgrp.ENDSELECT.
    IF sy-subrc <> 0 OR lv_classname IS INITIAL.
      MESSAGE e142(zsd_aif) DISPLAY LIKE 'E'.
    ENDIF.
  ENDIF.
  IF p_maxmsg < p_msgsiz.
    MESSAGE e146(zsd_aif) DISPLAY LIKE 'E'.
  ENDIF.

AT SELECTION-SCREEN OUTPUT.
  LOOP AT SCREEN.
    IF screen-group1 = 'PAR'.
      IF p_parall <> 'X'.
        screen-active = '0'.
      ELSE.
        screen-active = '1'.
      ENDIF.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.