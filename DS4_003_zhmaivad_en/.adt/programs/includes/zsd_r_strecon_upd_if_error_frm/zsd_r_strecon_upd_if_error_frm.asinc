" -----------------------------------------------------------------------
"  R E V I S I O N   H I S T O R Y                                     -
" -----------------------------------------------------------------------
" AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
"              |            | TRANSPORT REQUESTS                       -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 06.06.2023 | 20960     : [Build] - Stock reconciliati -
"              |            | DS4K951475                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 13.07.2023 | 22859     : [Build] - Stock reconciliati -
"              |            | DS4K957133                               -
" -----------------------------------------------------------------------
FORM update_interface_errors.
  IF lines( s_plant ) > 0 OR lines( s_storg ) > 0.
    SELECT plant, storagelocation FROM i_storagelocation
      INTO TABLE @DATA(lt_storagelocation)
      WHERE plant IN @s_plant AND storagelocation IN @s_storg. "#EC CI_SUBRC
  ELSE.
    APPEND VALUE #( ) TO lt_storagelocation.
  ENDIF.

  IF lines( lt_storagelocation ) > 0.
    LOOP AT lt_storagelocation ASSIGNING FIELD-SYMBOL(<ls_storagelocation>).
      MODIFY ENTITIES OF zsd_i_stock_difference_report
             ENTITY stockdifference
             EXECUTE storediffandallerrors FROM VALUE #(
                 ( %param-zdate            = sy-datum
                   %param-plant            = <ls_storagelocation>-plant
                   %param-storage_location = <ls_storagelocation>-storagelocation ) )
             FAILED   FINAL(ls_diff_failed).
      COMMIT ENTITIES.
    ENDLOOP.
  ELSE.
    MESSAGE i008(zcm_sd_stcock_snapsh) DISPLAY LIKE 'I'. " Check Plant
  ENDIF.

  SELECT COUNT( * ) AS count INTO @DATA(lv_count_difference)
    FROM zsd_i_sap_snapshot
    WHERE hasdifference   = @abap_true
      AND sapsnapshotdate = @sy-datum.                    "#EC CI_SUBRC

  IF lv_count_difference > 0.
    MESSAGE i010(zcm_sd_stcock_snapsh) WITH lv_count_difference.
  ENDIF.

  IF ls_diff_failed IS INITIAL.
    MESSAGE i003(zcm_sd_stcock_snapsh).
  ELSE.
    MESSAGE w011(zcm_sd_stcock_snapsh).
  ENDIF.
ENDFORM.