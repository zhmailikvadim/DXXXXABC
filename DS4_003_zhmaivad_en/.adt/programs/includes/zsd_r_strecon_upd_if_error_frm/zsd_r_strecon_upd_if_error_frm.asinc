" -----------------------------------------------------------------------
"  R E V I S I O N   H I S T O R Y                                     -
" -----------------------------------------------------------------------
" AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              -
"              |            | TRANSPORT REQUESTS                       -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 06.06.2023 | 20960     : [Build] - Stock reconciliati -
"              |            | DS4K951475                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 13.07.2023 | 22859     : [Build] - Stock reconciliati -
"              |            | DS4K957133                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 13.03.2024 | 29314     : [Feature] [Build] - Stock re -
"              |            | DS4K982092                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 13.03.2024 | 29314     : [Feature] [Build] - Stock re -
"              |            | DS4K982092                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 27.03.2024 | 30550     : [Feature] [Build] - Defect   -
"              |            | DS4K985281                               -
" -----------------------------------------------------------------------
" ZHMAIVAD     | 28.03.2024 | 30550     : [Feature] [Build] - Stock re -
"              |            | DS4K985335                                -
" -----------------------------------------------------------------------

FORM update_interface_errors.
  IF s_storg IS NOT INITIAL.
    SELECT i_storagelocation~plant           AS plant,
           i_storagelocation~storagelocation AS storagelocation
      FROM i_storagelocation
             INNER JOIN
               i_plant       ON i_storagelocation~plant = i_plant~plant
      INTO TABLE @DATA(lt_storagelocation)
      WHERE i_storagelocation~plant IN @s_plant AND i_storagelocation~storagelocation IN @s_storg.
  ELSE.
    SELECT plant AS plant FROM i_plant
      INTO CORRESPONDING FIELDS OF TABLE @lt_storagelocation ##TOO_MANY_ITAB_FIELDS
      WHERE plant IN @s_plant.
  ENDIF.
  IF sy-subrc <> 0.
    RETURN.
  ENDIF.

  IF lines( lt_storagelocation ) > 0.
    LOOP AT lt_storagelocation ASSIGNING FIELD-SYMBOL(<ls_storagelocation>).
      MODIFY ENTITIES OF zsd_i_stock_difference_report
             ENTITY stockdifference
             EXECUTE storediffandallerrors FROM VALUE #(
                 ( %param-zdate            = sy-datum
                   %param-plant            = <ls_storagelocation>-plant
                   %param-storage_location = <ls_storagelocation>-storagelocation ) )
             FAILED   FINAL(ls_diff_failed).
      COMMIT ENTITIES.

      DATA lt_diff_failed LIKE TABLE OF ls_diff_failed.

      IF ls_diff_failed IS NOT INITIAL.
        MESSAGE i011(zcm_sd_stcock_snapsh) WITH <ls_storagelocation>-plant <ls_storagelocation>-storagelocation.
        APPEND ls_diff_failed TO lt_diff_failed.
      ENDIF.

      SELECT COUNT( * ) AS count INTO @DATA(lv_count_difference) "#EC CI_SROFC_NESTED
        FROM zsd_i_sap_snapshot
        WHERE hasdifference   = @abap_true
          AND sapsnapshotdate = @sy-datum.                "#EC CI_SUBRC

      IF sy-subrc = 0 AND lv_count_difference > 0 AND sy-batch = abap_true.
        MESSAGE i010(zcm_sd_stcock_snapsh) WITH lv_count_difference <ls_storagelocation>-plant <ls_storagelocation>-storagelocation.
      ELSE.
        MESSAGE i014(zcm_sd_stcock_snapsh) WITH <ls_storagelocation>-plant <ls_storagelocation>-storagelocation.
      ENDIF.
      CLEAR lv_count_difference.
    ENDLOOP.
  ELSE.
    MESSAGE i008(zcm_sd_stcock_snapsh) DISPLAY LIKE 'I'.
  ENDIF.

  IF lt_diff_failed IS INITIAL.
    MESSAGE i003(zcm_sd_stcock_snapsh).
  ELSE.
    MESSAGE i013(zcm_sd_stcock_snapsh).
  ENDIF.
ENDFORM.