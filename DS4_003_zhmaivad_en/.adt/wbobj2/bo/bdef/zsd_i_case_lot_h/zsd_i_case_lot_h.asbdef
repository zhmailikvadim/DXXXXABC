/************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* GUPTANI1     | 30.08.2022 | 11675  : BE Dev - Case Lot Management App*
*              |            | DS4K921270                              *
*----------------------------------------------------------------------*
************************************************************************
* SRIKAVEN     | 12.01.2023 | 15915     : BE: Case Lot -Enhancement *
*              |            | DS4K937372                               *
*----------------------------------------------------------------------*
* NAGARSOW     | 19.09.2023 | 23489     : BE: Case Lot -Enhancement *
*              |            | DS4K963105                               *
*----------------------------------------------------------------------*
* NAGARSOW     | 11.12.2023 | 26971     : Development - Case lot app enhancem *
*              |            | DS4K971200                               *
*----------------------------------------------------------------------*
* BATCHVIN     | 04.12.2023 | 27069     : BE: Case Lot -Enhancement    *
*              |            | DS4K971200                               *
*----------------------------------------------------------------------*
* NAGARSOW     | 13.03.2024 | 29721     :[CR] - Case Lot Minor         *
*                                         Enhancements                 *
*              |            | DS4K982272                               *
*----------------------------------------------------------------------*/

managed implementation in class zbp_sd_i_case_lot_h unique;
strict;
with draft;

define behavior for zsd_i_case_lot_h alias CaseLotHeader
persistent table zsd_t_case_lot_h
draft table zsd_t_cl_h_d
lock master
total etag LastChangedAt
authorization master ( global, instance )
with additional save

etag master ChangedAt
{
  create;
  update ( features : instance );
  delete ( features : instance );
  association _CaseLotItem { create ( features : instance, authorization : update ); with draft; }

  field ( numbering : managed, readonly ) CaselotHUUID;
  field ( readonly ) CaseLotNo, TotalQuantity, TotalQtyPerCtn, numberofmpb, CaseLotStatus;
//  field ( mandatory ) PackingMode, NumberOfUnits;
  field ( features : instance ) NumberOfMPBPerCnt;
  action ( features : instance, authorization : none ) manualplanning result [1] $self;
  validation validatemandatory on save { create; }

  determination caseLotNumber on save { create; update; }
  determination changepackingmode on modify { create; field PackingMode; }
  determination calculateNoOfUnits on modify { field NumberOfUnits; }
  determination calculateNoOfpolybags on modify { field NumberOfMPBPerCnt; }
  draft action Resume;
  draft action Edit;
  draft action Activate;
  draft action Discard;

  draft determine action Prepare
  { validation validatemandatory;
  }

  mapping for zsd_t_case_lot_h
  { CaseLotHUUID = caseloth_uuid;
    SalesOrder = vbeln;
    CaselotNo = caselot_no;
    Material = matnr;
    Plant = werks;
    RequestedDelDate = edatu;
    SDDocumentCategory = vbtyp;
    NumberOfMPB = no_caselot_cnt;
    NumberOfMPBPerCnt = no_maspb_ctn;
    PackingMode = caselot_pkmode;
    CaseLotText = caselot_text;
    TotalActualQty = caselot_total_kwmeng;
    TotalConfirmedQty = caselot_total_kbmeng;
    TotalOrderQuantityUnit = sounit;
    NumberOfUnits = no_caselot_units;
    TotalQtyPerCtn = total_qty_ctn;
    TotalQuantity = total_caselot_qty;
    CreatedAt = created_at;
    CreatedBy = created_by;
    ChangedAt = changed_at;
    ChangedBy = changed_by;
    LastChangedAt = last_changed_at;
    CaseLotStatus = CL_STATUS; }
}

define behavior for zsd_i_case_lot_i alias CaseLotItem
draft table zsd_t_cl_i_d
lock dependent by _CaseLotHeader
with unmanaged save
authorization dependent by _CaseLotHeader
etag master ChangedAt
{

  update ( features : instance );
  field ( readonly ) CaselotHUUID, Checkqty, CLTotalQty;
  association _CaseLotHeader { with draft; }
  field ( numbering : managed, readonly ) CaseLotIUUID;
  field ( features : instance ) CaseLotQuantity, HigherLevelItem,
  SalesOrderItem,
  ScheduleLine,
  ConfirmedDeliveryDate, Material, CaselotItem, Criticality;

  mapping for ZSD_T_CASE_LOT_I
  { CaselotHUUID = caseloth_uuid;
    CaselotIUUID = caseloti_uuid;
    CaselotItem = caselot_item;
    Material = matnr;
    HigherLevelItem = uepos;
    SalesOrderItem = posnr;
    OrderQuantity = kwmeng;
    ConfdQuantity = kbmeng;
    CaseLotQuantity = caselot_cl_pb;
    CLTotalQty = caselot_tt_qty;
    CreatedAt = created_at;
    CreatedBy = created_by;
    ChangedAt = changed_at;
    ChangedBy = changed_by;
    ScheduleLine = etenr;
    ConfirmedDeliveryDate = edatu;
    TotalOrderQuantityUnit = sounit;
  }

}