class CL_ABAP_BEHV_AUTH_CONTEXT definition
  public
  final
  create public

  global friends CL_ABAP_BEHV_CTRL .

public section.

  class-data RC type I .

  class-methods CLASS_CONSTRUCTOR .
  class-methods INSERT
    importing
      !P_TICKET type I default -1
      !P_BDEF type ABP_ROOT_ENTITY_NAME
      !P_AC type ABP_ELEMENT_NAME
      !P_PROGRAM type SYST_CPROG
      !P_INCLUDE type SYST_CPROG
      !P_LINE type I  ##SHADOW[INSERT].
  protected section.
private section.

  types:
    begin of t_log_context,
      bdef    type abp_root_entity_name,
      ac      type abp_element_name,
      include type syst_cprog,
      line    type i,
    end of t_log_context .
  types:
    tt_log_context type standard table of t_log_context with empty key .
  types:
    begin of t_stack,
      program type syst_cprog,
      include type syst_cprog,
      line    type i,
      event   type string,
   end of t_stack.
  types tt_stack type standard table of t_stack with empty key.
  types:
    begin of t_context,
      bdef    type abp_root_entity_name,
      ac      type abp_element_name,
      program type syst_cprog,
      include type syst_cprog,
      line    type i,
      ctx     type ref to data,
    end of t_context .
  types:
    tt_context type standard table of t_context .
  types:
    begin of t_own_context,
    index type i,
    name type c length 30,
    context type ref to data,
  end of t_own_context .
  types:
    tt_own_context type standard table of t_own_context
    with unique sorted key by_name components name .

  types T_EXCEPTION type SU21_OBJ_EXCEPTION .
  types:
    tt_exception type sorted table of t_exception with unique key object .

  class-data GT_LOG_STACK  type TT_STACK ##NEEDED. "used implicitly by REGISTER
  class-data GT_LOG_CONTEXT type TT_LOG_CONTEXT ##NEEDED. "used implicitly by REGISTER
  class-data GT_CONTEXTS type TT_CONTEXT .
  class-data GT_OWN_CONTEXTS type TT_OWN_CONTEXT .
  class-data GT_EXCEPTION type TT_EXCEPTION.

  class-methods REGISTER
    importing
      !GROUP type AAB_ID_NAME optional
    changing
      !CONTEXTS type TT_CONTEXT   ##SHADOW[INSERT]
      !OWN_CONTEXTS type TT_OWN_CONTEXT optional .
ENDCLASS.



CLASS CL_ABAP_BEHV_AUTH_CONTEXT IMPLEMENTATION.


  method class_constructor.

    "reference to checkpoint group so that it is
    "pre-loaded with the program
    ASSERT id CC_RAP_CONTRACT  CONDITION 0 = 0 .

    "does not work as buffered views over view over vier over table
    "are not invalidated
    "select object from SABP_BEHV_AUTH_OBJ_EXCEPTION into table @gt_exception.

    "retrieve exception list from shared memory
    data: my_so_reader type ref to so_reader.
    create object my_so_reader.
    try.
        my_so_reader->get_exceptions( changing p_exceptions = gt_exception ).
    catch cx_failed.
      clear gt_exception.
    endtry.

    register(
      exporting
        group        = 'CC_RAP_CONTRACT'
      changing
        contexts     = gt_contexts
        own_contexts = gt_own_contexts  ).

  endmethod.


  method INSERT.

    "verify correct legal caller
    call 'BEHV_EXEC' id 'OP' field 16 id 'TICKET' field p_ticket.  "#EC CI_CCALL

    "make sure bdef is loaded
    data bdef_prog type program.
    bdef_prog = p_bdef.
    overlay  bdef_prog with '==============================BD'.
    perform nottobefound in program (bdef_prog) if found.

    "retrieve context
    data cover type c length 80.
    concatenate '(' bdef_prog ')%AUTH_CONTEXTS-' p_ac into cover.
    assign (cover) to field-symbol(<cover>).
    assert sy-subrc = 0.

    "store
    append value #( bdef = p_bdef
                    ac = p_ac
                    program = p_program
                    include = p_include
                    line = p_line
                    ctx = ref #( <cover> ) ) to gt_contexts.
  endmethod.


  method register by KERNEL MODULE ab_km_auth_registerx_788
                                   ab_km_auth_register_788
                                   ab_km_auth_register_787
                                   ab_km_auth_register_786
                                   ab_km_auth_register ignore.
  endmethod.
ENDCLASS.