class ZCL_FI_IC_MARK_UP_COSTS_FILTER definition
  public
  create public .

public section.

  types:
    BEGIN OF gtyp_s_Filters_FE,
        ControllingArea          TYPE zpe_tt_selectall_fe_filter,
        CompanyCode              TYPE zpe_tt_selectall_fe_filter,
        Plant                    TYPE zpe_tt_selectall_fe_filter,
        CostingDate              TYPE zpe_tt_selectall_fe_filter,
        Status                   TYPE zpe_tt_selectall_fe_filter,
        Article                  TYPE zpe_tt_selectall_fe_filter,
        BuyingCommission         TYPE zpe_tt_selectall_fe_filter,
        CostingVariant           TYPE zpe_tt_selectall_fe_filter,
        Currency                 TYPE zpe_tt_selectall_fe_filter,
        ExistingMarkUpPercentage TYPE zpe_tt_selectall_fe_filter,
        ExistingMarkUpValue      TYPE zpe_tt_selectall_fe_filter,
        MarkUpPercentage         TYPE zpe_tt_selectall_fe_filter,
        MarkUpValue              TYPE zpe_tt_selectall_fe_filter,
        StandardCustoms          TYPE zpe_tt_selectall_fe_filter,
        StandardFOB              TYPE zpe_tt_selectall_fe_filter,
        StandardFreight          TYPE zpe_tt_selectall_fe_filter,
        ValidFrom                TYPE zpe_tt_selectall_fe_filter,
      END OF gtyp_s_Filters_FE .
  types:
    BEGIN OF gtyp_s_Filters_BE,
        ControllingArea          TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        CompanyCode              TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        Plant                    TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        CostingDate              TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        Status                   TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        Article                  TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        BuyingCommission         TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        CostingVariant           TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        Currency                 TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        ExistingMarkUpPercentage TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        ExistingMarkUpValue      TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        MarkUpPercentage         TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        MarkUpValue              TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        StandardCustoms          TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        StandardFOB              TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        StandardFreight          TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
        ValidFrom                TYPE IF_RAP_QUERY_FILTER=>TT_RANGE_OPTION,
      END OF gtyp_s_Filters_BE .

  methods GET_FILTERS_BE
    importing
      !IS_FILTERS_FE type GTYP_S_FILTERS_FE
    returning
      value(RS_FILTERS_BE) type GTYP_S_FILTERS_BE
    raising
      ZCX_FI_IC_MARK_UP_COSTS
      ZCX_PE_FE2BE_CONVERSOR .
protected section.
private section.
ENDCLASS.



CLASS ZCL_FI_IC_MARK_UP_COSTS_FILTER IMPLEMENTATION.


  METHOD get_filters_be.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* BOBRULA      | 13.03.2023 | 18926: IC Mark-up costs maintenance App  *
*              |            |        - Select All workaround           *
*              |            | DS4K946491                               *
*----------------------------------------------------------------------*

    DATA(lt_component) = CAST cl_abap_structdescr(
      cl_abap_typedescr=>describe_by_data( is_filters_fe )
        )->get_components( ).

    LOOP AT lt_component ASSIGNING FIELD-SYMBOL(<ls_component>).

      ASSIGN COMPONENT <ls_component>-name OF STRUCTURE is_filters_fe
        TO FIELD-SYMBOL(<lt_filter_fe>).
      IF sy-subrc NE 0.
        RAISE EXCEPTION TYPE zcx_fi_ic_mark_up_costs
          MESSAGE e003(zfi_ic_mark_up_costs) WITH <ls_component>-name.
      ENDIF.

      ASSIGN COMPONENT <ls_component>-name OF STRUCTURE rs_filters_be
        TO FIELD-SYMBOL(<lt_filter_be>).
      IF sy-subrc NE 0.
        RAISE EXCEPTION TYPE zcx_fi_ic_mark_up_costs
          MESSAGE e003(zfi_ic_mark_up_costs) WITH <ls_component>-name.
      ENDIF.

      TRY.
          <lt_filter_be> = COND if_rap_query_filter=>tt_range_option(
            WHEN <ls_component>-name IN zcl_pe_hm_basic=>get_range( iv_id = '1418' ) THEN
                 NEW zcl_pe_fe2be_conversor(  )->get_be_daterange( <lt_filter_fe> )
            ELSE
                 NEW zcl_pe_fe2be_conversor(  )->get_be_range( <lt_filter_fe> ) ).
        CATCH zcx_pe_hm ##NO_HANDLER.
      ENDTRY.

    ENDLOOP.

  ENDMETHOD.
ENDCLASS.