CLASS zcl_im_sc_ei_sto_po_check DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_badi_interface .
    INTERFACES if_ex_me_process_po_cust .
    TYPES: BEGIN OF gtyp_eket_tab,
             mandt          TYPE mandt,
             ebeln          TYPE ebeln,
             ebelp          TYPE ebelp,
             etenr          TYPE etenr,
             zz1_eldat_init TYPE dats,
             zz1_eindt_init TYPE dats,
             zz1_mbdat_init TYPE dats,
             zz1_mng02_init TYPE mng06,
           END OF gtyp_eket_tab.
    TYPES : gtyp_t_eket_tab TYPE STANDARD TABLE OF gtyp_eket_tab.
protected section.
private section.

  methods GET_ZTABLE_DATA
    importing
      !IV_EBELN type EBELN
    exporting
      value(ET_TAB) type GTYP_T_EKET_TAB .
  methods GET_REQUIRED_DATA_TO_UPD
    importing
      !IR_HEADER type ref to IF_PURCHASE_ORDER_MM
      !IT_CHECK type GTYP_T_EKET_TAB
      !IV_EBELN type EBELN
    exporting
      !ET_TAB type GTYP_T_EKET_TAB .
  methods UPD_ZTABLE_DATA
    importing
      !IT_TAB type GTYP_T_EKET_TAB .
  methods CHECK_VALID_ORDERS
    importing
      !IR_HEADER type ref to IF_PURCHASE_ORDER_MM
    returning
      value(RV_FLAG) type CHAR1 .
ENDCLASS.



CLASS ZCL_IM_SC_EI_STO_PO_CHECK IMPLEMENTATION.


METHOD if_ex_me_process_po_cust~check.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *

* SRIKAVEN     | 15.11.2022 | 15170     : Development - EKET           *
*              |            | DS4K935182                               *
*----------------------------------------------------------------------*
  DATA lv_order_reason_text TYPE char20.
  DATA lv_order_type2 TYPE ekko-zz1_zorderreason_pdh.
  CONSTANTS : lc_hardcode_id TYPE zpe_hm_id VALUE '212',
              lc_i           TYPE char1 VALUE 'I',
              lc_eq          TYPE char2 VALUE 'EQ',
              lc_w           TYPE char1 VALUE 'W',
              lc_separator   TYPE char1 VALUE ','.
  TRY.
      DATA(lt_value) =  zcl_pe_hm_basic=>get_table( iv_id = lc_hardcode_id ).
    CATCH zcx_pe_hm INTO DATA(lo_exc_002).
      DATA(lv_text) = lo_exc_002->get_text( ).
      MESSAGE lv_text TYPE lc_w.
  ENDTRY.
  DATA lr_bsart TYPE RANGE OF ekko-bsart.

  lr_bsart  = VALUE #( FOR ls_val IN lt_value ( sign   = lc_i
                                                option = lc_eq
                                                low    = ls_val-field1 ) ) .
  DATA(ls_header) =  im_header->get_data( ).

  CLEAR ch_failed.
  IF ls_header-bsart IN lr_bsart  AND ls_header-zz1_zorderreason_pdh IS NOT INITIAL.

    IF line_exists( lt_value[ field1 =  CONV #( ls_header-bsart ) field2 =  ls_header-zz1_zorderreason_pdh  ] ) . "#EC CI_STDSEQ
      CLEAR ch_failed.
    ELSE.

      ch_failed = abap_true.
      LOOP AT lt_value ASSIGNING FIELD-SYMBOL(<ls_value>) WHERE field1 = CONV char30( ls_header-bsart ). "#EC CI_STDSEQ
        lv_order_type2 = <ls_value>-field2.
        lv_order_reason_text = lv_order_type2 && lc_separator && lv_order_reason_text.
      ENDLOOP.
      MESSAGE  e001(zsd_msg_cls_cst) WITH lv_order_reason_text.
    ENDIF.
  ENDIF.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~close.

ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~fieldselection_header.

ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~fieldselection_header_refkeys.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~fieldselection_item.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~fieldselection_item_refkeys.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~initialize.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~open.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
* SRIKAVEN     | 15.11.2022 | 15170     : Development - EKET           *
*              |            | DS4K935182                               *
*----------------------------------------------------------------------*

METHOD if_ex_me_process_po_cust~post.
  DATA(lv_flag) = check_valid_orders( im_header ).
  IF lv_flag IS INITIAL.

    get_ztable_data( EXPORTING iv_ebeln = im_ebeln
                     IMPORTING et_tab = DATA(lt_check) ).

    get_required_data_to_upd( EXPORTING ir_header = im_header
                                        it_check = lt_check
                                        iv_ebeln = im_ebeln
                              IMPORTING et_tab = DATA(lt_tab) ).

    upd_ztable_data(  lt_tab ).
  ENDIF.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~process_account.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~process_header.
ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
* SRIKAVEN     | 15.11.2022 | 15170     : Development - EKET           *
*              |            | DS4K935182                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~process_item.

ENDMETHOD.


##NEEDED
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 22.07.2022 | 11662     : [Development] STOs additional fields *
*              |            | DS4K921038                               *
*----------------------------------------------------------------------*
METHOD if_ex_me_process_po_cust~process_schedule.
ENDMETHOD.


  METHOD get_required_data_to_upd.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 15.11.2022 | 15170     : Development - EKET           *
*              |            | DS4K935182                               *
*----------------------------------------------------------------------*

    CLEAR et_tab.
    DATA(lt_items) = ir_header->get_items( ).
    SELECT purchaseorderitem , zz1_zrsd_pd_item_pdi FROM i_purchaseorderitemtp INTO TABLE @DATA(lt_ekpo) WHERE purchaseorder = @iv_ebeln.
    IF sy-subrc IS NOT INITIAL.
      CLEAR lt_ekpo.
    ENDIF.
                                                         "#EC CI_NESTED
    LOOP AT lt_items ASSIGNING FIELD-SYMBOL(<ls_items>).
*...// Get Item Details
      DATA(ls_items) = <ls_items>-item->get_data( ).
      IF ls_items-elikz IS INITIAL AND ls_items-loekz IS INITIAL.
*...// Get Schedule Line Details
        DATA(lt_schedule) =     <ls_items>-item->get_schedules( ).
                                                         "#EC CI_NESTED
        LOOP AT lt_schedule ASSIGNING FIELD-SYMBOL(<ls_schedule>). "#EC CI_NESTED
          DATA(ls_schedule_data) = <ls_schedule>-schedule->get_data( ).
*...// Check Schedule line Data
                                                         "#EC CI_STDSEQ
          IF ls_schedule_data-mng02 IS INITIAL OR
             line_exists( it_check[ ebeln = iv_ebeln ebelp = ls_schedule_data-ebelp  ] ). "#EC CI_STDSEQ
          ELSE.
*...// Append Value to update the Ztable
            APPEND VALUE #( ebeln = iv_ebeln
            ebelp = ls_schedule_data-ebelp
            etenr = ls_schedule_data-etenr
            zz1_eldat_init = ls_schedule_data-eldat
            zz1_eindt_init = ls_schedule_data-eindt
            zz1_mbdat_init = ls_schedule_data-mbdat
            zz1_mng02_init = ls_schedule_data-mng02 ) TO et_tab.
          ENDIF.
          READ TABLE lt_ekpo ASSIGNING FIELD-SYMBOL(<ls_ekpo>) WITH KEY purchaseorderitem = ls_schedule_data-ebelp. "#EC CI_STDSEQ
          IF sy-subrc IS INITIAL AND <ls_ekpo>-zz1_zrsd_pd_item_pdi NE ls_items-zz1_zrsd_pd_item_pdi.
            APPEND VALUE #( ebeln = iv_ebeln
             ebelp = ls_schedule_data-ebelp
             etenr = ls_schedule_data-etenr
             zz1_eldat_init = ls_schedule_data-eldat
             zz1_eindt_init = ls_schedule_data-eindt
             zz1_mbdat_init = ls_schedule_data-mbdat
             zz1_mng02_init = ls_schedule_data-mng02 ) TO et_tab.
          ENDIF.

        ENDLOOP.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD get_ztable_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 15.11.2022 | 15170     : Development - EKET           *
*              |            | DS4K935182                               *
*----------------------------------------------------------------------*


    SELECT mandt,ebeln, ebelp,etenr,zz1_eldat_init,zz1_eindt_init,zz1_mbdat_init,zz1_mng02_init
    FROM zsc_t_eket_dates INTO CORRESPONDING FIELDS OF TABLE @et_tab WHERE ebeln = @iv_ebeln.
    IF sy-subrc IS NOT INITIAL.
      CLEAR et_tab.
    ENDIF.
                                                         "#EC CI_VALPAR
  ENDMETHOD.


  METHOD upd_ztable_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 15.11.2022 | 15170     : Development - EKET           *
*              |            | DS4K935182                               *
*----------------------------------------------------------------------*

*...// update the table
    IF it_tab IS NOT INITIAL.
      CALL FUNCTION 'ZSC_FM_EKET_INIT_VAL_UPD' IN UPDATE TASK
        TABLES
          it_tab = it_tab.
    ENDIF.
  ENDMETHOD.


  METHOD check_valid_orders.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* SRIKAVEN     | 15.11.2022 | 15170     : Development - EKET           *
*              |            | DS4K935182                               *
* RAPANKON     | 20.05.2023 | 20619     : CR[33704] First RDD/CDD/     *
*              |            | DS4K950725                               *
*----------------------------------------------------------------------*
    DATA lr_bsart TYPE RANGE OF esart.

    TRY.
        DATA(lt_value) =  zcl_pe_hm_basic=>get_table( iv_id = '212' ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.

    lr_bsart  = VALUE #( FOR ls_val IN lt_value ( sign   = rsmds_c_sign-including
                                                  option = rsmds_c_option-equal
                                                  low    = ls_val-field1 ) ) .
    DATA(ls_header) =  ir_header->get_data( ).
*...// Check the order type is in HM or not and orderreason is filled
    IF ls_header-bsart NOT IN lr_bsart.
      rv_flag = abap_true.
    ENDIF.

    IF rv_flag IS INITIAL.
*...// Business switch logic for initial Fields
      TRY.
          DATA(lt_rsd_cds_act_hm) = zcl_pe_hm_basic=>get_table( '979' ).
          IF ( NOT line_exists( lt_rsd_cds_act_hm[ field1 = ls_header-bsart ] ) ). "#EC CI_STDSEQ
            rv_flag = abap_true.
          ENDIF.
        CATCH zcx_pe_hm.
          RETURN.
      ENDTRY.
    ENDIF.
  ENDMETHOD.
ENDCLASS.