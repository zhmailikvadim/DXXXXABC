CLASS zcl_sd_sto_process_base DEFINITION
  PUBLIC
  ABSTRACT
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES zif_sd_sto_process_handler.
    METHODS constructor
      IMPORTING
        it_bsart               TYPE zif_sd_sto_ud_const=>mtyp_t_bsart             OPTIONAL    "
        iv_ekorg               TYPE zif_sd_sto_ud_const=>mtyp_ekorg               OPTIONAL    "
        it_ekgrp               TYPE zif_sd_sto_ud_const=>mtyp_t_ekgrp             OPTIONAL
        it_sto_ud              TYPE zif_sd_sto_ud_const=>mtyp_t_sto_ud            OPTIONAL
        it_ebeln               TYPE zif_sd_sto_ud_const=>mtyp_t_ebeln             OPTIONAL
        it_werks_r             TYPE zif_sd_sto_ud_const=>mtyp_t_werks_r           OPTIONAL    "
        it_lgort               TYPE zif_sd_sto_ud_const=>mtyp_t_lgort             OPTIONAL
        it_werks_s             TYPE zif_sd_sto_ud_const=>mtyp_t_werks_s           OPTIONAL
        it_bsgru               TYPE zif_sd_sto_ud_const=>mtyp_t_bsgru             OPTIONAL
        it_erdat               TYPE zif_sd_sto_ud_const=>mtyp_t_erdat             OPTIONAL    "
        it_ernam               TYPE zif_sd_sto_ud_const=>mtyp_t_ernam             OPTIONAL
        iv_days_from_gr        TYPE zif_sd_sto_ud_const=>mtyp_days_from_gr        OPTIONAL
        iv_days_from_gi        TYPE zif_sd_sto_ud_const=>mtyp_days_from_gi        OPTIONAL
        iv_is_search_by_ud_sto TYPE zif_sd_sto_ud_const=>mtyp_is_search_by_ud_sto OPTIONAL
        iv_sto_extra_indicator TYPE zif_sd_sto_ud_const=>mtyp_sto_extra_indicator OPTIONAL.

  PROTECTED SECTION.

    TYPES:
      mtyp_t_sto_ud_log TYPE SORTED TABLE OF zsd_t_sto_ud_log WITH NON-UNIQUE KEY under_delivered_sto under_delivered_sto_item .
    TYPES:
      BEGIN OF mtyp_s_pur_order_key,
        ebeln TYPE I_PurchaseOrderAPI01-PurchaseOrder,
        ebelp TYPE I_PurchaseOrderItemAPI01-PurchaseOrderItem,
      END OF mtyp_s_pur_order_key .
    TYPES:
      BEGIN OF mtyp_s_pur_order_key_del,
        ebeln TYPE I_PurchaseOrderAPI01-PurchaseOrder,
        ebelp TYPE I_PurchaseOrderItemAPI01-PurchaseOrderItem,
        xblnr TYPE xblnr1,
      END OF mtyp_s_pur_order_key_del,
      mtyp_t_pur_order_key_del TYPE STANDARD TABLE OF mtyp_s_pur_order_key_del WITH KEY ebeln ebelp.
    TYPES:
      BEGIN OF mtyp_s_sto_ud_header,
        ebeln            TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrder,
        ebelp            TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrderItem,
        xblnr            TYPE I_PurchaseOrderHistoryAPI01-DocumentReferenceID,
        ekorg            TYPE I_PurchaseOrderAPI01-PurchasingOrganization,
        bsart            TYPE I_PurchaseOrderAPI01-PurchaseOrderType,
        quant_short      TYPE I_PurchaseOrderHistoryAPI01-Quantity,
        validation_error TYPE string,
      END OF mtyp_s_sto_ud_header .
    TYPES:
      BEGIN OF mtyp_s_sto_ud_header_2,
        ebeln            TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrder,
        ebelp            TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrderItem,
        outbound_dn      TYPE I_POSupplierConfirmationAPI01-DeliveryDocument,
        ekorg            TYPE I_PurchaseOrderAPI01-PurchasingOrganization,
        bsart            TYPE I_PurchaseOrderAPI01-PurchaseOrderType,
        quant_short      TYPE I_PurchaseOrderHistoryAPI01-Quantity,
        validation_error TYPE string,
      END OF mtyp_s_sto_ud_header_2 .
    TYPES:
      mtyp_t_pur_order_key TYPE STANDARD TABLE OF mtyp_s_pur_order_key WITH KEY ebeln ebelp .
    TYPES:
      BEGIN OF mtyp_s_pur_order_data,
        ebeln                  TYPE I_PurchaseOrderAPI01-PurchaseOrder,
        ebelp                  TYPE I_PurchaseOrderItemAPI01-PurchaseOrderItem,
        bukrs                  TYPE I_PurchaseOrderAPI01-CompanyCode,
        ihrez                  TYPE I_PurchaseOrderAPI01-CorrespncExternalReference,
        bsart                  TYPE I_PurchaseOrderAPI01-PurchaseOrderType,
        lifnr                  TYPE I_PurchaseOrderAPI01-Supplier,
        ekorg                  TYPE I_PurchaseOrderAPI01-PurchasingOrganization,
        ekgrp                  TYPE I_PurchaseOrderAPI01-PurchasingGroup,
        reswk                  TYPE I_PurchaseOrderAPI01-SupplyingPlant,
        adrnr                  TYPE I_PurchaseOrderAPI01-ManualSupplierAddressID,
        zz1_zsales_channel_pdh TYPE I_PurchaseOrderAPI01-zz1_zsales_channel_pdh,
        netpr                  TYPE I_PurchaseOrderItemAPI01-NetPriceAmount,
        matnr                  TYPE I_PurchaseOrderItemAPI01-Material,
        menge                  TYPE I_PurchaseOrderItemAPI01-OrderQuantity,
        meins                  TYPE I_PurchaseOrderItemAPI01-PurchaseOrderQuantityUnit,
        werks                  TYPE I_PurchaseOrderItemAPI01-Plant,
        umrez                  TYPE I_PurchaseOrderItemAPI01-OrderItemQtyToBaseQtyNmrtr,
        umren                  TYPE I_PurchaseOrderItemAPI01-OrderItemQtyToBaseQtyDnmntr,
        lmein                  TYPE I_PurchaseOrderItemAPI01-BaseUnit,
        ematn                  TYPE I_PurchaseOrderItemAPI01-ManufacturerMaterial,
        lgort                  TYPE I_PurchaseOrderItemAPI01-StorageLocation,
        txz01                  TYPE I_PurchaseOrderItemAPI01-PurchaseOrderItemText,
        ko_prctr               TYPE I_PurchaseOrderItemAPI01-ProfitCenter,
        idnlf                  TYPE I_PurchaseOrderItemAPI01-SupplierMaterialNumber,
      END OF mtyp_s_pur_order_data .
    TYPES:
      mtyp_t_pur_order_data TYPE STANDARD TABLE OF mtyp_s_pur_order_data WITH DEFAULT KEY .
    TYPES:
      BEGIN OF mtyp_s_material_sto_ud,
        ebeln TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrder,
        ebelp TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrderItem,
        matnr TYPE I_Material-Material,
        maktx TYPE I_ProductDescription-ProductDescription,
      END OF mtyp_s_material_sto_ud .
    TYPES:
      mtyp_t_material_sto_ud TYPE STANDARD TABLE OF mtyp_s_material_sto_ud WITH DEFAULT KEY .
    TYPES:
      BEGIN OF mtyp_s_ekbe_pgi,
        ebeln TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrder,
        ebelp TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrderItem,
        belnr TYPE I_PurchaseOrderHistoryAPI01-PurchasingHistoryDocument,
        vgabe TYPE I_PurchaseOrderHistoryAPI01-PurchasingHistoryDocumentType,
      END OF mtyp_s_ekbe_pgi .
    TYPES:
      mtyp_t_ekbe_pgi TYPE STANDARD TABLE OF mtyp_s_ekbe_pgi WITH DEFAULT KEY .
    TYPES:
      BEGIN OF mtyp_s_matdoc,
        mblnr TYPE I_MaterialDocumentHeader_2-MaterialDocument, " mkpf
        mjahr TYPE I_MaterialDocumentHeader_2-MaterialDocumentYear,
      END OF mtyp_s_matdoc .
    TYPES:
      mtyp_t_matdoc TYPE STANDARD TABLE OF mtyp_s_matdoc WITH DEFAULT KEY .
    TYPES:
      BEGIN OF mtyp_s_matdoc_data,
        mblnr TYPE I_MaterialDocumentHeader_2-MaterialDocument,
        mjahr TYPE I_MaterialDocumentHeader_2-MaterialDocumentYear,
        werks TYPE I_MaterialDocumentItem_2-Plant, "mseg
        lgort TYPE I_MaterialDocumentItem_2-StorageLocation,
        matnr TYPE I_MaterialDocumentItem_2-Material,
        menge TYPE I_MaterialDocumentItem_2-QuantityInBaseUnit,
        meins TYPE I_MaterialDocumentItem_2-MaterialBaseUnit,
      END OF mtyp_s_matdoc_data .
    TYPES:
      mtyp_t_matdoc_data TYPE STANDARD TABLE OF mtyp_s_matdoc_data WITH DEFAULT KEY .
    TYPES:
      BEGIN OF mtyp_s_delivery_key,
        vbeln TYPE I_DeliveryDocument-DeliveryDocument,
        posnr TYPE I_DeliveryDocumentItem-DeliveryDocumentItem,
      END OF mtyp_s_delivery_key .
    TYPES:
      mtyp_t_delivery_keys TYPE STANDARD TABLE OF mtyp_s_delivery_key WITH KEY vbeln posnr .
    TYPES:
      BEGIN OF mtyp_s_delivery,
        vbeln       TYPE I_DeliveryDocument-DeliveryDocument,
        posnr       TYPE I_DeliveryDocumentItem-DeliveryDocumentItem,
        quantity_su TYPE ebumng,
      END OF mtyp_s_delivery .
    TYPES:
      mtyp_t_delivery TYPE STANDARD TABLE OF mtyp_s_delivery WITH KEY vbeln posnr .
    TYPES:
      BEGIN OF mtyp_s_delivery_data,
        vbeln TYPE I_DeliveryDocument-DeliveryDocument,
        posnr TYPE I_DeliveryDocumentItem-DeliveryDocumentItem,
        werks TYPE I_DeliveryDocumentItem-Plant,
        lgort TYPE I_DeliveryDocumentItem-StorageLocation,
        bwart TYPE I_DeliveryDocumentItem-GoodsMovementType,
        meins TYPE I_DeliveryDocumentItem-BaseUnit,
        vrkme TYPE I_DeliveryDocumentItem-DeliveryQuantityUnit,
        wbstk TYPE I_DeliveryDocument-OverallGoodsMovementStatus,
      END OF mtyp_s_delivery_data .
    TYPES:
      mtyp_t_deliveries_data TYPE STANDARD TABLE OF mtyp_s_delivery_data WITH DEFAULT KEY .
    TYPES:
      mtyp_t_bapiret TYPE STANDARD TABLE OF bapireturn .  "bapiret2 .
    TYPES:
      BEGIN OF mtyp_s_found_sto,
        ebeln       TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrder,
        ebelp       TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrderItem,
        xblnr       TYPE I_PurchaseOrderHistoryAPI01-DocumentReferenceID,
        short_quant TYPE I_PurchaseOrderHistoryAPI01-Quantity,
        bewtp       TYPE I_PurchaseOrderHistoryAPI01-PurchasingHistoryCategory,
        belnr       TYPE I_PurchaseOrderHistoryAPI01-PurchasingHistoryDocument,
      END OF mtyp_s_found_sto .

    TYPES:
      BEGIN OF mtyp_s_linked_sto_dn,
        ebeln    TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrder,
        ebelp    TYPE I_PurchaseOrderHistoryAPI01-PurchaseOrderItem,
        xblnr    TYPE I_PurchaseOrderHistoryAPI01-DocumentReferenceID,
        quantity TYPE I_PurchaseOrderHistoryAPI01-Quantity,
        bewtp    TYPE I_PurchaseOrderHistoryAPI01-PurchasingHistoryCategory,
        belnr    TYPE I_PurchaseOrderHistoryAPI01-PurchasingHistoryDocument,
        venum    TYPE I_HandlingUnitTP-HandlingUnitInternalID , " vekp
      END OF mtyp_s_linked_sto_dn,
      mtyp_t_linked_sto_dn TYPE STANDARD TABLE OF mtyp_s_linked_sto_dn WITH DEFAULT KEY.

    DATA mt_bsart TYPE zif_sd_sto_ud_const=>mtyp_t_bsart .
    DATA mv_ekorg TYPE zif_sd_sto_ud_const=>mtyp_ekorg .
    DATA mt_ekgrp TYPE zif_sd_sto_ud_const=>mtyp_t_ekgrp .
    DATA mt_sto_ud TYPE zif_sd_sto_ud_const=>mtyp_t_sto_ud .
    DATA mt_ebeln TYPE zif_sd_sto_ud_const=>mtyp_t_ebeln .
    DATA mt_werks_r TYPE zif_sd_sto_ud_const=>mtyp_t_werks_r .
    DATA mt_lgort TYPE zif_sd_sto_ud_const=>mtyp_t_lgort .
    DATA mt_werks_s TYPE zif_sd_sto_ud_const=>mtyp_t_werks_s .
    DATA mt_bsgru TYPE zif_sd_sto_ud_const=>mtyp_t_bsgru .
    DATA mt_erdat TYPE zif_sd_sto_ud_const=>mtyp_t_erdat .
    DATA mt_ernam TYPE zif_sd_sto_ud_const=>mtyp_t_ernam .
    DATA mv_sto_extra_indicator TYPE zif_sd_sto_ud_const=>mtyp_sto_extra_indicator .
    DATA mv_days_from_gr TYPE zif_sd_sto_ud_const=>mtyp_days_from_gr .
    DATA mv_days_from_gi TYPE zif_sd_sto_ud_const=>mtyp_days_from_gi .
    DATA mv_is_search_by_ud_sto TYPE zif_sd_sto_ud_const=>mtyp_is_search_by_ud_sto .
    DATA mt_hm_1081 TYPE zpe_tt_hm_table .
    DATA mt_hm_1087 TYPE zpe_tt_hm_table .
    DATA mt_hm_876_bwart TYPE zpe_tt_hm_table .
    DATA mv_hm_717_bwart_101 TYPE bwart .
    DATA mv_hm_685_sto_inter_zu50 TYPE zpe_hm_field .
    DATA mv_hm_685_sto_intra_zu55 TYPE zpe_hm_field .
    DATA mv_hm_685_sto_intra_zu65 TYPE zpe_hm_field .
    DATA mv_hm_685_sto_inter_zu60 TYPE zpe_hm_field .
    DATA mv_hm_1042_lfart_zel TYPE zpe_hm_field .
    CONSTANTS mc_no_data_found_error TYPE string VALUE 'Required data is not found' ##NO_TEXT.
    DATA:
      mt_sto_ud_logs TYPE STANDARD TABLE OF zsd_t_sto_ud_log .

    METHODS update_ud_sto_log
      IMPORTING
        !its_ud_sto_log TYPE mtyp_t_sto_ud_log
      RAISING
        zcx_sd_sto_ud .
    METHODS get_sto_ud_logs
      IMPORTING
        VALUE(iv_status)       TYPE zsd_sto_process_status
        !iv_process            TYPE zsd_sto_ud_process
      RETURNING
        VALUE(rts_sto_ud_logs) TYPE mtyp_t_sto_ud_log .
    METHODS vldt_plant_sloc_exist_in_hm
      RAISING
        zcx_sd_sto_ud .
  PRIVATE SECTION.

    DATA mv_alt_bapi_ibd_create TYPE abap_bool VALUE abap_true ##NO_TEXT.

    METHODS get_purchase_orders_data
      IMPORTING
        !it_purchase_order_keys TYPE mtyp_t_pur_order_key
      RETURNING
        VALUE(rt_result)        TYPE mtyp_t_pur_order_data .
    METHODS get_deliveries_data
      IMPORTING
        !it_delivery_keys TYPE mtyp_t_delivery_keys
      RETURNING
        VALUE(rt_result)  TYPE mtyp_t_deliveries_data .
    METHODS get_materials_from_ud_sto  ##RELAX
      IMPORTING
        !its_sto_ud_log            TYPE mtyp_t_sto_ud_log
      RETURNING
        VALUE(rt_materials_sto_ud) TYPE mtyp_t_material_sto_ud .
    METHODS create_ibd_using_alt_bapi
      IMPORTING
        !is_sto_ud_log     TYPE zsd_t_sto_ud_log
        !is_pur_order_data TYPE mtyp_s_pur_order_data
      EXPORTING
        !et_return         TYPE mtyp_t_bapiret
        !ev_delivery_no    TYPE i_deliverydocument-deliverydocument .
    METHODS is_intra_company_movement
      IMPORTING
        !iv_supplying_plant TYPE reswk
        !iv_receiving_plant TYPE ewerk
      RETURNING
        VALUE(rv_result)    TYPE abap_bool .
    METHODS get_ekbe_belnr_for_sto_created
      IMPORTING
        !it_purchase_order_keys TYPE mtyp_t_pur_order_key
      RETURNING
        VALUE(rt_result)        TYPE mtyp_t_ekbe_pgi .
    METHODS get_ekbe_pgi_for_refuse_return
      IMPORTING
        !it_purchase_order_keys TYPE mtyp_t_pur_order_key_del
      RETURNING
        VALUE(rt_result)        TYPE mtyp_t_ekbe_pgi .
    METHODS get_matdoc_pur_hist_data
      IMPORTING
        !it_matdoc       TYPE mtyp_t_matdoc
      RETURNING
        VALUE(rt_result) TYPE mtyp_t_matdoc_data .

    METHODS vldt_and_get_hu_refusal_info_2
      IMPORTING is_found_sto    TYPE mtyp_s_sto_ud_header_2
      RETURNING VALUE(rt_venum) TYPE mtyp_t_linked_sto_dn .

    TYPES:
      BEGIN OF mtyp_s_delivery_hu,
        venum    TYPE venum,
        vpobjkey TYPE c LENGTH 20,
        hu_item  TYPE vepos,
        menge    TYPE vemng,
        venum_hu TYPE c LENGTH 22,
      END OF mtyp_s_delivery_hu,
      mtyp_t_delivery_hu TYPE STANDARD TABLE OF mtyp_s_delivery_hu WITH DEFAULT KEY.


    METHODS get_objkey
      IMPORTING
        !it_items        TYPE mtyp_t_delivery
      RETURNING
        VALUE(rt_objkey) TYPE mtyp_t_delivery_hu .
ENDCLASS.



CLASS ZCL_SD_STO_PROCESS_BASE IMPLEMENTATION.


  METHOD zif_sd_sto_process_handler~create_refusal_sto.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.05.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA lt_return  TYPE STANDARD TABLE OF bapiret2.
    DATA ls_exp_po_header TYPE bapimepoheader.

    DATA ls_is_header TYPE mepoheader.
    DATA ls_is_header_x TYPE mepoheaderx.
    DATA lt_it_items  TYPE STANDARD TABLE OF mepoitem .
    DATA lt_it_items_x  TYPE STANDARD TABLE OF mepoitemx.

    "Select all entries in the STO UD Log table where given status and process
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status  iv_process = iv_process ).


    IF lts_sto_ud_logs IS NOT INITIAL.
      DATA(lt_pur_order_data) = get_purchase_orders_data( it_purchase_order_keys = VALUE #( FOR ls_line IN lts_sto_ud_logs
                                                                                          ( ebeln = ls_line-under_delivered_sto  ebelp = ls_line-under_delivered_sto_item ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).
        DATA(ls_pur_order_data) = VALUE #( lt_pur_order_data[ ebeln = <ls_sto_ud_log>-under_delivered_sto ebelp = <ls_sto_ud_log>-under_delivered_sto_item ] OPTIONAL ).

        DATA(ls_hm_rec_sloc) = VALUE #( mt_hm_1087[ field1 = mt_werks_r[ 1 ]-low ] OPTIONAL ).
        DATA(ls_hm_sen_sloc) = VALUE #( mt_hm_1087[ field1 = COND #( WHEN mt_werks_s IS NOT INITIAL THEN mt_werks_s[ 1 ]-low  ELSE ls_pur_order_data-reswk ) ] OPTIONAL ).


        " Fill PO Header
        ls_is_header-ihrez = <ls_sto_ud_log>-under_delivered_sto .
        ls_is_header-bukrs = ls_pur_order_data-bukrs.
        DATA(lv_is_intra_company) = is_intra_company_movement( iv_receiving_plant = ls_pur_order_data-werks
                                                               iv_supplying_plant = COND #( WHEN ls_pur_order_data-reswk IS NOT INITIAL THEN ls_pur_order_data-reswk ELSE ls_pur_order_data-lifnr ) ).

        ls_is_header-bsart = COND #( WHEN lv_is_intra_company = abap_true THEN mv_hm_685_sto_intra_zu55
                                                                          ELSE mv_hm_685_sto_inter_zu50  ).
        ls_is_header-ekorg = ls_pur_order_data-ekorg.
        ls_is_header-ekgrp = ls_pur_order_data-ekgrp.
        ls_is_header-reswk = ls_pur_order_data-reswk.
        ls_is_header-aedat = sy-datum.
        ls_is_header-lifnr = ls_pur_order_data-lifnr.
        ls_is_header-zz1_zsales_channel_pdh = ls_pur_order_data-zz1_zsales_channel_pdh.
        ls_is_header-zz1_zorderreason_pdh = zif_sd_sto_ud_const=>mc_order_reason-refusal_header.
        ls_is_header_x-ihrez = abap_true.
        ls_is_header_x-bukrs = abap_true.
        ls_is_header_x-bsart = abap_true.
        ls_is_header_x-ekorg = abap_true.
        ls_is_header_x-ekgrp = abap_true.
        ls_is_header_x-reswk = abap_true.
        ls_is_header_x-lifnr = abap_true.
        ls_is_header_x-aedat = abap_true.
        ls_is_header_x-zz1_zsales_channel_pdh = abap_true.
        ls_is_header_x-pincr = abap_true.
        ls_is_header_x-zz1_zorderreason_pdh = abap_true.

        " Fill PO Item
        lt_it_items = VALUE #( BASE lt_it_items
                              ( matnr     = ls_pur_order_data-matnr
                                ebelp     = <ls_sto_ud_log>-under_delivered_sto_item
                                werks     = ls_pur_order_data-werks
                                lgort    = ls_hm_rec_sloc-field2
                                reslo    = ls_hm_sen_sloc-field2
                                menge    = <ls_sto_ud_log>-quantity_short
                                meins    = ls_pur_order_data-meins
                                retpo    = abap_true
                                bsgru    = zif_sd_sto_ud_const=>mc_order_reason-refusal
                                netpr    = ls_pur_order_data-netpr
                                ) ).

        lt_it_items_x = VALUE #( BASE lt_it_items_x
                                ( ebelp_key = <ls_sto_ud_log>-under_delivered_sto_item
                                  matnr    = abap_true
                                  werks    = abap_true
                                  lgort    = abap_true
                                  reslo  = abap_true
                                  menge  = abap_true
                                  meins  = abap_true
                                  retpo  = abap_true
                                  bsgru  = abap_true
                                  netpr  = abap_true
                                  ) ).


        CALL FUNCTION 'DMCC_MIG_PUR_PO_CREATE'
          EXPORTING
            is_header  = ls_is_header
            is_headerx = ls_is_header_x
          IMPORTING
            ev_header  = ls_exp_po_header
          TABLES
            it_items   = lt_it_items
            it_itemx   = lt_it_items_x
            et_return  = lt_return.

        IF line_exists( lt_return[ type = 'E' ] ).
          DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ).
          <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
          <ls_sto_ud_log>-error_msg_text = ls_return-message.
          CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        ELSE.
          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
          <ls_sto_ud_log>-status = iv_status.
          <ls_sto_ud_log>-virtual_return_sto = ls_exp_po_header-po_number .
          CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
        ENDIF.

        CLEAR: lt_it_items, lt_it_items_x, ls_is_header, ls_is_header_x.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD get_materials_from_ud_sto.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.05.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    " Get corresponding materials from under-delivered STO and its item.
    IF its_sto_ud_log IS NOT INITIAL.
      SELECT
        purchase_history~PurchaseOrder AS ebeln,
        purchase_history~PurchaseOrderItem AS ebelp,
        purchase_history~Material AS matnr,
        i_productdescription~ProductDescription AS maktx
        FROM i_purchaseorderhistoryapi01 AS purchase_history
        INNER JOIN i_productdescription ON purchase_history~Material = i_productdescription~Product AND i_productdescription~language = @sy-langu
        FOR ALL ENTRIES IN @its_sto_ud_log
        WHERE  purchase_history~PurchaseOrder = @its_sto_ud_log-under_delivered_sto
        AND   purchase_history~PurchaseOrderItem = @its_sto_ud_log-under_delivered_sto_item
        AND   purchase_history~DocumentReferenceID  = @its_sto_ud_log-under_delivered_dn
        INTO CORRESPONDING FIELDS OF TABLE @rt_materials_sto_ud.
      IF sy-subrc = 0.
      ELSE.
        MESSAGE e002(zsd_sto_ud) INTO DATA(lv_msg).         "#EC NEEDED
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD get_sto_ud_logs.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
    IF mt_sto_ud_logs IS NOT INITIAL.
      SELECT *
        FROM zsd_t_sto_ud_log
        FOR ALL ENTRIES IN @mt_sto_ud_logs
        WHERE under_delivered_sto =  @mt_sto_ud_logs-under_delivered_sto
        AND   under_delivered_sto_item = @mt_sto_ud_logs-under_delivered_sto_item
        AND   status = @iv_status
        AND   process = @iv_process
        INTO CORRESPONDING FIELDS OF TABLE @rts_sto_ud_logs.
      IF sy-subrc <> 0.
        RETURN.
      ENDIF.
    ENDIF.

  ENDMETHOD.


  METHOD get_purchase_orders_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA lt_pur_orders_wo_item_key LIKE it_purchase_order_keys.
    DATA lt_pur_orders_with_item_key LIKE it_purchase_order_keys.

    LOOP AT it_purchase_order_keys ASSIGNING FIELD-SYMBOL(<ls_pur_order_key>).
      IF <ls_pur_order_key>-ebelp IS INITIAL AND <ls_pur_order_key>-ebeln IS NOT INITIAL.
        APPEND INITIAL LINE TO lt_pur_orders_wo_item_key ASSIGNING FIELD-SYMBOL(<ls_pur_order_wo_item_key>).
        <ls_pur_order_wo_item_key> = CORRESPONDING #( <ls_pur_order_key> ).
      ELSEIF  <ls_pur_order_key>-ebelp IS NOT INITIAL AND <ls_pur_order_key>-ebeln IS NOT INITIAL.
        APPEND INITIAL LINE TO lt_pur_orders_with_item_key ASSIGNING FIELD-SYMBOL(<ls_pur_order_with_item_key>).
        <ls_pur_order_with_item_key> = CORRESPONDING #( <ls_pur_order_key> ).
      ELSE.
        RETURN.
      ENDIF.
    ENDLOOP.

    IF lt_pur_orders_wo_item_key IS NOT INITIAL.
      SELECT
        PurchaseOrder                              AS ebeln,
        \_PurchaseOrder-CompanyCode                AS bukrs,
        \_PurchaseOrder-CorrespncExternalReference AS ihrez,
        \_PurchaseOrder-PurchaseOrderType          AS bsart,
        \_PurchaseOrder-Supplier                   AS lifnr,
        \_PurchaseOrder-PurchasingOrganization     AS ekorg,
        \_PurchaseOrder-PurchasingGroup            AS ekgrp,
        \_PurchaseOrder-SupplyingPlant             AS reswk,
        \_PurchaseOrder-ManualSupplierAddressID    AS adrnr,
        \_PurchaseOrder-zz1_zsales_channel_pdh     AS zz1_zsales_channel_pdh,
        NetPriceAmount                             AS netpr,
        PurchaseOrderItem                          AS ebelp,
        Material                                   AS matnr,
        OrderQuantity                              AS menge,
        PurchaseOrderQuantityUnit                  AS meins,
        Plant                                      AS werks,
        OrderItemQtyToBaseQtyNmrtr                 AS umrez,
        OrderItemQtyToBaseQtyDnmntr                AS umren,
        BaseUnit                                   AS lmein,
        ManufacturerMaterial                       AS ematn,
        StorageLocation                            AS lgort,
        PurchaseOrderItemText                      AS txz01,
        ProfitCenter                               AS ko_prctr,
        SupplierMaterialNumber                     AS idnlf
        FROM i_purchaseorderitemapi01 AS tab_ekpo
        INNER JOIN @lt_pur_orders_wo_item_key AS itab ON itab~ebeln = tab_ekpo~PurchaseOrder
        APPENDING CORRESPONDING FIELDS OF TABLE @rt_result. "#EC CI_SUBRC

    ENDIF.

    IF lt_pur_orders_with_item_key IS NOT INITIAL.
      SELECT
        PurchaseOrder                              AS ebeln,
        \_PurchaseOrder-CompanyCode                AS bukrs,
        \_PurchaseOrder-CorrespncExternalReference AS ihrez,
        \_PurchaseOrder-PurchaseOrderType          AS bsart,
        \_PurchaseOrder-Supplier                   AS lifnr,
        \_PurchaseOrder-PurchasingOrganization     AS ekorg,
        \_PurchaseOrder-PurchasingGroup            AS ekgrp,
        \_PurchaseOrder-SupplyingPlant             AS reswk,
        \_PurchaseOrder-ManualSupplierAddressID    AS adrnr,
        \_PurchaseOrder-zz1_zsales_channel_pdh     AS zz1_zsales_channel_pdh,
        NetPriceAmount                             AS netpr,
        PurchaseOrderItem                          AS ebelp,
        Material                                   AS matnr,
        OrderQuantity                              AS menge,
        PurchaseOrderQuantityUnit                  AS meins,
        Plant                                      AS werks,
        OrderItemQtyToBaseQtyNmrtr                 AS umrez,
        OrderItemQtyToBaseQtyDnmntr                AS umren,
        BaseUnit                                   AS lmein,
        ManufacturerMaterial                       AS ematn,
        StorageLocation                            AS lgort,
        PurchaseOrderItemText                      AS txz01,
        ProfitCenter                               AS ko_prctr,
        SupplierMaterialNumber                     AS idnlf
        FROM i_purchaseorderitemapi01 AS tab_ekpo
        INNER JOIN @lt_pur_orders_with_item_key AS itab ON  itab~ebeln = tab_ekpo~PurchaseOrder
                                                        AND itab~ebelp = tab_ekpo~PurchaseOrderItem
        APPENDING CORRESPONDING FIELDS OF TABLE @rt_result. "#EC CI_SUBRC
    ENDIF.

  ENDMETHOD.


  METHOD get_matdoc_pur_hist_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.05.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    IF  it_matdoc IS NOT INITIAL.
      SELECT
      mat_doc~MaterialDocument AS mblnr,
      mat_doc~MaterialDocumentYear AS mjahr,
      purchase_history~Plant AS werks,
      mat_doc~StorageLocation AS lgort,
      purchase_history~material AS matnr,
      purchase_history~quantity AS menge,
      mat_doc~MaterialBaseUnit AS meins
      FROM i_purchaseorderhistoryapi01 AS purchase_history
      INNER JOIN i_materialdocumentitem_2 AS mat_doc ON mat_doc~MaterialDocument = purchase_history~PurchasingHistoryDocument
                                                   AND mat_doc~MaterialDocumentYear = purchase_history~PurchasingHistoryDocumentYear
                                                   AND mat_doc~MaterialDocumentItem = purchase_history~PurchasingHistoryDocumentItem
      FOR ALL ENTRIES IN @it_matdoc
      WHERE purchase_history~PurchasingHistoryDocument = @it_matdoc-mblnr
        AND purchase_history~PurchasingHistoryDocumentYear = @it_matdoc-mjahr
      INTO CORRESPONDING FIELDS OF TABLE @rt_result.
      IF sy-subrc = 0.
        RETURN.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD is_intra_company_movement.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
    " If the company code Is the same (BURKS) then intracompany, if different then intercompany.
    SELECT SINGLE CompanyCode AS bukrs               "#EC CI_SEL_NESTED
      FROM i_valuationarea "t001k
      WHERE ValuationArea = @iv_supplying_plant
      INTO @DATA(lv_bukrs_supplying).
    IF sy-subrc = 0.
      SELECT SINGLE  CompanyCode AS bukrs            "#EC CI_SEL_NESTED
        FROM i_valuationarea
        WHERE ValuationArea = @iv_receiving_plant
        INTO @DATA(lv_bukrs_receiving).
      IF sy-subrc = 0 AND lv_bukrs_receiving = lv_bukrs_supplying.
        rv_result = abap_true.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD get_deliveries_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA lt_delivery_keys_wo_posnr LIKE it_delivery_keys.
    DATA lt_delivery_keys_with_posnr LIKE it_delivery_keys.

    LOOP AT it_delivery_keys ASSIGNING FIELD-SYMBOL(<ls_delivery_key>).
      IF <ls_delivery_key>-posnr IS INITIAL AND <ls_delivery_key>-vbeln IS NOT INITIAL.
        APPEND INITIAL LINE TO lt_delivery_keys_wo_posnr ASSIGNING FIELD-SYMBOL(<ls_delivery_wo_posnr>).
        <ls_delivery_wo_posnr> = CORRESPONDING #( <ls_delivery_key> ).
      ELSEIF  <ls_delivery_key>-posnr IS NOT INITIAL AND <ls_delivery_key>-vbeln IS NOT INITIAL.
        APPEND INITIAL LINE TO lt_delivery_keys_with_posnr ASSIGNING FIELD-SYMBOL(<ls_delivery_with_posnr>).
        <ls_delivery_with_posnr> = CORRESPONDING #( <ls_delivery_key> ).
      ELSE.
        RETURN.
      ENDIF.
    ENDLOOP.

    IF lt_delivery_keys_wo_posnr IS NOT INITIAL.
      SELECT
        DeliveryDocument                               AS vbeln,
        DeliveryDocumentItem                           AS posnr,
        Plant                                          AS werks,
        StorageLocation                                AS lgort,
        GoodsMovementType                              AS bwart,
        BaseUnit                                       AS meins,
        DeliveryQuantityUnit                           AS vrkme,
        \_DeliveryDocument-OverallGoodsMovementStatus  AS wbstk
        FROM  i_deliverydocumentitem AS tab_lips
        INNER JOIN @lt_delivery_keys_wo_posnr AS itab ON itab~vbeln = tab_lips~DeliveryDocument
        APPENDING CORRESPONDING FIELDS OF TABLE @rt_result. "#EC CI_SUBRC
    ENDIF.

    IF lt_delivery_keys_with_posnr IS NOT INITIAL.
      SELECT
        DeliveryDocument                               AS vbeln,
        DeliveryDocumentItem                           AS posnr,
        Plant                                          AS werks,
        StorageLocation                                AS lgort,
        GoodsMovementType                              AS bwart,
        BaseUnit                                       AS meins,
        DeliveryQuantityUnit                           AS vrkme,
        \_DeliveryDocument-OverallGoodsMovementStatus  AS wbstk
        FROM  i_deliverydocumentitem AS delivery_item
        INNER JOIN @lt_delivery_keys_with_posnr AS itab ON itab~vbeln = delivery_item~DeliveryDocument
                                                       AND itab~posnr = delivery_item~DeliveryDocumentItem
        APPENDING CORRESPONDING FIELDS OF TABLE @rt_result. "#EC CI_SUBRC

    ENDIF.

  ENDMETHOD.


  METHOD create_ibd_using_alt_bapi.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.05.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA   lt_komdlgn TYPE STANDARD TABLE OF komdlgn.
    DATA   lt_vbfs    TYPE STANDARD TABLE OF vbfs.
    DATA   lt_vbls    TYPE STANDARD TABLE OF vbls.

    DATA ls_vbsk    TYPE vbsk.
    DATA ls_komdlgn TYPE komdlgn.
    DATA ls_vbls    TYPE vbls.

    CLEAR: ev_delivery_no, et_return.

    ls_vbsk-mandt = sy-mandt.
    ls_vbsk-ernam = sy-uname.
    ls_vbsk-erdat = sy-datum.
    ls_vbsk-uzeit = sy-uzeit.

    IF mt_werks_r IS NOT INITIAL.
      DATA(ls_hm_sloc) = VALUE #( mt_hm_1087[ field1 = mt_werks_r[ 1 ]-low ] OPTIONAL ).
    ENDIF.

    ls_komdlgn-lfart = mv_hm_1042_lfart_zel.
    ls_komdlgn-matnr = is_pur_order_data-matnr.
    ls_komdlgn-werks = ls_hm_sloc-field1.
    ls_komdlgn-lfimg = is_sto_ud_log-quantity_short.
    ls_komdlgn-vrkme = is_pur_order_data-meins.
    ls_komdlgn-meins = is_pur_order_data-lmein.
    ls_komdlgn-vgbel = is_pur_order_data-ebeln.
    ls_komdlgn-vgpos = is_pur_order_data-ebelp.
    ls_komdlgn-ematn = is_pur_order_data-ematn.
    ls_komdlgn-vgtyp = 'V'.
    ls_komdlgn-kzazu = 'X'.

    ls_komdlgn-lifex = is_sto_ud_log-under_delivered_dn. "
    ls_komdlgn-verur = is_sto_ud_log-under_delivered_dn. "
    ls_komdlgn-lgort = ls_hm_sloc-field2.
    ls_komdlgn-lifnr = COND #( WHEN is_pur_order_data-reswk IS NOT INITIAL THEN is_pur_order_data-reswk ELSE is_pur_order_data-lifnr ).

    ls_komdlgn-kdmat = is_pur_order_data-idnlf.
    ls_komdlgn-spe_lifex_type = 'S'.
    ls_komdlgn-lfdat = sy-datum.
    ls_komdlgn-lfuhr = sy-uzeit.
    ls_komdlgn-wadat = sy-datum.
    ls_komdlgn-wauhr = sy-uzeit.
    ls_komdlgn-wadat_ist = sy-datum.

    APPEND ls_komdlgn TO lt_komdlgn.
    IF lt_komdlgn[] IS NOT INITIAL.
      CALL FUNCTION 'GN_DELIVERY_CREATE'
        EXPORTING
          vbsk_i        = ls_vbsk
        TABLES
          xkomdlgn      = lt_komdlgn
          xvbfs         = lt_vbfs
          xvbls         = lt_vbls
        EXCEPTIONS
          error_message = 1
          OTHERS        = 2.
    ENDIF.

    IF sy-subrc <> 0 OR line_exists( lt_vbfs[ msgty = 'E' ] ).
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      DATA: ls_return TYPE bapireturn.
      DATA(ls_vbfs) = VALUE #( lt_vbfs[ msgty = 'E' ] OPTIONAL ).
      CALL FUNCTION 'BALW_BAPIRETURN_GET'
        EXPORTING
          type       = ls_vbfs-msgty
          cl         = ls_vbfs-msgid
          number     = CONV symsgno( ls_vbfs-msgno )
          par1       = ls_vbfs-msgv1
          par2       = ls_vbfs-msgv2
          par3       = ls_vbfs-msgv3
          par4       = ls_vbfs-msgv4
        IMPORTING
          bapireturn = ls_return.
      APPEND ls_return TO et_return.
    ELSE.
      DATA(lv_index) = lines( lt_vbls ).
      READ TABLE lt_vbls INTO ls_vbls INDEX lv_index.     "#EC CI_SUBRC
      DATA(lv_vbeln) = ls_vbls-vbeln_lif.
      ev_delivery_no = lv_vbeln.
    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~find_under_delivery_sto_refu_2.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*

    DATA lt_sto_ud_log TYPE STANDARD TABLE OF zsd_t_sto_ud_log WITH NON-UNIQUE SORTED KEY
         key_fields COMPONENTS  under_delivered_sto under_delivered_sto_item under_delivered_dn under_delivered_dn_item.
    DATA lt_sto_ud_log_to_exclude TYPE STANDARD TABLE OF zsd_t_sto_ud_log WITH NON-UNIQUE SORTED KEY
         key_fields COMPONENTS  under_delivered_sto under_delivered_sto_item under_delivered_dn under_delivered_dn_item.
    DATA lts_sto_ud_log_to_update TYPE mtyp_t_sto_ud_log.


    SELECT
    FROM I_POSupplierConfirmationAPI01 AS supplier_confirmation
    INNER JOIN ekes AS ekes_tab  ON ekes_tab~ebeln = supplier_confirmation~PurchaseOrder
                                AND ekes_tab~ebelp = supplier_confirmation~PurchaseOrderItem
                                AND ekes_tab~etens = supplier_confirmation~SequentialNmbrOfSuplrConf
    FIELDS DISTINCT
      supplier_confirmation~PurchaseOrder                                                  AS ebeln,
      supplier_confirmation~PurchaseOrderItem                                              AS ebelp,
      supplier_confirmation~CreationDate                                                   AS erdat,
      supplier_confirmation~MRPRelevantQuantity                                            AS dabmg,
      supplier_confirmation~ConfirmedQuantity                                              AS menge,
      supplier_confirmation~ConfirmedQuantity - supplier_confirmation~MRPRelevantQuantity  AS quant_short,
      supplier_confirmation~DeliveryDocument                                               AS inbound_dn,
      supplier_confirmation~DeliveryDocumentItem                                           AS inbound_dn_item,
      ekes_tab~vbeln_st                                                                    AS outbound_dn,
      ekes_tab~vbelp_st                                                                    AS outbound_dn_item,
      CAST( '00000000' AS DATS )                                                           AS latest_outbound_erdat,
      CAST( '00000000' AS DATS )                                                           AS latest_inbound_erdat,
      CAST( 0 AS INT2 )                                                                    AS days_from_outbound_dn,
      CAST( 0 AS INT2 )                                                                    AS days_from_inbound_dn,
      \_PurchaseOrder-PurchaseOrderType                                                    AS bsart,
      \_PurchaseOrder-PurchasingOrganization                                               AS ekorg,
      \_PurchaseOrderItem-IsCompletelyDelivered                                            AS elikz,
      supplier_confirmation~SupplierConfirmationCategory                                   AS ebtyp
    WHERE \_PurchaseOrder-PurchaseOrderType                            IN @mt_bsart
      AND \_PurchaseOrder-PurchasingOrganization                       = @mv_ekorg
      AND \_PurchaseOrder-PurchasingGroup                              IN @mt_ekgrp
      AND \_PurchaseOrder-PurchaseOrder                                IN @mt_ebeln
      AND \_PurchaseOrderItem-Plant                                    IN @mt_werks_r
      AND \_PurchaseOrderItem-StorageLocation                          IN @mt_lgort
      AND \_PurchaseOrder-SupplyingPlant                               IN @mt_werks_s
      AND \_PurchaseOrderItem-PurchasingOrderReason                    IN @mt_bsgru
      AND \_PurchaseOrder-CreationDate                                 IN @mt_erdat
      AND \_PurchaseOrder-CreatedByUser                                IN @mt_ernam
      AND supplier_confirmation~SupplierConfirmationCategory           = 'LA'
      AND \_PurchaseOrder-PurchasingDocumentDeletionCode               = @space
      AND \_PurchaseOrderItem-PurchasingDocumentDeletionCode           = @space
      AND supplier_confirmation~IsDeleted                              = @space
      ORDER BY  supplier_confirmation~PurchaseOrder, supplier_confirmation~PurchaseOrderItem, supplier_confirmation~CreationDate
    INTO TABLE @DATA(lt_ud_sto).
    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE zcx_sd_sto_ud MESSAGE e003(zsd_sto_ud).
    ELSE.
      SELECT
      FROM I_SDDocumentMultiLevelProcFlow WITH PRIVILEGED ACCESS AS sd_doc_flow
      INNER JOIN @lt_ud_sto AS sto_ud  ON sd_doc_flow~PrecedingDocument          = sto_ud~outbound_dn
                                      AND sd_doc_flow~PrecedingDocumentItem      = sto_ud~outbound_dn_item
                                      AND sd_doc_flow~SubsequentDocumentCategory = 'R'
      FIELDS  sd_doc_flow~PrecedingDocument,
              sd_doc_flow~PrecedingDocumentItem,
              sd_doc_flow~CreationDate           AS erdat,
              sd_doc_flow~CreationTime           AS erzet,
              dats_days_between( sd_doc_flow~CreationDate, @sy-datum ) AS days_from_entry
      INTO TABLE @DATA(lt_vbfa_outbound)
        .
      IF sy-subrc = 0.
        " Check the last GI date: select ERDAT from table VBFA. If there are multiple records sort to find the latest date ERDAT.
        SORT lt_vbfa_outbound  BY PrecedingDocument PrecedingDocumentItem erdat DESCENDING erzet DESCENDING.
        LOOP AT lt_ud_sto ASSIGNING FIELD-SYMBOL(<ls_ud_sto_result>).
          READ TABLE lt_vbfa_outbound ASSIGNING FIELD-SYMBOL(<ls_vbfa_outbound>) WITH KEY PrecedingDocument     = <ls_ud_sto_result>-outbound_dn
                                                                                          PrecedingDocumentItem = <ls_ud_sto_result>-outbound_dn_item.
          IF sy-subrc = 0.
            <ls_ud_sto_result>-latest_outbound_erdat  = <ls_vbfa_outbound>-erdat.
            <ls_ud_sto_result>-days_from_outbound_dn  = <ls_vbfa_outbound>-days_from_entry.
          ENDIF.
        ENDLOOP.
      ENDIF.

      SELECT
      FROM I_SDDocumentMultiLevelProcFlow WITH PRIVILEGED ACCESS AS sd_doc_flow
      INNER JOIN @lt_ud_sto AS sto_ud  ON sd_doc_flow~PrecedingDocument          = sto_ud~inbound_dn
                                      AND sd_doc_flow~PrecedingDocumentItem      = sto_ud~inbound_dn_item
                                      AND sd_doc_flow~SubsequentDocumentCategory = 'R'
      FIELDS  sd_doc_flow~PrecedingDocument,
              sd_doc_flow~PrecedingDocumentItem,
              sd_doc_flow~CreationDate           AS erdat,
              sd_doc_flow~CreationTime           AS erzet,
              dats_days_between( sd_doc_flow~CreationDate, @sy-datum ) AS days_from_entry
      INTO TABLE @DATA(lt_vbfa_inbound)
        .
      IF sy-subrc = 0.
        " Check the last GI date: select ERDAT from table VBFA. If there are multiple records sort to find the latest date ERDAT.
        SORT lt_vbfa_outbound  BY PrecedingDocument PrecedingDocumentItem erdat DESCENDING erzet DESCENDING.
        LOOP AT lt_ud_sto ASSIGNING <ls_ud_sto_result>.
          READ TABLE lt_vbfa_inbound ASSIGNING FIELD-SYMBOL(<ls_vbfa_inbound>) WITH KEY PrecedingDocument     = <ls_ud_sto_result>-inbound_dn
                                                                                        PrecedingDocumentItem = <ls_ud_sto_result>-inbound_dn_item.
          IF sy-subrc = 0.
            <ls_ud_sto_result>-latest_inbound_erdat  = <ls_vbfa_inbound>-erdat.
            <ls_ud_sto_result>-days_from_inbound_dn  = <ls_vbfa_inbound>-days_from_entry.
          ENDIF.
        ENDLOOP.
      ENDIF.

      " Determine the Under-Delivered STOs
      LOOP AT lt_ud_sto ASSIGNING FIELD-SYMBOL(<ls_ud_sto>).
        READ TABLE mt_hm_1081 ASSIGNING FIELD-SYMBOL(<ls_hm_1081>) WITH KEY field1 = <ls_ud_sto>-ekorg field2 = <ls_ud_sto>-bsart field3 = mv_sto_extra_indicator. "#EC CI_STDSEQ
        IF sy-subrc <> 0.
          CONTINUE.
        ENDIF.
        " If there is no GoodsReceipt yet
        IF <ls_ud_sto>-dabmg = 0 AND <ls_ud_sto>-days_from_outbound_dn < mv_days_from_gi.
          CONTINUE.
          " if there are already some GoodsReceipt
        ELSEIF <ls_ud_sto>-dabmg > 0 AND <ls_ud_sto>-days_from_inbound_dn < mv_days_from_gr.
          CONTINUE.
        ENDIF.


        DATA(lt_refused_hu) = vldt_and_get_hu_refusal_info_2( is_found_sto = CORRESPONDING #( <ls_ud_sto> )  ).

*      " Compare the Handling Unit Qty with status REFU with under delivered qty
        DATA(lv_total_refused_quantity) = REDUCE menge_d( INIT lv_total_refused_hu_quant = 0
                                                          FOR ls_line IN lt_refused_hu
                                                          NEXT lv_total_refused_hu_quant = lv_total_refused_hu_quant + ls_line-quantity
                                                     ).
        IF lv_total_refused_quantity = 0.
          CONTINUE.
        ELSEIF lv_total_refused_quantity <> <ls_ud_sto>-quant_short AND <ls_ud_sto>-quant_short > 0.
          " Under delivered quantity is not refused quantity at Handling Unit Level. STO cannot be created
          MESSAGE e014(zsd_sto_ud) INTO DATA(lv_symsg).
          DATA(lv_validation_error) = lv_symsg .
        ENDIF.


        lt_sto_ud_log = VALUE #( BASE lt_sto_ud_log (
                                  under_delivered_sto       = <ls_ud_sto>-ebeln
                                  under_delivered_sto_item  = <ls_ud_sto>-ebelp
                                  status                    = iv_status
                                  process                   = <ls_hm_1081>-field5
                                  under_delivered_dn        = <ls_ud_sto>-outbound_dn
                                  under_delivered_dn_item   = <ls_ud_sto>-outbound_dn_item
                                  virtual_gr_ibd            = <ls_ud_sto>-inbound_dn
                                  virtual_gr_ibd_item       = <ls_ud_sto>-inbound_dn_item
                                  quantity_short            = <ls_ud_sto>-quant_short
                                  waiting_days_from_gr      = mv_days_from_gr
                                  waiting_days_from_gi      = mv_days_from_gi
                                  created_on                = sy-datum
                                  error_msg_text            = lv_validation_error
                                   ) ) .

        CLEAR: lv_validation_error, lv_total_refused_quantity.
      ENDLOOP.
    ENDIF.

    mt_sto_ud_logs = lt_sto_ud_log.

    " Validate that the existing logs with higher status would not be overwritten
    IF lt_sto_ud_log IS NOT INITIAL.
      SELECT *                                "#EC CI_ALL_FIELDS_NEEDED
        FROM zsd_t_sto_ud_log
        FOR ALL ENTRIES IN @lt_sto_ud_log
        WHERE under_delivered_sto = @lt_sto_ud_log-under_delivered_sto
        AND under_delivered_sto_item = @lt_sto_ud_log-under_delivered_sto_item
        AND status <> @iv_status
        INTO TABLE @lt_sto_ud_log_to_exclude.             "#EC CI_SUBRC
    ENDIF.

    lts_sto_ud_log_to_update = FILTER #( lt_sto_ud_log EXCEPT IN lt_sto_ud_log_to_exclude USING KEY key_fields
                                         WHERE under_delivered_sto = under_delivered_sto AND
                                               under_delivered_sto_item = under_delivered_sto_item ).

    IF lts_sto_ud_log_to_update IS NOT INITIAL.
      update_ud_sto_log( lts_sto_ud_log_to_update ).
    ENDIF.
  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~create_virtual_sto.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.05.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA lt_return  TYPE STANDARD TABLE OF bapiret2.
    DATA ls_exp_po_header TYPE bapimepoheader.

    DATA ls_is_header TYPE mepoheader.
    DATA ls_is_header_x TYPE mepoheaderx.
    DATA lt_it_items  TYPE STANDARD TABLE OF mepoitem .
    DATA lt_it_items_x  TYPE STANDARD TABLE OF mepoitemx.

    "Select all entries in the STO UD Log table where given status and process
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status  iv_process = iv_process ).


    IF lts_sto_ud_logs IS NOT INITIAL.
      DATA(lt_pur_order_data) = get_purchase_orders_data( it_purchase_order_keys = VALUE #( FOR ls_line IN lts_sto_ud_logs
                                                                                          ( ebeln = ls_line-under_delivered_sto  ebelp = ls_line-under_delivered_sto_item ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).
        DATA(ls_pur_order_data) = VALUE #( lt_pur_order_data[ ebeln = <ls_sto_ud_log>-under_delivered_sto ebelp = <ls_sto_ud_log>-under_delivered_sto_item ] OPTIONAL ).

        DATA(ls_hm_rec_sloc) = VALUE #( mt_hm_1087[ field1 = mt_werks_r[ 1 ]-low ] OPTIONAL ).
        DATA(ls_hm_sen_sloc) = VALUE #( mt_hm_1087[ field1 = COND #( WHEN mt_werks_s IS NOT INITIAL THEN mt_werks_s[ 1 ]-low  ELSE ls_pur_order_data-reswk ) ] OPTIONAL ).


        " Fill PO Header
        ls_is_header-ihrez = <ls_sto_ud_log>-under_delivered_sto .
        ls_is_header-bukrs = ls_pur_order_data-bukrs.
        DATA(lv_is_intra_company) = is_intra_company_movement( iv_receiving_plant = ls_pur_order_data-werks
                                                               iv_supplying_plant = COND #( WHEN ls_pur_order_data-reswk IS NOT INITIAL THEN ls_pur_order_data-reswk ELSE ls_pur_order_data-lifnr ) ).

        ls_is_header-bsart = COND #( WHEN lv_is_intra_company = abap_true THEN mv_hm_685_sto_intra_zu65
                                                                          ELSE mv_hm_685_sto_inter_zu60  ).
        ls_is_header-ekorg = ls_pur_order_data-ekorg.
        ls_is_header-ekgrp = ls_pur_order_data-ekgrp.
        ls_is_header-reswk = ls_pur_order_data-reswk.
        ls_is_header-aedat = sy-datum.
        ls_is_header-lifnr = ls_pur_order_data-lifnr.
        ls_is_header-zz1_zsales_channel_pdh = ls_pur_order_data-zz1_zsales_channel_pdh.
        ls_is_header-zz1_zorderreason_pdh = zif_sd_sto_ud_const=>mc_order_reason-virtual.
        ls_is_header_x-ihrez = abap_true.
        ls_is_header_x-bukrs = abap_true.
        ls_is_header_x-bsart = abap_true.
        ls_is_header_x-ekorg = abap_true.
        ls_is_header_x-ekgrp = abap_true.
        ls_is_header_x-reswk = abap_true.
        ls_is_header_x-lifnr = abap_true.
        ls_is_header_x-aedat = abap_true.
        ls_is_header_x-zz1_zsales_channel_pdh = abap_true.
        ls_is_header_x-pincr = abap_true. " to prevent automatic item number assignment.
        ls_is_header_x-zz1_zorderreason_pdh = abap_true.

        " Fill PO Item
        lt_it_items = VALUE #( BASE lt_it_items
                              ( matnr     = ls_pur_order_data-matnr
                                ebelp     = <ls_sto_ud_log>-under_delivered_sto_item
                                werks     = ls_pur_order_data-werks
                                lgort    = ls_hm_rec_sloc-field2
                                reslo    = ls_hm_sen_sloc-field2
                                menge    = <ls_sto_ud_log>-quantity_short
                                meins    = ls_pur_order_data-meins
                                retpo    = abap_true
                                ) ).
        lt_it_items_x = VALUE #( BASE lt_it_items_x
                                ( ebelp_key = <ls_sto_ud_log>-under_delivered_sto_item
                                  matnr    = abap_true
                                  werks    = abap_true
                                  lgort    = abap_true
                                  reslo  = abap_true
                                  menge  = abap_true
                                  meins  = abap_true
                                  retpo  = abap_true
                                  ) ).
        CALL FUNCTION 'DMCC_MIG_PUR_PO_CREATE'
          EXPORTING
            is_header  = ls_is_header
            is_headerx = ls_is_header_x
          IMPORTING
            ev_header  = ls_exp_po_header
          TABLES
            it_items   = lt_it_items
            it_itemx   = lt_it_items_x
            et_return  = lt_return.

        IF line_exists( lt_return[ type = 'E' ] ).
          DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ).
          <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
          <ls_sto_ud_log>-error_msg_text = ls_return-message.
          CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        ELSE.
          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
          <ls_sto_ud_log>-status = iv_status.
          <ls_sto_ud_log>-virtual_return_sto = ls_exp_po_header-po_number .
          CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
        ENDIF.

        CLEAR: lt_it_items, lt_it_items_x, ls_is_header, ls_is_header_x.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~create_refusal_outbound_dlvry.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 20.03.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    " Within this step, the OBD automatically created in step 4 will need to be retrieved and stored in the log table.
    " If OBD is not found in the PO history for the virtual return STO created in previous, then the OBD has to be created.
    DATA lt_stock_trans_items  TYPE STANDARD TABLE OF bapidlvreftosto.
    DATA lv_obd_delivery TYPE vbeln_vl.
    DATA lt_return  TYPE STANDARD TABLE OF bapiret2.


    "Select all entries in the STO UD Log table where given status and process
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status  iv_process = iv_process ).


    IF lts_sto_ud_logs IS NOT INITIAL.

      " Find their DN number: select BELNR from table EKBE where EBELN = virtual return STO number from Z table and VGABE = 8
      DATA(lt_ekbe_for_sto_created) = get_ekbe_belnr_for_sto_created( it_purchase_order_keys = VALUE #( FOR ls_line IN lts_sto_ud_logs
                                                                                           ( ebeln = ls_line-virtual_return_sto ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).
        READ TABLE lt_ekbe_for_sto_created ASSIGNING FIELD-SYMBOL(<ls_ekbe_for_sto_created>) WITH KEY ebeln = <ls_sto_ud_log>-under_delivered_sto.
        IF sy-subrc = 0.
          <ls_sto_ud_log>-virtual_obd_created = <ls_ekbe_for_sto_created>-belnr.
          <ls_sto_ud_log>-status = iv_status.
          lv_obd_delivery = <ls_ekbe_for_sto_created>-belnr.
        ELSE.

          lt_stock_trans_items = VALUE #( BASE lt_stock_trans_items
                                          ( ref_doc  = <ls_sto_ud_log>-virtual_return_sto
                                            ref_item = <ls_sto_ud_log>-under_delivered_sto_item
                                            dlv_qty  = <ls_sto_ud_log>-quantity_short
                                            sales_unit = 'EA'
                                            sales_unit_iso = 'EA' )  ).

          CALL FUNCTION 'BAPI_OUTB_DELIVERY_CREATE_STO'
            EXPORTING
              ship_point        = mt_werks_r[ 1 ]-low
              due_date          = sy-datum
            IMPORTING
              delivery          = lv_obd_delivery
            TABLES
              stock_trans_items = lt_stock_trans_items
              return            = lt_return.
          IF line_exists( lt_return[ type = 'E' ] ).
            DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ). "#EC CI_STDSEQ
            <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
            CONTINUE.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            <ls_sto_ud_log>-virtual_obd_created = lv_obd_delivery.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
          ENDIF.
        ENDIF.

        CLEAR: lt_stock_trans_items, lv_obd_delivery.
      ENDLOOP.


      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~create_refusal_inbound_deliver.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 22.03.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.05.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*

    DATA lt_return          TYPE STANDARD TABLE OF bapiret2 .
    DATA lv_ef_inb_deliv_num       TYPE I_DeliveryDocument-DeliveryDocument.

    " Get the Under-delivered STOs info from the log table
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status  iv_process = iv_process ).
    IF lts_sto_ud_logs IS NOT INITIAL.

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).

        " Check Inbound Delivery is created automatically,
        SELECT SINGLE DeliveryDocument   AS vbeln    "#EC CI_SEL_NESTED
          FROM I_POSupplierConfirmationAPI01 AS sup_conf_ekes
          WHERE CAST( sup_conf_ekes~SupplierConfirmationExtNumber AS SSTRING ) = @<ls_sto_ud_log>-virtual_obd_created
          INTO @DATA(lv_refusal_ibd).
        IF sy-subrc = 0.
          <ls_sto_ud_log>-status = iv_status.
          <ls_sto_ud_log>-refusal_ibd = lv_refusal_ibd.
          CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
        ELSE.
          " Otherwise, Call the BAPI to create it
          CALL FUNCTION 'SHP_IBDLV_CREATE_FROM_OBDLV'
            EXPORTING
              if_outb_deliv_num = <ls_sto_ud_log>-virtual_obd_created
            IMPORTING
              ef_inb_deliv_num  = lv_ef_inb_deliv_num
            TABLES
              et_return         = lt_return.

          IF line_exists( lt_return[ type = 'E' ] ) .
            DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ). "#EC CI_STDSEQ
            <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            <ls_sto_ud_log>-refusal_ibd = lv_ef_inb_deliv_num.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
          ENDIF.
          CLEAR:  ls_return.

          CLEAR: lt_return.
        ENDIF.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 18.03.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*

    IF iv_is_search_by_ud_sto = abap_true.
      IF it_sto_ud IS INITIAL.
        " UnderDelivered STO parameter is required to be filled
        MESSAGE e009(zsd_sto_ud).
      ENDIF.
    ELSEIF iv_is_search_by_ud_sto = abap_false AND ( it_bsart IS INITIAL OR iv_ekorg IS INITIAL OR it_werks_r IS INITIAL ).
      " STO Type, Pur.Org and Rec.Plant parameters are required to be filled
      MESSAGE e010(zsd_sto_ud).
    ENDIF.

    mt_bsart        = it_bsart       .
    mv_ekorg        = iv_ekorg       .
    mt_ekgrp        = it_ekgrp       .
    mt_sto_ud       = it_sto_ud      .
    mt_ebeln        = it_ebeln       .
    mt_werks_r      = it_werks_r     .
    mt_lgort        = it_lgort       .
    mt_werks_s      = it_werks_s     .
    mt_bsgru        = it_bsgru       .
    mt_erdat        = it_erdat       .
    mt_ernam        = it_ernam       .
    mv_days_from_gr = iv_days_from_gr.
    mv_days_from_gi = iv_days_from_gi.
    mv_is_search_by_ud_sto = iv_is_search_by_ud_sto.
    mv_sto_extra_indicator = iv_sto_extra_indicator.

    TRY.
        mt_hm_1081      = zcl_pe_hm_basic=>get_table( '1081' ).
        mt_hm_1087      = zcl_pe_hm_basic=>get_table( '1087' ).
        mt_hm_876_bwart = zcl_pe_hm_basic=>get_table( '876' ).
        mv_hm_685_sto_inter_zu50 = zcl_pe_hm_basic=>get_value( iv_id = '685'  iv_sequence = '009' ).
        mv_hm_685_sto_intra_zu55 = zcl_pe_hm_basic=>get_value( iv_id = '685'  iv_sequence = '010' ).
        mv_hm_685_sto_intra_zu65 = zcl_pe_hm_basic=>get_value( iv_id = '685'  iv_sequence = '012' ).
        mv_hm_685_sto_inter_zu60 = zcl_pe_hm_basic=>get_value( iv_id = '685'  iv_sequence = '011' ).
        mv_hm_1042_lfart_zel     = zcl_pe_hm_basic=>get_value( iv_id = '1042' iv_sequence = '001' ).
        mv_hm_717_bwart_101      = zcl_pe_hm_basic=>get_value( iv_id = '717'  iv_sequence = '001' ).

      CATCH zcx_pe_hm INTO DATA(lo_exception).
        MESSAGE lo_exception.
    ENDTRY.

  ENDMETHOD.


  METHOD get_ekbe_belnr_for_sto_created.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
    IF it_purchase_order_keys IS NOT INITIAL.
      SELECT
        PurchaseOrder AS ebeln,
        PurchaseOrderItem AS ebelp,
        PurchasingHistoryDocument AS belnr,
        PurchasingHistoryDocumentType AS vgabe,
        DocumentReferenceID  AS xblnr
        FROM i_purchaseorderhistoryapi01
        FOR ALL ENTRIES IN @it_purchase_order_keys
        WHERE PurchaseOrder = @it_purchase_order_keys-ebeln
        AND   PurchasingHistoryDocumentType = '8' "vgabe
        INTO CORRESPONDING FIELDS OF TABLE @rt_result.
      IF sy-subrc = 0.
        RETURN.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD get_ekbe_pgi_for_refuse_return.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 18.08.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    IF it_purchase_order_keys IS NOT INITIAL.
      SELECT
        PurchaseOrder AS ebeln,
        PurchaseOrderItem AS ebelp,
        PurchasingHistoryDocument AS belnr,
        PurchasingHistoryDocumentType AS vgabe,
        DocumentReferenceID  AS xblnr
        FROM i_purchaseorderhistoryapi01
        FOR ALL ENTRIES IN @it_purchase_order_keys
        WHERE PurchaseOrder = @it_purchase_order_keys-ebeln
        AND DocumentReferenceID  = @it_purchase_order_keys-xblnr
        AND   PurchasingHistoryDocumentType = '1' "vgabe
        INTO CORRESPONDING FIELDS OF TABLE @rt_result.
      IF sy-subrc = 0.
        RETURN.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD update_ud_sto_log.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.06.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    MODIFY zsd_t_sto_ud_log FROM TABLE its_ud_sto_log.
    IF sy-subrc = 0 AND sy-dbcnt > 0.
      COMMIT WORK AND WAIT.
    ENDIF.

    LOOP AT its_ud_sto_log ASSIGNING FIELD-SYMBOL(<ls_ud_sto_log>) WHERE error_msg_text IS NOT INITIAL . "#EC CI_SORTSEQ
      DELETE  mt_sto_ud_logs WHERE under_delivered_sto = <ls_ud_sto_log>-under_delivered_sto
                               AND under_delivered_sto_item =  <ls_ud_sto_log>-under_delivered_sto_item.
      IF mt_sto_ud_logs IS INITIAL.
        RAISE EXCEPTION TYPE zcx_sd_sto_ud MESSAGE e007(zsd_sto_ud) WITH |{ <ls_ud_sto_log>-status(1) }{ <ls_ud_sto_log>-status+1(1) + 1 }0| <ls_ud_sto_log>-error_msg_text.
*        RAISE EXCEPTION TYPE zcx_sd_sto_ud MESSAGE e007(zsd_sto_ud) WITH <ls_ud_sto_log>-status <ls_ud_sto_log>-error_msg_text.
      ENDIF.
    ENDLOOP.


  ENDMETHOD.


  METHOD vldt_plant_sloc_exist_in_hm.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.06.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    " Check if the selected plant and storage location combinations are maintained in Hardcode Manager
    IF mt_werks_s IS NOT INITIAL.
      SELECT plant~Plant AS werks
         FROM i_plant AS plant
         WHERE plant~Plant IN @mt_werks_r
           OR  plant~Plant IN @mt_werks_s
         INTO TABLE @DATA(lt_werks).
    ELSE.
      SELECT plant~Plant AS werks
        FROM i_plant AS plant
        WHERE plant~Plant IN @mt_werks_r
        INTO TABLE @lt_werks.
    ENDIF.

    IF sy-subrc = 0.
      IF mt_lgort IS NOT INITIAL.
        SELECT sloc~StorageLocation AS lgort       "#EC CI_NO_TRANSFORM
          FROM i_storagelocation AS sloc                "#EC CI_GENBUFF
          FOR ALL ENTRIES IN @lt_werks
          WHERE sloc~Plant = @lt_werks-werks
          AND sloc~StorageLocation IN @mt_lgort
          INTO TABLE @DATA(lt_lgort).
        IF sy-subrc = 0.
          LOOP AT lt_lgort ASSIGNING FIELD-SYMBOL(<ls_lgort>).
            LOOP AT lt_werks ASSIGNING FIELD-SYMBOL(<ls_werks>). "#EC CI_NESTED
              READ TABLE mt_hm_1087 ASSIGNING FIELD-SYMBOL(<ls_hm_1087>) WITH KEY field1 = <ls_werks>-werks  field2 = <ls_lgort>-lgort.
              IF sy-subrc <> 0.
                RAISE EXCEPTION TYPE zcx_sd_sto_ud MESSAGE e001(zsd_sto_ud) WITH <ls_werks>-werks <ls_lgort>-lgort .
              ENDIF.
            ENDLOOP.
          ENDLOOP.
        ENDIF.
      ELSE.
        LOOP AT lt_werks ASSIGNING <ls_werks>.
          READ TABLE mt_hm_1087 ASSIGNING <ls_hm_1087> WITH KEY field1 = <ls_werks>-werks .
          IF sy-subrc <> 0.
            RAISE EXCEPTION TYPE zcx_sd_sto_ud MESSAGE e001(zsd_sto_ud) WITH <ls_werks>-werks.
          ENDIF.
        ENDLOOP.
      ENDIF.
    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~create_inbound_delivery.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.06.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA ls_inb_del_header TYPE bbp_inbd_l.
    DATA lt_inb_del_detail TYPE TABLE OF bbp_inbd_d.
    DATA lt_return         TYPE TABLE OF bapireturn.
    DATA lv_delivery       TYPE I_DeliveryDocument-DeliveryDocument.

    " Get the Under-delivered STOs info from the log table
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status  iv_process = iv_process ).
    IF lts_sto_ud_logs IS NOT INITIAL.

      vldt_plant_sloc_exist_in_hm( ).

      " Get corresponding materials from under-delivered STO and its item.
      DATA(lt_pur_orders_data) = get_purchase_orders_data( it_purchase_order_keys = VALUE #( FOR ls_sto_ud_log IN lts_sto_ud_logs
                                                                                           ( ebeln = ls_sto_ud_log-under_delivered_sto  ebelp = ls_sto_ud_log-under_delivered_sto_item  ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).

        " Inbound Delivery details parameters
        DATA(ls_pur_order_data) = VALUE #( lt_pur_orders_data[ ebeln = <ls_sto_ud_log>-under_delivered_sto ebelp = <ls_sto_ud_log>-under_delivered_sto_item ] OPTIONAL ).
        IF ls_pur_order_data IS INITIAL.
          <ls_sto_ud_log>-error_msg_text = mc_no_data_found_error.
          CONTINUE.
        ENDIF.

        IF mv_alt_bapi_ibd_create = abap_true.
          create_ibd_using_alt_bapi( EXPORTING is_sto_ud_log     = <ls_sto_ud_log>
                                               is_pur_order_data = ls_pur_order_data
                                     IMPORTING et_return         = lt_return
                                               ev_delivery_no    = lv_delivery ).

          IF line_exists( lt_return[ type = 'E' ] ) OR lv_delivery IS INITIAL .
            DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ). "#EC CI_STDSEQ
            <ls_sto_ud_log>-error_msg_no = ls_return-code.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            <ls_sto_ud_log>-virtual_gr_ibd = lv_delivery.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text, ls_return.
          ENDIF.
        ELSE.
          ls_inb_del_header-delivery  = <ls_sto_ud_log>-under_delivered_dn.
          ls_inb_del_header-deliv_ext = <ls_sto_ud_log>-under_delivered_dn.
          ls_inb_del_header-deliv_date = sy-datum.
          ls_inb_del_header-deliv_time = sy-uzeit.


          lt_inb_del_detail = VALUE #( BASE lt_inb_del_detail
                                       ( delivery   = <ls_sto_ud_log>-under_delivered_dn
                                         deliv_item = <ls_sto_ud_log>-under_delivered_dn
                                         po_number  = <ls_sto_ud_log>-under_delivered_sto
                                         po_item    = <ls_sto_ud_log>-under_delivered_sto_item
                                         material   = ls_pur_order_data-matnr
                                         deliv_qty  = <ls_sto_ud_log>-quantity_short
                                         unit       = ls_pur_order_data-meins
                                          ) ).
          " Create Inbound delivery
          CALL FUNCTION 'BBP_INB_DELIVERY_CREATE'
            EXPORTING
              is_inb_delivery_header = ls_inb_del_header
            IMPORTING
              ef_delivery            = lv_delivery
            TABLES
              it_inb_delivery_detail = lt_inb_del_detail
              return                 = lt_return.
          IF line_exists( lt_return[ type = 'E' ] ) .
            ls_return = VALUE #( lt_return[ type = 'E' ] OPTIONAL ). "#EC CI_STDSEQ
            <ls_sto_ud_log>-error_msg_no = ls_return-code.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            <ls_sto_ud_log>-virtual_gr_ibd = lv_delivery.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
          ENDIF.
          CLEAR: ls_inb_del_header, lt_inb_del_detail, ls_return.
        ENDIF.

        CLEAR: lt_return.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~create_outbound_delivery.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 20.03.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.08.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    " Within this step, the OBD automatically created in step 4 will need to be retrieved and stored in the log table.
    " If OBD is not found in the PO history for the virtual return STO created in previous, then the OBD has to be created.
    DATA lt_stock_trans_items  TYPE STANDARD TABLE OF bapidlvreftosto.
    DATA lv_obd_delivery TYPE vbeln_vl.
    DATA lt_return  TYPE STANDARD TABLE OF bapiret2.

    "Select all entries in the STO UD Log table where given status and process
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status  iv_process = iv_process ).


    IF lts_sto_ud_logs IS NOT INITIAL.

      " Find their DN number: select BELNR from table EKBE where EBELN = virtual return STO number from Z table and VGABE = 8
      DATA(lt_ekbe_for_sto_created) = get_ekbe_belnr_for_sto_created( it_purchase_order_keys = VALUE #( FOR ls_line IN lts_sto_ud_logs
                                                                                           ( ebeln = ls_line-virtual_return_sto ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).
        READ TABLE lt_ekbe_for_sto_created ASSIGNING FIELD-SYMBOL(<ls_ekbe_for_sto_created>) WITH KEY ebeln = <ls_sto_ud_log>-virtual_return_sto.
        IF sy-subrc = 0.
          <ls_sto_ud_log>-virtual_obd_created = <ls_ekbe_for_sto_created>-belnr.
          <ls_sto_ud_log>-status = iv_status.
        ELSE.

          lt_stock_trans_items = VALUE #( BASE lt_stock_trans_items
                                          ( ref_doc = <ls_sto_ud_log>-virtual_return_sto
                                            ref_item = <ls_sto_ud_log>-under_delivered_sto_item
                                             )  ).

          CALL FUNCTION 'BAPI_OUTB_DELIVERY_CREATE_STO'
            IMPORTING
              delivery          = lv_obd_delivery
            TABLES
              stock_trans_items = lt_stock_trans_items
              return            = lt_return.
          IF line_exists( lt_return[ type = 'E' ] ).
            DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ). "#EC CI_STDSEQ
            <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            <ls_sto_ud_log>-virtual_obd_created = lv_obd_delivery.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
          ENDIF.
        ENDIF.

        CLEAR: lt_stock_trans_items.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~down_adjustment_receiving_plnt.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 22.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.08.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA ls_goodsmvt_header TYPE bapi2017_gm_head_01 .
    DATA ls_goodsmvt_code TYPE bapi2017_gm_code .
    DATA lt_goodsmvt_item TYPE STANDARD TABLE OF bapi2017_gm_item_create.
    DATA lt_return TYPE STANDARD TABLE OF bapiret2.
    DATA ls_goodsmvt_headret TYPE bapi2017_gm_head_ret.

    " Get the Under-delivered STOs info from the log table
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status   iv_process = iv_process ).
    IF lts_sto_ud_logs IS NOT INITIAL.
      DATA(ls_hm_bwart_702) = VALUE #( mt_hm_876_bwart[ field1 = '702' ] OPTIONAL ). "#EC CI_STDSEQ
      DATA(lt_matdoc_pur_hist_data) = get_matdoc_pur_hist_data( it_matdoc = VALUE #( FOR ls_sto_ud_log IN lts_sto_ud_logs
                                                                                    ( mblnr = ls_sto_ud_log-virtual_gr_matdoc
                                                                                      mjahr = ls_sto_ud_log-virtual_gr_matdoc_year ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).

        DATA(ls_matdoc_pur_hist_data) =  VALUE #( lt_matdoc_pur_hist_data[ mblnr = <ls_sto_ud_log>-virtual_gr_matdoc   mjahr = <ls_sto_ud_log>-virtual_gr_matdoc_year ] OPTIONAL ).
        IF ls_matdoc_pur_hist_data IS INITIAL.
          <ls_sto_ud_log>-error_msg_text = mc_no_data_found_error.
          CONTINUE.
        ENDIF.

        ls_goodsmvt_code = zif_sd_sto_ud_const=>mc_goodsmvt_code-code_06.
        ls_goodsmvt_header-doc_date     = sy-datum.
        ls_goodsmvt_header-pstng_date   = sy-datum.
        ls_goodsmvt_header-pr_uname     = sy-uname.
        ls_goodsmvt_header-ref_doc_no   = <ls_sto_ud_log>-virtual_gr_matdoc.

        lt_goodsmvt_item = VALUE #( BASE lt_goodsmvt_item
                                    ( po_number            = <ls_sto_ud_log>-virtual_return_sto
                                      po_item              = <ls_sto_ud_log>-under_delivered_sto_item
                                      move_type            = ls_hm_bwart_702-field1
                                      move_reas            = zif_sd_sto_ud_const=>mc_move_reason-reason_1003
                                      ref_doc              = <ls_sto_ud_log>-virtual_gr_matdoc
                                      plant                = ls_matdoc_pur_hist_data-werks
                                      entry_qnt            = ls_matdoc_pur_hist_data-menge
                                      entry_uom            = ls_matdoc_pur_hist_data-meins
                                      material             = ls_matdoc_pur_hist_data-matnr
                                      stge_loc             = ls_matdoc_pur_hist_data-lgort
                                      no_more_gr           = abap_true "
                                       ) ) .


        " Post the down adjustment in sending plant
        CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
          EXPORTING
            goodsmvt_header  = ls_goodsmvt_header
            goodsmvt_code    = ls_goodsmvt_code
          IMPORTING
            goodsmvt_headret = ls_goodsmvt_headret
          TABLES
            goodsmvt_item    = lt_goodsmvt_item
            return           = lt_return.
        IF line_exists( lt_return[ type = 'E' ] ).
          DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ).
          <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
          <ls_sto_ud_log>-error_msg_text = ls_return-message.
          CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        ELSE.
          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
          <ls_sto_ud_log>-status = iv_status.
          <ls_sto_ud_log>-down_adjustment_matdoc = ls_goodsmvt_headret-mat_doc.
          <ls_sto_ud_log>-down_adjustment_matdoc_year = ls_goodsmvt_headret-doc_year.
          CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
        ENDIF.
        CLEAR: lt_goodsmvt_item.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~down_adjustment_sending_plant.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 06.08.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA ls_goodsmvt_header TYPE bapi2017_gm_head_01 .
    DATA ls_goodsmvt_code TYPE bapi2017_gm_code .
    DATA lt_goodsmvt_item TYPE STANDARD TABLE OF bapi2017_gm_item_create.
    DATA lt_return TYPE STANDARD TABLE OF bapiret2.
    DATA ls_goodsmvt_headret TYPE bapi2017_gm_head_ret.

    " Get the Under-delivered STOs info from the log table
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status iv_process = iv_process ).
    IF lts_sto_ud_logs IS NOT INITIAL.
      DATA(ls_hm_bwart_702) = VALUE #( mt_hm_876_bwart[ field1 = '702' ] OPTIONAL ).
      DATA(lt_matdoc_pur_hist_data) = get_matdoc_pur_hist_data( it_matdoc = VALUE #( FOR ls_sto_ud_log IN lts_sto_ud_logs
                                                                                    ( mblnr = ls_sto_ud_log-virt_return_sto_gi_matdoc
                                                                                      mjahr = ls_sto_ud_log-virt_return_sto_gi_matdoc_year ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).

        DATA(ls_matdoc_pur_hist_data) =  VALUE #( lt_matdoc_pur_hist_data[ mblnr = <ls_sto_ud_log>-virt_return_sto_gi_matdoc   mjahr = <ls_sto_ud_log>-virt_return_sto_gi_matdoc_year ] OPTIONAL ).

        ls_goodsmvt_code = zif_sd_sto_ud_const=>mc_goodsmvt_code-code_06.
        ls_goodsmvt_header-doc_date     = sy-datum.
        ls_goodsmvt_header-pstng_date   = sy-datum.
        ls_goodsmvt_header-pr_uname     = sy-uname.
        ls_goodsmvt_header-ref_doc_no   = <ls_sto_ud_log>-virt_return_sto_gi_matdoc.

        lt_goodsmvt_item = VALUE #( BASE lt_goodsmvt_item
                                    ( po_number            = <ls_sto_ud_log>-virtual_return_sto
                                      po_item              = <ls_sto_ud_log>-under_delivered_sto_item
                                      move_type            = ls_hm_bwart_702-field1
                                      move_reas            = zif_sd_sto_ud_const=>mc_move_reason-reason_1002
                                      ref_doc              = <ls_sto_ud_log>-virt_return_sto_gi_matdoc
                                      plant                = ls_matdoc_pur_hist_data-werks
                                      entry_qnt            = ls_matdoc_pur_hist_data-menge
                                      entry_uom            = ls_matdoc_pur_hist_data-meins
                                      material             = ls_matdoc_pur_hist_data-matnr
                                      stge_loc             = ls_matdoc_pur_hist_data-lgort
                                      no_more_gr           = abap_true "
                                       ) ) .


        " Post the down adjustment in sending plant
        CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
          EXPORTING
            goodsmvt_header  = ls_goodsmvt_header
            goodsmvt_code    = ls_goodsmvt_code
          IMPORTING
            goodsmvt_headret = ls_goodsmvt_headret
          TABLES
            goodsmvt_item    = lt_goodsmvt_item
            return           = lt_return.
        IF line_exists( lt_return[ type = 'E' ] ).
          DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ).
          <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
          <ls_sto_ud_log>-error_msg_text = ls_return-message.
          CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        ELSE.
          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
          <ls_sto_ud_log>-status = iv_status.
          <ls_sto_ud_log>-down_adjustment_matdoc = ls_goodsmvt_headret-mat_doc.
          <ls_sto_ud_log>-down_adjustment_matdoc_year = ls_goodsmvt_headret-doc_year.
          CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
        ENDIF.
        CLEAR: lt_goodsmvt_item.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.
  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~find_under_delivery_sto_new.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA lt_sto_ud_log TYPE STANDARD TABLE OF zsd_t_sto_ud_log WITH NON-UNIQUE SORTED KEY
         key_fields COMPONENTS  under_delivered_sto under_delivered_sto_item under_delivered_dn under_delivered_dn_item.
    DATA lt_sto_ud_log_to_exclude TYPE STANDARD TABLE OF zsd_t_sto_ud_log WITH NON-UNIQUE SORTED KEY
         key_fields COMPONENTS  under_delivered_sto under_delivered_sto_item under_delivered_dn under_delivered_dn_item.
    DATA lts_sto_ud_log_to_update TYPE mtyp_t_sto_ud_log.


    SELECT
    FROM I_POSupplierConfirmationAPI01 AS supplier_confirmation
    INNER JOIN ekes AS ekes_tab  ON ekes_tab~ebeln = supplier_confirmation~PurchaseOrder
                                AND ekes_tab~ebelp = supplier_confirmation~PurchaseOrderItem
                                AND ekes_tab~etens = supplier_confirmation~SequentialNmbrOfSuplrConf
    FIELDS DISTINCT
      supplier_confirmation~PurchaseOrder                                                  AS ebeln,
      supplier_confirmation~PurchaseOrderItem                                              AS ebelp,
      supplier_confirmation~CreationDate                                                   AS erdat,
      supplier_confirmation~MRPRelevantQuantity                                            AS dabmg,
      supplier_confirmation~ConfirmedQuantity                                              AS menge,
      supplier_confirmation~ConfirmedQuantity - supplier_confirmation~MRPRelevantQuantity  AS quant_short,
      supplier_confirmation~DeliveryDocument                                               AS inbound_dn,
      supplier_confirmation~DeliveryDocumentItem                                           AS inbound_dn_item,
      ekes_tab~vbeln_st                                                                    AS outbound_dn,
      ekes_tab~vbelp_st                                                                    AS outbound_dn_item,
      CAST( '00000000' AS DATS )                                                           AS latest_outbound_erdat,
      CAST( '00000000' AS DATS )                                                           AS latest_inbound_erdat,
      CAST( 0 AS INT2 )                                                                    AS days_from_outbound_dn,
      CAST( 0 AS INT2 )                                                                    AS days_from_inbound_dn,
      \_PurchaseOrder-PurchaseOrderType                                                    AS bsart,
      \_PurchaseOrder-PurchasingOrganization                                               AS ekorg,
      \_PurchaseOrderItem-IsCompletelyDelivered                                            AS elikz,
      supplier_confirmation~SupplierConfirmationCategory                                   AS ebtyp
    WHERE \_PurchaseOrder-PurchaseOrderType                            IN @mt_bsart
      AND \_PurchaseOrder-PurchasingOrganization                       = @mv_ekorg
      AND \_PurchaseOrder-PurchasingGroup                              IN @mt_ekgrp
      AND \_PurchaseOrder-PurchaseOrder                                IN @mt_ebeln
      AND \_PurchaseOrderItem-Plant                                    IN @mt_werks_r
      AND \_PurchaseOrderItem-StorageLocation                          IN @mt_lgort
      AND \_PurchaseOrder-SupplyingPlant                               IN @mt_werks_s
      AND \_PurchaseOrderItem-PurchasingOrderReason                    IN @mt_bsgru
      AND \_PurchaseOrder-CreationDate                                 IN @mt_erdat
      AND \_PurchaseOrder-CreatedByUser                                IN @mt_ernam
      AND supplier_confirmation~SupplierConfirmationCategory           = 'LA'
      AND \_PurchaseOrder-PurchasingDocumentDeletionCode               = @space
      AND \_PurchaseOrderItem-PurchasingDocumentDeletionCode           = @space
      AND supplier_confirmation~IsDeleted                              = @space
      ORDER BY  supplier_confirmation~PurchaseOrder, supplier_confirmation~PurchaseOrderItem, supplier_confirmation~CreationDate
    INTO TABLE @DATA(lt_ud_sto).
    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE zcx_sd_sto_ud MESSAGE e003(zsd_sto_ud).
    ELSE.
      SELECT
      FROM I_SDDocumentMultiLevelProcFlow WITH PRIVILEGED ACCESS AS sd_doc_flow
      INNER JOIN @lt_ud_sto AS sto_ud  ON sd_doc_flow~PrecedingDocument          = sto_ud~outbound_dn
                                      AND sd_doc_flow~PrecedingDocumentItem      = sto_ud~outbound_dn_item
                                      AND sd_doc_flow~SubsequentDocumentCategory = 'R'
      FIELDS  sd_doc_flow~PrecedingDocument,
              sd_doc_flow~PrecedingDocumentItem,
              sd_doc_flow~CreationDate           AS erdat,
              sd_doc_flow~CreationTime           AS erzet,
              dats_days_between( sd_doc_flow~CreationDate, @sy-datum ) AS days_from_entry
      INTO TABLE @DATA(lt_outbound_deliveries)
        .
      IF sy-subrc = 0.
        " Check the last GI date: select ERDAT from table VBFA. If there are multiple records sort to find the latest date ERDAT.
        SORT lt_outbound_deliveries  BY PrecedingDocument PrecedingDocumentItem erdat DESCENDING erzet DESCENDING.
        LOOP AT lt_ud_sto ASSIGNING FIELD-SYMBOL(<ls_ud_sto_result>).
          READ TABLE lt_outbound_deliveries ASSIGNING FIELD-SYMBOL(<ls_vbfa_outbound>) WITH KEY PrecedingDocument     = <ls_ud_sto_result>-outbound_dn
                                                                                          PrecedingDocumentItem = <ls_ud_sto_result>-outbound_dn_item.
          IF sy-subrc = 0.
            <ls_ud_sto_result>-latest_outbound_erdat  = <ls_vbfa_outbound>-erdat.
            <ls_ud_sto_result>-days_from_outbound_dn  = <ls_vbfa_outbound>-days_from_entry.
          ENDIF.
        ENDLOOP.
      ENDIF.

      SELECT
      FROM I_SDDocumentMultiLevelProcFlow WITH PRIVILEGED ACCESS AS sd_doc_flow
      INNER JOIN @lt_ud_sto AS sto_ud  ON sd_doc_flow~PrecedingDocument          = sto_ud~inbound_dn
                                      AND sd_doc_flow~PrecedingDocumentItem      = sto_ud~inbound_dn_item
                                      AND sd_doc_flow~SubsequentDocumentCategory = 'R'
      FIELDS  sd_doc_flow~PrecedingDocument,
              sd_doc_flow~PrecedingDocumentItem,
              sd_doc_flow~CreationDate           AS erdat,
              sd_doc_flow~CreationTime           AS erzet,
              dats_days_between( sd_doc_flow~CreationDate, @sy-datum ) AS days_from_entry
      INTO TABLE @DATA(lt_vbfa_inbound)
        .
      IF sy-subrc = 0.
        " Check the last GI date: select ERDAT from table VBFA. If there are multiple records sort to find the latest date ERDAT.
        SORT lt_outbound_deliveries  BY PrecedingDocument PrecedingDocumentItem erdat DESCENDING erzet DESCENDING.
        LOOP AT lt_ud_sto ASSIGNING <ls_ud_sto_result>.
          READ TABLE lt_vbfa_inbound ASSIGNING FIELD-SYMBOL(<ls_vbfa_inbound>) WITH KEY PrecedingDocument     = <ls_ud_sto_result>-inbound_dn
                                                                                        PrecedingDocumentItem = <ls_ud_sto_result>-inbound_dn_item.
          IF sy-subrc = 0.
            <ls_ud_sto_result>-latest_inbound_erdat  = <ls_vbfa_inbound>-erdat.
            <ls_ud_sto_result>-days_from_inbound_dn  = <ls_vbfa_inbound>-days_from_entry.
          ENDIF.
        ENDLOOP.
      ENDIF.

      " Determine the Under-Delivered STOs
      LOOP AT lt_ud_sto ASSIGNING FIELD-SYMBOL(<ls_ud_sto>).
        READ TABLE mt_hm_1081 ASSIGNING FIELD-SYMBOL(<ls_hm_1081>) WITH KEY field1 = <ls_ud_sto>-ekorg field2 = <ls_ud_sto>-bsart field3 = mv_sto_extra_indicator. "#EC CI_STDSEQ
        IF sy-subrc <> 0.
          CONTINUE.
        ENDIF.
        " If there is no GoodsReceipt yet
        IF <ls_ud_sto>-dabmg = 0 AND <ls_ud_sto>-days_from_outbound_dn < mv_days_from_gi.
          CONTINUE.
          " if there are already some GoodsReceipt
        ELSEIF <ls_ud_sto>-dabmg > 0 AND <ls_ud_sto>-days_from_inbound_dn < mv_days_from_gr.
          CONTINUE.
        ENDIF.

        " Don't allow to consider already refused STOs for underdelivery handling
        DATA(lt_refused_hu) = vldt_and_get_hu_refusal_info_2( is_found_sto = CORRESPONDING #( <ls_ud_sto> )  ).

        IF lt_refused_hu IS NOT INITIAL.
          RAISE EXCEPTION TYPE zcx_sd_sto_ud MESSAGE e003(zsd_sto_ud).
        ENDIF.

        lt_sto_ud_log = VALUE #( BASE lt_sto_ud_log (
                                  under_delivered_sto       = <ls_ud_sto>-ebeln
                                  under_delivered_sto_item  = <ls_ud_sto>-ebelp
                                  status                    = iv_status
                                  process                   = <ls_hm_1081>-field5
                                  under_delivered_dn        = <ls_ud_sto>-outbound_dn
                                  under_delivered_dn_item   = <ls_ud_sto>-outbound_dn_item
                                  virtual_gr_ibd            = <ls_ud_sto>-inbound_dn
                                  virtual_gr_ibd_item       = <ls_ud_sto>-inbound_dn_item
                                  quantity_short            = <ls_ud_sto>-quant_short
                                  waiting_days_from_gr      = mv_days_from_gr
                                  waiting_days_from_gi      = mv_days_from_gi
                                  created_on                = sy-datum ) ) .

      ENDLOOP.
    ENDIF.

    mt_sto_ud_logs = lt_sto_ud_log.

    " Validate that the existing logs with higher status would not be overwritten
    IF lt_sto_ud_log IS NOT INITIAL.
      SELECT *                                "#EC CI_ALL_FIELDS_NEEDED
        FROM zsd_t_sto_ud_log
        FOR ALL ENTRIES IN @lt_sto_ud_log
        WHERE under_delivered_sto = @lt_sto_ud_log-under_delivered_sto
        AND under_delivered_sto_item = @lt_sto_ud_log-under_delivered_sto_item
        AND status <> @iv_status
        INTO TABLE @lt_sto_ud_log_to_exclude.             "#EC CI_SUBRC
    ENDIF.

    lts_sto_ud_log_to_update = FILTER #( lt_sto_ud_log EXCEPT IN lt_sto_ud_log_to_exclude USING KEY key_fields
                                         WHERE under_delivered_sto = under_delivered_sto AND
                                               under_delivered_sto_item = under_delivered_sto_item ).

    IF lts_sto_ud_log_to_update IS NOT INITIAL.
      update_ud_sto_log( lts_sto_ud_log_to_update ).
    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~post_goods_issue.

************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA ls_vbkok_wa TYPE vbkok.
    DATA lt_vbpok_tab  TYPE STANDARD TABLE OF vbpok.
    DATA lt_prot   TYPE STANDARD TABLE OF prott.
    DATA ls_return TYPE bapireturn.


    "Select all entries in the STO UD Log table with given status and process.
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status   iv_process = iv_process ).
    IF lts_sto_ud_logs IS NOT INITIAL.
      " Find their DN number: select BELNR from table EKBE where EBELN = virtual return STO number from Z table and VGABE = 8
      DATA(lt_ekbe_pgi) = get_ekbe_belnr_for_sto_created( it_purchase_order_keys = VALUE #( FOR ls_line IN lts_sto_ud_logs
                                                                                           ( ebeln = ls_line-virtual_return_sto ) ) ).


      DATA(lt_deliveries_data) = get_deliveries_data( it_delivery_keys = VALUE #( FOR ls_ekbe_pgi_line IN lt_ekbe_pgi
                                                                                ( vbeln = ls_ekbe_pgi_line-belnr ) ) ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).

        " Here check if it was automatically created already...
        " Check Inbound Delivery is created automatically,
        SELECT SINGLE DeliveryDocument   AS vbeln    "#EC CI_SEL_NESTED
          FROM I_POSupplierConfirmationAPI01 AS sup_conf_ekes
          WHERE CAST( sup_conf_ekes~SupplierConfirmationExtNumber AS SSTRING ) = @<ls_sto_ud_log>-virtual_obd_created
          INTO @DATA(lv_refusal_ibd).
        IF sy-subrc = 0 AND lv_refusal_ibd IS NOT INITIAL.
          <ls_sto_ud_log>-status = iv_status.
          SELECT MaterialDocument AS mblnr, MaterialDocumentYear AS mjahr "#EC CI_SEL_NESTED
          FROM I_MaterialDocumentHeader_2
          WHERE DeliveryDocument = @ls_vbkok_wa-vbeln_vl " le_vbeln
          ORDER BY mblnr DESCENDING
          INTO TABLE @DATA(lt_ekko_1).
          IF sy-subrc = 0.
            <ls_sto_ud_log>-virt_return_sto_gi_matdoc = lt_ekko_1[ 1 ]-mblnr .
            <ls_sto_ud_log>-virt_return_sto_gi_matdoc_year = lt_ekko_1[ 1 ]-mjahr.
          ENDIF.
        ELSE.


          DATA(ls_ekbe_pgi) = VALUE #( lt_ekbe_pgi[ ebeln = <ls_sto_ud_log>-virtual_return_sto ] OPTIONAL ).
          DATA(ls_lips) = VALUE #( lt_deliveries_data[ vbeln = ls_ekbe_pgi-belnr ] OPTIONAL ).

          ls_vbkok_wa-vbeln_vl = |{ ls_lips-vbeln ALPHA = IN }|.
          ls_vbkok_wa-vbeln    = |{ ls_lips-vbeln ALPHA = IN }|.
          ls_vbkok_wa-wabuc = abap_true.

          CALL FUNCTION 'WS_DELIVERY_UPDATE_2'
            EXPORTING
              vbkok_wa               = ls_vbkok_wa
              synchron               = abap_true
              delivery               = ls_vbkok_wa-vbeln_vl
              if_error_messages_send = ' '
            TABLES
              vbpok_tab              = lt_vbpok_tab
              prot                   = lt_prot.

          IF line_exists( lt_prot[ msgty = 'E' ] ).
            DATA(ls_prot) = VALUE #( lt_prot[ msgty = 'E' ] OPTIONAL ).
            CALL FUNCTION 'BALW_BAPIRETURN_GET'
              EXPORTING
                type       = ls_prot-msgty
                cl         = ls_prot-msgid
                number     = CONV symsgno( ls_prot-msgno )
                par1       = ls_prot-msgv1
                par2       = ls_prot-msgv2
                par3       = ls_prot-msgv3
                par4       = ls_prot-msgv4
              IMPORTING
                bapireturn = ls_return.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            <ls_sto_ud_log>-error_msg_no   = ls_return-code.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            SELECT MaterialDocument AS mblnr, MaterialDocumentYear AS mjahr "#EC CI_SEL_NESTED
            FROM I_MaterialDocumentHeader_2
            WHERE DeliveryDocument = @ls_vbkok_wa-vbeln_vl
            ORDER BY mblnr DESCENDING
            INTO TABLE @DATA(lt_ekko).
            IF sy-subrc = 0.
              <ls_sto_ud_log>-virt_return_sto_gi_matdoc = lt_ekko[ 1 ]-mblnr .
              <ls_sto_ud_log>-virt_return_sto_gi_matdoc_year = lt_ekko[ 1 ]-mjahr.
            ENDIF.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
          ENDIF.
          CLEAR: ls_vbkok_wa.
        ENDIF.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~post_goods_issue_refusal.

************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA ls_vbkok_wa TYPE vbkok.
    DATA lt_vbpok_tab  TYPE STANDARD TABLE OF vbpok.
    DATA lt_prot   TYPE STANDARD TABLE OF prott.
    DATA ls_return TYPE bapireturn.

    "Select all entries in the STO UD Log table with given status and process.
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status   iv_process = iv_process ).
    IF lts_sto_ud_logs IS NOT INITIAL.
      " Find their DN number: select BELNR from table EKBE where EBELN = virtual return STO number from Z table and VGABE = 1
      DATA(lt_ekbe_pgi) = get_ekbe_pgi_for_refuse_return( it_purchase_order_keys = VALUE #( FOR ls_line IN lts_sto_ud_logs
                                                                                           ( ebeln = ls_line-virtual_return_sto
                                                                                             xblnr = ls_line-virtual_obd_created ) ) ).


      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).

        " Here check if it was automatically created already...
        " Check Inbound Delivery is created automatically,
        READ TABLE lt_ekbe_pgi ASSIGNING FIELD-SYMBOL(<ls_ekbe_pgi>) WITH KEY ebeln = <ls_sto_ud_log>-virtual_return_sto.
        IF sy-subrc = 0 AND <ls_ekbe_pgi>-belnr IS NOT INITIAL.

          <ls_sto_ud_log>-status = iv_status.
          <ls_sto_ud_log>-virt_return_sto_gi_matdoc = <ls_ekbe_pgi>-belnr.
          <ls_sto_ud_log>-virt_return_sto_gi_matdoc_year = sy-datum(4).
          CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
        ELSE.



          ls_vbkok_wa-vbeln_vl = |{ <ls_sto_ud_log>-virtual_obd_created ALPHA = IN }|.
          ls_vbkok_wa-vbeln    = |{ <ls_sto_ud_log>-virtual_obd_created ALPHA = IN }|.
          ls_vbkok_wa-wabuc = abap_true.

          CALL FUNCTION 'WS_DELIVERY_UPDATE_2'
            EXPORTING
              vbkok_wa               = ls_vbkok_wa
              synchron               = abap_true
              delivery               = ls_vbkok_wa-vbeln_vl
              if_error_messages_send = ' '
            TABLES
              vbpok_tab              = lt_vbpok_tab
              prot                   = lt_prot.

          IF line_exists( lt_prot[ msgty = 'E' ] ).
            DATA(ls_prot) = VALUE #( lt_prot[ msgty = 'E' ] OPTIONAL ).
            CALL FUNCTION 'BALW_BAPIRETURN_GET'
              EXPORTING
                type       = ls_prot-msgty
                cl         = ls_prot-msgid
                number     = CONV symsgno( ls_prot-msgno )
                par1       = ls_prot-msgv1
                par2       = ls_prot-msgv2
                par3       = ls_prot-msgv3
                par4       = ls_prot-msgv4
              IMPORTING
                bapireturn = ls_return.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            <ls_sto_ud_log>-error_msg_no   = ls_return-code.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            SELECT MaterialDocument AS mblnr, MaterialDocumentYear AS mjahr "#EC CI_SEL_NESTED
            FROM I_MaterialDocumentHeader_2
            WHERE DeliveryDocument = @ls_vbkok_wa-vbeln_vl
            ORDER BY mblnr DESCENDING
            INTO TABLE @DATA(lt_ekko).
            IF sy-subrc = 0.
              <ls_sto_ud_log>-virt_return_sto_gi_matdoc = lt_ekko[ 1 ]-mblnr .
              <ls_sto_ud_log>-virt_return_sto_gi_matdoc_year = lt_ekko[ 1 ]-mjahr.
            ENDIF.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
          ENDIF.
          CLEAR: ls_vbkok_wa.
        ENDIF.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~post_gr_for_virtual_return_sto.

************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA ls_goodsmvt_header TYPE bapi2017_gm_head_01 .
    DATA ls_goodsmvt_code   TYPE bapi2017_gm_code .
    DATA lt_goodsmvt_item   TYPE STANDARD TABLE OF bapi2017_gm_item_create.
    DATA lt_return          TYPE STANDARD TABLE OF bapiret2.
    DATA ls_goodsmvt_headret TYPE bapi2017_gm_head_ret.

    "Select all entries in the STO UD Log table with given status and process.
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status  = iv_prev_status   iv_process = iv_process  ).

    IF lts_sto_ud_logs IS NOT INITIAL.
      DATA(lt_po_items) = get_purchase_orders_data( it_purchase_order_keys = VALUE #( FOR ls_line IN lts_sto_ud_logs
                                                                                    ( ebeln = ls_line-virtual_return_sto ) ) ).

      DATA(ls_hm_bwart_161) = VALUE #( mt_hm_876_bwart[ field1 = '161' ] OPTIONAL ).


      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).

        ls_goodsmvt_code = zif_sd_sto_ud_const=>mc_goodsmvt_code-code_01.
        ls_goodsmvt_header-doc_date = sy-datum.
        ls_goodsmvt_header-pstng_date = sy-datum.
        ls_goodsmvt_header-pr_uname = sy-uname.

        LOOP AT lt_po_items ASSIGNING FIELD-SYMBOL(<ls_return_sto_item>) WHERE ebeln = <ls_sto_ud_log>-virtual_return_sto. "#EC CI_STDSEQ  "#EC CI_NESTED
          lt_goodsmvt_item = VALUE #( BASE lt_goodsmvt_item
                                      ( po_number            = <ls_return_sto_item>-ebeln
                                        po_item              = <ls_return_sto_item>-ebelp
                                        move_type            =  ls_hm_bwart_161-field1 " 161
                                        mvt_ind              = zif_sd_sto_ud_const=>mc_movement_indicator-purchase_order "'B'
                                        entry_qnt            = <ls_sto_ud_log>-quantity_short
                                        entry_uom            = <ls_return_sto_item>-meins
                                        entry_uom_iso        = <ls_sto_ud_log>-quantity_short
                                        plant                = <ls_return_sto_item>-werks
                                        material             = <ls_return_sto_item>-matnr
                                        stge_loc             = <ls_return_sto_item>-lgort
                                         ) ) .

        ENDLOOP.

        " post the GR for the Virtual return STO
        CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
          EXPORTING
            goodsmvt_header  = ls_goodsmvt_header
            goodsmvt_code    = ls_goodsmvt_code
          IMPORTING
            goodsmvt_headret = ls_goodsmvt_headret
          TABLES
            goodsmvt_item    = lt_goodsmvt_item
            return           = lt_return.
        IF line_exists( lt_return[ type = 'E' ] ).
          DATA(ls_return) = VALUE #( lt_return[ type = 'E' ] OPTIONAL ). "#EC CI_STDSEQ
          <ls_sto_ud_log>-error_msg_no = |{ ls_return-id }-{ ls_return-number }|.
          <ls_sto_ud_log>-error_msg_text = ls_return-message.
          CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        ELSE.
          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
          <ls_sto_ud_log>-status = iv_status.
          <ls_sto_ud_log>-virt_return_sto_gr_matdoc = ls_goodsmvt_headret-mat_doc.
          <ls_sto_ud_log>-virt_return_sto_gr_matdoc_year = ls_goodsmvt_headret-doc_year.
          CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
        ENDIF.

        CLEAR: lt_goodsmvt_item.
      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).
    ENDIF.

  ENDMETHOD.


  METHOD zif_sd_sto_process_handler~post_virtual_goods_receipt.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 13.02.2023 | 17175 : Virutal Return STO UnderDelivery *
*              |            | DS4K941244                               *
*----------------------------------------------------------------------*
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*

    DATA ls_vbkok_wa  TYPE vbkok.
    DATA lt_vbpok_tab TYPE STANDARD TABLE OF vbpok.
    DATA lt_prot      TYPE STANDARD TABLE OF prott.
    DATA ls_return    TYPE bapireturn .
    DATA lt_objects TYPE mtyp_t_delivery_hu.


    " Get the Under-delivered STOs info from the log table
    DATA(lts_sto_ud_logs) = get_sto_ud_logs( iv_status = iv_prev_status   iv_process = iv_process ).
    IF lts_sto_ud_logs IS NOT INITIAL.

      DATA(lt_deliveries_data)   = get_deliveries_data( it_delivery_keys = VALUE #( FOR ls_sto_ud_log IN lts_sto_ud_logs
                                                                                   ( vbeln = ls_sto_ud_log-virtual_gr_ibd ) ) ).
      vldt_plant_sloc_exist_in_hm( ).

      LOOP AT lts_sto_ud_logs ASSIGNING FIELD-SYMBOL(<ls_sto_ud_log>).
        DATA(ls_delivery_data) = VALUE #( lt_deliveries_data[ vbeln = <ls_sto_ud_log>-virtual_gr_ibd ] OPTIONAL ). "#EC CI_STDSEQ
        IF ls_delivery_data-wbstk <> 'C'.

          ls_vbkok_wa-vbeln_vl = |{ <ls_sto_ud_log>-virtual_gr_ibd ALPHA = IN }|.
          ls_vbkok_wa-vbeln    = |{ <ls_sto_ud_log>-virtual_gr_ibd ALPHA = IN }|.
          ls_vbkok_wa-wabuc = abap_true.

          CALL FUNCTION 'WS_DELIVERY_UPDATE_2'
            EXPORTING
              vbkok_wa               = ls_vbkok_wa
              synchron               = abap_true
              delivery               = ls_vbkok_wa-vbeln_vl
              if_error_messages_send = ' '
            TABLES
              vbpok_tab              = lt_vbpok_tab
              prot                   = lt_prot.

          IF line_exists( lt_prot[ msgty = 'E' ] ).
            DATA(ls_prot) = VALUE #( lt_prot[ msgty = 'E' ] OPTIONAL ).
            CALL FUNCTION 'BALW_BAPIRETURN_GET'
              EXPORTING
                type       = ls_prot-msgty
                cl         = ls_prot-msgid
                number     = CONV symsgno( ls_prot-msgno )
                par1       = ls_prot-msgv1
                par2       = ls_prot-msgv2
                par3       = ls_prot-msgv3
                par4       = ls_prot-msgv4
              IMPORTING
                bapireturn = ls_return.
            <ls_sto_ud_log>-error_msg_text = ls_return-message.
            <ls_sto_ud_log>-error_msg_no   = ls_return-code.
            CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
          ELSE.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
            <ls_sto_ud_log>-status = iv_status.
            SELECT mdh~materialdocument AS mblnr, mdh~materialdocumentyear AS mjahr "#EC CI_SEL_NESTED
            FROM i_materialdocumentheader_2 AS mdh
              LEFT OUTER JOIN i_materialdocumentitem_2 AS mdi
              ON mdh~materialdocument = mdi~materialdocument AND
                 mdh~materialdocumentyear = mdi~materialdocumentyear
            WHERE mdh~deliverydocument = @ls_vbkok_wa-vbeln_vl AND
                  mdi~quantityinbaseunit = @<ls_sto_ud_log>-quantity_short
            ORDER BY mblnr DESCENDING
            INTO TABLE @DATA(lt_ekko).
            IF sy-subrc = 0.
              <ls_sto_ud_log>-virtual_gr_matdoc = lt_ekko[ 1 ]-mblnr .
              <ls_sto_ud_log>-virtual_gr_matdoc_year = lt_ekko[ 1 ]-mjahr.
            ENDIF.
            CLEAR: <ls_sto_ud_log>-error_msg_no, <ls_sto_ud_log>-error_msg_text.
          ENDIF.
          CLEAR: ls_vbkok_wa.
        ENDIF.



        " Change handling unit status for virtual return and downadjustment process
        IF iv_process <> zif_sd_sto_ud_const=>mc_process-refusal AND <ls_sto_ud_log>-virtual_gr_matdoc IS NOT INITIAL.
          lt_objects = get_objkey( it_items = VALUE #( ( vbeln = <ls_sto_ud_log>-virtual_gr_ibd
                                                         posnr = <ls_sto_ud_log>-virtual_gr_ibd_item
                                                         quantity_su = <ls_sto_ud_log>-quantity_short ) ) ).
          IF lt_objects IS NOT INITIAL.
            LOOP AT lt_objects ASSIGNING FIELD-SYMBOL(<ls_hu_object>). "#EC CI_NESTED
              zcl_sd_rp_hu_status=>get_instance( )->update_hu_status( iv_handling_unit_id = <ls_hu_object>-venum
                                                                      iv_new_status       = 'VRET' ).

            ENDLOOP.
            CALL FUNCTION 'BAPI_TRANSACTION_COMMIT' EXPORTING wait = abap_true.
          ENDIF.
        ENDIF.

      ENDLOOP.

      update_ud_sto_log( lts_sto_ud_logs ).

    ENDIF.

  ENDMETHOD.


  METHOD get_objkey.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 09.08.2023 | 19848 : STO UnderDelivery Handling       *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*

    DATA lt_r_posnr TYPE RANGE OF posnr_vl.
    DATA ls_r_posnr LIKE LINE OF lt_r_posnr.


    IF it_items[] IS INITIAL.
      RETURN.
    ENDIF.

    LOOP AT it_items ASSIGNING FIELD-SYMBOL(<ls_itm>).
      CLEAR ls_r_posnr.
      ls_r_posnr-sign = 'I'.
      ls_r_posnr-option = 'EQ'.
      ls_r_posnr-low = <ls_itm>-posnr.
      COLLECT ls_r_posnr INTO lt_r_posnr.
    ENDLOOP.

    SELECT DISTINCT                                  "#EC CI_SEL_NESTED
     hu_vekp~HandlingUnitInternalID AS venum,
     hu_vekp~HandlingUnitReferenceDocument AS vpobjkey,
     handlingunititem       AS hu_item,
     hu_tab~handlingunitquantity AS menge,
     CAST( hu_vekp~HandlingUnitInternalID AS CHAR( 22 ) ) AS venum_hu
     FROM i_handlingunittp AS hu_vekp
     INNER JOIN @it_items AS i_del_items ON hu_vekp~HandlingUnitReferenceDocument = i_del_items~vbeln
     INNER JOIN i_handlingunititemtp AS hu_tab ON hu_vekp~HandlingUnitInternalID = hu_tab~handlingunitinternalid
     INTO TABLE @DATA(lt_result).
    IF sy-subrc = 0.
      LOOP AT lt_result ASSIGNING FIELD-SYMBOL(<ls_result>).
        <ls_result>-venum_hu = |HU{ <ls_result>-venum ALPHA = IN }|.
        " Ignore already completed HUs
        SELECT SINGLE stat AS status                 "#EC CI_SEL_NESTED
          FROM husstat AS hu_vekp_txt
          WHERE objnr = @<ls_result>-venum_hu
          AND stat = @zif_sd_sto_ud_const=>mc_hu_status-comp
          AND inact = @abap_false
          INTO @DATA(lv_result_txt).
        IF sy-subrc = 0 OR lv_result_txt IS NOT INITIAL.
          CONTINUE.
        ELSE.
          APPEND <ls_result> TO rt_objkey.
        ENDIF.
      ENDLOOP.
    ENDIF.

  ENDMETHOD.


  METHOD vldt_and_get_hu_refusal_info_2.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ADAKHIKB     | 06.06.2023 | 19848 : Development - Store Refusal Auto *
*              |            | DS4K949317                               *
*----------------------------------------------------------------------*
    DATA: ls_refused_handling_units LIKE LINE OF rt_venum.
    " Find the inbound delivery(store) from the STO
    SELECT                       "#EC CI_SEL_NESTED       "#EC CI_SUBRC
      sup_conf_ekes~PurchaseOrder                 AS ebeln,
      sup_conf_ekes~PurchaseOrderItem             AS ebelp,
      sup_conf_ekes~DeliveryDocument              AS vbeln,
      SUM( hu_item_vepo~HandlingUnitQuantity )           AS menge
      FROM I_POSupplierConfirmationAPI01 AS sup_conf_ekes
      INNER JOIN i_handlingunititemtp    AS hu_item_vepo ON hu_item_vepo~HandlingUnitReferenceDocument =  sup_conf_ekes~DeliveryDocument
      WHERE PurchaseOrder = @is_found_sto-ebeln
      AND   PurchaseOrderItem = @is_found_sto-ebelp
      AND   SupplierConfirmationExtNumber = @is_found_sto-outbound_dn
      GROUP BY  sup_conf_ekes~PurchaseOrder,
      sup_conf_ekes~PurchaseOrderItem ,
      sup_conf_ekes~DeliveryDocument
      INTO TABLE @DATA(lt_sup_conf_ekes).
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    " Find Handling Units
    SELECT DISTINCT                                  "#EC CI_SEL_NESTED
     hu_vekp~HandlingUnitInternalID AS venum,
     hu_vekp~HandlingUnitReferenceDocument AS vpobjkey,
     hu_tab~handlingunitquantity AS menge,
     CAST( hu_vekp~HandlingUnitInternalID AS CHAR( 22 ) ) AS venum_hu
     FROM i_handlingunittp AS hu_vekp
     INNER JOIN @lt_sup_conf_ekes AS i_sup_conf_ekes ON hu_vekp~HandlingUnitReferenceDocument = i_sup_conf_ekes~vbeln
     INNER JOIN i_handlingunititemtp AS hu_tab ON hu_vekp~HandlingUnitInternalID = hu_tab~handlingunitinternalid
     INTO TABLE @DATA(lt_result).
    IF sy-subrc = 0.
      LOOP AT lt_result ASSIGNING FIELD-SYMBOL(<ls_result>).
        <ls_result>-venum_hu = |HU{ <ls_result>-venum ALPHA = IN }|.
        " Check Status of the Handling Units
        SELECT SINGLE stat AS status                 "#EC CI_SEL_NESTED
          FROM husstat AS hu_vekp_txt
          WHERE objnr = @<ls_result>-venum_hu
          AND stat = @zif_sd_sto_ud_const=>mc_hu_status-refu
          AND inact = @abap_false
          INTO @DATA(lv_result_txt).
        IF sy-subrc = 0 OR lv_result_txt IS NOT INITIAL.
          ls_refused_handling_units = CORRESPONDING #( is_found_sto ).
          ls_refused_handling_units-venum = <ls_result>-venum.

          ls_refused_handling_units-quantity = <ls_result>-menge.
          APPEND ls_refused_handling_units TO rt_venum.

        ENDIF.
        CLEAR: lv_result_txt.
      ENDLOOP.

*      " Compare the Handling Unit Qty with status REFU with under delivered qty, and log the error if necessary

    ENDIF.

  ENDMETHOD.
ENDCLASS.