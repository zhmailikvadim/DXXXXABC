CLASS zcl_sd_vas_upl_validations DEFINITION
  PUBLIC
  ABSTRACT
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES zif_sd_validation_methods .

    METHODS constructor
      IMPORTING
        is_vas_cond TYPE zsd_s_vas_cond .
    CLASS-METHODS: validate_line
      CHANGING
        cs_vas_cond          TYPE zsd_s_vas_cond
      RETURNING
        VALUE(rt_return_tab) TYPE bapiret2_tab,
      get_obj_for_application_type       CHANGING
                                                   cs_vas_cond        TYPE zsd_s_vas_cond
                                         RETURNING VALUE(ro_instance) TYPE REF TO zcl_sd_vas_upl_validations.
  PROTECTED SECTION.
    TYPES:
      BEGIN OF mtyp_date_brf,
        date        TYPE dats,
        time        TYPE tims,
        timestamp   TYPE fdt_timestamp,
        offset_time TYPE fdt_utc_offset_time,
        offset_sign TYPE tznutcsign,
        type        TYPE fdt_timepoint_type,
      END OF mtyp_date_brf,

      BEGIN OF mtyp_formats,
        valpos     TYPE dd07l-valpos,
        domvalue_l TYPE dd07l-domvalue_l,
      END OF mtyp_formats,

      BEGIN OF       mtyp_shas_config,
        vkorg             TYPE vkorg,
        ekgrp             TYPE ekgrp,
        kunnr             TYPE kunnr,
        zsd_serviden      TYPE zsd_serviden,
        fsh_mg_attribute1 TYPE fsh_mg_attribute1,
        fsh_vas_sub_ser   TYPE fsh_vas_sub_ser,
        datab             TYPE mtyp_date_brf,
        datbi             TYPE mtyp_date_brf,
        fsh_vas_ser       TYPE fsh_vas_ser,
        zsd_siidentifier  TYPE zsd_siidentifier,
        zsd_condition     TYPE zsd_condition,
        zsd_field_type    TYPE zsd_field_type,
        zsd_fld_len_min   TYPE zsd_fld_len_min,
        zsd_fld_len_max   TYPE zsd_fld_len_max,
        zsd_format        TYPE zsd_format,
        zsd_data          TYPE zsd_data,
      END OF mtyp_shas_config,
      mtyp_tt_shas_config TYPE SORTED TABLE OF  mtyp_shas_config WITH NON-UNIQUE KEY fsh_vas_sub_ser fsh_vas_ser.
    METHODS:
      set_configuration ABSTRACT,
      get_additional_data,

      get_config_line IMPORTING VALUE(iv_vas_code) TYPE fieldname
                      RETURNING VALUE(rs_config)   TYPE mtyp_shas_config,

      add_error_message IMPORTING VALUE(iv_msg_number) TYPE symsgno,
      add_warning_message IMPORTING VALUE(iv_msg_number) TYPE symsgno,
      add_index_for_delete IMPORTING VALUE(iv_index) TYPE sy-tabix,

      check_plausibility IMPORTING VALUE(iv_format) TYPE zsd_format
                                   VALUE(iv_value)  TYPE zsd_vas_dyn_field_value
                         RETURNING VALUE(rv_result) TYPE boolean.


    DATA: ms_vas_condition           TYPE zsd_s_vas_cond,
          mts_shas_config            TYPE mtyp_tt_shas_config,
          mt_return_tab              TYPE bapiret2_tab,
          mts_fsh_vas_subser         TYPE SORTED TABLE OF fsh_vas_subser WITH UNIQUE KEY fsh_vas_sub_ser fsh_vas_ser,
          mt_indexes_for_delete      TYPE RANGE OF sy-tabix,
          ms_product                 TYPE mara,
          mv_pricingreferenceproduct TYPE i_productsalesdelivery-pricingreferenceproduct,
          mv_application_area        TYPE kappl,
          mts_formats                TYPE SORTED TABLE OF mtyp_formats WITH NON-UNIQUE KEY valpos.

    CONSTANTS:
      mc_z_siidentifier_i TYPE zsd_t_shas_cnfg-z_siidentifier VALUE 'I',
      mc_z_siidentifier_s TYPE zsd_t_shas_cnfg-z_siidentifier VALUE 'S',
      mc_z_condition_m    TYPE zsd_t_shas_cnfg-z_condition VALUE  'M',
      mc_z_condition_o    TYPE zsd_t_shas_cnfg-z_condition VALUE  'O'.
ENDCLASS.



CLASS ZCL_SD_VAS_UPL_VALIDATIONS IMPLEMENTATION.


  METHOD zif_sd_validation_methods~vas_values_max_lenghts_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      IF strlen( <ls_vas_condition>-field_value ) > get_config_line( <ls_vas_condition>-field_name )-zsd_fld_len_max.
        add_index_for_delete( lv_sy_tabix ).
        add_error_message( 011 ) ##NUMBER_OK.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD validate_line.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    DATA(lo_validations) = get_obj_for_application_type( CHANGING cs_vas_cond = cs_vas_cond ).
    lo_validations->zif_sd_validation_methods~run_all_validations(  ).

    cs_vas_cond = lo_validations->ms_vas_condition.
    rt_return_tab = lo_validations->mt_return_tab.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_values_min_lenghts_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      IF strlen( <ls_vas_condition>-field_value ) < get_config_line( <ls_vas_condition>-field_name )-zsd_fld_len_min.
        add_index_for_delete( lv_sy_tabix ).
        add_error_message( 010 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD get_additional_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    DATA:
      lt_fsh_vas_subser TYPE RANGE OF fsh_vas_sub_ser.

    lt_fsh_vas_subser = VALUE #( FOR ls_vas_sub_services IN ms_vas_condition-vas_sub_services
                                                          ( sign = 'I'
                                                            option = 'EQ'
                                                            low = ls_vas_sub_services-field_name ) ).
    SORT lt_fsh_vas_subser BY low.
    DELETE ADJACENT DUPLICATES FROM lt_fsh_vas_subser COMPARING low.

    SELECT  fsh_vas_ser, fsh_vas_sub_ser "#EC CI_SUBRC ##TOO_MANY_ITAB_FIELDS
     FROM fsh_vas_subser
     WHERE fsh_vas_sub_ser IN @lt_fsh_vas_subser
      INTO CORRESPONDING FIELDS OF TABLE @mts_fsh_vas_subser ##TOO_MANY_ITAB_FIELDS.

    SELECT SINGLE fsh_mg_at1 ##WARN_OK                    "#EC CI_SUBRC
      FROM mara
      INTO @ms_product
      WHERE matnr = @ms_vas_condition-material.

    SELECT SINGLE pricingreferenceproduct ##WARN_OK       "#EC CI_SUBRC
      FROM i_productsalesdelivery
      INTO @mv_pricingreferenceproduct
      WHERE product = @ms_vas_condition-material
        AND productsalesorg = @ms_vas_condition-material.


    SELECT valpos, domvalue_l                             "#EC CI_SUBRC
      FROM dd07l                                      "#EC CI_SGLSELECT
       INTO CORRESPONDING FIELDS OF TABLE @mts_formats ##TOO_MANY_ITAB_FIELDS.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_codes_types_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      IF get_config_line( <ls_vas_condition>-field_name )-zsd_field_type <> <ls_vas_condition>-data_element.
        add_index_for_delete( lv_sy_tabix ).
        add_error_message( 009 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD add_warning_message.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    APPEND VALUE #( id = 'ZSD_AIF_VAS_COND_UPL'
                    type = 'W'
                    number = iv_msg_number
                    message_v1 = ms_vas_condition-material ) TO mt_return_tab.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_codes_values_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_z_condition) = get_config_line( <ls_vas_condition>-field_name )-zsd_condition.
      DATA(lv_vas_code) = <ls_vas_condition>-field_name.

      IF lv_vas_code = '' AND lv_z_condition = mc_z_condition_m.
        add_warning_message( 008 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_values_serviden_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    DATA:
        lt_serviden TYPE RANGE OF zsd_serviden.
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      TRY.
          lt_serviden = VALUE #( FOR ls_serviden IN zcl_pe_hm_basic=>get_table( '1261' )
                                                              ( sign = 'I'
                                                                option = 'EQ'
                                                                low = ls_serviden-field1 ) ).
        CATCH zcx_pe_hm.
          RETURN.
      ENDTRY.

      IF  <ls_vas_condition>-field_name IN lt_serviden
        AND get_config_line( <ls_vas_condition>-field_name )-zsd_serviden <> <ls_vas_condition>-field_value.

        add_index_for_delete( lv_sy_tabix ).
        add_error_message( 012 ) ##NUMBER_OK.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_values_data_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    DATA:
      lt_zdata_range TYPE RANGE OF zsd_serviden.

    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.
      DATA(lv_zdata) = get_config_line( <ls_vas_condition>-field_name )-zsd_data.

      IF lv_zdata <> ''.
        SPLIT lv_zdata AT ',' INTO: TABLE DATA(lt_zdata).

        lt_zdata_range = VALUE #( FOR lv_data IN lt_zdata
                                              ( sign = 'I'
                                                option = 'EQ'
                                                low = lv_data ) ).

        IF <ls_vas_condition>-field_value NOT IN lt_zdata_range.
          add_index_for_delete( lv_sy_tabix ).
          add_error_message( 013 ) ##NUMBER_OK.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD check_plausibility.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    DATA:
        lv_date TYPE sy-datum.

    iv_format = abap_true.

    TRY.

        IF iv_format = mts_formats[ valpos = '0001' ] OR iv_format = mts_formats[ valpos = '0002' ].
          lv_date = COND #( WHEN iv_format = mts_formats[ valpos = '0002' ] THEN |{ iv_value+6(2)  }| &  |{ iv_value+4(2) }| &  |{ iv_value+4 }|
                                ELSE iv_value ).

          CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
            EXPORTING
              date                      = lv_date
            EXCEPTIONS
              plausibility_check_failed = 1
              OTHERS                    = 2.

          IF sy-subrc <> 0.
            rv_result = abap_false.
          ENDIF.

          RETURN.
        ENDIF.

        IF iv_format = mts_formats[ valpos = '0003' ] AND iv_value CN '0123456789,'.
          rv_result = abap_false.
        ENDIF.

        IF iv_format = mts_formats[ valpos = '0004' ] AND iv_value CN '0123456789.'.
          rv_result = abap_false.
        ENDIF.

        IF iv_format = mts_formats[ valpos = '0005' ] AND iv_value CN '0123456789'.
          rv_result = abap_false.
        ENDIF.
      CATCH cx_sy_itab_line_not_found.
        RETURN.
    ENDTRY.
  ENDMETHOD.


  METHOD add_error_message.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    MESSAGE e001(zsd_aif_vas_cond_upl) INTO DATA(lv_message) ##NEEDED ##MG_MISSING.
    MESSAGE e002(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e003(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e004(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e005(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e006(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e007(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e008(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e009(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e010(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e011(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e012(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e013(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e014(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e015(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.
    MESSAGE e016(zsd_aif_vas_cond_upl) INTO lv_message ##NEEDED ##MG_MISSING.

    APPEND VALUE #( id = 'ZSD_AIF_VAS_COND_UPL'
                    type = 'E'
                    number = iv_msg_number
                    message_v1 = ms_vas_condition-material ) TO mt_return_tab.
  ENDMETHOD.


  METHOD add_index_for_delete.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    APPEND VALUE #(  sign = 'I'
                     option = 'EQ'
                     low = iv_index ) TO mt_indexes_for_delete.
  ENDMETHOD.


  METHOD constructor.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    ms_vas_condition = is_vas_cond.
  ENDMETHOD.


  METHOD get_config_line.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    TRY.
        rs_config = mts_shas_config[ fsh_vas_sub_ser = iv_vas_code
                                     fsh_vas_ser = mts_fsh_vas_subser[ fsh_vas_sub_ser = iv_vas_code ] ] ##NUMBER_OK ##WARN_OK .
      CATCH cx_sy_itab_line_not_found.
        add_error_message( 16 ).
        RETURN.
    ENDTRY.
  ENDMETHOD.


  METHOD get_obj_for_application_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 25.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 14.04.2023 | 19866     : Feature - Development - VAS  *
*              |            | DS4K949378                               *
*----------------------------------------------------------------------*
    DATA:
      lt_vas_sd TYPE RANGE OF kschl,
      lt_vas_mm TYPE RANGE OF kschl.

    TRY.
        lt_vas_sd = VALUE #( FOR ls_serviden IN zcl_pe_hm_basic=>get_table( '1267' )
                                                           ( sign = 'I'
                                                             option = 'EQ'
                                                             low = ls_serviden-field1 ) ).


        lt_vas_mm = VALUE #( FOR ls_serviden IN zcl_pe_hm_basic=>get_table( '1267' )
                                                           ( sign = 'I'
                                                             option = 'EQ'
                                                             low = ls_serviden-field2 ) ).
      CATCH zcx_pe_hm.
        RETURN.
    ENDTRY.

    IF cs_vas_cond-kschl IN lt_vas_sd.
      ro_instance = NEW lhc_sd_vas_upl_validations_sd( cs_vas_cond  ).
    ELSEIF cs_vas_cond-kschl IN lt_vas_mm.
      ro_instance = NEW lhc_sd_vas_upl_validations_mm( cs_vas_cond  ).
    ENDIF.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~item_division.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      IF get_config_line( <ls_vas_condition>-field_name )-fsh_mg_attribute1 <>  ms_product-fsh_mg_at1.
        add_index_for_delete( lv_sy_tabix ).

        IF ms_product-fsh_mg_at1  = ''.
          add_error_message( 006 ).
        ELSE.
          add_error_message( 005 ).
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~material_availability.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    SELECT SINGLE product
     FROM i_product
     WHERE product = @ms_vas_condition-material
      INTO @DATA(lv_matnr) ##NEEDED.

    IF sy-subrc <> 0.
      SELECT SINGLE product
        FROM i_product
          WHERE product = @ms_vas_condition-material
          INTO @lv_matnr ##NEEDED.

      IF sy-subrc <> 0.
        add_error_message( 001 ).
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~run_all_validations.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    zif_sd_validation_methods~material_availability(  ).

    IF lines( mts_shas_config ) = 0.
      RETURN.
    ENDIF.

    zif_sd_validation_methods~validity_period(  ).
    zif_sd_validation_methods~item_division(  ).
    zif_sd_validation_methods~size_item_identifier(  ).
    zif_sd_validation_methods~vas_codes_values_cfg_tbl(  ).
    zif_sd_validation_methods~vas_codes_types_cfg_tbl(  ).
    zif_sd_validation_methods~vas_codes_formats_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_min_lenghts_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_max_lenghts_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_serviden_cfg_tbl(  ).
    zif_sd_validation_methods~vas_values_data_cfg_tbl(  ).
  ENDMETHOD.


  METHOD zif_sd_validation_methods~size_item_identifier.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*

    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      IF get_config_line( <ls_vas_condition>-field_name )-zsd_siidentifier = mc_z_siidentifier_i
        AND mv_pricingreferenceproduct <> ms_vas_condition-material.

        ms_vas_condition-material = mv_pricingreferenceproduct.
      ENDIF.

      IF get_config_line( <ls_vas_condition>-field_name )-zsd_siidentifier = mc_z_siidentifier_s
        AND  mv_pricingreferenceproduct = ms_vas_condition-material.

        add_index_for_delete( lv_sy_tabix ).
        add_error_message( 007 ).
      ENDIF.

      EXIT.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~validity_period.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      IF get_config_line( <ls_vas_condition>-field_name )-datab-date > ms_vas_condition-date_from
                          OR get_config_line( <ls_vas_condition>-field_name )-datbi-date <  ms_vas_condition-date_to.

        add_index_for_delete( lv_sy_tabix ).
        add_error_message( 005 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD zif_sd_validation_methods~vas_codes_formats_cfg_tbl.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.02.2023 | 18099     : Feature - Development - VAS  *
*              |            | DS4K944075                               *
*----------------------------------------------------------------------*
    LOOP AT ms_vas_condition-vas_sub_services ASSIGNING FIELD-SYMBOL(<ls_vas_condition>).
      DATA(lv_sy_tabix) = sy-tabix.

      IF check_plausibility( iv_format = get_config_line( <ls_vas_condition>-field_name )-zsd_format iv_value = <ls_vas_condition>-field_value ).
        add_index_for_delete( lv_sy_tabix ).
        add_error_message( 009 ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
ENDCLASS.