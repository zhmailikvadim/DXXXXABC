@Metadata.layer: #CORE
@UI: {
    headerInfo:{
            typeName: 'Stock Difference Report',
            typeNamePlural: 'Stock Difference List',
            title: {
                type: #STANDARD, value: 'Article'
            },
            description: {
                value: 'ArticleDescription'
            }
    },
    presentationVariant: [
        {
            requestAtLeast: [
              'WmsSnapshotDateCriticality',
              'StatusCriticality',
              'DifferenceCriticality',
              'BlockedDifferenceCriticality',
              'UnrectrictedDifferenceCritical',
              'InQualityInspDifferenceCritica'
            ]
        }
    ]
}
@Search.searchable: true
@UI.lineItem: [{criticality: 'StatusCriticality'}]
annotate view zsd_c_stock_difference_report with
{
  @UI.facet:  [
                {
                  id:              'HeaderData1',
                  type:            #FIELDGROUP_REFERENCE,
                  purpose:         #HEADER,
                  position:        10,
                  targetQualifier: 'HeaderData1'
                },
                {
                  id:              'HeaderData2',
                  type:            #FIELDGROUP_REFERENCE,
                  purpose:         #HEADER,
                  position:        20,
                  targetQualifier: 'HeaderData2'
                },
                {
                  id:              'HeaderData3',
                  type:            #DATAPOINT_REFERENCE,
                  purpose:         #HEADER,
                  position:        30,
                  targetQualifier: 'HeaderData3'
                },
                {
                  id:              'GeneralInformation',
                  type:            #COLLECTION,
                  label:           'General Information',
                  position:        40
                },
                {
                  id:              'WMSSTOCK',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'WMS Stock',
                  position:        50,
                  targetQualifier: 'WMSSTOCK'
                },
                {
                  id:              'SAPSTOCK',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'SAP Stock',
                  position:        60,
                  targetQualifier: 'SAPSTOCK'
                },
                { id:              'TimingAdjustment',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Timing Adjustment',
                  position:        65,
                  targetQualifier: 'TimingAdjustment'
                },
                { id:              'GoodsReceiptErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'GR Errors',
                  position:        70,
                  targetQualifier: 'GoodsReceiptErrors'
                },
                { id:              'GoodsIssueErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'GI Errors',
                  position:        72,
                  targetQualifier: 'GoodsIssueErrors'
                },
                { id:              'InventoryPostingErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Inventary Errors',
                  position:        74,
                  targetQualifier: 'InventoryPostingErrors'
                },
                { id:              'EcomErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Ecom Errors',
                  position:        76,
                  targetQualifier: 'EcomErrors'
                },
                {
                  id:              'RealDifferenceQty',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Real Qty Difference',
                  position:        80,
                  targetQualifier: 'RealDifferenceQty'
                },
                {
                  id:              'DifferenceQty',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Difference Qty',
                  position:        82,
                  targetQualifier: 'DifferenceQty'
                },
                {
                  id:              'DifferenceValue',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Difference Value',
                  position:        84,
                  targetQualifier: 'DifferenceValue'
                },
                {
                  label: 'Historical Data',
                  id  : 'HistoricalData',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_HistorySapSnapShot',
                  position: 90
               },
               {
                  label: 'Interface Goods Receipt Error(AIF)',
                  id  : 'PartialGoodsReceipt',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsPartialGoodsReceipt',
                  position: 100
               },
               {
                  label: 'Interface Goods Issue Error(IDoc)',
                  id  : 'GoodsIssueError',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsGoodsIssueIdoc',
                  position: 110
                },
               {
                  label: 'Interface Inventory Posting Error(AIF)',
                  id  : 'InventoryPostingError',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsInventoryPosting',
                  position: 113
                },
                {
                  label: 'OMNI Shipment Errors(IDoc)',
                  id  : 'OMNIShipmentError',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_ErrorsOmniShipmentConfirm',
                  position: 117
                },
                {
                  label: 'Timing Adjustment Rows',
                  id  : 'TimingAdjustmentRows',
                  purpose: #STANDARD,
                  type : #LINEITEM_REFERENCE,
                  targetElement: '_TimingAdjustmentRows',
                  position: 120
                 }]

  @UI.lineItem: [{ type: #FOR_ACTION,position: 10, dataAction: 'ReadyForStockCorrection',invocationGrouping: #CHANGE_SET,  label: 'Ready for Stock Correction' },
                 { type: #FOR_ACTION,position: 30, dataAction: 'Recalculate', invocationGrouping: #CHANGE_SET, label: 'Recalculate' }]
  @EndUserText.label: 'Currency Code'
  CurrencyCode;
  @UI:{lineItem:       [{ position: 10, importance: #HIGH,criticality: 'StatusCriticality' }],
  fieldGroup: [{ position: 10, qualifier: 'HeaderData1'}]}
  @UI.selectionField: [ { position: 90 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name    : 'zsd_i_difference_status_vh',
                                                   element : 'Status'}}]
  @EndUserText.label: 'Status'
  Status;
  @UI:{lineItem:       [{ position: 20, importance: #HIGH ,criticality: 'WmsSnapshotDateCriticality',criticalityRepresentation: #WITHOUT_ICON}]}
  @UI.selectionField: [ { position: 30  } ]
  @Consumption.valueHelpDefinition: [{ entity : { name: 'zsd_i_date_value_help', element: 'WmsSnapshotDate'}, distinctValues: true, presentationVariantQualifier: 'value_help'}]
  //@Consumption.filter:{multipleSelections: false, selectionType: null}
  //@Consumption.filter:{ mandatory: true}
  @EndUserText.label: 'WMS Snapshot Date'
  WmsSnapshotDate;
  @UI:{lineItem:       [{ position: 30, importance: #HIGH }], fieldGroup: [{ position: 10, qualifier: 'HeaderData1'}]}
  @UI.selectionField: [ { position: 10 } ]
  @Consumption.filter:{ selectionType:#SINGLE,mandatory: true}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_plant_diff_base_vh', element : 'Plant'}}]
  @EndUserText.label: 'Plant'
  Plant;
  @UI.selectionField: [ { position: 20 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_storage_loc_diff_base_vh', element : 'StorageLocation'},
                            additionalBinding: [{ element: 'Plant', localElement: 'Plant'}] }]
  @Consumption.filter:{multipleSelections: false,mandatory: true}
  @EndUserText.label: 'Storage Location'
  StorageLocation;
  @UI:{lineItem:       [{ position: 50, importance: #HIGH }]}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_article_diff_vh', element : 'Article'} }]
  @UI.selectionField: [ { position: 50 } ]
  @EndUserText.label: 'Article Number'
  Article;
  @UI.hidden: true
  @Search.defaultSearchElement : true
  @Search.fuzzinessThreshold : 0.8
  @Search.ranking : #HIGH
  ArticleForSearch;
  @EndUserText.label: 'Article Description'
  @Search.defaultSearchElement : true
  @Search.fuzzinessThreshold : 0.8
  @Search.ranking : #MEDIUM
  ArticleDescription;
  @UI:{ fieldGroup: [{ position: 30, qualifier: 'HeaderData1'}]}
  @EndUserText.label: 'Generic Article'
  @UI.selectionField: [ { position: 60 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_generic_article_diff_vh', element : 'GenericArticle'} }]
  @Search.defaultSearchElement : true
  @Search.fuzzinessThreshold : 0.8
  @Search.ranking : #LOW
  GenericArticle;
  @EndUserText.label: 'Material Type'
  @UI.selectionField: [ { position: 80 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_material_type_diff_vh', element : 'MaterialType'} }]
  @UI:{fieldGroup: [{ position: 10, qualifier: 'HeaderData2'}]}
  MaterialType;
  @UI:{fieldGroup: [{ position: 20, qualifier: 'HeaderData2'}]}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_material_group_diff_vh', element : 'ArticleGroup'} }]
  @EndUserText.label: 'Material Group'
  @UI.selectionField: [ { position: 70 } ]
  ArticleGroup;
  @UI.dataPoint: {qualifier: 'HeaderData3', title: 'Price'}
  Price;
  @UI:{fieldGroup: [{ position: 3, qualifier: 'HeaderData1'}]}
  SalesOrderNumber;
  @UI:{fieldGroup: [{ position: 5, qualifier: 'HeaderData1'}]}
  SalesOrderItem;
  @UI:{ lineItem: [{ position: 60 , importance: #HIGH}],fieldGroup: [{ position: 10,label: 'UU', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'UU SAP'
  UnrectrictedSapStockQuantity;
  @UI:{ lineItem: [{ position: 70 , importance: #HIGH}],fieldGroup: [{ position: 10, label: 'UU', qualifier: 'WMSSTOCK'}]}
  @EndUserText.label: 'UU WMS'
  UnrectrictedWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'TimingAdjustment'}]}
  @EndUserText.label: 'UU Errors Timing'
  UnrectrictedTimingAdjustment;
  @UI:{ lineItem: [{ position: 90 , importance: #HIGH, criticality: 'UnrectrictedDifferenceCritical',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 10, label: 'UU',qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'UU Diff'
  UnrectrictedDifferenceQty;
  @UI:{ lineItem: [{ position: 100 , importance: #HIGH}],  fieldGroup: [{ position: 20, label: 'BL', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'BL SAP'
  BlockedSapStockQuantity;
  @EndUserText.label: 'BL WMS'
  @UI:{ lineItem: [{ position: 110 , importance: #HIGH}],fieldGroup: [{ position: 20,label: 'BL', qualifier: 'WMSSTOCK'}]}
  BlockedWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 20,label: 'BL', qualifier: 'TimingAdjustment'}]}
  @EndUserText.label: 'BL Errors Timing'
  BlockedTimingAdjustment;
  @UI:{ lineItem: [{ position: 130 , importance: #HIGH, criticality: 'BlockedDifferenceCriticality',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 20, label: 'BL',qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'BL Diff'
  BlockedDifferenceQty;
  @UI:{ lineItem: [{ position: 140 , importance: #HIGH}],fieldGroup: [{ position: 30, label: 'QI', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'QI SAP'
  InQualityInspSapStockQuantity;
  @UI:{ lineItem: [{ position: 150 , importance: #HIGH}],fieldGroup: [{ position: 30, label: 'QI', qualifier: 'WMSSTOCK'}]}
  @EndUserText.label: 'QI WMS'
  InQualityInspWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI', qualifier: 'TimingAdjustment'}]}
  @EndUserText.label: 'QI Timing Total'
  InQualityInspTimingAdjustment;
  @UI:{ lineItem: [{ position: 170 , importance: #HIGH, criticality: 'InQualityInspDifferenceCritica',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 30, label: 'QI',qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'QI Diff'
  InQualityInspDifferenceQty;
  @UI:{ lineItem: [{ position: 180 , importance: #HIGH, criticality: 'DifferenceCriticality',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 40,label: 'Total', qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'TOTAL NET DIFF'
  DifferenceQty;
  @UI:{fieldGroup: [{ position: 50,label: 'Absolute Total', qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'TOTAL NET DIFF ABS'
  AbsoluteDifferenceQty;
  @UI:{fieldGroup: [{ position: 10, label: 'UU',qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'UU DIFF VALUE'
  UnrectrictedDifferenceValue;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'BL DIFF VALUE'
  BlockedDifferenceValue;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI', qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'QI DIFF VALUE'
  InQualityInspDifferenceValue;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU',qualifier: 'RealDifferenceQty'}]}
  @EndUserText.label: 'UU Real Diff'
  BwUnrectrictedDifferenceQty;
  @UI:{fieldGroup: [{ position: 20, label: 'QI',qualifier: 'RealDifferenceQty'}]}
  @EndUserText.label: 'QI Real Diff'
  BwInQualityInspDifferenceQty;
  @UI:{ fieldGroup: [{ position: 30, label: 'BL',qualifier: 'RealDifferenceQty'}]}
  @EndUserText.label: 'BL Real Diff'
  BwBlockedDifferenceQty;
  @UI:{ fieldGroup: [{ position: 40,label: 'Total', qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'TOTAL DIFF VALUE'
  DifferenceValue;
  @UI:{fieldGroup: [{ position: 50,label: 'Total Absolute', qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'TOTAL DIFF VALUE ABS'
  AbsoluteDifferenceValue;
  @UI:{fieldGroup: [{ position: 40,label: 'Time', qualifier: 'WMSSTOCK'}]}
  @EndUserText.label: 'WMS Time'
  WmsTime;
  @UI:{fieldGroup: [{ position: 40,label: 'Time', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'SAP Time'
  SapStampTime;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'GoodsIssueErrors'}]}
  @EndUserText.label: 'UU Int GI Error' 
  ErrorsGoodsIssueQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'GoodsReceiptErrors'}]}
  @EndUserText.label: 'UU Int GR Error'
  ErrorsGoodsReceiptUUQuantity;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'GoodsReceiptErrors'}]}
  @EndUserText.label: 'BL Int GR Error'
  ErrorsGoodsReceiptBlQuantity;
  @UI:{ fieldGroup: [{ position: 30, label: 'QI', qualifier: 'GoodsReceiptErrors'}]}
  @EndUserText.label: 'QI Int GR Error'
  ErrorsGoodsReceiptQiQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU',qualifier: 'InventoryPostingErrors'}]}
  @EndUserText.label: 'UU Int Inv Error'
  ErrorsInventoryPostingUnQty;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'InventoryPostingErrors'}]}
  @EndUserText.label: 'BL Int Inv Error'
  ErrorsInventoryPostingBlQty;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI',qualifier: 'InventoryPostingErrors'}]}
  @EndUserText.label: 'QI Int Inv Error'
  ErrorsInventoryPostingQiQty;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'EcomErrors'}]}
  @EndUserText.label: 'UU GR ECOM Error'
  ErrorsGrEcom;
  @UI:{ fieldGroup: [{ position: 20, label: 'UU', qualifier: 'EcomErrors'}]}
  @EndUserText.label: 'UU GI ECOM Error'
  ErrorsGiEcom;
  @EndUserText.label: 'Unit of Measure'
  Meins;
  @UI.selectionField: [{ position: 100 } ]
  @EndUserText.label: 'Display only differences'
  @Consumption.filter.defaultValue: 'X'
  DisplayOnlyDiffOrViceVersa;
  @UI.selectionField: [{ position: 130 } ]
  @EndUserText.label: 'Display only Latest Snapshot Dates'
  @Consumption.filter.defaultValue: 'X'
  OnlyFreshestWmsSnapshotDate;
  @EndUserText.label: 'Last Snapshot Date'
  MaxBwSnapshotDate;
  @EndUserText.label: 'Max WMS Snapshot Date'
  WmsMaxStampDate;
  @EndUserText.label: 'Max SAP Snapshot Date'
  SapMaxStampDate;

  @UI.hidden: true
  WmsSnapshotDateCriticality;
  @UI.hidden: true
  StatusCriticality;
  @UI.hidden: true
  DifferenceCriticality;
  @UI.hidden: true
  BlockedDifferenceCriticality;
  @UI.hidden: true
  UnrectrictedDifferenceCritical;
  @UI.hidden: true
  InQualityInspDifferenceCritica;

}