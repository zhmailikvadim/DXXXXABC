*eject
*
* 149733 07.05.1999 PH rm06inp0 W채hrung aus Konditionen in Info
*
*----------------------------------------------------------------------*
* Preisfindung aufrufen                                                *
*----------------------------------------------------------------------*
FORM preisfindung_info USING prf_calct
                       CHANGING c_meprtax STRUCTURE meprtax
                                c_currency LIKE sy-waers.   "149733

  DATA: lv_txdat       TYPE txdat,
        lv_tax_country TYPE fot_tax_country.

  CLEAR c_meprtax.
  CLEAR c_currency.                                         "149733
  PERFORM preisfindung_vor_info USING 'P'.

  IF komp-mglme NE 0 AND rc_meins NE 'A'.
    PERFORM pricing USING prf_calct.
    IF sy-subrc EQ 0.
*... W채hrung aus Kondition ermitteln .................................*
      LOOP AT tkomv WHERE kposn EQ komp-kposn               "149733
                    AND   kntyp EQ 'H'                      "149733
                    AND   kinak EQ space.                   "149733
      ENDLOOP.
      IF sy-subrc IS INITIAL.                               "149733
        c_currency = tkomv-waers.                           "149733
      ENDIF.                                                "149733

      lv_txdat = eine-txdat_from.

*  Steuerkennzeichen 체bernehmen
      LOOP AT tkomv WHERE kposn EQ komp-kposn
                    AND   koaid EQ 'D' "Steuerkondition
                    AND   kinak EQ space.
*   IN ME1E reporting the taxcode and tax valid from date need not be replaced.
        IF tkomv-mwsk1 NE space.
*    d.h. Steuerkennzeichenfindung ist aktiv --> geht vor Infosatz
          eine-mwskz = tkomv-mwsk1.
          cl_special_functions_ck=>get_costing_info(
                                        IMPORTING
                                          ev_bwdat = lv_txdat ).
          EXIT.
        ENDIF.
      ENDLOOP.

      IF lv_txdat IS INITIAL AND tkomv-mwsk1 IS NOT INITIAL.
        lv_txdat = simul-prsdt.
      ENDIF.

      IF eine-mwskz NE space AND prf_calct EQ 'B'.          "AND
* der Transparenz halber immer durchf체hren
*         ( MEPRI-PSEFF NE SPACE OR MEPRI-PSTAX NE SPACE ).
        lv_tax_country = eine-tax_country.

        IF eine-werks NE space.
          PERFORM t001_setzen_werk.

          IF t001-bukrs IS NOT INITIAL AND
             lv_tax_country IS INITIAL AND
             cl_fot_txa_utilities=>agent->is_tax_abroad_active( t001-bukrs ) = abap_true.
            PERFORM get_country_for_ccode USING    t001-bukrs
                                          CHANGING lv_tax_country.
          ENDIF.

          PERFORM kond_taxes USING eine-mwskz t001-bukrs komk-waerk
                                   komp-netwr komp-kposn t001w-txjcd lv_txdat lv_tax_country
                             CHANGING c_meprtax.
        ELSE.
          PERFORM t001_setzen_ekorg.

          IF t001-bukrs IS NOT INITIAL AND
             lv_tax_country IS INITIAL AND
             cl_fot_txa_utilities=>agent->is_tax_abroad_active( t001-bukrs ) = abap_true.
            PERFORM get_country_for_ccode USING    t001-bukrs
                                          CHANGING lv_tax_country.
          ENDIF.

          PERFORM kond_taxes USING eine-mwskz t001-bukrs komk-waerk
                                   komp-netwr komp-kposn space lv_txdat lv_tax_country
                             CHANGING c_meprtax.
        ENDIF.
        PERFORM pricing USING 'A'.     "nur neu bewerten
      ENDIF.
      IF prf_calct EQ 'B'.
        PERFORM wert_ermitteln.
        PERFORM preisfindung_uebernahme.
      ENDIF.
    ENDIF.
  ELSE.
    REFRESH tkomv.
  ENDIF.

ENDFORM.



*&---------------------------------------------------------------------*
*& Form GET_COUNTRY_FOR_CCODE
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> IV_CCODE
*&      <-- CV_COUNTRY
*&---------------------------------------------------------------------*
FORM get_country_for_ccode USING    iv_ccode   TYPE bukrs
                           CHANGING cv_country TYPE land1.

  CALL FUNCTION 'MEX_GET_COUNTRY_FROM_CCODE'
    EXPORTING
      iv_ccode   = iv_ccode
    IMPORTING
      ev_country = cv_country.

ENDFORM.