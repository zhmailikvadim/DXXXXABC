************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 19.03.2023 | 18988     : [Build] - Stock reconciliati *
*              |            | DS4K946765                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 14.04.2023 | 19610     : FB_19610_SD_Feature - DEFECT - Stock Reconciliation *
*              |            | DS4K949060                               *
*----------------------------------------------------------------------*
* ZHMAIVAD     | 17.04.2023 | 19611     : [Build] - Stock reconciliati *
*              |            | DS4K950069                               *
*----------------------------------------------------------------------*

FUNCTION zsd_fm_stock_corr_clear_up_adj
  IMPORTING
    VALUE(it_keys) TYPE zsd_tt_correction_report_keys
    VALUE(iv_background) TYPE boolean OPTIONAL.



  IF iv_background = abap_false.
    CALL FUNCTION 'ZSD_FM_STOCK_CORR_CLEAR_UP_ADJ'
      IN BACKGROUND TASK
      AS SEPARATE UNIT
      EXPORTING
        it_keys               = it_keys
        iv_background         = abap_true
      EXCEPTIONS
        communication_failure = 1
        system_failure        = 2.

    IF sy-subrc = 0.
      CALL FUNCTION 'START_OF_BACKGROUNDTASK'
        EXPORTING
          startdate = sy-datum
        EXCEPTIONS
          OTHERS    = 1.

      IF sy-subrc = 0.
        COMMIT WORK.
      ENDIF.
    ENDIF.

    RETURN.
  ENDIF.

  WAIT UP TO zcl_sd_stock_recon_base=>gc_fiori_bckgr_delay_befo_strt SECONDS.

  DATA lt_keys     TYPE TABLE    FOR ACTION IMPORT  zsd_i_stock_corr_rprt~clearupadjustment.
  lt_keys = CORRESPONDING #( it_keys ).

  MODIFY ENTITIES OF zsd_i_stock_corr_rprt
    ENTITY stockcorrection
    EXECUTE  clearupadjustment FROM lt_keys
      MAPPED DATA(ls_mapped)
      FAILED   DATA(ls_failed)
      REPORTED DATA(ls_reported).

  COMMIT ENTITIES
    RESPONSE OF zsd_i_stock_corr_rprt
    FAILED DATA(ls_failed_commit) ##needed.
ENDFUNCTION.