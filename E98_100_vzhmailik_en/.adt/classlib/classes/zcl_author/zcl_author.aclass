CLASS zcl_author DEFINITION
  PUBLIC
  INHERITING FROM zcl_person
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    DATA country TYPE land1 .

    EVENTS add_author
      EXPORTING
        VALUE(ex_author) TYPE zabc_s_author .

    METHODS constructor
      IMPORTING
        !im_first_name TYPE zabc_first_name OPTIONAL
        !im_last_name  TYPE zabc_last_name OPTIONAL
        !im_birth_date TYPE zabc_birth_date OPTIONAL
        !im_country    TYPE land1 OPTIONAL
        !im_id         TYPE zabc_person_id OPTIONAL .

    METHODS zif_cud~add_to_library
        REDEFINITION .
    METHODS zif_cud~delete_from_library
        REDEFINITION .
    METHODS zif_cud~update_in_library
        REDEFINITION .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_author IMPLEMENTATION.


  METHOD constructor.
    super->constructor(
      im_first_name = im_first_name
      im_last_name  = im_last_name
      im_birth_date = im_birth_date ).

    id = COND #( WHEN im_id IS SUPPLIED THEN im_id
                 WHEN im_id IS NOT SUPPLIED THEN zcl_library=>get_next_number( ir_object = me ) ).

    country    = im_country.
  ENDMETHOD.


  METHOD zif_cud~add_to_library.
    DATA(lo_library) = zcl_library=>get_instance( ).

    IF id IS NOT INITIAL.
      TRY.
          lo_library->check_id_exist( EXPORTING im_id = id ir_object = me ).
        CATCH zcx_library INTO DATA(r_exception).
          MESSAGE r_exception->get_text( ) TYPE 'W'.
          EXIT.
      ENDTRY.
    ENDIF.

    id = zcl_library=>get_next_number( ir_object = me ).

    DATA(ls_author) = VALUE zabc_s_author( author_id = id
                                           author_first_name = first_name
                                           author_last_name = last_name
                                           birth_date = birth_date
                                           country = country
                                           langu = sy-langu ).


    RAISE EVENT zif_cud~change_entity EXPORTING ex_entity = ls_author ex_insert = 'X'.
  ENDMETHOD.


  METHOD zif_cud~delete_from_library.
    DATA(ls_author) = VALUE zabc_s_author( author_id = id
                                           author_first_name = first_name
                                           author_last_name = last_name
                                           birth_date = birth_date
                                           country = country
                                           langu = sy-langu ).

    RAISE EVENT zif_cud~change_entity EXPORTING ex_entity = ls_author ex_delete = 'X'.
  ENDMETHOD.


  METHOD zif_cud~update_in_library.
    DATA(ls_author) = VALUE zabc_s_author( author_id = id
                                           author_first_name = first_name
                                           author_last_name = last_name
                                           birth_date = birth_date
                                           country = country
                                           langu = sy-langu ).

    RAISE EVENT zif_cud~change_entity EXPORTING ex_entity = ls_author ex_update = 'X'.
  ENDMETHOD.
ENDCLASS.