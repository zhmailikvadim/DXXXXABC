CLASS zcl_reader DEFINITION
  PUBLIC
  INHERITING FROM zcl_person
  CREATE PUBLIC .

  PUBLIC SECTION.

    DATA phone_number TYPE zabc_phone_number .

    EVENTS add_reader
      EXPORTING
        VALUE(ex_reader) TYPE zabc_s_reader .

    METHODS constructor
      IMPORTING
        !im_first_name   TYPE zabc_first_name OPTIONAL
        !im_last_name    TYPE zabc_last_name OPTIONAL
        !im_birth_date   TYPE zabc_birth_date OPTIONAL
        !im_phone_number TYPE zabc_phone_number OPTIONAL
        !im_id           TYPE zabc_person_id OPTIONAL .

    METHODS get_info
        REDEFINITION .
    METHODS zif_cud~add_to_library
        REDEFINITION .
    METHODS zif_cud~delete_from_library
        REDEFINITION .
    METHODS zif_cud~update_in_library
        REDEFINITION .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_reader IMPLEMENTATION.


  METHOD constructor.
    super->constructor(
      im_first_name = im_first_name
      im_last_name  = im_last_name
      im_birth_date = im_birth_date ).

    id = COND #( WHEN im_id IS NOT SUPPLIED THEN zcl_library=>get_next_number( ir_object = me )
                 WHEN im_id IS SUPPLIED THEN im_id ).

    phone_number = im_phone_number.
  ENDMETHOD.


  METHOD get_info.
    re_info = super->get_info( ) && ', Phone: ' && phone_number.
  ENDMETHOD.


  METHOD zif_cud~add_to_library.
    DATA(lo_library) = zcl_library=>get_instance( ).

    IF id IS NOT INITIAL.
      TRY.
          lo_library->check_id_exist( EXPORTING im_id = id ir_object = me ).
        CATCH zcx_library INTO DATA(r_exception).
          MESSAGE r_exception->get_text( ) TYPE 'W'.
          EXIT.
      ENDTRY.
    ENDIF.

    id = zcl_library=>get_next_number( ir_object = me ).

    DATA(ls_reader) = VALUE zabc_s_reader( person_id = id
                                           person_first_name = first_name
                                           person_last_name = last_name
                                           birth_date = birth_date
                                           phone_number = phone_number
                                           langu = sy-langu ).

    RAISE EVENT zif_cud~change_entity EXPORTING ex_entity = ls_reader ex_insert = 'X'.
  ENDMETHOD.


  METHOD zif_cud~delete_from_library.
    DATA(ls_reader) = VALUE zabc_s_reader( person_id = id
          person_first_name = first_name
          person_last_name = last_name
          birth_date = birth_date
          phone_number = phone_number
          langu = sy-langu ).

    RAISE EVENT zif_cud~change_entity EXPORTING ex_entity = ls_reader ex_delete = 'X'.
  ENDMETHOD.


  METHOD zif_cud~update_in_library.
    DATA(ls_reader) = VALUE zabc_s_reader( person_id = id
          person_first_name = first_name
          person_last_name = last_name
          birth_date = birth_date
          phone_number = phone_number
          langu = sy-langu ).

    RAISE EVENT zif_cud~change_entity EXPORTING ex_entity = ls_reader ex_update = 'X'.
  ENDMETHOD.
ENDCLASS.