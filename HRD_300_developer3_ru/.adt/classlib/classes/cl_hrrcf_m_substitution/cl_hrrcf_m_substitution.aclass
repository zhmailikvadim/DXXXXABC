class CL_HRRCF_M_SUBSTITUTION definition
  public
  inheriting from CL_WD_COMPONENT_ASSISTANCE
  create public .

*"* public components of class CL_HRRCF_M_SUBSTITUTION
*"* do not include other source files here!!!
public section.

  data ACTION_ON_POPUP type RCF_WD_ACTION .
  constants ACTION_ADD type RCF_WD_ACTION value '1'. "#EC NOTEXT
  constants ACTION_EDIT type RCF_WD_ACTION value '2'. "#EC NOTEXT
  constants ALL_SUBST_TYPES type RCF_SUBSTITUTION_TYPE value 'RCFA'. "#EC NOTEXT
  constants FALSE type BOOLE_D value ' '. "#EC NOTEXT
  constants TRUE type BOOLE_D value 'X'. "#EC NOTEXT
  constants HRRCF0001 type MSGID value 'HRRCF0001'. "#EC NOTEXT
  constants INFORMATION type MSGTY value 'I'. "#EC NOTEXT
  constants ERROR type MSGTY value 'E'. "#EC NOTEXT
  data SELECTED_MANAGER_USER type UNAME .
  data SELECTED_MANAGER_NAME type STEXT .
  data SELECTED_MANAGER_EMPLOYEEID type PERNR_D .
  data MANAGERS type RCF_T_MANAGER .
  data SUBSTITUTION_AFTER_IMAGES type RCF_SUBSTITUTION_UI_TAB .
  data SUBSTITUTION_BEFORE_IMAGES type RCF_SUBSTITUTION_UI_TAB .
  data IS_ADMINISTRATOR_APPLICATION type BOOLE_D .

  methods GET_SUBSTITUTIONS_FOR_MANAGER
    importing
      !IV_MANAGER type RCF_MANAGER
    exporting
      !ET_SUBSTITUTIONS type RCF_SUBSTITUTION_UI_TAB
      !ET_MESSAGES type BAPIRETTAB
      !EV_OK type BOOLE_D .
  methods UPDATE_SUBST_TO_DATABASE
    importing
      !SUBSTITUTIONS type RCF_SUBSTITUTION_UI_TAB .
  methods GET_USER_FOR_PERNR
    importing
      !EMPLOYEEID type PERNR_D
    exporting
      !USER type USER
      !NAME type STEXT
      !IS_FOUND type BOOLE_D .
  methods GET_PERNR_FOR_USER
    importing
      !USER type USER
    exporting
      !EMPLOYEEID type PERNR_D
      !NAME type STEXT
      !IS_FOUND type BOOLE_D .
  methods GET_UI_DATA
    importing
      !SUBSTITUTION type T77RCF_SUBST
    returning
      value(SUBSTITUTION_UI) type RCF_SUBSTITUTION_UI .
protected section.
*"* protected components of class CL_HRRCF_M_SUBSTITUTION
*"* do not include other source files here!!!
private section.
*"* private components of class CL_HRRCF_M_SUBSTITUTION
*"* do not include other source files here!!!
ENDCLASS.



CLASS CL_HRRCF_M_SUBSTITUTION IMPLEMENTATION.


METHOD get_pernr_for_user.

  DATA candidate            TYPE REF TO cl_hrrcf_candidate.

  CLEAR employeeid.
  CLEAR name.
  is_found = false.

  TRY.
      CALL METHOD cl_hrrcf_candidate=>get(
                    EXPORTING user       = user
                    IMPORTING candidate  = candidate ).

      employeeid = candidate->employeeid.
      name       = candidate->centraldataperson-fullname.
      is_found = true.
    CATCH cx_root.
      is_found = false.
  ENDTRY.

ENDMETHOD.


METHOD get_substitutions_for_manager.

  DATA substitutions   TYPE t77rcf_subst_tab.
  DATA substitution    TYPE t77rcf_subst.
  DATA substitution_ui TYPE rcf_substitution_ui.

  CALL METHOD cl_hrrcf_substitution=>get_substitutions_for_manager
    EXPORTING
      iv_manager       = iv_manager
    IMPORTING
      et_substitutions = substitutions
      et_messages      = et_messages
      ev_ok            = ev_ok.

  LOOP AT substitutions INTO substitution.
    substitution_ui = me->get_ui_data( substitution ).
    IF substitution_ui-substitute_pernr IS NOT INITIAL AND substitution_ui-substitute_pernr <> '00000000'
      AND substitution_ui-substitute_name IS NOT INITIAL.   " PVA 1808163
    APPEND substitution_ui TO et_substitutions.
    ENDIF.
  ENDLOOP.

ENDMETHOD.


METHOD get_ui_data.

  DATA local_value TYPE domvalue_l.
  DATA dd07v_wa    TYPE dd07v.

  MOVE-CORRESPONDING substitution TO substitution_ui.

  CALL METHOD me->get_pernr_for_user(
                            EXPORTING user       = substitution-substitute
                            IMPORTING name       = substitution_ui-substitute_name
                                      employeeid = substitution_ui-substitute_pernr ) .

  local_value = substitution-subst_type.
  CALL FUNCTION 'DDUT_DOMVALUE_TEXT_GET'
    EXPORTING
      name       = 'RCF_SUBSTITUTION_TYPE'
      value      = local_value
      texts_only = me->true
    IMPORTING
      dd07v_wa   = dd07v_wa
    EXCEPTIONS
      OTHERS     = 1.
  IF sy-subrc = 0 .
    substitution_ui-subst_type_text = dd07v_wa-ddtext.
  ELSE.
    substitution_ui-subst_type_text = substitution-subst_type.
  ENDIF.

ENDMETHOD.


METHOD get_user_for_pernr.

  DATA candidate            TYPE REF TO cl_hrrcf_candidate.

  CLEAR user.
  CLEAR name.
  is_found = false.

  TRY.
      CALL METHOD cl_hrrcf_candidate=>get(
                    EXPORTING employeeid = employeeid
                    IMPORTING candidate  = candidate ).

      user = candidate->user.
      name = candidate->centraldataperson-fullname.
      is_found = true.
    CATCH cx_root.
    is_found = false.
  ENDTRY.

ENDMETHOD.


METHOD update_subst_to_database.

  DATA substitution_dbs TYPE t77rcf_subst_tab.
  DATA substitution     TYPE rcf_substitution_ui.
  DATA substitution_db  TYPE t77rcf_subst.

  LOOP AT substitutions INTO substitution.
    move-corresponding substitution to substitution_db.
    append substitution_db to substitution_dbs.
  ENDLOOP.

  CALL METHOD cl_hrrcf_substitution=>update_substitutions(
    EXPORTING
              it_managers      = me->managers
              it_substitutions = substitution_dbs ).

ENDMETHOD.
ENDCLASS.