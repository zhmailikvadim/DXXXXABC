class CL_HRRCF_T77RCF_EXT_PUB definition
  public
  final
  create public .

*"* public components of class CL_HRRCF_T77RCF_EXT_PUB
*"* do not include other source files here!!!
public section.

  interfaces IF_HRRCF_CONSTANTS .

  aliases DELETE_OPERATION
    for IF_HRRCF_CONSTANTS~DELETE_OPERATION .
  aliases FALSE
    for IF_HRRCF_CONSTANTS~FALSE .
  aliases HIGH_DATE
    for IF_HRRCF_CONSTANTS~HIGH_DATE .
  aliases INSERT_OPERATION
    for IF_HRRCF_CONSTANTS~INSERT_OPERATION .
  aliases LOW_DATE
    for IF_HRRCF_CONSTANTS~LOW_DATE .
  aliases MODIFY_OPERATION
    for IF_HRRCF_CONSTANTS~MODIFY_OPERATION .
  aliases MSGCL_ADMIN
    for IF_HRRCF_CONSTANTS~MSGCL_ADMIN .
  aliases MSGCL_USER
    for IF_HRRCF_CONSTANTS~MSGCL_USER .
  aliases TRUE
    for IF_HRRCF_CONSTANTS~TRUE .

  class-methods INSERT_RECORD
    importing
      !RECORD type T77RCF_EXT_PUB
    raising
      CX_HRRCF .
  class-methods DELIMIT_RECORD
    importing
      !KEY type HRIKEY
    raising
      CX_HRRCF .
  class-methods MARK_AS_PUBLISHED
    importing
      !POSTING type ref to CL_HRRCF_POSTING .
  class-methods MARK_AS_UNPUBLISHED
    importing
      !POSTING type ref to CL_HRRCF_POSTING optional
      !PS_POSTING_INSTANCE type P5122 optional
    changing
      !T77RCF_EXT_PUB_LINE type T77RCF_EXT_PUB optional
    raising
      CX_HRRCF_DATABASE .
  class-methods GET_CURRENT_PUBLICATIONS
    importing
      !POSTING type ref to CL_HRRCF_POSTING
    exporting
      !RECORDS type T77RCF_EXT_PUB_TAB .
  class-methods SET_WITHDRAW_FLAG
    importing
      !KEY type HRIKEY
    raising
      CX_HRRCF .
  class-methods GET_ALL_PUBLICATIONS
    returning
      value(T77RCF_EXT_PUB_TAB) type T77RCF_EXT_PUB_TAB .
  class-methods DELETE_TABLE_LINE
    importing
      !T77RCF_EXT_PUB_LINE type T77RCF_EXT_PUB
    raising
      CX_HRRCF_DATABASE .
  class-methods GET_ALL_UNPUBLICATIONS
    returning
      value(T77RCF_EXT_PUB_TAB) type T77RCF_EXT_PUB_TAB .
  class-methods GET_SINGLE_PUBLICATION
    importing
      !HROBJECT type HROBJECT
    exporting
      !T77RCF_EXT_PUB_TAB type T77RCF_EXT_PUB_TAB .
  class-methods GET_SINGLE_UNPUBLICATION
    importing
      !HROBJECT type HROBJECT
    exporting
      !T77RCF_EXT_PUB_TAB type T77RCF_EXT_PUB_TAB .
  class-methods GET_ALL_PUBLISHED_ENTRIES
    returning
      value(T77RCF_EXT_PUB_TAB) type T77RCF_EXT_PUB_TAB .
  class-methods CHANGE_PUBL_PERIOD
    importing
      !PS_POSTING_INSTANCE type P5122
    raising
      CX_HRRCF_DATABASE .
protected section.
*"* protected components of class CL_HRRCF_T77RCF_INT_PUB
*"* do not include other source files here!!!
private section.
*"* private components of class CL_HRRCF_T77RCF_INT_PUB
*"* do not include other source files here!!!
ENDCLASS.



CLASS CL_HRRCF_T77RCF_EXT_PUB IMPLEMENTATION.


METHOD change_publ_period.
* Author: JDH

  DATA: lv_message_par1 TYPE symsgv,
        lv_message_par2 TYPE symsgv.

  UPDATE t77rcf_ext_pub
     SET start_date = ps_posting_instance-start_date
         end_date   = ps_posting_instance-end_date
   WHERE plvar = ps_posting_instance-plvar AND
         otype = ps_posting_instance-otype AND
         objid = ps_posting_instance-objid AND
         subty = ps_posting_instance-subty AND
         istat = ps_posting_instance-istat AND
         begda = ps_posting_instance-begda AND
         endda = ps_posting_instance-endda AND
         varyf = ps_posting_instance-varyf AND
         seqnr = ps_posting_instance-seqnr.

  IF sy-subrc NE 0.
    IF 1 = 0.
      MESSAGE e153(hrrcf0001) WITH 't77rcf_ext_pub' lv_message_par2 .
*   Kein ensprechender Eintrag in Tabelle &1 verhanden
    ENDIF.

    lv_message_par1 = 't77rcf_ext_pub'.

    CONCATENATE  ps_posting_instance-plvar
                 ps_posting_instance-otype
                 ps_posting_instance-objid
                 INTO lv_message_par2 SEPARATED BY space .

    RAISE EXCEPTION TYPE cx_hrrcf_database
      EXPORTING
         message_type = 'E'
         message_class = msgcl_admin
         message_number = '153'
         message_par1 = lv_message_par1
         message_par2 = lv_message_par2.
  ENDIF.

ENDMETHOD.


METHOD delete_table_line .

  DATA lv_message_par1 TYPE symsgv.
  DATA lv_message_par2 TYPE symsgv.

  DELETE t77rcf_ext_pub FROM t77rcf_ext_pub_line.

  IF sy-subrc NE 0.

    IF 1 = 0.
      MESSAGE e152(hrrcf0001) WITH 't77rcf_ext_pub' lv_message_par2.
*   Fehler beim LÃ¶schen eines Eintrages aus Tabelle &1
    ENDIF.

    lv_message_par1 = 't77rcf_ext_pub'.
    lv_message_par2 = t77rcf_ext_pub_line.

    RAISE EXCEPTION TYPE cx_hrrcf_database
      EXPORTING
        message_type = 'E'
        message_class = 'HRRCF0001'
        message_number = '152'
        message_par1 = lv_message_par1
        message_par2 = lv_message_par2
        .
  ENDIF.

ENDMETHOD.


METHOD delimit_record .
* If the publication is withdrawn at the same day, it
* will be deleted, otherwise the end_date is set to yesterday
  DATA:
    ls_hrobject       TYPE hrobject,
    ls_t77rcf_ext_pub TYPE t77rcf_ext_pub.
*
  SELECT SINGLE * FROM t77rcf_ext_pub
    INTO ls_t77rcf_ext_pub WHERE
        plvar = key-plvar AND
        otype = key-otype AND
        objid = key-objid AND
        subty = key-subty AND
        istat = key-istat AND
        begda = key-begda AND
        endda = key-endda AND
        varyf = key-varyf AND
        seqnr = key-seqnr.
  IF sy-subrc IS INITIAL.
    IF ls_t77rcf_ext_pub-start_date GE sy-datum.
*     records which are published today or in the future will be deleted
      DELETE t77rcf_ext_pub FROM ls_t77rcf_ext_pub.
      IF NOT sy-subrc IS INITIAL.
        MOVE-CORRESPONDING key TO ls_hrobject.
*       Preliminary
        RAISE EXCEPTION TYPE cx_hrrcf
          EXPORTING
            hrobject = ls_hrobject.
      ENDIF.
    ELSE.
*     At the moment the publication is delimited it should not be
*     available. To achieve this the end of the publication period
*     is set to the previous day.
      ls_t77rcf_ext_pub-end_date = sy-datum - 1.
      UPDATE t77rcf_ext_pub FROM ls_t77rcf_ext_pub.
      IF NOT sy-subrc IS INITIAL.
        MOVE-CORRESPONDING key TO ls_hrobject.
*       Preliminary
        RAISE EXCEPTION TYPE cx_hrrcf
          EXPORTING
            hrobject = ls_hrobject.
      ENDIF.
    ENDIF.
  ELSE.
    MOVE-CORRESPONDING key TO ls_hrobject.
*   Preliminary
    RAISE EXCEPTION TYPE cx_hrrcf
      EXPORTING
        hrobject = ls_hrobject.
  ENDIF.
*
ENDMETHOD.


METHOD get_all_publications .

* get all entries which should be published
  SELECT * FROM t77rcf_ext_pub INTO TABLE t77rcf_ext_pub_tab "#EC CI_NOFIELD
                           WHERE start_date <= sy-datum AND
                                 end_date   >= sy-datum AND
                                 withdraw    = false    AND
                                 published   = false.

ENDMETHOD.


METHOD GET_ALL_PUBLISHED_ENTRIES .

* get all entries which should be published
  SELECT * FROM t77rcf_ext_pub INTO TABLE t77rcf_ext_pub_tab "#EC CI_NOFIELD
                           WHERE start_date <= sy-datum AND
                                 end_date   >= sy-datum AND
                                 withdraw    = false.

ENDMETHOD.


METHOD get_all_unpublications .
* Additions: VSD
  DATA lt_t77rcf_ext_pub TYPE t77rcf_ext_pub_tab.

* end of publication periode is reached
  SELECT * FROM t77rcf_ext_pub INTO TABLE lt_t77rcf_ext_pub "#EC CI_NOFIELD
                               WHERE end_date < sy-datum
                                  OR withdraw = true.

  APPEND LINES OF lt_t77rcf_ext_pub TO t77rcf_ext_pub_tab.

ENDMETHOD.


METHOD get_current_publications .
  DATA:
    ls_t77rcf_ext_pub TYPE          t77rcf_ext_pub,
    lt_t77rcf_ext_pub TYPE TABLE OF t77rcf_ext_pub.
*
  CLEAR records.
*
  SELECT * FROM t77rcf_ext_pub
    INTO TABLE lt_t77rcf_ext_pub
    WHERE plvar = posting->hrobject-plvar AND
          otype = posting->hrobject-otype AND
          objid = posting->hrobject-objid .
*
  IF sy-subrc IS INITIAL.
    LOOP AT lt_t77rcf_ext_pub
         INTO ls_t77rcf_ext_pub
         WHERE end_date   GE sy-datum AND
               start_date LE sy-datum.
      APPEND ls_t77rcf_ext_pub TO records.
    ENDLOOP.
  ENDIF.
*
ENDMETHOD.


METHOD get_single_publication .

  SELECT * FROM t77rcf_ext_pub INTO TABLE t77rcf_ext_pub_tab
                           WHERE objid     =  hrobject-objid
                           AND   otype     =  hrobject-otype
                           AND   plvar     =  hrobject-plvar
                           AND   withdraw  =  false
                           AND   ( start_date <= sy-datum AND
                                 end_date >= sy-datum ) .

ENDMETHOD.


METHOD get_single_unpublication .
* Additions: VSD
  DATA lt_t77rcf_ext_pub TYPE t77rcf_ext_pub_tab.

  SELECT * FROM t77rcf_ext_pub INTO TABLE lt_t77rcf_ext_pub
                           WHERE ( objid     =  hrobject-objid
                           AND   otype     =  hrobject-otype
                           AND   plvar     =  hrobject-plvar )
                           AND   ( end_date  < sy-datum
                           OR withdraw = true ).

  APPEND LINES OF lt_t77rcf_ext_pub TO t77rcf_ext_pub_tab.
*  CLEAR lt_t77rcf_ext_pub.
*
*  SELECT * FROM t77rcf_ext_pub INTO TABLE lt_t77rcf_ext_pub
*                           WHERE objid     =  hrobject-objid
*                           AND   otype     =  hrobject-otype
*                           AND   plvar     =  hrobject-plvar
*                           AND   withdraw  =  true.
*
*  APPEND LINES OF lt_t77rcf_ext_pub TO t77rcf_ext_pub_tab.

ENDMETHOD.


METHOD INSERT_RECORD .
  DATA:
    ls_key TYPE hrikey.
*
  MOVE-CORRESPONDING record TO ls_key.
  IF NOT ls_key IS INITIAL.
*
    INSERT t77rcf_ext_pub FROM record .
    IF NOT sy-subrc IS INITIAL.
* Preliminary
      RAISE EXCEPTION TYPE cx_hrrcf.
    ENDIF.
*
  ENDIF.
*
ENDMETHOD.


METHOD mark_as_published .
  DATA:
    ls_t77rcf_ext_pub TYPE          t77rcf_ext_pub,
    lt_t77rcf_ext_pub TYPE TABLE OF t77rcf_ext_pub.
*
  SELECT * FROM t77rcf_ext_pub
    INTO TABLE lt_t77rcf_ext_pub
    WHERE plvar = posting->hrobject-plvar
      AND otype = posting->hrobject-otype
      AND objid = posting->hrobject-objid .
*
  IF sy-subrc IS INITIAL.
    LOOP AT lt_t77rcf_ext_pub
         INTO ls_t77rcf_ext_pub
         WHERE end_date   GE sy-datum AND
               start_date LE sy-datum.
      ls_t77rcf_ext_pub-published = true.
      UPDATE t77rcf_ext_pub FROM ls_t77rcf_ext_pub.
    ENDLOOP.
  ENDIF.
*
ENDMETHOD.


METHOD mark_as_unpublished .
* Additions: JDH (parameter ps_posting_instance)
*
  DATA lv_message_par1 TYPE symsgv.
  DATA lv_message_par2 TYPE symsgv.

  IF posting IS SUPPLIED.

    UPDATE t77rcf_ext_pub
       SET published = false
     WHERE plvar     = posting->hrobject-plvar AND
           otype     = posting->hrobject-otype AND
           objid     = posting->hrobject-objid .
*
    IF sy-subrc NE 0.
      IF 1 = 0.
        MESSAGE e153(hrrcf0001) WITH 't77rcf_ext_pub' lv_message_par2 .
*   Kein ensprechender Eintrag in Tabelle &1 verhanden
      ENDIF.

      lv_message_par1 = 't77rcf_ext_pub'.
      CONCATENATE  posting->hrobject-plvar
                   posting->hrobject-otype
                   posting->hrobject-objid
                   INTO lv_message_par2 SEPARATED BY space .

      RAISE EXCEPTION TYPE cx_hrrcf_database
        EXPORTING
           message_type = 'E'
           message_class = 'HRRCF0001'
           message_number = '153'
           message_par1 = lv_message_par1
           message_par2 = lv_message_par2
          .
    ENDIF.
  ENDIF.

  IF ps_posting_instance IS SUPPLIED.

    UPDATE t77rcf_ext_pub
       SET published = false
     WHERE plvar      = ps_posting_instance-plvar      AND
           otype      = ps_posting_instance-otype      AND
           objid      = ps_posting_instance-objid      AND
           subty      = ps_posting_instance-subty      AND
           istat      = ps_posting_instance-istat      AND
           begda      = ps_posting_instance-begda      AND
           endda      = ps_posting_instance-endda      AND
           varyf      = ps_posting_instance-varyf      AND
           seqnr      = ps_posting_instance-seqnr      AND
           published  = true                           AND
           withdraw   = false.

*    If no database record was found for this specification, it is likely that the record has only just been
*    created and has no 'published' flag yet -> no error handling necessary. If there are other reasons for
*    the lack of a DB record, we decided not to do any error handling at this point either.

  ENDIF.

  IF t77rcf_ext_pub_line IS SUPPLIED.

    t77rcf_ext_pub_line-published = false.

    UPDATE  t77rcf_ext_pub FROM t77rcf_ext_pub_line.

    IF sy-subrc NE 0.
      IF 1 = 0.
        MESSAGE e153(hrrcf0001) WITH 't77rcf_ext_pub' lv_message_par2.
*   Kein ensprechender Eintrag in Tabelle &1 verhanden
      ENDIF.

      lv_message_par1 = 't77rcf_ext_pub'.
      lv_message_par2 = t77rcf_ext_pub_line.

      RAISE EXCEPTION TYPE cx_hrrcf_database
        EXPORTING
           message_type = 'E'
           message_class = 'HRRCF0001'
           message_number = '153'
           message_par1 = lv_message_par1
           message_par2 = lv_message_par2
          .
    ENDIF.
  ENDIF.

ENDMETHOD.


METHOD set_withdraw_flag .
* This method should only be called if the publication period
* is not over
  DATA:
    ls_hrobject       TYPE hrobject.
*
  UPDATE t77rcf_ext_pub SET withdraw = true
    WHERE
      plvar = key-plvar AND
      otype = key-otype AND
      objid = key-objid AND
      subty = key-subty AND
      istat = key-istat AND
      begda = key-begda AND
      endda = key-endda AND
      varyf = key-varyf AND
      seqnr = key-seqnr.
  IF NOT sy-subrc IS INITIAL.
*  It's worse to throw an exception since we can expect an
*  entry, although there might be a  reorg of the table.
    MOVE-CORRESPONDING key TO ls_hrobject.
*       Preliminary
    RAISE EXCEPTION TYPE cx_hrrcf
      EXPORTING
        hrobject = ls_hrobject.
  ENDIF.
*
ENDMETHOD.
ENDCLASS.