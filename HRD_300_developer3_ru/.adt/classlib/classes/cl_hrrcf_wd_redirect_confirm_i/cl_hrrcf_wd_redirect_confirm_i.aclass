class CL_HRRCF_WD_REDIRECT_CONFIRM_I definition
  public
  final
  create public .

*"* public components of class CL_HRRCF_WD_REDIRECT_CONFIRM_I
*"* do not include other source files here!!!
public section.

  interfaces IF_HTTP_EXTENSION .
protected section.
*"* protected components of class CL_HRRCF_WD_REDIRECT_CONFIRM_I
*"* do not include other source files here!!!
private section.
*"* private components of class CL_HRRCF_WD_REDIRECT_CONFIRM_I
*"* do not include other source files here!!!
ENDCLASS.



CLASS CL_HRRCF_WD_REDIRECT_CONFIRM_I IMPLEMENTATION.


METHOD if_http_extension~handle_request.

  DATA: ls_field          TYPE ihttpnvp,
        ls_url_param      TYPE ihttpnvp,
        lt_field          TYPE tihttpnvp,
        lt_field2         TYPE tihttpnvp,
        lt_url_params     TYPE tihttpnvp,
        lv_application    TYPE string,
        lv_url            TYPE string,
        lv_rtl_included   TYPE wdy_boolean,
        lv_acc_included   TYPE wdy_boolean,
        lv_theme_included TYPE wdy_boolean,
        lv_dest           TYPE rfcdest,
        lv_config_id      TYPE rcf_wd_config_id,
        lo_badi           TYPE REF TO hrrcf00_det_applconfig_general.

  CONSTANTS: lc_config_id TYPE string VALUE 'sap-wd-configId'. "#EC NOTEXT

  server->request->get_form_fields( CHANGING fields = lt_field ).

  lv_application = 'hrrcf_a_act_cnf_ind_int'.

  LOOP AT lt_field INTO ls_field.
    IF ls_field-name = 'sap-ep-themeroot'.
      lv_theme_included = abap_true.
    ENDIF.
    IF ls_field-name = 'sap-accessibility'.
      lv_acc_included = abap_true.
    ENDIF.
    IF ls_field-name = 'sap-rtl'.
      lv_rtl_included = abap_true.
    ENDIF.
    APPEND ls_field TO lt_field2.
  ENDLOOP.

  IF lv_theme_included = abap_false OR
     lv_acc_included = abap_false OR
     lv_rtl_included = abap_false.

    TRY.
        lv_dest = cl_hrrcf_mdl_wd_services=>get_bl_destination( ).

        CALL FUNCTION 'HRRCF_MDL_UIS_URL_PARAMS_GET'
          DESTINATION lv_dest
          IMPORTING
            et_params = lt_url_params.

      CATCH cx_root.
*      ignore exception
    ENDTRY.
*   no theme available
    IF lv_theme_included = abap_false.
      READ TABLE lt_url_params INTO ls_url_param WITH KEY name = 'sap-ep-themeroot'.
      IF sy-subrc = 0.
        APPEND ls_url_param TO lt_field2.
      ENDIF.
    ENDIF.

*   no acc available
    IF lv_acc_included = abap_false.
      READ TABLE lt_url_params INTO ls_url_param WITH KEY name = 'sap-accessibility'.
      IF sy-subrc = 0.
        APPEND ls_url_param TO lt_field2.
      ENDIF.
    ENDIF.

*   no rtl available
    IF lv_rtl_included = abap_false.
      READ TABLE lt_url_params INTO ls_url_param WITH KEY name = 'sap-rtl'.
      IF sy-subrc = 0.
        APPEND ls_url_param TO lt_field2.
      ENDIF.
    ENDIF.
  ENDIF.

* Call BAdI to retrieve the application configuration ID
  TRY.
      GET BADI lo_badi.
      IF lo_badi IS BOUND.
        CALL BADI lo_badi->determine_appl_config
          EXPORTING
            iv_wd_application_name = lv_application
          IMPORTING
            ev_config_id           = lv_config_id.
      ENDIF.

    CATCH cx_badi_not_implemented.                      "#EC NO_HANDLER
* No BAdI implementation found
      CLEAR lv_config_id.
  ENDTRY.

  IF lv_config_id IS NOT INITIAL.
    ls_field-name = lc_config_id.
    ls_field-value = lv_config_id.
    APPEND ls_field TO lt_field2.
  ENDIF.

  CALL METHOD cl_wd_utilities=>construct_wd_url
    EXPORTING
      application_name = lv_application
      in_parameters    = lt_field2
    IMPORTING
      out_local_url    = lv_url.

* redirect to internal confirmation
  CALL METHOD server->response->redirect
    EXPORTING
      url = lv_url.

ENDMETHOD.
ENDCLASS.