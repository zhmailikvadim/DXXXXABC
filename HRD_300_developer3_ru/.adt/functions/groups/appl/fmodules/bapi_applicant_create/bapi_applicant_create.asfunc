* XDVN480519 0202 vacancy assignment in case of delimited positions
* 4.6A
* XCZK029699  0299  Qualifikationsauspr√§gungen 4-stellig
* WBIK031606  0199  Anlegen von internen Bewerbern & E-MAIL
*
FUNCTION BAPI_APPLICANT_CREATE
  IMPORTING
    VALUE(AP_GROUP) LIKE BAPIAPLORG-AP_GROUP
    VALUE(AP_SUBGRP) LIKE BAPIAPLORG-AP_SUBGRP
    VALUE(PERS_AREA) LIKE BAPIAPLORG-PERS_AREA
    VALUE(P_SUBAREA) LIKE BAPIAPLORG-P_SUBAREA
    VALUE(PERSONNEL) LIKE BAPIAPLORG-PERSONNEL OPTIONAL
    VALUE(FOA_KEY) LIKE BAPIAPLPER-FOA_KEY OPTIONAL
    VALUE(F_NAME) LIKE BAPIAPLPER-F_NAME
    VALUE(L_NAME) LIKE BAPIAPLPER-L_NAME
    VALUE(BIRTHNAME) LIKE BAPIAPLPER-BIRTHNAME OPTIONAL
    VALUE(SECONDNAME) LIKE BAPIAPLPER-LAST_NAME2 OPTIONAL
    VALUE(KNOWN_AS) LIKE BAPIAPLPER-KNOWN_AS OPTIONAL
    VALUE(SSN) LIKE BAPIAPLPER-SSN OPTIONAL
    VALUE(B_DATE) LIKE BAPIAPLPER-B_DATE OPTIONAL
    VALUE(TITLE) LIKE BAPIAPLPER-TITLE OPTIONAL
    VALUE(2ND_TITLE) LIKE BAPIAPLPER-2ND_TITLE OPTIONAL
    VALUE(ARI_TITLE) LIKE BAPIAPLPER-ARI_TITLE OPTIONAL
    VALUE(AFFIX) LIKE BAPIAPLPER-AFFIX OPTIONAL
    VALUE(PREFIX_2) LIKE BAPIAPLPER-PREFIX_2 OPTIONAL
    VALUE(GENDER) LIKE BAPIAPLPER-GENDER OPTIONAL
    VALUE(BIRTHPLACE) LIKE BAPIAPLPER-BIRTHPLACE OPTIONAL
    VALUE(MAR_STAT) LIKE BAPIAPLPER-MAR_STAT OPTIONAL
    VALUE(INITIALS) LIKE BAPIAPLPER-INITIALS OPTIONAL
    VALUE(NATION) LIKE BAPIAPLPER-NATION OPTIONAL
    VALUE(LANGU) LIKE BAPIAPLPER-LANGU
    VALUE(C_O) LIKE BAPIAPLADR-C_O OPTIONAL
    VALUE(STREET) LIKE BAPIAPLADR-STREET
    VALUE(2ND_ADD_LN) LIKE BAPIAPLADR-2ND_ADD_LN OPTIONAL
    VALUE(CITY) LIKE BAPIAPLADR-CITY
    VALUE(REGION) LIKE BAPIAPLADR-REGION OPTIONAL
    VALUE(DISTRICT) LIKE BAPIAPLADR-DISTRICT OPTIONAL
    VALUE(PCD_CITY) LIKE BAPIAPLADR-PCD_CITY
    VALUE(CNTRY) LIKE BAPIAPLADR-CNTRY OPTIONAL
    VALUE(TEL_NO) LIKE BAPIAPLADR-TEL_NO OPTIONAL
    VALUE(ADVERT) LIKE BAPIAPPLIC-ADVERT OPTIONAL
    VALUE(UNSAPPGP) LIKE BAPIAPPLIC-UNSAPPGP OPTIONAL
    VALUE(MEDIUM) LIKE BAPIAPPLIC-MEDIUM OPTIONAL
    VALUE(EMPLOYEENUMBER) LIKE BAPIP0001-PERNR OPTIONAL
    VALUE(COMMUNICATION_TYPE) LIKE BAPIP0105B-USERTYPE OPTIONAL
    VALUE(E_MAIL) LIKE BAPIHR0105-E_MAIL OPTIONAL
  EXPORTING
    VALUE(APPLICANTNUMBER) LIKE BAPIAPPL-PERNR
    VALUE(RETURN) LIKE BAPIRETURN
    VALUE(RETURN2) LIKE BAPIRET2
  TABLES
    EDUCATION LIKE BAPIEDUC OPTIONAL
    PREVIOUS_EMPLOYMENT LIKE BAPIEMPLOY OPTIONAL
    QUALIFICATION LIKE BAPIQUALI OPTIONAL
    VACANCY_ASSIGNMENT LIKE BAPIVACASS OPTIONAL.




INFOTYPES: 0001, 0002, 0006, 0022, 0023, 0024, 4001, 4002,
           4005, 0105.                                      "WBIK031606

*------------------------ XDVN480519 begin -----------------------------
DATA: lv_plvar LIKE plog-plvar.
DATA: lv_orga  LIKE t77s0-gsval.
DATA: lv_obj_begda LIKE sy-datum.
DATA: lv_obj_endda LIKE sy-datum.
DATA: lv_index LIKE sy-tabix.
DATA: wa_vacancy_assignment LIKE vacancy_assignment.
*------------------------ XDVN480519 end -------------------------------
*****************Begin of SPCLAK1300497********************************************
  DATA: structure LIKE pme45.
  DATA: feature LIKE t549b-namen.
  DATA: back LIKE pspar-itbld.
  DATA: status(1).
  DATA: return1 TYPE bapiret1.
*****************End of SPCLAK1300497********************************************
  P0001-PERSG = AP_GROUP.
  P0001-PERSK = AP_SUBGRP.
  P0001-WERKS = PERS_AREA.
  P0001-BTRTL = P_SUBAREA.
  P0001-SACHP = PERSONNEL.

  P0002-ANRED = FOA_KEY.
  P0002-FAMST = MAR_STAT.
  P0002-GBDAT = B_DATE.
  P0002-GBPAS = B_DATE.                                     "XDPK129267
  P0002-GBORT = BIRTHPLACE.
  P0002-GESCH = GENDER.
  P0002-INITS = INITIALS.
  P0002-NACHN = L_NAME.
  P0002-NAME2 = BIRTHNAME.
  P0002-NACH2 = SECONDNAME.
  P0002-NAMZU = ARI_TITLE.
  P0002-NATIO = NATION.
  P0002-RUFNM = KNOWN_AS.
  P0002-SPRSL = LANGU.
  P0002-TITEL = TITLE.
  P0002-TITL2 = 2ND_TITLE.
  P0002-VORNA = F_NAME.
  P0002-VORS2 = PREFIX_2.
  P0002-VORSW = AFFIX.
  P0002-PERID = SSN.

  P0006-LAND1 = CNTRY.
  P0006-LOCAT = 2ND_ADD_LN.
  P0006-NAME2 = C_O.
  P0006-ORT01 = CITY.
  P0006-ORT02 = DISTRICT.
  P0006-PSTLZ = PCD_CITY.
  P0006-STATE = REGION.
  P0006-STRAS = STREET.
  P0006-TELNR = TEL_NO.

  P4001-MEDIA = MEDIUM.
  P4001-OFFID = ADVERT.
  P4001-SPAPL = UNSAPPGP.

  P4005-EMPNR = EMPLOYEENUMBER.                             "WBIK031606

  P0105-USRTY = COMMUNICATION_TYPE.                         "WBIK031606
  P0105-SUBTY = COMMUNICATION_TYPE.                         "WBIK031606
  P0105-USRID_LONG = E_MAIL.                                "WBIK031606
*****************Begin of SPCLAK1300497********************************************
  structure-land1 = p0006-land1.
  feature = 'CSSCR'.

* read the feature
  CALL FUNCTION 'HR_FEATURE_BACKFIELD'
    EXPORTING
      feature                     = feature
      struc_content               = structure
      kind_of_error               = space
    IMPORTING
      back                        = back
    CHANGING
      status                      = status
    EXCEPTIONS
      dummy                       = 1
      error_operation             = 2
      no_backvalue                = 3
      feature_not_generated       = 4
      invalid_sign_in_funid       = 5
      field_in_report_tab_in_pe03 = 6
      OTHERS                      = 7.
  IF sy-subrc = 0.
* transfer the return value to p0006 structure
    p0006-itbld = back.
  ELSE.
    CALL FUNCTION 'BALW_BAPIRETURN_GET1'
      EXPORTING
        type       = sy-msgty
        cl         = sy-msgid
        number     = sy-msgno
        par1       = sy-msgv1
        par2       = sy-msgv2
        par3       = sy-msgv3
        par4       = sy-msgv4
      IMPORTING
        bapireturn = return1
      EXCEPTIONS
        OTHERS     = 1.

    MOVE-CORRESPONDING return1 TO return2.
* exit the BAPI
    EXIT.
  ENDIF.

*****************End of SPCLAK1300497**********************************************
  LOOP AT EDUCATION.
    CLEAR P0022.
    P0022-BEGDA = EDUCATION-FROM_DATE.
    P0022-ENDDA = EDUCATION-TO_DATE.
    P0022-INSTI = EDUCATION-INSTITUTE.
    P0022-SLABS = EDUCATION-CERTIFIC.
    P0022-SLART = EDUCATION-EDUC_EST.
    P0022-SLAND = EDUCATION-CNTRY.
    P0022-SLTP1 = EDUCATION-BR_STUDY_1.
    P0022-SLTP2 = EDUCATION-BR_STUDY_2.
    P0022-EMARK = EDUCATION-MARK.                             "4.6C
    APPEND P0022.
  ENDLOOP.

  LOOP AT PREVIOUS_EMPLOYMENT.
    CLEAR P0023.
    P0023-BEGDA = PREVIOUS_EMPLOYMENT-FROM_DATE.
    P0023-ENDDA = PREVIOUS_EMPLOYMENT-TO_DATE.
    P0023-BRANC = PREVIOUS_EMPLOYMENT-INDUSTRY.
    P0023-ARBGB = PREVIOUS_EMPLOYMENT-EMPLOYER.
    P0023-LAND1 = PREVIOUS_EMPLOYMENT-CNTRY.
    P0023-ORT01 = PREVIOUS_EMPLOYMENT-CITY.
    P0023-TAETE = PREVIOUS_EMPLOYMENT-JOB.
    P0023-ANSVX = PREVIOUS_EMPLOYMENT-EMPLOYMENT_CONTRACT.   "4.6C
    APPEND P0023.
  ENDLOOP.

  LOOP AT QUALIFICATION.
    CLEAR P0024.
    P0024-QUALI = QUALIFICATION-QUALIF.
    if not QUALIFICATION-PROFIC is initial.                 "XCZK029699
      P0024-AUSPR = QUALIFICATION-PROFIC.                   "XCZK029699
    elseif not QUALIFICATION-PROFIC_4 is initial.           "XCZK029699
      P0024-AUSPR = QUALIFICATION-PROFIC_4.                 "XCZK029699
    endif.                                                  "XCZK029699
    APPEND P0024.
  ENDLOOP.

*------------------------ XDVN480519 begin -----------------------------
  CALL FUNCTION 'RH_INTEGRATION_CHECK'
         IMPORTING
              INTEGRATED_PLVAR = LV_PLVAR
              ORG_INTEGRATION  = LV_ORGA.
  CLEAR lv_index.
*------------------------ XDVN480519 end -------------------------------

  LOOP AT VACANCY_ASSIGNMENT.
    CLEAR P4002.
    P4002-APRIO = VACANCY_ASSIGNMENT-PRIORITY.
    P4002-APSTV = VACANCY_ASSIGNMENT-VA_STATUS.
    P4002-OBJID = VACANCY_ASSIGNMENT-VACANCY.
    P4002-OTYPE = 'S'.
    P4002-OTYPO = 'AP'.
    P4002-STREA = VACANCY_ASSIGNMENT-STAT_REA.
*------------------------ XDVN480519 begin ----------------------------
    P4002-BEGDA = sy-datum.
    P4002-ENDDA = high_date.
    IF ( NOT LV_PLVAR IS INITIAL ) AND LV_ORGA EQ 'X'.
      CALL FUNCTION 'RH_READ_OBJECT'
        EXPORTING
          PLVAR                 = lv_plvar
          OTYPE                 = 'S'
          OBJID                 = p4002-objid
        IMPORTING
          OBEG                  = lv_obj_begda
          OEND                  = lv_obj_endda
        EXCEPTIONS
          NOT_FOUND             = 1
          OTHERS    = 2.
      IF SY-SUBRC = 0.
        IF lv_obj_endda LT sy-datum.
          CLEAR P4002.
        ELSE.
          P4002-ENDDA = lv_obj_endda.
        ENDIF.
      ENDIF.
    ENDIF.
    IF NOT P4002 IS INITIAL.
      lv_index = lv_index + 1.
      READ TABLE vacancy_assignment INDEX lv_index
                 INTO wa_vacancy_assignment
                 TRANSPORTING priority.
      p4002-aprio = wa_vacancy_assignment-priority.
*------------------------ XDVN480519 end ------------------------------
      APPEND P4002.
    ENDIF.                                                  "XDVN480519
  ENDLOOP.

  CALL FUNCTION 'P_APP_CREATE_APPLICANT'
       EXPORTING
            INFTY_0001         = P0001
            INFTY_0002         = P0002
            INFTY_0006         = P0006
            INFTY_4001         = P4001
            INFTY_4005         = P4005                      "WBIK031606
            INFTY_0105         = P0105                      "WBIK031606
       IMPORTING
            aplno              = applicantnumber
       TABLES
            INFTY_0022         = P0022
            INFTY_0023         = P0023
            INFTY_0024         = P0024
            INFTY_4002         = P4002
       EXCEPTIONS
            NO_UPDATE_POSSIBLE = 1
            OTHERS             = 2.

  IF SY-SUBRC NE 0.
    CALL FUNCTION 'BALW_BAPIRETURN_GET'
         EXPORTING
              TYPE       = 'E'
              CL         = SY-MSGID
              NUMBER     = SY-MSGNO
              PAR1       = SY-MSGV1
              PAR2       = SY-MSGV2
              PAR3       = SY-MSGV3
              PAR4       = SY-MSGV4
         IMPORTING
              BAPIRETURN = RETURN
         EXCEPTIONS
              OTHERS     = 1.
**************************************LAK2181454******************************************
    IF sy-subrc NE 0.
      CALL FUNCTION 'BALW_BAPIRETURN_GET2'
        EXPORTING
          type   = 'E'
          cl     = sy-msgid
          number = sy-msgno
          par1   = sy-msgv1
          par2   = sy-msgv2
          par3   = sy-msgv3
          par4   = sy-msgv4
        IMPORTING
          return = RETURN2.
    ENDIF.
**************************************LAK2181454******************************************
  ENDIF.

ENDFUNCTION.
