FUNCTION BAPI_APPLICATION_CREATE
  IMPORTING
    VALUE(VALIDITYBEGIN) LIKE BAPIP0001-BEGDA
    VALUE(MEDIUM) LIKE BAPIAPPLIC-MEDIUM OPTIONAL
    VALUE(ADVERT) LIKE BAPIAPPLIC-ADVERT OPTIONAL
    VALUE(UNSAPPG) LIKE BAPIAPPLIC-UNSAPPGP OPTIONAL
    VALUE(APPLNUMBER) LIKE BAPIAPPL-PERNR
  EXPORTING
    VALUE(RETURN) LIKE BAPIRETURN1
    VALUE(APPLICANTNUMBER) LIKE BAPIAPPL-PERNR
    VALUE(ENTRYDATE) LIKE BAPIP0001-BEGDA
    VALUE(NUMBER) LIKE BAPIP0001-SEQNR.



  TABLES: P4000, P4001, PME15.         "#EC NEEDED
  DATA: BACK LIKE P0001-SUBTY, SUBRC LIKE SY-SUBRC.
  DATA FEATURE LIKE T549B-NAMEN.
  DATA: APPLICATIONKEY LIKE BAPIPAKEY.
  FIELD-SYMBOLS <STRUC_CONTENT>.
*
  MOVE: APPLNUMBER TO APPLICANTNUMBER,
        VALIDITYBEGIN TO ENTRYDATE.

* Create a new application (IT4001)
  MOVE VALIDITYBEGIN TO P4001-BEGDA.
  MOVE MEDIUM TO P4001-MEDIA.
  MOVE ADVERT TO P4001-OFFID.
  MOVE UNSAPPG TO P4001-SPAPL.
  MOVE APPLICANTNUMBER TO P4001-PERNR.
*
  CALL FUNCTION 'HR_INFOTYPE_OPERATION'
       EXPORTING
            INFTY         = '4001'
            NUMBER        = APPLICANTNUMBER
            VALIDITYBEGIN = VALIDITYBEGIN
            RECORD        = P4001
            OPERATION     = CREATE
            TCLAS         = 'B'
            DIALOG_MODE   = '0'
            NOCOMMIT      = 'X'
       IMPORTING
            KEY           = APPLICATIONKEY
            RETURN        = RETURN.

  MOVE APPLICATIONKEY-RECORDNR TO NUMBER.

  IF RETURN CO ' 0'.
* read feature EVTAP to obtain event which sets applicant status to '1'
    PME15-HGMID = '0'.
    PME15-APSTA = '1'.
    FEATURE = 'EVTAP'.
    ASSIGN ('PME15') TO <STRUC_CONTENT>.
    CALL FUNCTION 'HR_FEATURE_BACKFIELD'
         EXPORTING
              FEATURE                     = FEATURE
              STRUC_CONTENT               = <STRUC_CONTENT>
              KIND_OF_ERROR               = ' '
         IMPORTING
              BACK                        = BACK
         CHANGING
              STATUS                      = SUBRC
         EXCEPTIONS
              DUMMY                       = 1
              ERROR_OPERATION             = 2
              NO_BACKVALUE                = 3
              FEATURE_NOT_GENERATED       = 4
              INVALID_SIGN_IN_FUNID       = 5
              FIELD_IN_REPORT_TAB_IN_PE03 = 6
              ERROR_MESSAGE               = 7
              OTHERS                      = 0.
    IF SY-SUBRC NE 0 OR SUBRC NE 0.
* create error message
      CALL FUNCTION 'BALW_BAPIRETURN_GET1'
           EXPORTING
                TYPE       = 'E'
                CL         = SY-MSGID
                NUMBER     = SY-MSGNO
                PAR1       = SY-MSGV1
                PAR2       = SY-MSGV2
                PAR3       = SY-MSGV3
                PAR4       = SY-MSGV4
           IMPORTING
                BAPIRETURN = RETURN
           EXCEPTIONS
                OTHERS     = 0.
    ELSE.
* set applicant status to '1' (in processing) (IT4000)
      MOVE APPLICANTNUMBER TO P4000-PERNR.
*      MOVE BACK TO P4000-SUBTY.                     "LAK2098209
      MOVE VALIDITYBEGIN TO P4000-BEGDA.
      MOVE HIGH_DATE TO P4000-ENDDA.
      MOVE BACK TO P4000-MASSN.
      MOVE '1' TO P4000-APSTA.
      CALL FUNCTION 'HR_INFOTYPE_OPERATION'
           EXPORTING
                INFTY         = '4000'
                NUMBER        = APPLICANTNUMBER
                VALIDITYEND   = HIGH_DATE
                VALIDITYBEGIN = VALIDITYBEGIN
                RECORD        = P4000
                TCLAS         = 'B'
                OPERATION     = CREATE
                DIALOG_MODE   = '0'
                NOCOMMIT      = 'X'
           IMPORTING
                RETURN        = RETURN.
    ENDIF.
  ENDIF.
ENDFUNCTION.
