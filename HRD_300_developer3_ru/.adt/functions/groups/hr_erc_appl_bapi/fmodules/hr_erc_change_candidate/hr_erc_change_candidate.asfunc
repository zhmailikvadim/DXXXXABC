FUNCTION HR_ERC_CHANGE_CANDIDATE
  IMPORTING
    VALUE(CANDIDATE) TYPE HROBJECT
    VALUE(EXT_APPL_DATA) LIKE BAPI_ERC_MD_EXT
  TABLES
    RETURN LIKE BAPIRET2 OPTIONAL.





* Author: HGB

  DATA: lo_exception TYPE REF TO cx_hrrcf,
        lt_return    TYPE bapirettab.

  CLEAR: return,
         return[].

  TRY.
*     Update candidate's personal data
      PERFORM maintain_pers_data
        TABLES
          lt_return
        USING
          candidate
          ext_appl_data.

      IF lt_return IS NOT INITIAL.
        APPEND LINES OF lt_return TO return.
      ENDIF.

*     Update candidate's target group
      PERFORM assign_target_group
        TABLES
          lt_return
        USING
          candidate
          ext_appl_data-interest_grp.

      IF lt_return IS NOT INITIAL.
        APPEND LINES OF lt_return TO return.
      ENDIF.

*     Update candidate's contact data
      PERFORM update_contact_data_mde
        TABLES
          lt_return
        USING
          candidate
          ext_appl_data.

      IF lt_return IS NOT INITIAL.
        APPEND LINES OF lt_return TO return.
      ENDIF.

    CATCH cx_hrrcf INTO lo_exception.
      CALL METHOD cl_hrrcf_exception_handler=>write_exception_log
        EXPORTING
          ex      = lo_exception.
      CALL FUNCTION 'BALW_BAPIRETURN_GET2'
        EXPORTING
          type    = lo_exception->message_type
          cl      = lo_exception->message_class
          number  = lo_exception->message_number
          par1    = lo_exception->message_par1
          par2    = lo_exception->message_par2
          par3    = lo_exception->message_par3
          par4    = lo_exception->message_par4
        IMPORTING
          return  = return.
      APPEND return TO return.
  ENDTRY.

  DELETE return WHERE type NE c_error AND type NE c_abort.

ENDFUNCTION.
