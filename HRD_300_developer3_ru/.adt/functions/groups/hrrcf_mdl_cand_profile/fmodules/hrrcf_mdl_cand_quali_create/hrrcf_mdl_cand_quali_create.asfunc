FUNCTION HRRCF_MDL_CAND_QUALI_CREATE
  IMPORTING
    VALUE(RECORD) TYPE RCF_S_MDL_CAND_QUALIFICATION
    VALUE(CAND_HROBJECT) TYPE HROBJECT OPTIONAL
  EXPORTING
    VALUE(MESSAGES) TYPE BAPIRETTAB.




  DATA ls_cand_hrobject TYPE hrobject.
  DATA lo_bl TYPE REF TO cl_hrrcf_candidate_infotype_bl.
  DATA lo_ex TYPE REF TO cx_root.
  DATA ls_message TYPE bapiret2.

* clear returning parameters
  REFRESH messages.

  TRY.

*     get candidate hrobject for user, if necessary
      IF NOT cand_hrobject IS SUPPLIED.
        CALL METHOD cl_hrrcf_candidate_admin_bl=>get_candidate
          EXPORTING
            p_user          = sy-uname
          IMPORTING
            p_cand_hrobject = ls_cand_hrobject.
      ELSE.
*     check whether imported candidate object belongs to user
        IF cl_hrrcf_candidate_admin_bl=>is_cand_of_current_user( is_cand = cand_hrobject ) = abap_true.
          ls_cand_hrobject = cand_hrobject.
        ELSE.
          RAISE EXCEPTION TYPE cx_hrrcf_authority
            EXPORTING
              message_class  = 'HRRCF0002'
              message_number = '300'
              message_type   = 'A'.
        ENDIF.
      ENDIF.

*     get instance of business logic facade
      CALL METHOD cl_hrrcf_candidate_infotype_bl=>get_instance
        RECEIVING
          return = lo_bl.

      CHECK lo_bl IS BOUND.

*     set LUW mode
      cl_hrrcf_simple_main_object=>set_luw_mode( ).
      SET UPDATE TASK LOCAL.

*     save additional qualification
      IF record-q_object_type EQ 'QK'.
        CALL METHOD lo_bl->maintain_additional_qualis
          EXPORTING
            ps_cand_hrobject      = ls_cand_hrobject
            ps_cand_qualification = record-p5105
            p_operation           = 'I'
            p_description         = record-additional_quali_txt
          IMPORTING
            pt_return             = messages.

*     insert infotype data
      ELSE.
        CALL METHOD lo_bl->maintain_cand_qualification
          EXPORTING
            ps_cand_hrobject = ls_cand_hrobject
            ps_qualification = record-p5105
            p_operation      = 'I'
          IMPORTING
            pt_return        = messages.
      ENDIF.

    CATCH cx_root INTO lo_ex.
*     write exception to slg1
      cl_hrrcf_exception_handler=>write_exception_log( ex = lo_ex ).

*--   Convert exception into BAPI return parameter
      IF 1 = 0.
        MESSAGE a802(hrrcf0002) WITH 'HRRCF_MDL_CAND_QUALI_CREATE'.
      ENDIF.
      CALL FUNCTION 'BALW_BAPIRETURN_GET2'
        EXPORTING
          type   = 'A'
          cl     = 'HRRCF0002'
          number = '802'
          par1   = 'HRRCF_MDL_CAND_QUALI_CREATE'
        IMPORTING
          return = ls_message.
      APPEND ls_message TO messages.
  ENDTRY.

ENDFUNCTION.
