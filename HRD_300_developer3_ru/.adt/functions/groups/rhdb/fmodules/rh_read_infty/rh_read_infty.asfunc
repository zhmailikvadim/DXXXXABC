FUNCTION RH_READ_INFTY
  IMPORTING
    VALUE(AUTHORITY) TYPE ANY DEFAULT 'DISP'
    VALUE(WITH_STRU_AUTH) TYPE ANY DEFAULT 'X'
    VALUE(PLVAR) TYPE PLOG-PLVAR OPTIONAL
    VALUE(OTYPE) TYPE PLOG-OTYPE OPTIONAL
    VALUE(OBJID) TYPE PLOG-OBJID OPTIONAL
    VALUE(INFTY) LIKE PLOG-INFTY DEFAULT SPACE
    VALUE(ISTAT) LIKE PLOG-ISTAT DEFAULT SPACE
    VALUE(EXTEND) LIKE HRRHDB-EXTEND DEFAULT 'X'
    VALUE(SUBTY) LIKE PLOG-SUBTY DEFAULT SPACE
    VALUE(BEGDA) LIKE PLOG-BEGDA DEFAULT '19000101'
    VALUE(ENDDA) LIKE PLOG-ENDDA DEFAULT '99991231'
    VALUE(CONDITION) LIKE HRRHDB-CONDITION DEFAULT '00000'
    VALUE(INFTB) LIKE PPPCH-INFTB DEFAULT '1'
    VALUE(SORT) LIKE HRRHDB-SORT DEFAULT 'X'
    VALUE(VIA_T777D) TYPE ANY DEFAULT SPACE
  TABLES
    INNNN TYPE STANDARD TABLE
    OBJECTS LIKE HROBJECT OPTIONAL
  EXCEPTIONS
    ALL_INFTY_WITH_SUBTY
    NOTHING_FOUND
    NO_OBJECTS
    WRONG_CONDITION
    WRONG_PARAMETERS.



  FIELD-SYMBOLS : <SORT>.
*  DATA : BEGIN OF ALL_TAB OCCURS 10.                   "ANDUNI
*          INCLUDE STRUCTURE WPLOG.                     "ANDUNI
*  DATA : END   OF ALL_TAB.                             "ANDUNI
  DATA : OBJECTS_LINES TYPE I.
  DATA : INNNN_LINES   TYPE I.
  DATA : BEGIN OF REQ_OTYPE OCCURS 1,
           OTYPE LIKE PLOG-OTYPE,
         END   OF REQ_OTYPE.
  DATA : BEGIN OF REQ_INFTY OCCURS 1,
           INFTY LIKE PLOG-INFTY,
         END   OF REQ_INFTY.

  data : type_innnn type ref to data.                   "ANDUNI
  field-symbols: <lcl_innnn> type standard table.       "ANDUNI

  data: lcl_subrc type sy-subrc.                      "ANDXX

  PERFORM CLIENT_SET.

  REFRESH : INNNN.

  PERFORM CHECK_OBJECT_INPUT                          "ANDXX
              TABLES
                 OBJECTS
              USING
                 PLVAR
                 OTYPE
                 OBJID
                 lcl_subrc.

  case lcl_subrc.                                     "ANDXX
   when 0.                                            "ANDXX
   when 1.                                            "ANDXX
     raise wrong_parameters.                          "ANDXX
   when 2.                                            "ANDXX
     raise nothing_found.                             "ANDXX
  endcase.                                            "ANDXX

*  DESCRIBE TABLE OBJECTS LINES OBJECTS_LINES.        "ANDXX
*  IF OBJECTS_LINES EQ 0.                             "ANDXX
*    RAISE NO_OBJECTS.                                "ANDXX
*  ENDIF.                                             "ANDXX

  IF AUTHORITY EQ SPACE.
    CLEAR WITH_STRU_AUTH.
  ENDIF.

  CASE INFTY.
    WHEN '1001'.
      CALL FUNCTION 'RH_READ_INFTY_1001'
           EXPORTING
                AUTHORITY       = AUTHORITY
                WITH_STRU_AUTH  = WITH_STRU_AUTH
                PLVAR           = PLVAR
                OTYPE           = OTYPE
                OBJID           = OBJID
                ISTAT           = ISTAT
                EXTEND          = EXTEND
                SUBTY           = SUBTY
                BEGDA           = BEGDA
                ENDDA           = ENDDA
                SORT            = SORT
                CONDITION       = CONDITION
           TABLES
                I1001           = INNNN
                OBJECTS         = OBJECTS
           EXCEPTIONS
                WRONG_CONDITION = 4
                OTHERS          = 8.
      IF SY-SUBRC EQ 4.
        RAISE WRONG_CONDITION.
      ENDIF.
    WHEN SPACE.
      IF SUBTY NE SPACE.
        RAISE ALL_INFTY_WITH_SUBTY.
      ENDIF.
      DESCRIBE TABLE OBJECTS LINES OBJECTS_LINES.      "ANDXX
      IF OBJECTS_LINES EQ 0.                           "ANDXX
        RAISE wrong_parameters.                        "ANDXX
      ENDIF.                                           "ANDXX
      REFRESH : REQ_OTYPE, REQ_INFTY.
      LOOP AT OBJECTS.
        IF WITH_STRU_AUTH NE SPACE.
          CALL FUNCTION 'RH_STRU_AUTHORITY_CHECK'
               EXPORTING
                    FCODE  = AUTHORITY
                    PLVAR  = OBJECTS-PLVAR
                    OTYPE  = OBJECTS-OTYPE
                    OBJID  = OBJECTS-OBJID
               EXCEPTIONS
                    OTHERS = 4.
          IF SY-SUBRC GT 0.
            DELETE OBJECTS.
            CONTINUE.
          ENDIF.
        ENDIF.
        REQ_OTYPE-OTYPE = OBJECTS-OTYPE.
        COLLECT REQ_OTYPE.
      ENDLOOP.
      DESCRIBE TABLE OBJECTS LINES OBJECTS_LINES.
      IF OBJECTS_LINES EQ 0.
        RAISE NOTHING_FOUND.
      ENDIF.
      IF VIA_T777D EQ SPACE.
        LOOP AT REQ_OTYPE.
          SELECT * FROM T777I CLIENT SPECIFIED
                              INTO TABLE I777I
                              WHERE MANDT EQ RHDB_CLIENT
                              AND   OTYPE EQ REQ_OTYPE-OTYPE.
          LOOP AT I777I.
            CHECK NOT I777I-INFTY IS INITIAL.
            REQ_INFTY-INFTY = I777I-INFTY.
            COLLECT REQ_INFTY.
          ENDLOOP.
        ENDLOOP.
      ELSE.
        PERFORM FILL_I777D.
        LOOP AT I777D.
          CHECK I777D-INFTY NE '1022'.
          APPEND I777D-INFTY TO REQ_INFTY.
        ENDLOOP.
      ENDIF.
      create data type_innnn like standard table of innnn. "ANDUNI
      assign type_innnn->* to <lcl_innnn>.                 "ANDUNI
      LOOP AT REQ_INFTY.
*        REFRESH ALL_TAB.                                  "ANDUNI
        refresh <lcl_innnn>.                               "ANDUNI
        CALL FUNCTION 'RH_READ_INFTY'
             EXPORTING
                  AUTHORITY       = AUTHORITY
                  WITH_STRU_AUTH  = SPACE
                  PLVAR           = PLVAR
                  OTYPE           = OTYPE
                  OBJID           = OBJID
                  INFTY           = REQ_INFTY-INFTY
                  ISTAT           = ISTAT
                  EXTEND          = EXTEND
                  SUBTY           = SUBTY
                  BEGDA           = BEGDA
                  ENDDA           = ENDDA
                  SORT            = SPACE
                  INFTB           = INFTB
                  CONDITION       = CONDITION
             TABLES
*                  INNNN           = ALL_TAB            "ANDUNI
                  INNNN           = <lcl_innnn>         "ANDUNI
                  OBJECTS         = OBJECTS
             EXCEPTIONS
                  OTHERS          = 0.
*        APPEND LINES OF ALL_TAB TO INNNN.              "ANDUNI
        APPEND LINES OF <lcl_innnn> TO INNNN.           "ANDUNI
      ENDLOOP.
      IF SORT NE SPACE.
        ASSIGN INNNN(WPLOG_KEY_LENGTH) TO <SORT>.
        SORT INNNN BY <SORT>.
      ENDIF.
    WHEN '1000'.
      CALL FUNCTION 'RH_READ_INFTY_1000'
           EXPORTING
                AUTHORITY       = AUTHORITY
                WITH_STRU_AUTH  = WITH_STRU_AUTH
                PLVAR           = PLVAR
                OTYPE           = OTYPE
                OBJID           = OBJID
                ISTAT           = ISTAT
                EXTEND          = EXTEND
                BEGDA           = BEGDA
                ENDDA           = ENDDA
                SORT            = SORT
                CONDITION       = CONDITION
           TABLES
                I1000           = INNNN
                OBJECTS         = OBJECTS
           EXCEPTIONS
                WRONG_CONDITION = 4
                OTHERS          = 8.
      IF SY-SUBRC EQ 4.
        RAISE WRONG_CONDITION.
      ENDIF.
    WHEN OTHERS.
      CALL FUNCTION 'RH_READ_INFTY_NNNN'
           EXPORTING
                AUTHORITY       = AUTHORITY
                WITH_STRU_AUTH  = WITH_STRU_AUTH
                PLVAR           = PLVAR
                OTYPE           = OTYPE
                OBJID           = OBJID
                INFTY           = INFTY
                ISTAT           = ISTAT
                EXTEND          = EXTEND
                SUBTY           = SUBTY
                BEGDA           = BEGDA
                ENDDA           = ENDDA
                SORT            = SORT
                INFTB           = INFTB
                CONDITION       = CONDITION
           TABLES
                INNNN           = INNNN
                OBJECTS         = OBJECTS
           EXCEPTIONS
                WRONG_CONDITION = 4
                OTHERS          = 8.
      IF SY-SUBRC EQ 4.
        RAISE WRONG_CONDITION.
      ENDIF.
  ENDCASE.

  DESCRIBE TABLE INNNN LINES INNNN_LINES.
  IF INNNN_LINES EQ 0.
    RAISE NOTHING_FOUND.
  ENDIF.
ENDFUNCTION.
