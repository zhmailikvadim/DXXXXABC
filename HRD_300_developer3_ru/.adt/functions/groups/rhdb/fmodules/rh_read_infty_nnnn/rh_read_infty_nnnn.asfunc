FUNCTION RH_READ_INFTY_NNNN
  IMPORTING
    VALUE(AUTHORITY) TYPE ANY DEFAULT 'DISP'
    VALUE(WITH_STRU_AUTH) TYPE ANY DEFAULT 'X'
    VALUE(PLVAR) TYPE PLOG-PLVAR OPTIONAL
    VALUE(OTYPE) TYPE PLOG-OTYPE OPTIONAL
    VALUE(OBJID) TYPE PLOG-OBJID OPTIONAL
    VALUE(INFTY) LIKE PLOG-INFTY
    VALUE(ISTAT) LIKE PLOG-ISTAT DEFAULT SPACE
    VALUE(EXTEND) LIKE HRRHDB-EXTEND DEFAULT 'X'
    VALUE(SUBTY) LIKE PLOG-SUBTY DEFAULT SPACE
    VALUE(BEGDA) LIKE PLOG-BEGDA DEFAULT '19000101'
    VALUE(ENDDA) LIKE PLOG-ENDDA DEFAULT '99991231'
    VALUE(CONDITION) LIKE HRRHDB-CONDITION DEFAULT '00000'
    VALUE(INFTB) LIKE PPPCH-INFTB DEFAULT '1'
    VALUE(SORT) LIKE HRRHDB-SORT DEFAULT 'X'
  TABLES
    INNNN TYPE STANDARD TABLE
    OBJECTS LIKE HROBJECT OPTIONAL
  EXCEPTIONS
    NOTHING_FOUND
    WRONG_CONDITION
    INFOTYP_NOT_SUPPORTED
    WRONG_PARAMETERS.



  data: type_innnn type ref to data.                     "ANDUNI
  FIELD-SYMBOLS : <SORT>.
  field-symbols: <innnn> type ppkey.                  "ANDXX

  data: lcl_subrc type sy-subrc.                      "ANDXX

  REFRESH : INNNN.

  IF INFTY = '1000' OR
     INFTY = '1001'.
    MESSAGE E380(5A) WITH INFTY RAISING INFOTYP_NOT_SUPPORTED.
  ENDIF.

  IF AUTHORITY EQ SPACE.
    CLEAR WITH_STRU_AUTH.
  ENDIF.

  IF EXTEND EQ 'D'.
    PERFORM GET_DYN_COND USING CONDITION.
    IF DYN_COND_LINES EQ 0 AND SUBTY_COND_LINES EQ 0.
      RAISE WRONG_CONDITION.
    ENDIF.
  ENDIF.

  PERFORM CHECK_OBJECT_INPUT                          "ANDXX
              TABLES
                 OBJECTS
              USING
                 PLVAR
                 OTYPE
                 OBJID
                 lcl_subrc.

  case lcl_subrc.                                     "ANDXX
   when 0.                                            "ANDXX
   when 1.                                            "ANDXX
     raise wrong_parameters.                          "ANDXX
   when 2.                                            "ANDXX
     raise nothing_found.                             "ANDXX
  endcase.                                            "ANDXX


  DESCRIBE TABLE OBJECTS LINES OBJECTS_LINES.

  IF OBJECTS_LINES EQ 0.
*    RAISE NOTHING_FOUND.                             "ANDXX
*   Nur PLVAR wurde übergeben. Es sollen also alle    "ANDXX
*   Objekte in dieser Planvariante selektiert werden, "ANDXX
*   die den anderen Selektionsbedingungen entsprechen."ANDXX
    CLEAR OTJID_RANGE.                                "ANDXX
    REFRESH OTJID_RANGE.                              "ANDXX
    CALL FUNCTION 'RH_BASE_READ_INFTY_NNNN'           "ANDXX
         EXPORTING
              AUTHORITY      = AUTHORITY
              WITH_STRU_AUTH = SPACE
              BEGDA          = BEGDA
              ENDDA          = ENDDA
              EXTEND         = EXTEND
              ISTAT          = ISTAT
              PLVAR          = PLVAR
              SORT           = SORT
              SUBTY          = SUBTY
              INFTY          = INFTY
              INFTB          = INFTB
         TABLES
              INNNN          = INNNN
              RANGE          = OTJID_RANGE
         EXCEPTIONS
              OTHERS         = 4.
    IF SY-SUBRC GT 0.
      RAISE NOTHING_FOUND.
    ENDIF.

*  Berechtigungsprüfung für das Quellobjekt, falls gefordert.
   if with_stru_auth = 'X'.                                   "ANDXX
     assign innnn to <innnn> casting.                         "ANDXX
     loop at innnn.                                           "ANDXX
       CALL FUNCTION 'RH_STRU_AUTHORITY_CHECK'                "ANDXX
          EXPORTING
               FCODE                    = AUTHORITY
               PLVAR                    = <innnn>-plvar
               OTYPE                    = <innnn>-otype
               OBJID                    = <innnn>-objid
          EXCEPTIONS
               NO_STRU_AUTHORITY        = 1
               NO_STRU_AUTHORITY_HYPER  = 2
               NO_STRU_AUTHORITY_AT_ALL = 3
               NO_BASE_AUTHORITY        = 4
               OTHERS                   = 5.

        IF SY-SUBRC <> 0.
          delete innnn.
        ENDIF.
      endloop.
    endif.

  ELSEIF OBJECTS_LINES EQ 1.
    READ TABLE OBJECTS INDEX 1.           "#EC CI_NOORDER
    IF WITH_STRU_AUTH NE SPACE.
      CALL FUNCTION 'RH_STRU_AUTHORITY_CHECK'
           EXPORTING
                FCODE  = AUTHORITY
                PLVAR  = OBJECTS-PLVAR
                OTYPE  = OBJECTS-OTYPE
                OBJID  = OBJECTS-OBJID
           EXCEPTIONS
                OTHERS = 4.
      IF SY-SUBRC GT 0.
        RAISE NOTHING_FOUND.
      ENDIF.
    ENDIF.
    CLEAR OTJID_RANGE.
    REFRESH OTJID_RANGE.
    OTJID_RANGE-SIGN   = 'I'.
    OTJID_RANGE-OPTION = 'EQ'.
    OTJID_RANGE-LOW    = OBJECTS-OTYPE.
    OTJID_RANGE-LOW+2  = OBJECTS-OBJID.
    APPEND OTJID_RANGE.
    CALL FUNCTION 'RH_BASE_READ_INFTY_NNNN'
         EXPORTING
              AUTHORITY      = AUTHORITY
              WITH_STRU_AUTH = SPACE
              BEGDA          = BEGDA
              ENDDA          = ENDDA
              EXTEND         = EXTEND
              ISTAT          = ISTAT
              PLVAR          = OBJECTS-PLVAR
              SORT           = SORT
              SUBTY          = SUBTY
              INFTY          = INFTY
              INFTB          = INFTB
         TABLES
              INNNN          = INNNN
              RANGE          = OTJID_RANGE
         EXCEPTIONS
              OTHERS         = 4.
    IF SY-SUBRC GT 0.
      RAISE NOTHING_FOUND.
    ENDIF.
  ELSE.
*    REFRESH : SEL_NNNN.                                    "ANDUNI
    SEL_AUTH   = AUTHORITY.
    SEL_EXTEND = EXTEND.
    SEL_INFTY  = INFTY.
    SEL_SUBTY  = SUBTY.
    SEL_ISTAT  = ISTAT.
    SEL_BEGDA  = BEGDA.
    SEL_ENDDA  = ENDDA.
    SEL_INFTB  = INFTB.

    create data type_innnn like standard table of innnn.    "ANDUNI
    assign type_innnn->* to <sel_nnnn>.                     "ANDUNI

    CALL FUNCTION 'RH_BASE_PARTITION'
         EXPORTING
              FORM            = 'READ_IT_NNNN'
              REPID           = 'SAPLRHDB'
              authority       = space                      "ANDAUTH
              CHECK_STRU_AUTH = WITH_STRU_AUTH
         TABLES
              OBJECTS         = OBJECTS.

*   IF SORT NE SPACE.                                      "ANDUNI
*     ASSIGN SEL_NNNN(WPLOG_KEY_LENGTH) TO <SORT>.         "ANDUNI
*     SORT SEL_NNNN BY <SORT>.                             "ANDUNI
*   ENDIF.                                                 "ANDUNI
*    INNNN[] = SEL_NNNN[].                                 "ANDUNI
    innnn[] = <sel_nnnn>[].                                "ANDUNI

    IF SORT NE SPACE.                                      "ANDUNI
      ASSIGN INNNN(WPLOG_KEY_LENGTH) TO <SORT>.            "ANDUNI
      SORT INNNN BY <SORT>.                                "ANDUNI
    endif.                                                 "ANDUNI

    DESCRIBE TABLE INNNN LINES SY-TFILL.
    IF SY-TFILL EQ 0.
      RAISE NOTHING_FOUND.
    ENDIF.
  ENDIF.

  CLEAR : ACTUAL_CONDITION, DYN_COND_LINES, SUBTY_COND_LINES.

ENDFUNCTION.

*---------------------------------------------------------------------*
*       FORM READ_IT_NNNN                                             *
*---------------------------------------------------------------------*
*       Lesen Infotyp NNNN                                            *
*---------------------------------------------------------------------*
*  -->  RIN_RANGE                                                     *
*  -->  RIN_OBJECTS                                                   *
*  -->  RIN_PLVAR                                                     *
*---------------------------------------------------------------------*
FORM READ_IT_NNNN TABLES RIN_RANGE
                         RIN_OBJECTS STRUCTURE HROBJECT
                  USING  RIN_PLVAR.  "#EC CALLED

  CALL FUNCTION 'RH_BASE_READ_INFTY_NNNN'
       EXPORTING
            AUTHORITY      = SEL_AUTH
            WITH_STRU_AUTH = SPACE
            BEGDA          = SEL_BEGDA
            ENDDA          = SEL_ENDDA
            EXTEND         = SEL_EXTEND
            ISTAT          = SEL_ISTAT
            PLVAR          = RIN_PLVAR
            SORT           = SPACE
            SUBTY          = SEL_SUBTY
            INFTY          = SEL_INFTY
            INFTB          = SEL_INFTB
       TABLES
*            INNNN          = SEL_NNNN                     "ANDUNI
            INNNN          = <sel_nnnn>                    "ANDUNI
            RANGE          = RIN_RANGE
       EXCEPTIONS
            OTHERS         = 0.

ENDFORM.
