FUNCTION CHANGEDOCUMENT_OPEN
  IMPORTING
    VALUE(OBJECTCLASS) LIKE CDHDR-OBJECTCLAS
    VALUE(OBJECTID) LIKE CDHDR-OBJECTID
    VALUE(PLANNED_CHANGE_NUMBER) LIKE CDHDR-PLANCHNGNR DEFAULT SPACE
    VALUE(PLANNED_OR_REAL_CHANGES) LIKE CDHDR-CHANGE_IND DEFAULT SPACE
  EXCEPTIONS
    SEQUENCE_INVALID.






  IF sequence NE space.
*    Es wurde fuer das letzte Objekt kein CLOSE durchgefuehrt
    MESSAGE e327 WITH 'SEQUENCE_INVALID' 'CDOK'
                 RAISING sequence_invalid.
  ENDIF.

  CLEAR: ins_cdpos[], gt_cdpos_uid[], gt_cdpos_str[], gs_cdpos_uid,
         anz_insert, aendnr, gs_cdpos_str.
  objektklasse = objectclass.
  objektid     = objectid.
  plan_number  = plan_number_act   = planned_change_number.
  sequence     = inhomogen_version = 'X'.

  IF gv_chartextlen IS INITIAL.
* hole die max. Länge des Feldes cdtxt-textart
    descr_ref = cl_abap_typedescr=>describe_by_data( ls_cdtxt-textart ).
    elemdescr_ref ?= descr_ref.
    gv_chartextlen = elemdescr_ref->output_length.
  ENDIF.
* hole einmalig die max. Länge des Feldes CDPOS-TABKEY
  IF gv_cdpostabkeylen IS INITIAL.
    descr_ref = cl_abap_typedescr=>describe_by_data( gs_cdpos-tabkey ).
    elemdescr_ref ?= descr_ref.
    gv_cdpostabkeylen = elemdescr_ref->output_length.
  ENDIF.

* feststellen, ob geplante oder echte Aenderungen           "B20K022940
  CASE planned_or_real_changes.                             "B20K022940
    WHEN real.                                              "B20K022940
      CLEAR plan_number.                                    "B20K022940
    WHEN planned.                                           "B20K022940
      plan_number = planned.                                "B20K022940
  ENDCASE.                                                  "B20K022940

ENDFUNCTION.
