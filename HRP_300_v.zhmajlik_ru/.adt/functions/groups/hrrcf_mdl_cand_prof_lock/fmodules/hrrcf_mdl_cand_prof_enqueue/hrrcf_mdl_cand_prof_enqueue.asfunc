FUNCTION HRRCF_MDL_CAND_PROF_ENQUEUE
  IMPORTING
    IS_CAND_HROBJECT TYPE HROBJECT
    IV_PLOGSYS TYPE CSL_DE-EOLS OPTIONAL
  EXPORTING
    ET_MESSAGES TYPE BAPIRETTAB.



* Author: PEV
  DATA:
    ls_cand_hrobject       TYPE hrobject,
    lv_enqueueargument     TYPE csl_de-eoargs,
    l_ret2                 TYPE bapiret2,
    lv_dest                TYPE rfcdest,
    lv_client              TYPE sy-mandt,
    l_text                 TYPE string,
    l_par1                 TYPE symsgv,
    l_par2                 TYPE symsgv,
    lo_ex                  TYPE REF TO cx_root.


  CLEAR et_messages[].

* 1. SAP lock
  IF iv_plogsys IS INITIAL.
    CALL METHOD cl_hrrcf_wd_services=>get_bl_destination
      RECEIVING
        rfc_dest = lv_dest.
  ELSE.
    lv_dest = iv_plogsys.
  ENDIF.

  CALL FUNCTION 'HRRCF_MDL_CAND_SET_LOCK' DESTINATION lv_dest
    EXPORTING
      is_cand_hrobject = is_cand_hrobject
    IMPORTING
      et_messages      = et_messages.

* 2. If C2 scenario then set CSL
  IF iv_plogsys IS NOT INITIAL.

    CALL FUNCTION 'HRRCF_MDL_GET_LOGSYS_CLIENT' DESTINATION lv_dest
      EXPORTING
        iv_logsys        = iv_plogsys
      IMPORTING
        ev_client        = lv_client
      EXCEPTIONS
        logsys_not_found = 1
        OTHERS           = 2.

    IF sy-subrc <> 0.
      IF 1 = 0.
*       For where-used list
        MESSAGE a124(hrrcf0002).
      ENDIF.
      CALL FUNCTION 'BALW_BAPIRETURN_GET2'
        EXPORTING
          type   = 'A'
          cl     = 'HRRCF0002'
          number = '124'
        IMPORTING
          return = l_ret2.
      APPEND l_ret2 TO et_messages.
    ELSE.

      CONCATENATE lv_client is_cand_hrobject INTO lv_enqueueargument.

      CALL FUNCTION 'CSLEO_ENQUEUE'
        EXPORTING
          plogsys           = iv_plogsys
          pddicenqobj       = 'EHPLOGIE'
          penqueueargstring = lv_enqueueargument
*         PMONINFO          = ' '
          pactivation       = 'LCT'
          pscope            = '1'
*         PWAIT             = ' '
*         PCOLLECT          = ' '
*         PEOCOLS           = ' '
*         PEOCOUS           = ' '
        EXCEPTIONS
          foreign_lock      = 1
          no_connection     = 2
          inconsistency     = 3
          OTHERS            = 4.

      IF sy-subrc <> 0.
        l_text = cl_wd_utilities=>get_otr_text_by_alias(
'paoc_rcf_ui/candidate' ).
        l_par1 = l_text.
        l_par2 = is_cand_hrobject-objid.
        IF 1 EQ 2.
*       For where-used list
          MESSAGE e101(hrrcf0002) WITH l_par1 l_par2.       "#EC *
*       The object (&1: &2) cannot be locked for editing
        ENDIF.
        CALL FUNCTION 'BALW_BAPIRETURN_GET2'
          EXPORTING
            type   = 'A'
            cl     = 'HRRCF0002'
            number = '101'
          IMPORTING
            return = l_ret2.
        APPEND l_ret2 TO et_messages.

      ENDIF.
    ENDIF.
  ENDIF.

ENDFUNCTION.
