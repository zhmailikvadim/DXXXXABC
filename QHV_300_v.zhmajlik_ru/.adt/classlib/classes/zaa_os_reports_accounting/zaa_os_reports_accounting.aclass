CLASS zaa_os_reports_accounting DEFINITION
  PUBLIC
  INHERITING FROM cl_gos_service
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_gos_service_multiple .

    METHODS execute
        REDEFINITION .
  PROTECTED SECTION.

    TYPES:
      BEGIN OF lty_s_menu.
        INCLUDE TYPE zaa_gos_menu.
    TYPES: name TYPE trdir-name,
           END OF lty_s_menu .

    DATA gv_gos_type TYPE zaa_gos_menu_type VALUE 'ACCOUNT' ##NO_TEXT.
    DATA:
      ct_menu TYPE STANDARD TABLE OF lty_s_menu .

    METHODS check_status
        REDEFINITION .
  PRIVATE SECTION.
ENDCLASS.



CLASS ZAA_OS_REPORTS_ACCOUNTING IMPLEMENTATION.


  METHOD check_status.
    ep_status = SWITCH #( is_lporb-typeid WHEN 'BUS1022' THEN mp_status_active ELSE mp_status_invisible ) .
    IF ep_status = mp_status_active.
      me->if_gos_service_multiple~gp_active = sppf_true.
    ENDIF.

  ENDMETHOD.


  METHOD execute.
  ENDMETHOD.


  METHOD if_gos_service_multiple~execute_function.
    DATA:
      BEGIN OF ls_anln_key,
        bukrs TYPE anla-bukrs,
        anln1 TYPE anla-anln1,
        anln2 TYPE anla-anln2,
      END OF ls_anln_key.

    ls_anln_key = gs_lporb.

    ASSIGN ct_menu[ report = ip_fcode ] TO FIELD-SYMBOL(<ls_menu>).
    IF sy-subrc NE 0.
      RETURN.
    ENDIF.

    IF <ls_menu>-skip_first_screen = abap_true.
      SUBMIT (ip_fcode)
        WITH p_bukrs = ls_anln_key-bukrs WITH p_anln1 = ls_anln_key-anln1 WITH p_anln2 = ls_anln_key-anln2
        AND RETURN.
    ELSE.
      SUBMIT (ip_fcode) WITH p_bukrs = ls_anln_key-bukrs
        WITH p_anln1 = ls_anln_key-anln1 WITH p_anln2 = ls_anln_key-anln2
        VIA SELECTION-SCREEN AND RETURN.
    ENDIF.
  ENDMETHOD.


  METHOD if_gos_service_multiple~get_function_list.
    DATA: lt_menu TYPE STANDARD TABLE OF lty_s_menu.

    SELECT a1~*, a2~name FROM zaa_gos_menu AS a1
      LEFT JOIN trdir AS a2 ON a2~name = a1~report AND a2~subc = '1'   "Выполняемая программа
      INTO TABLE @ct_menu[]
      WHERE a1~type = @me->gv_gos_type
      ORDER BY a1~menu_pos.

    "Если в Name нет значения - отчёт с таким именем не найден - пометить inactive
    et_functions[] = VALUE #(
      FOR <ls_menu> IN ct_menu[]
      ( VALUE sgos_msrv(
        function = <ls_menu>-report
        text = <ls_menu>-title
        status = SWITCH #( <ls_menu>-name
          WHEN '' THEN mp_status_inactive
          ELSE mp_status_active ) ) )
      ).

  ENDMETHOD.
ENDCLASS.