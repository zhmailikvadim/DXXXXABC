CLASS zaa_zsum_calc DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_amdp_marker_hdb.
    TYPES:
      BEGIN OF gty_s_date,
        budat TYPE d,              "дата действия
        period type char6,
        year  TYPE anlc-gjahr,
        month TYPE anek-monat,
        cond  TYPE char1,
      END OF gty_s_date .
    TYPES:
    gty_t_date TYPE STANDARD TABLE OF gty_s_date.

    CLASS-METHODS:
      get_dates
        IMPORTING
          VALUE(iv_bukrs) TYPE anla-bukrs
          VALUE(iv_anln1) TYPE anla-anln1
          VALUE(iv_anln2) TYPE anla-anln2
          VALUE(iv_afabe) TYPE anlb-afabe
          VALUE(iv_date)  TYPE d
        EXPORTING
          VALUE(et_dates) TYPE gty_t_date.

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZAA_ZSUM_CALC IMPLEMENTATION.


  METHOD get_dates
         BY DATABASE PROCEDURE FOR HDB LANGUAGE SQLSCRIPT
         USING  setleaf anla zrevdate faav_anep faav_anek.

    declare lv_mandt string := session_context('CLIENT');
    declare lv_year STRING := LEFT(:iv_date,4);
    declare lv_month STRING := month(:iv_date);
    et_dates =
        SELECT anek.budat as budat,
               substring (budat, 1, 6) as period,
               year (budat) as year,
               substring (budat, 5, 2) as month,
               'M' as cond
               FROM faav_anep
               JOIN faav_anek AS anek ON anek.mandt = faav_anep.mandt
                                     and anek.bukrs = faav_anep.bukrs
                                     and anek.anln1 = faav_anep.anln1
                                     and anek.anln2 = faav_anep.anln2
                                     and anek.lnran = faav_anep.lnran
                 where faav_anep.mandt = :lv_mandt
                   AND faav_anep.bukrs = :iv_bukrs
                   AND faav_anep.anln1 = :iv_anln1
                   AND faav_anep.anln2 = :iv_anln2
                   AND faav_anep.afabe = :iv_afabe
                   AND faav_anep.bwasl in ( select valfrom
                                              from setleaf
                                             where setclass = '0000'
                                               and subclass = ''
                                               and setname  = 'Z_TTYPE_AMORT' )
                   and anek.budat < :iv_date
        union all
        select z.vallast as budat,
               substring (z.vallast, 1, 6) as period,
               year (z.vallast) as year,
               month (z.vallast) as month,
               'M' as cond
               FROM anla AS a
               INNER JOIN zrevdate AS z ON z.invnr = a.invnr
               where  a.bukrs = :iv_bukrs
                 AND a.anln1 = :iv_anln1
                 AND a.anln2 = :iv_anln2
                 AND z.vallast < :iv_date
        ;

  endmethod.
ENDCLASS.