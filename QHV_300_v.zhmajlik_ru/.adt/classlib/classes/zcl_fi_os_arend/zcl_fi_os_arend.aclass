*********************************************************************
* Описание : Ведомость наличия основных средств, сданных в аренду
* Код ТС   : АСУБН.САП-КСУП.DEV.030-БНУ.55.O-131
* Разработчик : Ворожун Ю.С.
* Постановка задачи : Ганжа Т.И.
*********************************************************************
CLASS zcl_fi_os_arend DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
  INTERFACES if_amdp_marker_hdb.

  TYPES:
     BEGIN OF gty_s_data,
         pos              TYPE i,
         bukrs            TYPE bukrs,
         name_bukrs       TYPE char210,
         ktansw           TYPE ktansw,                 "Счет, субсчет
         name_ktansw      TYPE char70,
         anlkl            TYPE anla-anlkl,             "Класс
         name_anlkl       TYPE char70,                 "Класс наименование
         amortgr          TYPE glofaaassetdata-glo_rus_depgr, "Группа номер
         name_amortgr     TYPE char58,                 "Группа наименование
         anln1            TYPE anla-anln1,             "Системный номер
         invnr            TYPE anla-invnr,             "Инвентарный номер
         txt              TYPE char260,                "наименование объекта
         perst            TYPE acdoca-hsl,             "Первоначальная стоимость, руб.коп.
         partner          TYPE zcase-zz_partner,       "Контрагент
         name_partner     TYPE char255,                "Контрагент наименование
         zz_ndoc          TYPE char50,                 "№  договора
         zz_dcdoc         TYPE datum,                  "Дата заключения договора
         period           TYPE char25,                 "Период действия договора
         waers            TYPE t001-waers,
         anln2            TYPE anla-anln2,             "Субномер
      END OF gty_s_data,
     gty_t_data TYPE STANDARD TABLE OF gty_s_data WITH EMPTY KEY.

     CLASS-METHODS:
      get_data IMPORTING  VALUE(iv_budat)  TYPE budat
                          VALUE(iv_afabe)   TYPE anlc-afabe
                          VALUE(iv_flt)    TYPE string        "фильтр по прочим полям
               EXPORTING  VALUE(et_data)   TYPE gty_t_data
                 RAISING  cx_amdp_error.

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_FI_OS_AREND IMPLEMENTATION.


METHOD get_data BY DATABASE PROCEDURE FOR HDB
                            LANGUAGE SQLSCRIPT
                            OPTIONS READ-ONLY
                            USING anla ankt zanlu t095 tka02 t001 skat but000
                                  glofaaASsetdata J_3RFDEPRGROUPT
                                  zafull zcase zaa_utils_hdb=>get_asset_values.

    DECLARE lv_mandt    CHAR( 3) := sessiON_context( 'CLIENT' );
    DECLARE lv_spras    CHAR( 1) := sessiON_context( 'LOCALE_SAP' );

  lt_data =
     SELECT ktansw,
            skat.txt50                                            AS name_ktansw,
            anla.bukrs,
            t001.butxt                                            AS name_bukrs,
            anla.anlkl,
            ankt.txk50                                            AS name_anlkl,
            glo.glo_rus_depgr                                     AS amortgr,
            j_3gr.descript                                        AS name_amortgr,
            anla.anln1,                                           --Системный номер
            anla.invnr,                                           --Инвентарный номер
            zaf.txtf1 || char( 32 ) ||  zaf.txtf2  AS txt,               --наименование объекта
            vals.kansw                      AS perst,             --первоначальная стоимость, руб.коп.
            --zcase.zz_partner || char( 32 ) || ' ' || but.name_org1 || ' ' || but.name_org2 || ' ' || but.name_org3 || ' ' || but.name_org4 AS partner,
            zcase.zz_partner AS partner,
            but.name_org1 || ' ' || but.name_org2 || ' ' || but.name_org3 || ' ' || but.name_org4 AS name_partner,
            zcase.zz_ndoc,                                        --№  договора
            zcase.zz_dcdoc,                                       --Дата заключения договора
            left( zcase.ps_active_tstart, 2 ) || '.' || lpad( month( zcase.ps_active_tstart ), 2, 0 ) || '.' || year( zcase.ps_active_tstart )  || ' - ' ||
            left( zcase.ps_active_tend, 2 )   || '.' || lpad( month( zcase.ps_active_tend ), 2, 0 )   || '.' || year( zcase.ps_active_tend )  AS period,
            t001.waers,
            anla.anln2
            FROM anla AS anla
            INNER JOIN t001 AS t001   ON t001.mandt  = anla.mandt
                                     AND t001.bukrs  = anla.bukrs
            INNER JOIN t095  AS t095  ON t095.mandt  = anla.mandt
                                     AND t095.ktopl  = t001.ktopl
                                     AND t095.ktogr  = anla.ktogr
                                     AND t095.afabe  = :iv_afabe

            INNER JOIN tka02 AS tka02 ON tka02.bukrs = anla.bukrs                    --присвоение ке
                                     AND tka02.mandt = anla.mandt

            LEFT JOIN ankt   AS ankt  ON ankt.mandt  = anla.mandt
                                     AND ankt.SPRAS  = :lv_spras
                                     AND ankt.ANLKL  = anla.anlkl
            LEFT JOIN zanlu  AS zau   ON zau.mandt   = anla.mandt
                                     AND zau.invnr   = anla.invnr

            LEFT JOIN zafull AS zaf   ON zaf.mandt = anla.mandt
                                     AND zaf.bukrs = anla.bukrs
                                     AND zaf.anln1 = anla.anln1
                                     AND zaf.anln2 = anla.anln2

            LEFT JOIN glofaaassetdata AS glo ON glo.mandt = anla.mandt
                                     AND glo.bukrs = anla.bukrs
                                     AND glo.anln1 = anla.anln1
                                     AND glo.anln2 = anla.anln2

            LEFT JOIN J_3RFDEPRGROUPT AS j_3gr  ON j_3gr.mandt   = anla.mandt
                                               AND j_3gr.SPRSL   = :lv_spras
                                               AND j_3gr.amortgr = glo.glo_rus_depgr

            LEFT JOIN skat AS skat   ON skat.mandt = anla.mandt
                                    AND skat.SPRAS = :lv_spras
                                    AND skat.ktopl = t001.ktopl
                                    AND skat.saknr = t095.ktansw
            LEFT JOIN zcase AS zcase ON zcase.mandt   = zau.mandt
                                    AND zcase.ext_key = zau.abnnr
                                    AND zcase.langu   = :lv_spras

            LEFT JOIN but000 AS but  ON but.client    = anla.mandt
                                    AND but.partner   = zcase.zz_partner

            LEFT OUTER JOIN  "ZAA_UTILS_HDB=>GET_ASSET_VALUES"( :lv_mandt, :iv_budat,  '' )  AS vals ON vals.bukrs = anla.bukrs
                                                                                                    AND vals.anln1 = anla.anln1
                                                                                                    AND vals.anln2 = anla.anln2
                                                                                                    AND vals.afabe = '01'
           WHERE anla.mandt = :lv_mandt
             AND ( anla.deakt >= :iv_budat  OR anla.deakt < '19000101'  )
             AND anla.zugdt > '19000101'
             AND anla.zugdt <= :iv_budat
             ORDER BY ktansw, bukrs, anlkl, anln1, anln2, invnr ;

    lt_data = apply_filter (:lt_data, :iv_flt);

    IF IS_EMPTY ( :lt_data ) THEN RETURN;
    END IF;

    et_data =
            select row_number( ) over ( order by lt.ktansw asc) as pos,
                   lt.bukrs,
                   lt.name_bukrs,
                   lt.ktansw,
                   lt.name_ktansw,
                   lt.anlkl,
                   lt.name_anlkl,
                   lt.amortgr,
                   lt.name_amortgr,
                   lt.anln1,
                   lt.invnr,
                   lt.txt,
                   lt.perst,
                   lt.partner,
                   lt.name_partner,
                   lt.zz_ndoc,
                   lt.zz_dcdoc,
                   lt.period,
                   lt.waers,
                   lt.anln2
             from :lt_data as lt;

ENDMETHOD.
ENDCLASS.