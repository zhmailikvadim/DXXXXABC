CLASS tc_atp_sup_usage_001
    DEFINITION FINAL FOR TESTING
    DURATION SHORT
    RISK LEVEL HARMLESS.

  PRIVATE SECTION.
    DATA cut TYPE REF TO cl_atp_info_api_usage.

    CLASS-DATA osql_test_environment TYPE REF TO if_osql_test_environment.

    CLASS-METHODS:
      class_setup,
      class_teardown.

    METHODS:
      setup,
      teardown.

    METHODS mrparea_timeseries  FOR TESTING.
    METHODS test_wrong_function FOR TESTING.

ENDCLASS.



CLASS tc_atp_sup_usage_001 IMPLEMENTATION.
  METHOD class_setup.
    osql_test_environment = cl_osql_test_environment=>create( VALUE #( ( 'SUSAGEMANAGEMENT') ( 'SUSAGE') ) ).
  ENDMETHOD.


  METHOD class_teardown.
    osql_test_environment->destroy( ).
  ENDMETHOD.


  METHOD setup.
    DATA usage     TYPE STANDARD TABLE OF susagemanagement.
    DATA cal_month TYPE susage-calendar_month.

    cal_month = sy-datum+0(6).
    cut = NEW cl_atp_info_api_usage( ).

    usage = VALUE #(
        calendar_month = cal_month
        ( feature = cl_atp_info_api_usage=>if_atp_info_api_usage~feature-atp_info_api_usage counter = '1000' type = if_atp_info_api_usage=>type-mrparea subtype = if_atp_info_api_usage=>subtype-timeseries )
        ( feature = cl_atp_info_api_usage=>if_atp_info_api_usage~feature-atp_info_api_usage counter = '1000' type = if_atp_info_api_usage=>type-mrparea subtype = if_atp_info_api_usage=>subtype-at_date_time )
        ( feature = cl_atp_info_api_usage=>if_atp_info_api_usage~feature-atp_info_api_usage counter = '1000' type = if_atp_info_api_usage=>type-mrparea subtype = if_atp_info_api_usage=>subtype-of_quantity ) ).
    osql_test_environment->insert_test_data( i_data = usage ).
  ENDMETHOD.


  METHOD teardown.
    osql_test_environment->clear_doubles( ).
  ENDMETHOD.


  METHOD mrparea_timeseries.
    cut->if_atp_info_api_usage~count_usage( feature_name    = if_atp_info_api_usage=>feature-atp_info_api_usage
                                            function_name   = if_atp_info_api_usage=>function_name-susage_insert
                                            feature_type    = if_atp_info_api_usage=>type-mrparea
                                            feature_subtype = if_atp_info_api_usage=>subtype-timeseries ).
    SELECT * FROM susagemanagement INTO TABLE @DATA(susagemanagements) WHERE type = @if_atp_info_api_usage=>type-mrparea and subtype = @if_atp_info_api_usage=>subtype-timeseries.
    cl_abap_unit_assert=>assert_not_initial( susagemanagements ).
    cl_abap_unit_assert=>assert_equals( exp = 1001 act = susagemanagements[ 1 ]-counter ).
  ENDMETHOD.

  METHOD test_wrong_function.
    cut->if_atp_info_api_usage~count_usage( feature_name    = if_atp_info_api_usage=>feature-atp_info_api_usage
                                            function_name   = '1if_atp_info_api_usage'
                                            feature_type    = if_atp_info_api_usage=>type-mrparea
                                            feature_subtype = if_atp_info_api_usage=>subtype-timeseries ).
    SELECT * FROM susagemanagement INTO TABLE @DATA(susagemanagements) WHERE type = @if_atp_info_api_usage=>type-mrparea AND subtype = @if_atp_info_api_usage=>subtype-timeseries.
    cl_abap_unit_assert=>assert_not_initial( susagemanagements ).
    cl_abap_unit_assert=>assert_equals( exp = 1000 act = susagemanagements[ 1 ]-counter ).

  ENDMETHOD.
ENDCLASS.