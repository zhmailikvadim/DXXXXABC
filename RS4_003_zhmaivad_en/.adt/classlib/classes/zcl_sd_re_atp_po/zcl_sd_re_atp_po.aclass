CLASS zcl_sd_re_atp_po DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

*"* public components of class CL_EXM_IM_ME_PROCESS_PO_CUST
*"* do not include other source files here!!!
  PUBLIC SECTION.
    TYPE-POOLS mmmfd .

    INTERFACES if_badi_interface .
    INTERFACES if_ex_me_process_po_cust .

    CLASS-DATA gt_atpfield TYPE tbl_atpfield .
  PROTECTED SECTION.
*"* protected components of class CL_EXM_IM_ME_PROCESS_PO_CUST
*"* do not include other source files here!!!
private section.

  data MIF_HEADER type ref to IF_PURCHASE_ORDER_MM .
  data MV_TRTYP type TRTYP .
  data MS_HEADER type MEPOHEADER .
  data MT_SCHEDULES type PURCHASE_ORDER_SCHEDULES .
  data MT_ITEM_DATA type PURCHASE_ORDER_ITEMS .
  class-data GT_VAS type FSH_VASM_BUFFER_T .
  class-data GS_HEADER_PREV type MEPOHEADER .

  methods INIT_VAS_DATA .
  methods UPDATE_VAS_FOR_ITEM
    importing
      !IV_EBELP type EBELP
      !IT_VAS_DATA type FSH_VASM_BUFFER_T .
  methods CALL_ATP_ITEMS
    importing
      !IT_ITEMS type TAB_MEPOITEM .
*"* private components of class CL_EXM_IM_ME_PROCESS_PO_CUST
*"* do not include other source files here!!!
ENDCLASS.



CLASS ZCL_SD_RE_ATP_PO IMPLEMENTATION.


  METHOD call_atp_items.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 19.09.2023 | 24690     : Feature - POC - [CR] - Re- ATP being cal *
*              |            | DS4K962136                               *
*----------------------------------------------------------------------*

    LOOP AT it_items ASSIGNING FIELD-SYMBOL(<ls_item>).
      CALL FUNCTION 'MEPO_DOC_ITEM_ATP_EXP'
        EXPORTING
          im_item    = <ls_item>
          im_ac_mode = cl_mmpur_constants=>expl_check. "" "expl_displ
    ENDLOOP.
  ENDMETHOD.


  METHOD if_ex_me_process_po_cust~check  ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
* ZUJSKAS      | 19.04.2023 | 20377     : Feature - [Defect] - Re- ATP being calle *
*              |            | DS4K950228                               *
* ZUJSKAS      | 19.09.2023 | 24690     : Feature - POC - [CR] - Re- ATP being cal *
*              |            | DS4K962136                               *
*----------------------------------------------------------------------*

  ENDMETHOD.                    "IF_EX_ME_PROCESS_PO_CUST~CHECK


  METHOD if_ex_me_process_po_cust~close ##NEEDED.

************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*

  ENDMETHOD.                    "IF_EX_ME_PROCESS_PO_CUST~CLOSE


  METHOD if_ex_me_process_po_cust~fieldselection_header ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
* ZUJSKAS      | 19.09.2023 | 24690     : Feature - POC - [CR] - Re- ATP being cal *
*              |            | DS4K962136                               *
*----------------------------------------------------------------------*
  ENDMETHOD.


  METHOD if_ex_me_process_po_cust~fieldselection_header_refkeys ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
  ENDMETHOD.


  METHOD if_ex_me_process_po_cust~fieldselection_item ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
* GERTSVIK     | 19.05.2023 | 20159     : Feature - [Build] DC Capacit *
*              |            | DS4K949863                               *
*----------------------------------------------------------------------*
    DATA: ls_item                 TYPE mepoitem,
          lo_atp_pal_tsrs_chk_imp TYPE REF TO zcl_sd_atp_pal_tsrs_chk_imp,
          lv_vtweg                TYPE zsd_pal_cc_dch,
          lt_sched_lines          TYPE tab_meposchedule,
          lo_po                   TYPE REF TO cl_po_header_handle_mm.

    CHECK gt_atpfield IS NOT INITIAL.
    LOOP AT mt_item_data ASSIGNING FIELD-SYMBOL(<ls_item_data>).
      ls_item = <ls_item_data>-item->get_data( ).
      mt_schedules = <ls_item_data>-item->get_schedules( ).

      CHECK line_exists( gt_atpfield[ delnr = ls_item-ebeln
                                      delps = ls_item-ebelp ] ).

      lo_atp_pal_tsrs_chk_imp = NEW #( ).
      IF mt_schedules[] IS NOT INITIAL.

        LOOP AT mt_schedules ASSIGNING  FIELD-SYMBOL(<ls_schedule>). "#EC CI_NESTED
          DATA(ls_deliv_schedl) = <ls_schedule>-schedule->get_data( ).
          APPEND ls_deliv_schedl TO lt_sched_lines.
        ENDLOOP.

        SORT lt_sched_lines BY eindt ASCENDING.        "#EC CI_SORTLOOP
        READ TABLE lt_sched_lines ASSIGNING FIELD-SYMBOL(<ls_sched_lines>) INDEX 1.
        IF sy-subrc = 0.
          DATA(lv_req_dlvdate) = <ls_sched_lines>-eindt.
        ENDIF.
      ENDIF.
      DATA(ls_sto_header) = cl_atp_pal_check_context=>get_stocktransportorder( ls_item-ebeln ).
      DATA(ls_stock) = cl_atp_pal_check_context=>get_stocktransportorderitem( VALUE #( atprelevantdocument     = ls_item-ebeln
                                                                                       atprelevantdocumentitem = ls_item-ebelp ) ).
      IF ls_stock IS NOT INITIAL.
        DATA(ls_shipping) = cl_atp_pal_check_context=>get_stocktranspordershipping( VALUE #( atprelevantdocument     = ls_item-ebeln
                                                                                             atprelevantdocumentitem = ls_item-ebelp ) ).
      ENDIF.

      IF lv_req_dlvdate IS NOT INITIAL.
        DATA(lv_plant) = ls_sto_header-supplyingplant.
        DATA(lv_vkorg) = ls_shipping-salesorganization.
        DATA(lv_auart) = ms_header-bsart.
        DATA(lv_kunnr) = ms_header-kunnr.
        DATA(lv_vstel) = ls_shipping-shippingpoint.
        lv_vtweg = ms_header-zz1_zsales_channel_pdh.
        IF lo_atp_pal_tsrs_chk_imp->check_exclusion(
          iv_werks = lv_plant
          iv_vkorg = lv_vkorg
          iv_vtweg = lv_vtweg
          iv_auart = lv_auart
          iv_kunnr = lv_kunnr
          iv_vstel = lv_vstel
          ) EQ abap_true. " check for exclusion
          RETURN.
        ENDIF.
* Find Number of days from BRF+ decision table
        DATA(lv_days) = lo_atp_pal_tsrs_chk_imp->find_num_days(
         iv_werks = lv_plant
         iv_vkorg = lv_vkorg
         iv_vtweg = lv_vtweg
          ).

        IF lv_days IS NOT INITIAL.
          DATA(lv_date) = sy-datlo.
          lv_date = lv_date + lv_days .

          IF lv_date > lv_req_dlvdate.
            ls_item-zz1_rdd_days_po_pdi = lv_days.
            lo_po ?= im_header.
            lo_po->my_ibs_firewall_on = 'X'.
            lo_po->my_cust_firewall_on = 'X'.
            <ls_item_data>-item->set_data( ls_item ).
            im_header->set_changed( ).
          ENDIF.
        ENDIF.
      ENDIF.

    ENDLOOP.
  ENDMETHOD.                    "IF_EX_ME_PROCESS_PO_CUST~FIELDSELECTION_ITEM


  METHOD if_ex_me_process_po_cust~fieldselection_item_refkeys ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
  ENDMETHOD.


  METHOD if_ex_me_process_po_cust~initialize ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*.

  ENDMETHOD.                    "IF_EX_ME_PROCESS_PO_CUST~INITIALIZE


  METHOD if_ex_me_process_po_cust~open ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
* GERTSVIK     | 19.05.2023 | 20159     : Feature - [Build] DC Capacit *
*              |            | DS4K949863                               *
*----------------------------------------------------------------------*
* ZUJSKAS      | 19.09.2023 | 24690     : Feature - POC - [CR] - Re- ATP being cal *
*              |            | DS4K962136                               *
*----------------------------------------------------------------------*
    FREE gt_atpfield.
    mv_trtyp = im_trtyp.
    mif_header = im_header.

    ms_header = mif_header->get_data( ).
    mt_item_data = mif_header->get_items( ).

    IF im_trtyp = 'V'. "change mode => need to initialize VAS data
      init_vas_data( ).
    ENDIF.
  ENDMETHOD.                    "IF_EX_ME_PROCESS_PO_CUST~OPEN


  METHOD if_ex_me_process_po_cust~post ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* GERTSVIK     | 19.05.2023 | 20159     : Feature - [Build] DC Capacit *
*              |            | DS4K949863                               *
*----------------------------------------------------------------------*
  ENDMETHOD.                    "IF_EX_ME_PROCESS_PO_CUST~POST


  METHOD if_ex_me_process_po_cust~process_account ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
  ENDMETHOD.


  METHOD if_ex_me_process_po_cust~process_header.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
* ZUJSKAS      | 19.09.2023 | 24690     : Feature - POC - [CR] - Re- ATP being cal *
*              |            | DS4K962136                               *
*----------------------------------------------------------------------*

    DATA:lt_fsh_vasm_buffer TYPE fsh_vasm_buffer_t,
         lt_itemdata_atp    TYPE tab_mepoitem.

    DATA(ls_header) = im_header->get_data( ).
    DATA(lt_items) = im_header->get_items( ).

    IF gs_header_prev IS NOT INITIAL AND ( gs_header_prev-zz1_zsales_channel_pdh <> ls_header-zz1_zsales_channel_pdh OR
                                           gs_header_prev-zz1_zorderreason_pdh <> ls_header-zz1_zorderreason_pdh ).



      LOOP AT lt_items ASSIGNING FIELD-SYMBOL(<ls_items>).
        DATA(ls_itemdata) = <ls_items>-item->get_data( ).
        APPEND ls_itemdata TO lt_itemdata_atp.
      ENDLOOP.

    ENDIF.
    gs_header_prev = ls_header.
    LOOP AT lt_items ASSIGNING <ls_items>.
      ls_itemdata = <ls_items>-item->get_data( ).

      CALL FUNCTION 'FSH_VAS_GET'
        EXPORTING
          iv_fsh_transaction = ls_itemdata-fsh_transaction
          iv_fsh_item_group  = ls_itemdata-fsh_item_group
        IMPORTING
          et_fsh_vasm_buffer = lt_fsh_vasm_buffer.

      DATA(lt_vas) = VALUE fsh_vasm_buffer_t(
                            FOR <ls_vas> IN lt_fsh_vasm_buffer WHERE ( fsh_refps = ls_itemdata-ebelp )
                            ( <ls_vas> ) ).

      LOOP AT lt_vas ASSIGNING FIELD-SYMBOL(<ls_vas_s>). "#EC CI_NESTED
        CLEAR: <ls_vas_s>-fsh_vas_txt_ind,
               <ls_vas_s>-fsh_vas_txt_key.
      ENDLOOP.

      DATA(lt_vas_prev) = VALUE fsh_vasm_buffer_t(
                            FOR <ls_vas_prev> IN gt_vas WHERE ( fsh_refps = ls_itemdata-ebelp )
                            ( <ls_vas_prev> ) ).

      SORT: lt_vas BY fsh_item,     "#EC CI_SORTLOOP
            lt_vas_prev BY fsh_item.  "#EC CI_SORTLOOP

      IF  lt_vas <> lt_vas_prev.
        update_vas_for_item( iv_ebelp = ls_itemdata-ebelp
                             it_vas_data = lt_vas ).
        APPEND ls_itemdata TO lt_itemdata_atp.
      ENDIF.
    ENDLOOP.
    IF lt_itemdata_atp IS NOT INITIAL.
      SORT lt_itemdata_atp BY ebelp.
      DELETE ADJACENT DUPLICATES FROM lt_itemdata_atp COMPARING ebelp. "dont call atp for same item twice
      call_atp_items( lt_itemdata_atp ).
    ENDIF.
  ENDMETHOD.


  METHOD if_ex_me_process_po_cust~process_item ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
* GERTSVIK     | 19.05.2023 | 20159     : Feature - [Build] DC Capacit *
*              |            | DS4K949863                               *
*----------------------------------------------------------------------*
    DATA: ls_mepoitem             TYPE mepoitem,
          lo_atp_pal_tsrs_chk_imp TYPE REF TO zcl_sd_atp_pal_tsrs_chk_imp,
          lv_vtweg                TYPE zsd_pal_cc_dch,
          lt_sched_lines          TYPE tab_meposchedule.

    CHECK mv_trtyp = 'H'.
    DATA(ls_header) = mif_header->get_data( ).
    DATA(lt_schedules) = im_item->get_schedules( ).
    DATA(lt_item_data) = mif_header->get_items( ).
    DATA(ls_item) = im_item->get_data( ).

    lo_atp_pal_tsrs_chk_imp = NEW #( ).
    IF lt_schedules[] IS NOT INITIAL.

      LOOP AT lt_schedules ASSIGNING  FIELD-SYMBOL(<ls_schedule>).
        DATA(ls_deliv_schedl) = <ls_schedule>-schedule->get_data( ).
        APPEND ls_deliv_schedl TO lt_sched_lines.
      ENDLOOP.

      SORT lt_sched_lines BY eindt ASCENDING.
      READ TABLE lt_sched_lines ASSIGNING FIELD-SYMBOL(<ls_sched_lines>) INDEX 1.
      IF sy-subrc = 0.
        DATA(lv_req_dlvdate) = <ls_sched_lines>-eindt.
      ENDIF.
    ENDIF.
    DATA(ls_sto_header) = cl_atp_pal_check_context=>get_stocktransportorder( ls_item-ebeln ).
    DATA(ls_stock) = cl_atp_pal_check_context=>get_stocktransportorderitem( VALUE #( atprelevantdocument     = ls_item-ebeln
                                                                                     atprelevantdocumentitem = ls_item-ebelp ) ).
    IF ls_stock IS NOT INITIAL.
      DATA(ls_shipping) = cl_atp_pal_check_context=>get_stocktranspordershipping( VALUE #( atprelevantdocument     = ls_item-ebeln
                                                                                           atprelevantdocumentitem = ls_item-ebelp ) ).
    ENDIF.

    IF lv_req_dlvdate IS NOT INITIAL.
      DATA(lv_plant) = ls_sto_header-supplyingplant.
      DATA(lv_vkorg) = ls_shipping-salesorganization.
      DATA(lv_auart) = ls_header-bsart.
      DATA(lv_kunnr) = ls_header-kunnr.
      DATA(lv_vstel) = ls_shipping-shippingpoint.
      lv_vtweg = ls_header-zz1_zsales_channel_pdh.
      IF lo_atp_pal_tsrs_chk_imp->check_exclusion(
        iv_werks = lv_plant
        iv_vkorg = lv_vkorg
        iv_vtweg = lv_vtweg
        iv_auart = lv_auart
        iv_kunnr = lv_kunnr
        iv_vstel = lv_vstel
        ) EQ abap_true. " check for exclusion
        RETURN.
      ENDIF.
* Find Number of days from BRF+ decision table
      DATA(lv_days) = lo_atp_pal_tsrs_chk_imp->find_num_days(
       iv_werks = lv_plant
       iv_vkorg = lv_vkorg
       iv_vtweg = lv_vtweg
        ).

      IF lv_days IS NOT INITIAL.
        DATA(lv_date) = sy-datlo.
        lv_date = lv_date + lv_days .

        IF lv_date > lv_req_dlvdate.
          LOOP AT lt_item_data ASSIGNING FIELD-SYMBOL(<ls_item_data>).
            ls_mepoitem = <ls_item_data>-item->get_data( ).
            ls_mepoitem-zz1_rdd_days_po_pdi = lv_days.
            <ls_item_data>-item->set_data( ls_mepoitem ).
          ENDLOOP.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDMETHOD.                    "IF_EX_ME_PROCESS_PO_CUST~PROCESS_ITEM


  METHOD if_ex_me_process_po_cust~process_schedule ##NEEDED.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 08.03.2023 | 18841     : Feature - [Development] Outb *
*              |            | DS4K946290                               *
*----------------------------------------------------------------------*
  ENDMETHOD.


  METHOD init_vas_data.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 19.09.2023 | 24690     : Feature - POC - [CR] - Re- ATP being cal *
*              |            | DS4K962136                               *
*----------------------------------------------------------------------*


    DATA: lt_fsh_vasm_buffer TYPE fsh_vasm_buffer_t.

    LOOP AT mt_item_data ASSIGNING FIELD-SYMBOL(<ls_item_data>).

      DATA(ls_itemdata) = <ls_item_data>-item->get_data( ).

      CALL FUNCTION 'FSH_VAS_GET'
        EXPORTING
          iv_fsh_transaction = ls_itemdata-fsh_transaction
          iv_fsh_item_group  = ls_itemdata-fsh_item_group
        IMPORTING
          et_fsh_vasm_buffer = lt_fsh_vasm_buffer.

      DATA(lt_vas) = VALUE fsh_vasm_buffer_t(
                            FOR <ls_vas> IN lt_fsh_vasm_buffer WHERE ( fsh_refps = ls_itemdata-ebelp )
                            ( <ls_vas> ) ).

      LOOP AT lt_vas ASSIGNING FIELD-SYMBOL(<ls_vas_s>). "#EC CI_NESTED
        CLEAR: <ls_vas_s>-fsh_vas_txt_ind,
               <ls_vas_s>-fsh_vas_txt_key.
      ENDLOOP.

      update_vas_for_item( iv_ebelp = ls_itemdata-ebelp
                           it_vas_data = lt_vas ).

    ENDLOOP.

  ENDMETHOD.


  method UPDATE_VAS_FOR_ITEM.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZUJSKAS      | 19.09.2023 | 24690     : Feature - POC - [CR] - Re- ATP being cal *
*              |            | DS4K962136                               *
*----------------------------------------------------------------------*

    DELETE gt_vas WHERE fsh_refps = iv_ebelp.
    APPEND LINES OF it_vas_data TO gt_vas.

  endmethod.
ENDCLASS.