CLASS zcl_sd_stock_reconciliat_notif DEFINITION
  PUBLIC
  INHERITING FROM /iwngw/cl_notif_provider_abs
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    CONSTANTS:
      BEGIN OF gc_process_param,
        provider_id      TYPE /iwngw/notif_provider_id     VALUE 'ZSD_STOCK_RECONCILIATION_PROV',
        type_key_success TYPE /iwngw/notification_type_key VALUE 'UpdateSuccess',
        type_key_failed  TYPE /iwngw/notification_type_key VALUE 'UpdateFailed',
      END OF gc_process_param .

    CLASS-METHODS send_notification
      IMPORTING
        it_recipient    TYPE /iwngw/if_notif_provider=>ty_t_notification_recipient
        iv_type_key     TYPE /iwngw/if_notif_provider=>ty_s_notification_type_id-type_key
        iv_type_version TYPE /iwngw/if_notif_provider=>ty_s_notification_type_id-version
      RAISING
        cx_uuid_error.

    METHODS /iwngw/if_notif_provider_ext~get_notification_parameters
        REDEFINITION .
    METHODS /iwngw/if_notif_provider_ext~get_notification_type
        REDEFINITION .
    METHODS /iwngw/if_notif_provider_ext~get_notification_type_text
        REDEFINITION .
    METHODS /iwngw/if_notif_provider_ext~handle_action
        REDEFINITION .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_SD_STOCK_RECONCILIAT_NOTIF IMPLEMENTATION.


  METHOD /iwngw/if_notif_provider_ext~get_notification_parameters.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.03.2023 | 18988     : [Feature] [Build] - Stock re *
*              |            | DS4K946765                               *
*----------------------------------------------------------------------*

    CLEAR: et_parameter.
    DATA(lv_lang) = sy-langu.
    SET LANGUAGE lv_lang.
  ENDMETHOD.


  METHOD /iwngw/if_notif_provider_ext~get_notification_type.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.03.2023 | 18988     : [Feature] [Build] - Stock re *
*              |            | DS4K946765                               *
*----------------------------------------------------------------------*
    CLEAR:
      es_notification_type,
      et_notification_action.

    es_notification_type = VALUE #( is_groupable   = abap_true
                                    type_key       = iv_type_key
                                    version        = iv_type_version
                                    is_email_enbld = abap_false ).
  ENDMETHOD.


  METHOD /iwngw/if_notif_provider_ext~get_notification_type_text.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.03.2023 | 18988     : [Feature] [Build] - Stock re *
*              |            | DS4K946765                               *
*----------------------------------------------------------------------*
    DATA: lv_title    TYPE string,
          lv_subtitle TYPE string.

    lv_title = COND #( WHEN iv_type_version = '01'  THEN TEXT-010
                       WHEN iv_type_version = '02'  THEN TEXT-011
                       WHEN iv_type_version = '03'  THEN TEXT-013 ).

    CLEAR   et_action_text.

    CASE iv_type_key.
      WHEN gc_process_param-type_key_success.
        lv_title = lv_title && TEXT-001.
      WHEN gc_process_param-type_key_failed.
        lv_title = lv_title && TEXT-002.
    ENDCASE.

    lv_subtitle = TEXT-003.

    et_type_text = VALUE #(
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-description        value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_grouped   value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_public    value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-template_sensitive value = lv_title )
             ( name = /iwngw/if_notif_provider_ext=>gc_template_names-subtitle           value = lv_subtitle  ) ).
  ENDMETHOD.


  METHOD /iwngw/if_notif_provider_ext~handle_action.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.03.2023 | 18988     : [Feature] [Build] - Stock re *
*              |            | DS4K946765                               *
*----------------------------------------------------------------------*
    CLEAR: es_result.
    RETURN.
  ENDMETHOD.


  METHOD send_notification.
************************************************************************
*  R E V I S I O N   H I S T O R Y                                     *
************************************************************************
* AUTHOR       | DATE       | CHANGE NUMBER & DESCRIPTION              *
*              |            | TRANSPORT REQUESTS                       *
************************************************************************
* ZHMAIVAD     | 21.03.2023 | 18988     : [Feature] [Build] - Stock re *
*              |            | DS4K946765                               *
*----------------------------------------------------------------------*
    TRY.
        /iwngw/cl_notification_api=>clear_cache( gc_process_param-provider_id ).
      CATCH /iwngw/cx_notification_api ##NO_HANDLER.
    ENDTRY.

    TRY.
        /iwngw/cl_notification_api=>create_notifications(
          iv_provider_id  = gc_process_param-provider_id
          it_notification = VALUE #(
                              ( id           = cl_uuid_factory=>create_system_uuid( )->create_uuid_x16( )
                                type_key     = iv_type_key
                                type_version = iv_type_version
                                navigation_target_object = 'Action'
                                navigation_target_action = COND #( WHEN iv_type_version = '01'  THEN 'MaterialMovement-zstockdfflist'
                                                                   WHEN iv_type_version = '03'  THEN 'MaterialMovement-zstockcrrlist' )
                                priority     = SWITCH #( iv_type_key
                                                         WHEN gc_process_param-type_key_success THEN /iwngw/if_notif_provider=>gcs_priorities-low
                                                         WHEN gc_process_param-type_key_failed  THEN /iwngw/if_notif_provider=>gcs_priorities-high )
                                recipients   = it_recipient
                              ) ) ).

      CATCH /iwngw/cx_notification_api.
        RETURN.
    ENDTRY.
  ENDMETHOD.
ENDCLASS.