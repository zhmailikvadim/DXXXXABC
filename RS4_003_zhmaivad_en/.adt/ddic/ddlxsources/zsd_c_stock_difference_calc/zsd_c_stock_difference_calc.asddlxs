@Metadata.layer: #CORE
@UI: {
    headerInfo:{
            typeName: 'Stock Difference Report',
            typeNamePlural: 'Stock Difference List',
            title: {
                type: #STANDARD, value: 'Article'
            }
    },
    presentationVariant: [
        {
            requestAtLeast: [
              'DifferenceCriticality',
              'BlockedDifferenceCriticality',
              'UnrectrictedDifferenceCritical',
              'InQualityInspDifferenceCritica'
            ]
        }
    ]
}
annotate view zsd_i_stock_difference_calc with
{
  @UI.facet:  [
                {
                  id:              'DedicatedOrder',
                  type:            #FIELDGROUP_REFERENCE,
                  purpose:         #HEADER,
                  position:        10,
                  targetQualifier: 'DedicatedOrder'
                },
                {
                  id:              'ArticleData',
                  type:            #FIELDGROUP_REFERENCE,
                  purpose:         #HEADER,
                  position:        20,
                  targetQualifier: 'ArticleData'
                },
                {
                  id:              'ValuationData',
                  type:            #DATAPOINT_REFERENCE,
                  purpose:         #HEADER,
                  position:        30,
                  targetQualifier: 'ValuationData'
                },
                {
                  id:              'GeneralInformation',
                  type:            #COLLECTION,
                  label:           'General Information',
                  position:        40
                },
                {
                  id:              'WMSSTOCK',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'WMS Stock',
                  position:        50,
                  targetQualifier: 'WMSSTOCK'
                },
                {
                  id:              'SAPSTOCK',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'SAP Stock',
                  position:        60,
                  targetQualifier: 'SAPSTOCK'
                },
                { id:              'TimingAdjustment',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Timing Adjustment',
                  position:        65,
                  targetQualifier: 'TimingAdjustment'
                },
                { id:              'GoodsReceiptErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'GR Errors',
                  position:        70,
                  targetQualifier: 'GoodsReceiptErrors'
                },
                { id:              'GoodsIssueErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'GI Errors',
                  position:        72,
                  targetQualifier: 'GoodsIssueErrors'
                },
                { id:              'InventoryPostingErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Inventory Errors',
                  position:        74,
                  targetQualifier: 'InventoryPostingErrors'
                },
                { id:              'EcomErrors',
                  purpose:         #STANDARD,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Ecom Errors',
                  position:        76,
                  targetQualifier: 'EcomErrors'
                },
                {
                  id:              'IncludingErrorsDifferenceQty',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Including Errors Difference Qty',
                  position:        80,
                  targetQualifier: 'IncludingErrorsDifferenceQty'
                },
                {
                  id:              'DifferenceQty',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Difference Qty',
                  position:        82,
                  targetQualifier: 'DifferenceQty'
                },
                {
                  id:              'DifferenceValue',
                  purpose:         #STANDARD ,
                  parentId:        'GeneralInformation',
                  type:            #FIELDGROUP_REFERENCE,
                  label:           'Difference Value',
                  position:        84,
                  targetQualifier: 'DifferenceValue'
                }]
  @EndUserText.label: 'Currency Code'
  CurrencyCode;
  @UI:{lineItem:       [{ position: 20, importance: #HIGH }]}
  @UI.selectionField: [ { position: 30  } ]
  @Consumption.valueHelpDefinition: [{ entity : { name: 'zsd_i_date_value_help', element: 'WmsSnapshotDate'}, distinctValues: true, presentationVariantQualifier: 'SortByDate'}]
  @EndUserText.label: 'WMS Snapshot Date'
  @Consumption.filter:{ mandatory: true}
  WmsSnapshotDate;
  @UI:{lineItem:       [{ position: 30, importance: #HIGH }], fieldGroup: [{ position: 10, qualifier: 'DedicatedOrder'}]}
  @UI.selectionField: [ { position: 10 } ]
  @Consumption.filter:{ selectionType:#SINGLE, mandatory: true}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_plant_diff_base_vh', element : 'Plant'}}]
  @EndUserText.label: 'Plant'
  Plant;
  @UI.selectionField: [ { position: 20 } ]
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_storage_loc_diff_base_vh', element : 'StorageLocation'},
                            additionalBinding: [{ element: 'Plant', localElement: 'Plant'}] }]
  @Consumption.filter:{multipleSelections: false,mandatory: true}
  @EndUserText.label: 'Storage Location'
  StorageLocation;
  @UI:{lineItem:       [{ position: 50, importance: #HIGH }]}
  @Consumption.valueHelpDefinition: [{ entity : { name : 'zsd_i_article_diff_vh', element : 'Article'} }]
  @UI.selectionField: [ { position: 50 } ]
  @EndUserText.label: 'Article'
  Article;
  @UI.dataPoint: {qualifier: 'ValuationData', title: 'Price'}
  Price;
  @UI:{fieldGroup: [{ position: 3, qualifier: 'DedicatedOrder'}]}
  SalesOrderNumber;
  @UI:{fieldGroup: [{ position: 5, qualifier: 'DedicatedOrder'}]}
  SalesOrderItem;
  @UI:{ lineItem: [{ position: 60 , importance: #HIGH}],fieldGroup: [{ position: 10,label: 'UU', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'UU SAP'
  @UI.dataPoint: {
  title: 'Unit Price',
  description: 'Name',
  valueFormat: {
    scaleFactor: 1000,
    numberOfFractionalDigits: 1 }
  }
  UnrectrictedSapStockQuantity;
  @UI:{ lineItem: [{ position: 70 , importance: #HIGH}],fieldGroup: [{ position: 10, label: 'UU', qualifier: 'WMSSTOCK'}]}
  @EndUserText.label: 'UU WMS'
  UnrectrictedWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'TimingAdjustment'}]}
  @EndUserText.label: 'UU Errors Timing'
  UnrectrictedTimingAdjustment;
  @UI:{ lineItem: [{ position: 90 , importance: #HIGH, criticality: 'UnrectrictedDifferenceCritical',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 10, label: 'UU',qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'UU Diff'
  UnrectrictedDifferenceQty;
  @UI:{ lineItem: [{ position: 100 , importance: #HIGH}],  fieldGroup: [{ position: 20, label: 'BL', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'BL SAP'
  BlockedSapStockQuantity;
  @EndUserText.label: 'BL WMS'
  @UI:{ lineItem: [{ position: 110 , importance: #HIGH}],fieldGroup: [{ position: 20,label: 'BL', qualifier: 'WMSSTOCK'}]}
  BlockedWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 20,label: 'BL', qualifier: 'TimingAdjustment'}]}
  @EndUserText.label: 'BL Errors Timing'
  BlockedTimingAdjustment;
  @UI:{ lineItem: [{ position: 130 , importance: #HIGH, criticality: 'BlockedDifferenceCriticality',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 20, label: 'BL',qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'BL Diff'
  BlockedDifferenceQty;
  @UI:{ lineItem: [{ position: 140 , importance: #HIGH}],fieldGroup: [{ position: 30, label: 'QI', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'QI SAP'
  InQualityInspSapStockQuantity;
  @UI:{ lineItem: [{ position: 150 , importance: #HIGH}],fieldGroup: [{ position: 30, label: 'QI', qualifier: 'WMSSTOCK'}]}
  @EndUserText.label: 'QI WMS'
  InQualityInspWmsStockQuantity;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI', qualifier: 'TimingAdjustment'}]}
  @EndUserText.label: 'QI Timing Total'
  InQualityInspTimingAdjustment;
  @UI:{ lineItem: [{ position: 170 , importance: #HIGH, criticality: 'InQualityInspDifferenceCritica',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 30, label: 'QI',qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'QI Diff'
  InQualityInspDifferenceQty;
  @UI:{ lineItem: [{ position: 180 , importance: #HIGH, criticality: 'DifferenceCriticality',
  criticalityRepresentation: #WITHOUT_ICON}],
  fieldGroup: [{ position: 40,label: 'Total', qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'Total Net Diff'
  DifferenceQty;
  @UI:{fieldGroup: [{ position: 50,label: 'Absolute Total', qualifier: 'DifferenceQty'}]}
  @EndUserText.label: 'Total Net Diff Abs'
  AbsoluteDifferenceQty;
  @UI:{fieldGroup: [{ position: 10, label: 'UU',qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'UU Diff Value'
  UnrectrictedDifferenceValue;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'BL Diff Value'
  BlockedDifferenceValue;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI', qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'QI Diff Value'
  InQualityInspDifferenceValue;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU',qualifier: 'IncludingErrorsDifferenceQty'}]}
  @EndUserText.label: 'UU Incl Err Diff'
  RealUnrectrictedDifferenceQty;
  @UI:{fieldGroup: [{ position: 20, label: 'QI',qualifier: 'IncludingErrorsDifferenceQty'}]}
  @EndUserText.label: 'QI Incl Err Diff'
  RealInQualityInspDifferenceQty;
  @UI:{ fieldGroup: [{ position: 30, label: 'BL',qualifier: 'IncludingErrorsDifferenceQty'}]}
  @EndUserText.label: 'BL Incl Err Diff'
  RealBlockedDifferenceQty;
  @UI:{ fieldGroup: [{ position: 40,label: 'Total', qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'Total Diff Value'
  DifferenceValue;
  @UI:{fieldGroup: [{ position: 50,label: 'Total Absolute', qualifier: 'DifferenceValue'}]}
  @EndUserText.label: 'Total Diff Value Abs'
  AbsoluteDifferenceValue;
  @UI:{fieldGroup: [{ position: 40,label: 'Time', qualifier: 'WMSSTOCK'}]}
  @EndUserText.label: 'WMS Time'
  WmsTime;
  @UI:{fieldGroup: [{ position: 40,label: 'Time', qualifier: 'SAPSTOCK'}]}
  @EndUserText.label: 'SAP Time'
  SapTime;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'GoodsIssueErrors'}]}
  @EndUserText.label: 'UU Int GI Error'
  ErrorsGoodsIssueQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU', qualifier: 'GoodsReceiptErrors'}]}
  @EndUserText.label: 'UU Int GR Error'
  ErrorsGoodsReceiptUUQuantity;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'GoodsReceiptErrors'}]}
  @EndUserText.label: 'BL Int GR Error'
  ErrorsGoodsReceiptBlQuantity;
  @UI:{ fieldGroup: [{ position: 30, label: 'QI', qualifier: 'GoodsReceiptErrors'}]}
  @EndUserText.label: 'QI Int GR Error'
  ErrorsGoodsReceiptQiQuantity;
  @UI:{ fieldGroup: [{ position: 10, label: 'UU',qualifier: 'InventoryPostingErrors'}]}
  @EndUserText.label: 'UU Int Inv Error'
  ErrorsInventoryPostingUnQty;
  @UI:{ fieldGroup: [{ position: 20, label: 'BL',qualifier: 'InventoryPostingErrors'}]}
  @EndUserText.label: 'BL Int Inv Error'
  ErrorsInventoryPostingBlQty;
  @UI:{ fieldGroup: [{ position: 30,label: 'QI',qualifier: 'InventoryPostingErrors'}]}
  @EndUserText.label: 'QI Int Inv Error'
  ErrorsInventoryPostingQiQty;
  @UI:{ fieldGroup: [{ position: 10, label: 'GR', qualifier: 'EcomErrors'}]}
  @EndUserText.label: 'UU GR ECOM Error'
  ErrorsGrEcom;
  @UI:{ fieldGroup: [{ position: 20, label: 'GI', qualifier: 'EcomErrors'}]}
  @EndUserText.label: 'UU GI ECOM Error'
  ErrorsGiEcom;
  @UI.selectionField: [{ position: 200 } ]
  @EndUserText.label: 'Display Only differences'
  HasDifference;
  @UI.hidden: true
  DifferenceCriticality;
  @UI.hidden: true
  BlockedDifferenceCriticality;
  @UI.hidden: true
  UnrectrictedDifferenceCritical;
  @UI.hidden: true
  InQualityInspDifferenceCritica;

}