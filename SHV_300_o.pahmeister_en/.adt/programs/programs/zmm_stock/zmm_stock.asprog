*********************************************************************
* Описание         : Ведомость остатков
* Код ТС           : DEV.030.04-18R
* Разработчик      : Коваленко А.В.
* Постановка задачи: Мойский А.Н.
* Дата постановки  : 05.09.2012
* Дата создания    : 15.09.2012
* Redmine #        : 1423
*********************************************************************
* Изменения
* Дата         Автор             Заявка  Характер и причина изменения
*********************************************************************
* 08.11.2013   Rudzko V.         #1423#note-144
*                                Приложение 6. (Проверка наличия фактур для разделяемых партий)
*                                Перед автоматическим разделением партий при резервировании необходимо
*                                осуществить проверку наличия фактур (при наличии соответствующих заказов
*                                на поставку).
*********************************************************************
* 10.02.2014   YVarazhun         #1837#note-89
*                                1. "Наименование организации" заполнять:
*                                ADRC-NAME1+ ADRC-NAME2+ ADRC-NAME3+ ADRC-NAME4
*                                для ADRC-ADDRNUMBER=T001-ADRNR, T001-BUKRS=T001K-BUKRS;
*                                2. Добавить в шаблон обозначения "Председатель", "Члены комиссии".
**************************************************************************************************
* 13.02.2014   YVarazhun         #2246#note-103
*                                1. Необходимо цену расчитывать путем деления суммы на количество
*                                и далее не округляя цену умножать на нужное количество
***************************************************************************************************
* 14.02.2014   Hubenka           #1423
**                                Добавлено копирование текста при создании ДМ с дробной ценой
**************************************************************************************************
* 24.03.2014   Kolesnik V.       #1423 note 166
**************************************************************************************************
* 24.06.2014   Bondar Y.         #5873
**************************************************************************************************
* 11.08.2015   VSasnouski        #8062
*                                Добавление функционала для ввода БЕ получателя
*                                с последующим сохранением в признаке партии Z_CLIENT
**************************************************************************************************
* 17.08.2015   VSasnouski        #8062
*                                1. Поле "БЕ получатель" переименовано в "Заказчик ТМЦ"
*                                2. Добавлено поле "Наименование заказчика ТМЦ"
*                                3. Для заказчика ТМЦ изменен источник данных для поля -
*                                   вместо справочников БЕ используются значения признака
*                                   партии "Заказчик ТМЦ".
**************************************************************************************************
* 17.08.2015   VSasnouski        #8062
*                                Подправлено затирание признаков партии при установке
*                                заказчика ТМЦ
**************************************************************************************************
* 28.08.2015   VSasnouski        #1423 note 192
*                                Реализован режим редактирования классификаций партий
**************************************************************************************************
* 16.03.2017   YVarazhun         #13135 DEV.030.04-18R Ведомость остатков
**************************************************************************************************
* 13.04.2017   HDamanikava       #13345 DEV.030.04-18R Ведомость остатков
**************************************************************************************************
* 18.05.2017   YVarazhun         #13135 В экран создания резервирования добавить поле "Заказ"
**************************************************************************************************
* 18.05.2017   YVarazhun         #13165 DEV.030-МТО.02-12.Е-015 Инвентаризационная опись 10-инв (АЕИ)
**************************************************************************************************
* 23.05.2017   YVarazhun         #13135 В экран создания резервирования добавить поле "Партнер"
**************************************************************************************************
* 16.01.2019   DSidarenka        #13135 Замена Deprecated J_3rMobved -> J_3rMobvedh, реж. Инвентаризации
*                                https://service.iba.by/redmine/issues/13135#note-40
**************************************************************************************************
* 16.01.2019   YBondar           #13135 DHVK944335
*                                Добавление галки учет даваль материалов
**************************************************************************************************
* 14.03.2020   Бутвиловская Д    8000000046 DHVK951917
*                                Разработка роли для просмотра Z-транзакций по сбыту
**************************************************************************************************
* 12.04.2021   D.Kruhliakou      #23809 Контроль вовлечения невостебованных ТМЦ в производственную деятельность
**************************************************************************************************
* 30.03.2023   V.Zaretsky        #27498 Добавление полей Доверенность НСС и Доверенность субподрядчика
**************************************************************************************************
REPORT  zmm_stock NO STANDARD PAGE HEADING.

INCLUDE zmm_stock_top.
*&---------------------------------------------------------------------*
*&      Экран выбора
*&---------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK sel1 WITH FRAME TITLE TEXT-s01.
SELECT-OPTIONS:
  s_matnr FOR mara-matnr MEMORY ID mat,
  s_werks FOR t001l-werks OBLIGATORY MEMORY ID wrk,
  s_lgort FOR t001l-lgort MODIF ID lgo,
  s_charg FOR mchb-charg MEMORY ID cha,
  s_hkont FOR ska1-saknr MEMORY ID sak,
  s_matkl FOR mara-matkl,
  s_ekgrp FOR marc-ekgrp,
  s_posid FOR prps-posid MODIF ID psp.
PARAMETERS:
  p_mslb AS CHECKBOX DEFAULT '' USER-COMMAND mslb.

SELECTION-SCREEN BEGIN OF LINE.                                              "{Hubenka #4352 18.10.2013
SELECTION-SCREEN POSITION 1.
PARAMETERS p_mstb AS CHECKBOX DEFAULT '' USER-COMMAND mstb.
SELECTION-SCREEN POSITION 3.
SELECTION-SCREEN COMMENT 3(70) TEXT-s06 FOR FIELD p_mstb.
SELECTION-SCREEN END OF LINE.                                                "}Hubenka #4352 18.10.2013

SELECTION-SCREEN BEGIN OF LINE.                                              "{Hubenka #4352 18.10.2013
SELECTION-SCREEN POSITION 1.
PARAMETERS p_mska AS CHECKBOX DEFAULT '' USER-COMMAND mska.
SELECTION-SCREEN POSITION 3.
SELECTION-SCREEN COMMENT 3(70) TEXT-s05 FOR FIELD p_mska.
SELECTION-SCREEN END OF LINE.                                                "}Hubenka #4352 18.10.2013

SELECT-OPTIONS:
  s_lifnr FOR lfa1-lifnr MODIF ID lif.
SELECTION-SCREEN END OF BLOCK sel1.

SELECTION-SCREEN BEGIN OF BLOCK sel2 WITH FRAME TITLE TEXT-s02.
PARAMETERS:
  p_mtext  AS CHECKBOX,
  p_avail  AS CHECKBOX DEFAULT 'X',
  p_class  AS CHECKBOX DEFAULT '' USER-COMMAND classif,
  p_edtclf AS CHECKBOX DEFAULT '' USER-COMMAND edit_classif MODIF ID ecl,    "28.08.2015 VSasnouski rm#1423#note-192 Включение режима редактирования классификаций партий
  p_dmsumm AS CHECKBOX DEFAULT '' USER-COMMAND dmsumm MODIF ID dms,
  p_equip  AS CHECKBOX DEFAULT '' USER-COMMAND equip MODIF ID equ,           "05.01.2024 D.Kruhliakou #34302 Оборотные ведомости по оборудованию
  pa_rever TYPE zmm_3rmoptved3 DEFAULT 'X'.

SELECTION-SCREEN BEGIN OF BLOCK sel4 WITH FRAME TITLE TEXT-s04.
PARAMETERS:
  p_dmcost AS CHECKBOX DEFAULT '' USER-COMMAND dmcost MODIF ID dmc,
  p_dmdate TYPE sydatum MODIF ID dmd.
SELECTION-SCREEN END OF BLOCK sel4.

" --> 16.04.2021 D.Kruhliakou
PARAMETERS:
  p_graph  AS CHECKBOX DEFAULT '' USER-COMMAND graph MODIF ID gr,
  p_edtgrh AS CHECKBOX DEFAULT '' USER-COMMAND edit_graph MODIF ID egr.
" <-- 16.04.2021 D.Kruhliakou
SELECTION-SCREEN END OF BLOCK sel2.

SELECTION-SCREEN BEGIN OF BLOCK sel3 WITH FRAME TITLE TEXT-s03.
PARAMETERS:
  p_reserv AS CHECKBOX DEFAULT '' MODIF ID hid,
  p_invent AS CHECKBOX DEFAULT '' USER-COMMAND invent MODIF ID hid,
  p_invdat TYPE datum MODIF ID inv,
  p_asdecl AS CHECKBOX MODIF ID dcl.                                         "11.08.2015 VSasnouski rm#8062 Чекбокс для вывода колонки "Заказчик ТМЦ"
SELECTION-SCREEN END OF BLOCK sel3.

PARAMETERS:
  p_selm NO-DISPLAY.

*&---------------------------------------------------------------------*
*&  Include           ZFM_TPLNR_DYNAMIC_LIST_SSCR
*&---------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK sel5 WITH FRAME TITLE TEXT-h02.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(30) TEXT-h04 FOR FIELD p_var.
PARAMETERS: p_var TYPE zpm_atinn_vals-atinnvar.

SELECTION-SCREEN:
  PUSHBUTTON  54(18)  but_crte USER-COMMAND crte VISIBLE LENGTH 2,
  PUSHBUTTON  57(18)  but_chng USER-COMMAND chng VISIBLE LENGTH 2,
  PUSHBUTTON  60(18)  but_disp USER-COMMAND disp VISIBLE LENGTH 2,
  PUSHBUTTON  63(18)  but_delt USER-COMMAND delt VISIBLE LENGTH 2.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK sel5.

INCLUDE:
  z_application_log,
  zmm_stock_c01,
  zmm_stock_c02,
  zmm_stock_f01,
  zmm_stock_i01,
  zmm_stock_o01,
  zmm_stock_print,
  zmm_stock_scr0220.

*&---------------------------------------------------------------------*
* I N I T I A L I Z A T I O N
*&---------------------------------------------------------------------*
INITIALIZATION.
  CLEAR g_error.
  gv_show_new_text = 'Дополнительные записи'(t10).
  CLEAR: gt_makt[], gt_mchb[], gt_mbew[], gt_stock[].
  SET PARAMETER ID 'ZPM_ATKLA' FIELD 'BN_*'.
  PERFORM add_icon USING:
          icon_create  '' but_crte,
          icon_change  '' but_chng,
          icon_display '' but_disp,
          icon_delete  '' but_delt.

* Загрузка варианта по умолчанию
  IMPORT gv_f4 TO gv_f4 FROM MEMORY ID zfi_memory=>gv_zmm_stock.
  FREE MEMORY ID zfi_memory=>gv_zmm_stock.
  IF gv_f4 IS INITIAL.
    PERFORM f_set_default_variant.
  ENDIF.

*&---------------------------------------------------------------------*
* A T   S E L E C T I O N - S C R E E N
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
  CLEAR gv_hide_buttons.

  IF NOT p_selm IS INITIAL.
    CLEAR: p_invent, p_reserv.
  ENDIF.
  IF p_dmcost = 'X' AND p_dmdate IS INITIAL.
    p_dmdate = sy-datum.
  ENDIF.
  AUTHORITY-CHECK OBJECT 'ZSD_DISPLA' ID 'ACTVT' FIELD '02'. " 8000000046 AK_BUTVILOVS
  IF sy-subrc <> 0.
    gv_hide_buttons = abap_true.
  ENDIF.
  LOOP AT SCREEN.
    IF screen-group1 = 'INV'.
      IF p_invent = 'X'.
        screen-active = '1'.
      ELSE.
        screen-active = '0'.
      ENDIF.
    ENDIF.
    IF screen-group1 = 'HID'.
      IF NOT p_selm IS INITIAL.
        screen-active = '0'.
      ELSE.
        screen-active = '1'.
      ENDIF.
    ENDIF.
    IF screen-group1 = 'ECL'.                                                  "{28.08.2015 VSasnouski rm#1423#note-192 Включение режима редактирования классификаций партий
      IF p_class = 'X'.
        screen-active = '1'.

        IF p_dmsumm IS INITIAL AND p_edtgrh IS INITIAL. "--> 16.04.2021 D.Kruhliakou
          screen-input = '1'.
        ELSE.
          screen-input = '0'.
          CLEAR p_edtclf.
        ENDIF.
      ELSE.
        screen-active = '0'.
        CLEAR p_edtclf.
      ENDIF.
    ENDIF.                                                                     "}28.08.2015 VSasnouski rm#1423#note-192 Включение режима редактирования классификаций партий
    IF screen-group1 = 'DMS'.
      IF p_class = 'X'.
        screen-active = '1'.

        IF p_edtclf IS INITIAL AND p_edtgrh IS INITIAL. "--> 16.04.2021 D.Kruhliakou
          screen-input = '1'.
        ELSE.
          screen-input = '0'.
          CLEAR p_dmsumm.
        ENDIF.
      ELSE.
        screen-active = '0'.
      ENDIF.
    ENDIF.
    IF screen-group1 = 'DMC'.
      IF p_dmsumm = 'X'.
        screen-active = '1'.
      ELSE.
        screen-active = '0'.
      ENDIF.
    ENDIF.
    IF screen-group1 = 'DMD'.
      IF p_dmcost = 'X'.
        screen-active = '1'.
      ELSE.
        screen-active = '0'.
      ENDIF.
    ENDIF.
    IF screen-group1 = 'LIF'.
      IF p_mslb = 'X'.
        screen-active = '1'.
      ELSE.
        screen-active = '0'.
      ENDIF.
    ENDIF.
    IF screen-group1 = 'LGO'.
      IF p_mslb = 'X' OR p_mstb = 'X'
        OR p_mska = 'X'.                                                       "Hubenka #4352 19.10.2013
        screen-active = '0'.
      ELSE.
        screen-active = '1'.
      ENDIF.
    ENDIF.
    IF screen-group1 = 'PSP'.
      IF p_mslb = 'X'.
        screen-active = '0'.
      ELSE.
        screen-active = '1'.
      ENDIF.
    ENDIF.

    IF screen-group1 = 'DCL'.                                                  "{28.08.2015 VSasnouski rm#1423#note-192 Включение режима редактирования классификаций партий
      IF p_class = 'X'.
        screen-input = '0'.
        CLEAR p_asdecl.
      ELSE.
        screen-input = '1'.
      ENDIF.
    ENDIF.                                                                     "}28.08.2015 VSasnouski rm#1423#note-192 Включение режима редактирования классификаций партий

    "--> 16.04.2021 D.Kruhliakou
    IF screen-group1 = 'GR'.
      AUTHORITY-CHECK OBJECT 'Z_MM_GRAPH' ID 'ACTVT' FIELD '03'.
      IF sy-subrc = 0.
        screen-active = 1.
      ELSE.
        screen-active = 0.
        CLEAR p_graph.
        CLEAR p_edtgrh.
      ENDIF.
    ENDIF.

    IF screen-group1 = 'EGR'.
      AUTHORITY-CHECK OBJECT 'Z_MM_GRAPH' ID 'ACTVT' FIELD '32'.
      IF sy-subrc = 0.
        IF p_graph = 'X'.
          screen-active = 1.

          IF p_edtclf IS INITIAL AND p_dmsumm IS INITIAL.
            screen-input = 1.
          ELSE.
            screen-input = 0.
            CLEAR p_edtgrh.
          ENDIF.
        ELSE.
          screen-active = 0.
          CLEAR p_edtgrh.
        ENDIF.
      ELSE.
        screen-active = 0.
        CLEAR p_edtgrh.
      ENDIF.
    ENDIF.
    "<-- 16.04.2021 D.Kruhliakou

    "--> 05.01.2024 D.Kruhliakou #34302 Оборотные ведомости по оборудованию
    IF screen-group1 = 'EQU'.
      IF p_class = 'X'.
        screen-active = '1'.
      ELSE.
        screen-active = '0'.
        CLEAR p_equip.
      ENDIF.
    ENDIF.
    "<-- 05.01.2024 D.Kruhliakou #34302

    IF gv_hide_buttons = abap_true.
      IF   screen-name = 'BUT_CRTE'
        OR screen-name = 'BUT_CHNG'
        OR screen-name = 'BUT_DELT'.
        screen-active = '0'.
      ENDIF.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

AT SELECTION-SCREEN.
  IF p_class IS INITIAL.
    CLEAR p_dmsumm.
  ENDIF.
  IF p_invent IS INITIAL.
    CLEAR p_invdat.
  ENDIF.
  IF p_dmsumm IS INITIAL.
    CLEAR p_dmcost.
  ENDIF.
  IF p_dmcost IS INITIAL.
    CLEAR p_dmdate.
  ENDIF.
  IF p_mslb IS INITIAL.
    CLEAR s_lifnr[].
  ENDIF.
*  IF p_mslb = 'X' AND p_mstb = 'X'.                                           "Hubenka #4352 19.10.2013
  IF p_mslb = 'X' AND ( p_mstb = 'X' OR p_mska = 'X' ).                        "Hubenka #4352 19.10.2013
    MESSAGE e159(zmm).
  ENDIF.
  IF p_mslb = 'X'.
    CLEAR: s_lgort[], s_posid[].
  ENDIF.
  IF p_mstb = 'X'
    OR p_mska = 'X'.                                                           "Hubenka #4352 19.10.2013
    CLEAR: s_lgort[].
  ENDIF.

  PERFORM variant_user_command USING p_var sy-ucomm.

*&---------------------------------------------------------------------*
* S T A R T  -  O F  -  S E L E C T I O N
*&---------------------------------------------------------------------*
START-OF-SELECTION.
  IF p_dmcost = 'X' AND p_dmdate IS INITIAL.
    MESSAGE s096(zmm) DISPLAY LIKE 'E' WITH '' ''.
    EXIT.
  ENDIF.
  IF p_invent = 'X' AND p_invdat IS INITIAL.
    MESSAGE s079(zmm) DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.
  IF p_reserv = 'X'.
    p_avail = 'X'.
  ENDIF.

  PERFORM select_lgort.
  CHECK g_error IS INITIAL.
  PERFORM select_t024.
  PERFORM clear_int_tables.
  PERFORM select_t030.
  PERFORM select_zmm_res.
  IF NOT p_var IS INITIAL.
    PERFORM read_dyn_sel_tvarv.
  ENDIF.
  IF p_invdat IS INITIAL.
    IF s_posid[] IS INITIAL AND p_mslb IS INITIAL
      AND p_mstb IS INITIAL
      AND p_mska IS INITIAL.                                                   "Hubenka #4352 19.10.2013

      PERFORM select_mchb.
    ENDIF.
  ELSE.
    "16/01/2019 - j_3rmobved устарела. SAP Note 2227963, использовать j_3rmobvedh
    "PERFORM submit_j_3rmobved.
    PERFORM submit_j_3rmobvedh.
  ENDIF.

  PERFORM clear_int_tables.
  IF p_invdat IS INITIAL.
    IF p_mslb IS INITIAL AND p_mstb IS INITIAL
      AND p_mska IS INITIAL.                                                   "Hubenka #4352 19.10.2013
      PERFORM select_mspr.
    ENDIF.
    IF p_mslb = 'X'.
      PERFORM select_mslb.
    ENDIF.
    IF p_mstb = 'X'.
      PERFORM select_mstb.
      PERFORM select_mstq.
    ENDIF.
    IF p_mska = 'X'.                                                           "{Hubenka #4352 19.10.2013
      PERFORM select_mska.
    ENDIF.                                                                     "}Hubenka #4352 19.10.2013
  ENDIF.
  PERFORM filter_saknr.
  PERFORM select_makt.
  PERFORM select_matkl.
  PERFORM select_prps.
  PERFORM read_long_text.
  PERFORM calc_avail_stock.
  PERFORM create_dynamic_int_table.
  PERFORM resb_add_show USING space.
*&---------------------------------------------------------------------*
* E N D  -  O F  -  S E L E C T I O N
*&---------------------------------------------------------------------*
END-OF-SELECTION.
  CLEAR gv_bi_mode.
  IMPORT v_mode = gv_bi_mode FROM MEMORY ID zfi_memory=>gv_z_set_mm_data_bi.
  FREE MEMORY ID zfi_memory=>gv_z_set_mm_data_bi.

  IF gv_bi_mode IS NOT INITIAL.
    IF <gf_idata_tab> IS ASSIGNED.
      EXPORT t_fcat = gt_fieldcatalog1 TO MEMORY ID zfi_memory=>gv_z_set_mm_data_bi_fcat COMPRESSION ON.
      EXPORT t_data = <gf_idata_tab>   TO MEMORY ID zfi_memory=>gv_z_set_mm_data_bi_data COMPRESSION ON.
    ENDIF.

    RETURN.
  ENDIF.

  IF NOT <gf_idata_tab> IS ASSIGNED.
    MESSAGE s070(zmm) DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.
  IF <gf_idata_tab> IS ASSIGNED AND NOT <gf_idata_tab> IS INITIAL.
    CALL SCREEN 100.
  ENDIF.
