"! <p class="shorttext synchronized" lang="en">Lib. Determination: Cleanup durable locks during save</p>
CLASS /bobf/cl_lib_d_clean_dura_lock DEFINITION
  PUBLIC
  INHERITING FROM /bobf/cl_lib_a_d_v_draft_super
  FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.

    INTERFACES /bobf/if_frw_determination.


  PROTECTED SECTION.

    METHODS initialize REDEFINITION.


  PRIVATE SECTION.

    DATA mo_lock_manager TYPE REF TO /bobf/cl_lib_durable_lock_mngr.


ENDCLASS.



CLASS /BOBF/CL_LIB_D_CLEAN_DURA_LOCK IMPLEMENTATION.


  METHOD /bobf/if_frw_determination~check.

  ENDMETHOD.


  METHOD /bobf/if_frw_determination~check_delta.

  ENDMETHOD.


  METHOD /bobf/if_frw_determination~execute.

    CLEAR eo_message.
    CLEAR et_failed_key.

    initialize( iv_bo_key = is_ctx-bo_key  iv_node_key = is_ctx-node_key iv_content_key = is_ctx-det_key ).

    " Draft determinations shouldn't be called with active keys (=> filter them out, but don't set ET_FAILED_KEY)...
    mo_draft_active->separate_keys( EXPORTING iv_node_key  = is_ctx-node_key
                                              it_key       = it_key
                                    IMPORTING et_draft_key = DATA(lt_draft_key) ).
    IF lt_draft_key IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lr_conf_det_exectime) = mo_configuration->get_determination( is_ctx-exectime ).
    IF line_exists( lr_conf_det_exectime->det_list->*[ det_key = is_ctx-det_key ]-det_trigger->*[ create = abap_false update = abap_false delete = abap_true ] ).
      " determination DURABLE_LOCK_CLEANUP_DELETE triggers only on DELETE...
      mo_lock_manager->determ_dura_lock_clean_delete( lt_draft_key ).
    ELSE.
      " determinations DURABLE_LOCK_CLEANUP_SUCC_SAVE, DURABLE_LOCK_CLEANUP_FAIL_SAVE, DURABLE_LOCK_CLEANUP_CLEANUP trigger on CREATE, UPDATE and DELETE...
      mo_lock_manager->determ_dura_lock_clean_final( lt_draft_key ).
    ENDIF.

  ENDMETHOD.


  METHOD initialize.

    super->initialize( iv_bo_key = iv_bo_key  iv_node_key = iv_node_key iv_content_key = iv_content_key ).
    mo_lock_manager = /bobf/cl_lib_durable_lock_mngr=>get_instance( mo_configuration ).

  ENDMETHOD.
ENDCLASS.